
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	СтруктураИзменяемыхРеквизитов = СтруктураИзменяемыхРеквизитов();
	Для Каждого КлючИЗначение Из СтруктураИзменяемыхРеквизитов Цикл
		Если Не ЭтотОбъект[КлючИЗначение.Ключ+"Флаг"] Тогда
			НепроверяемыеРеквизиты.Добавить(КлючИЗначение.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	НачислятьАмортизацию = (ПорядокУчета=Перечисления.ПорядокУчетаСтоимостиВнеоборотныхАктивов.НачислятьАмортизацию);
	АмортизацияПоНаработке = (МетодНачисленияАмортизации=Перечисления.СпособыНачисленияАмортизацииОС.ПропорциональноОбъемуПродукции);
	
	Если НачислятьАмортизацию И Не АмортизацияПоНаработке Тогда
		НепроверяемыеРеквизиты.Добавить("ПоказательНаработки");
		НепроверяемыеРеквизиты.Добавить("ОбъемНаработки");
	КонецЕсли;
	
	Если Не НачислятьАмортизацию Тогда
		НепроверяемыеРеквизиты.Добавить("МетодНачисленияАмортизации");
		НепроверяемыеРеквизиты.Добавить("СчетАмортизации");
		НепроверяемыеРеквизиты.Добавить("СтатьяРасходов");
		НепроверяемыеРеквизиты.Добавить("АналитикаРасходов");
		НепроверяемыеРеквизиты.Добавить("ПоказательНаработки");
		НепроверяемыеРеквизиты.Добавить("ОбъемНаработки");
	КонецЕсли;
	
	Если Не НачислятьАмортизацию Или АмортизацияПоНаработке Тогда
		НепроверяемыеРеквизиты.Добавить("СрокИспользования");
	КонецЕсли;
	
	Если Не НачислятьАмортизацию Или МетодНачисленияАмортизации<>Перечисления.СпособыНачисленияАмортизацииОС.УменьшаемогоОстатка Тогда
		НепроверяемыеРеквизиты.Добавить("КоэффициентУскорения");
	КонецЕсли;
	
	Если НачислятьАмортизацию Тогда
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
			ЭтотОбъект, , НепроверяемыеРеквизиты, Отказ);
		
	КонецЕсли;
	
	КлючевыеРеквизиты = Новый Массив;
	КлючевыеРеквизиты.Добавить("ОсновноеСредство");
	ОбщегоНазначенияУТ.ПроверитьНаличиеДублейСтрокТЧ(
		ЭтотОбъект, "ОсновныеСредства", КлючевыеРеквизиты, Отказ, НСтр("ru='Основные средства';uk='Основні засоби'"));
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипОснования = ТипЗнч(ДанныеЗаполнения);
	Если ТипОснования = Тип("СправочникСсылка.ОбъектыЭксплуатации") Тогда
		ОсновныеСредства.Добавить().ОсновноеСредство = ДанныеЗаполнения;
	КонецЕсли;
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТекущиеЗначения.ПорядокУчета КАК ПорядокУчета,
		|	ТекущиеЗначения.СчетУчета КАК СчетУчета,
		|	ТекущиеЗначения.СчетАмортизации КАК СчетАмортизации,
		|	ТекущиеЗначения.МетодНачисленияАмортизации КАК МетодНачисленияАмортизации,
		|	ТекущиеЗначения.СрокИспользования КАК СрокИспользования,
		|	ТекущиеЗначения.ПоказательНаработки КАК ПоказательНаработки,
		|	ТекущиеЗначения.ОбъемНаработки КАК ОбъемНаработки,
		|	ТекущиеЗначения.КоэффициентУскорения КАК КоэффициентУскорения,
		|	ТекущиеЗначения.СтатьяРасходов КАК СтатьяРасходов,
		|	ТекущиеЗначения.АналитикаРасходов КАК АналитикаРасходов,
		|	ТекущиеЗначения.ЛиквидационнаяСтоимость,
		|	ТекущиеЗначения.ЛиквидационнаяСтоимостьПредставления
		|ИЗ
		|	РегистрСведений.ОсновныеСредстваМеждународныйУчет.СрезПоследних(
		|			&Дата,
		|			ОсновноеСредство В (&ОсновныеСредства)
		|				И Регистратор <> &ТекущийРегистратор) КАК ТекущиеЗначения"
	);
	Запрос.УстановитьПараметр("Дата", Новый Граница(?(ЗначениеЗаполнено(Дата), Дата, ТекущаяДатаСеанса()), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ОсновныеСредства", ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство"));
	Запрос.УстановитьПараметр("ТекущийРегистратор", Ссылка);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		СтруктураИзменяемыхРеквизитов = СтруктураИзменяемыхРеквизитов();
		Для Каждого КлючИЗначение Из СтруктураИзменяемыхРеквизитов Цикл
			Если Не ЭтотОбъект[КлючИЗначение.Ключ+"Флаг"] Тогда
				ЭтотОбъект[КлючИЗначение.Ключ] = Выборка[КлючИЗначение.Ключ];
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ОчиститьЗаписатьДвижения(Движения, "Международный, ОсновныеСредстваМеждународныйУчет");
	
	ПроверитьВозможностьИзмененияПараметров(Отказ);
	
	// Инициализация дополнительных свойств для проведения документа
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	// Инициализация данных документа
	Документы.ИзменениеПараметровОСМеждународныйУчет.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение
	Для Каждого ТаблицаДвижений Из ДополнительныеСвойства.ТаблицыДляДвижений Цикл
		ПроведениеСервер.ОтразитьДвижения(ТаблицаДвижений.Значение, Движения[ТаблицаДвижений.Ключ], Отказ);
	КонецЦикла;
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтруктураИзменяемыхРеквизитов()
	
	Возврат Новый Структура(
		"ПорядокУчета,
		|СчетУчета, СчетАмортизации, ЛиквидационнаяСтоимость, ЛиквидационнаяСтоимостьПредставления,
		|МетодНачисленияАмортизации, СрокИспользования, ПоказательНаработки, ОбъемНаработки, КоэффициентУскорения,
		|СтатьяРасходов, АналитикаРасходов"
	);
	
КонецФункции

Процедура ИнициализироватьДокумент()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
КонецПроцедуры

Процедура ПроверитьВозможностьИзмененияПараметров(Отказ=Ложь)
	
	ТребуемоеСостояние = Новый Структура("Организация, Состояние", Организация, Перечисления.СостоянияОС.ПринятоКУчету);
	ПараметрыПроверки = Новый Структура("ДатаСведений, ИсключаемыйРегистратор", Дата, Ссылка);
	Ошибки = МеждународныйУчетВнеоборотныеАктивы.ПроверитьСостояниеВнеоборотныхАктивов(
		ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство"), ТребуемоеСостояние, ПараметрыПроверки);
	Если Ошибки=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из ОсновныеСредства Цикл
		
		Данные = Ошибки.Получить(Строка.ОсновноеСредство);
		Если Данные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстОшибки = НСтр("ru='Учетные данные основного средства ""%1"" не могут быть изменены.';uk='Облікові дані основного засобу ""%1"" не можуть бути змінені.'") + Символы.ПС;
		ТекстОшибки = СтрЗаменить(ТекстОшибки, "%1", Строка.ОсновноеСредство);
		Если Данные.Состояние <> Перечисления.СостоянияОС.ПринятоКУчету Тогда
			ТекстОшибки = ТекстОшибки
				+ НСтр("ru='Объект не принят к учету';uk='Об''єкт не прийнятий до обліку'");
		Иначе
			ТекстОшибки = ТекстОшибки
				+ СтрЗаменить(НСтр("ru='Объект принят к учету в организации ""%1""';uk='Об''єкт прийнято до обліку в організації ""%1""'"), "%1", Данные.Организация);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОсновныеСредства", Строка.НомерСтроки, "ОсновноеСредство"),
			,
			Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли