#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	ДанныеДляПроведения = ПолучитьДанныеДляПроведения();
	
	УчетСтраховыхВзносов.СформироватьДоходыСтраховыеВзносы(Движения, Отказ, Организация, МесяцРасчетногоПериода, ДанныеДляПроведения.НачисленияПоСотрудникам, Истина);
	УчетСтраховыхВзносов.СформироватьИсчисленныеВзносы(Движения, Отказ, Организация, МесяцРасчетногоПериода, ДанныеДляПроведения.ВзносыИсчисленные);
	УчетСтраховыхВзносов.СформироватьСтраховыеВзносыПоФизическимЛицам(Движения, Отказ, Организация, МесяцРасчетногоПериода, Ссылка, ДанныеДляПроведения.ВзносыИсчисленные);
	УчетСтраховыхВзносов.СформироватьРасчетыСФондом(Движения, Отказ, Организация, ДанныеДляПроведения.РасчетСФондом);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьДанныеДляПроведения()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеОДоходах.Ссылка.ФизическоеЛицо,
	|	СУММА(ДанныеОДоходах.Результат) КАК Сумма,
	|   ДанныеОДоходах.ВидЕСВ,
	|   ДанныеОДоходах.ЭтоОтпускные,
	|   НАЧАЛОПЕРИОДА(ДанныеОДоходах.НалоговыйПериод, МЕСЯЦ) КАК НалоговыйПериод
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.СведенияОДоходах КАК ДанныеОДоходах
	|ГДЕ
	|	ДанныеОДоходах.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОДоходах.Ссылка.ФизическоеЛицо,
	|   ДанныеОДоходах.ВидЕСВ,
	|   ДанныеОДоходах.ЭтоОтпускные,
	|   НАЧАЛОПЕРИОДА(ДанныеОДоходах.НалоговыйПериод, МЕСЯЦ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Взносы.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Взносы.ВидЕСВ КАК ВидЕСВ,
	|	Взносы.КатегорияЕСВ КАК КатегорияЕСВ,
	|	НАЧАЛОПЕРИОДА(Взносы.ДатаНачала, МЕСЯЦ) КАК НалоговыйПериод,
	|	Взносы.ДатаНачала КАК ДатаНачала,
	|	Взносы.ДатаОкончания КАК ДатаОкончания,
	|	Взносы.Налог КАК Налог,
	|	Взносы.СтатьяНалоговойДекларации КАК СтатьяНалоговойДекларации,
	|	Взносы.База КАК База,
	|	Взносы.БазаВзноса КАК БазаВзноса,
	|	Взносы.БазаОтпускные КАК БазаОтпускные,
	|	Взносы.БазаДоначисление КАК БазаДоначисление,
	|	Взносы.Результат КАК Результат,
	|	Взносы.РезультатДоначисление КАК РезультатДоначисление,
	|	Взносы.Предел КАК Предел,
	|	Взносы.Ставка КАК Ставка,
	|	Взносы.МинимальнаяБаза КАК МинимальнаяБаза,
	|	Взносы.КоэффициентСтавки КАК КоэффициентСтавки,
	|	Взносы.БазаВычет КАК БазаВычет,
	|	Взносы.РезультатПолная КАК РезультатПолная,
	|	Взносы.ТипНачисления КАК ТипНачисления,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаВзносов.Взносы) КАК СпособРасчета
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.Взносы КАК Взносы
	|ГДЕ
	|	Взносы.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Взносы.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Взносы.ВидЕСВ КАК ВидЕСВ,
	|	Взносы.КатегорияЕСВ КАК КатегорияЕСВ,
	|	НАЧАЛОПЕРИОДА(Взносы.ДатаНачала, МЕСЯЦ) КАК НалоговыйПериод,
	|	Взносы.ДатаНачала КАК ДатаНачала,
	|	Взносы.ДатаОкончания КАК ДатаОкончания,
	|	Взносы.Налог КАК Налог,
	|	Взносы.СтатьяНалоговойДекларации КАК СтатьяНалоговойДекларации,
	|	Взносы.База КАК База,
	|	Взносы.БазаВзноса КАК БазаВзноса,
	|	Взносы.БазаОтпускные КАК БазаОтпускные,
	|	Взносы.БазаДоначисление КАК БазаДоначисление,
	|	Взносы.Результат КАК Результат,
	|	Взносы.РезультатДоначисление КАК РезультатДоначисление,
	|	Взносы.Предел КАК Предел,
	|	Взносы.Ставка КАК Ставка,
	|	Взносы.МинимальнаяБаза КАК МинимальнаяБаза,
	|	Взносы.КоэффициентСтавки КАК КоэффициентСтавки,
	|	Взносы.БазаВычет КАК БазаВычет,
	|	Взносы.РезультатПолная КАК РезультатПолная,
	|	Взносы.ТипНачисления КАК ТипНачисления,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаВзносов.ВзносыФОТ) КАК СпособРасчета
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.ВзносыФОТ КАК Взносы
	|ГДЕ
	|	Взносы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСФондами.Ссылка.МесяцРасчетногоПериода КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	РасчетыСФондами.Ссылка.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыСФондами.Ссылка.Организация КАК Организация,
	|	РасчетыСФондами.Налог КАК Налог,
	|	НАЧАЛОПЕРИОДА(РасчетыСФондами.ПериодВзаиморасчетов, МЕСЯЦ) КАК ПериодВзаиморасчетов,
	|	РасчетыСФондами.Сумма КАК Сумма
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам.РасчетыСФондами КАК РасчетыСФондами
	|ГДЕ
	|	РасчетыСФондами.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацияУчетаПоСтраховымВзносам.ФизическоеЛицо
	|ПОМЕСТИТЬ ВТФизическиеЛица
	|ИЗ
	|	Документ.ОперацияУчетаПоСтраховымВзносам КАК ОперацияУчетаПоСтраховымВзносам
	|ГДЕ
	|	ОперацияУчетаПоСтраховымВзносам.Ссылка = &Ссылка
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("МесяцРасчетногоПериода", МесяцРасчетногоПериода);
	Результаты = Запрос.ВыполнитьПакет();
	
	НачисленияПоСотрудникам = Результаты[0].Выгрузить();
	ВзносыИсчисленные = Результаты[1].Выгрузить();
	РасчетСФондом = Результаты[2].Выгрузить();
	
	ДанныеДляПроведения = Новый Структура("НачисленияПоСотрудникам, ВзносыИсчисленные, РасчетСФондом,  МенеджерВременныхТаблиц", 
						НачисленияПоСотрудникам,
						ВзносыИсчисленные,
						РасчетСФондом,
						Запрос.МенеджерВременныхТаблиц);
						
	Возврат ДанныеДляПроведения;

КонецФункции

#КонецОбласти

#КонецЕсли