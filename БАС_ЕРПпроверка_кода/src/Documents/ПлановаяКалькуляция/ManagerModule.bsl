#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	ВводНаОснованииПереопределяемый.ДобавитьКомандуСоздатьНаОснованииБизнесПроцессЗадание(КомандыСоздатьНаОсновании);
	
КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПлановаяКалькуляция) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ПлановаяКалькуляция.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ПлановаяКалькуляция);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ДобавитьКомандуСоздатьНаОснованииСпецификации(КомандыСоздатьНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПлановаяКалькуляция) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Обработчик = "ВводНаОснованииУТКлиент.СоздатьНаОснованииСпецификации";
		КомандаСоздатьНаОсновании.Идентификатор = "СоздатьНаОснованииСпецификации";
		КомандаСоздатьНаОсновании.Представление = НСтр("ru='Плановая калькуляция';uk='Планова калькуляція'");
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	Возврат; //В дальнейшем будет добавлен код команд
	
КонецПроцедуры


// Заполняет плановую калькуляцию в коллекции значений.
//
// Параметры:
//	Коллекция - ДанныеФормыКоллекция, ТаблицаЗначений - Коллекция, в которой требуется заполнить плановую калькуляцию
//	МассивСтрок - Массив - Массив строк, в которых требуется заполнение
//	ПараметрыКоллекции - Структура - Сведения о полях, используемых для подбора калькуляции
//	Организация - СправочникСсылка.Организации - Организация, по которой выполняется поиск подходящей калькуляции
//	Период - Дата - Дата, на которую выполняется поиск калькуляции
//
Процедура ЗаполнитьКалькуляцииВКоллекции(Коллекция, МассивСтрок, ПараметрыКоллекции, Организация = Неопределено, Период = Неопределено) Экспорт
	
	Период = ?(ЗначениеЗаполнено(Период), Период, КонецДня(ТекущаяДатаСеанса()));
	Организация = ?(ЗначениеЗаполнено(Организация), Организация, Справочники.Организации.ПустаяСсылка());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	//%Распоряжение% КАК Распоряжение,
	|	//%Спецификация% КАК Спецификация,
	|	//%ДатаПотребности% КАК ДатаПотребности,
	|	//%Организация% КАК Организация,
	|	//%ТипСтоимости% КАК ТипСтоимости,
	|	Строки.Номенклатура КАК Номенклатура,
	|	Строки.Характеристика КАК Характеристика,
	|	Строки.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВтПродукция
	|ИЗ
	|	&Коллекция КАК Строки
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтПродукция.Номенклатура КАК Номенклатура,
	|	ВтПродукция.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.Ссылка, Значение(Документ.ЗаказНаПроизводство.ПустаяСсылка)) КАК ЗаказНаПроизводство,
	|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.КодСтроки, 0) КАК КодСтрокиЗаказаНаПроизводство,
	|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.Спецификация, ВтПродукция.Спецификация) КАК Спецификация,
	|	ВтПродукция.ДатаПотребности КАК ДатаПотребности,
	|	ВтПродукция.Организация КАК Организация,
	|	ВтПродукция.НомерСтроки КАК НомерСтроки,
	|	1 КАК КоличествоПоЗаказу,
	|	1 КАК КоличествоПоСпецификации,
	|	1 КАК ДоляСтоимости
	|ПОМЕСТИТЬ СпецификацияВыходныеИзделия
	|ИЗ
	|	ВтПродукция КАК ВтПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|		ПО ВтПродукция.Распоряжение.Распоряжение = ЗаказНаПроизводствоПродукция.Ссылка
	|			И ВтПродукция.Распоряжение.КодСтроки = ЗаказНаПроизводствоПродукция.КодСтроки
	|ГДЕ
	|	&БезОтбораПоТипуСтоимости ИЛИ ВтПродукция.ТипСтоимости = &ТипСтоимости
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|"+ ТекстЗапросаДействующиеКалькуляции()+
	"ВЫБРАТЬ
	|	СпецификацияВыходныеИзделия.НомерСтроки КАК НомерСтроки,
	|	СпецификацияВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	СпецификацияВыходныеИзделия.Характеристика КАК Характеристика,
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ПО СпецификацияВыходныеИзделия.ЗаказНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство
	|			И СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|ГДЕ
	|	ДействующаяКалькуляция.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.ЗаказНаПроизводство)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпецификацияВыходныеИзделия.НомерСтроки КАК НомерСтроки,
	|	СпецификацияВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	СпецификацияВыходныеИзделия.Характеристика КАК Характеристика,
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ПО СпецификацияВыходныеИзделия.Спецификация = ДействующаяКалькуляция.ВыходноеИзделиеСпецификация
	|ГДЕ
	|	ДействующаяКалькуляция.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.РесурснаяСпецификация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпецификацияВыходныеИзделия.НомерСтроки КАК НомерСтроки,
	|	СпецификацияВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	СпецификацияВыходныеИзделия.Характеристика КАК Характеристика,
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ПО СпецификацияВыходныеИзделия.НомерСтроки = ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки
	|ГДЕ
	|	ДействующаяКалькуляция.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.Изделие)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Коллекция", Коллекция.Выгрузить(МассивСтрок));
	
	Если ПараметрыКоллекции.Свойство("Распоряжение") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Распоряжение%", "Строки." + ПараметрыКоллекции.Распоряжение);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Распоряжение%", "ЗНАЧЕНИЕ(Документ.МаршрутныйЛистПроизводства.ПустаяСсылка)");
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("Спецификация") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Спецификация%", "Строки." + ПараметрыКоллекции.Спецификация);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Спецификация%", "ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)");
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("ДатаПотребности") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%ДатаПотребности%", "Строки." + ПараметрыКоллекции.ДатаПотребности);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%ДатаПотребности%", "&Период");
		Запрос.УстановитьПараметр("Период", Период);
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("Организация") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Организация%", "Строки." + ПараметрыКоллекции.Организация);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%Организация%", "&Организация");
		Запрос.УстановитьПараметр("Организация", Организация);
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("ТипСтоимости") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%ТипСтоимости%", "Строки." + ПараметрыКоллекции.ТипСтоимости);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//%ТипСтоимости%", "ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)");
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("ОтборПоТипуСтоимости") Тогда
		Запрос.УстановитьПараметр("БезОтбораПоТипуСтоимости", Ложь);
		Запрос.УстановитьПараметр("ТипСтоимости", ПараметрыКоллекции.ОтборПоТипуСтоимости);
	Иначе
		Запрос.УстановитьПараметр("БезОтбораПоТипуСтоимости", Истина);
		Запрос.УстановитьПараметр("ТипСтоимости", Неопределено);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	
	СтруктураОтбора = Новый Структура("НомерСтроки");
	
	Выборка = Результат.Выбрать();
	
	Если МассивСтрок = Неопределено Тогда
		ТабличнаяЧасть = Коллекция;
	Иначе
		ТабличнаяЧасть = МассивСтрок;
	КонецЕсли;
	
	Для Каждого Строка Из ТабличнаяЧасть Цикл
		СтруктураОтбора.НомерСтроки = Строка.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураОтбора) Тогда
			Строка.Калькуляция = Выборка.Калькуляция;
			
			Если ПараметрыКоллекции.Свойство("ОчищатьВидЦены") Тогда
				Строка.ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
			КонецЕсли;
			
		Иначе
			Строка.Калькуляция = Документы.ПлановаяКалькуляция.ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет цену номенклатуры по плановой калькуляции в коллекции значений.
//
// Параметры:
//	Коллекция - ДанныеФормыКоллекция, ТаблицаЗначений - Коллекция, в которой требуется заполнить плановую калькуляцию
//	МассивСтрок - Массив - Массив строк, в которых требуется заполнение
//	ПараметрыКоллекции - Структура - Сведения о полях, используемых для подбора калькуляции
//
Процедура ЗаполнитьЦеныПоКалькуляцииВКоллекции(Коллекция, МассивСтрок, ПараметрыКоллекции) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Распоряжение КАК Распоряжение,
	|	&Спецификация КАК Спецификация,
	|	&ТипСтоимости КАК ТипСтоимости,
	|	Строки.Номенклатура КАК Номенклатура,
	|	Строки.Характеристика КАК Характеристика,
	|	Строки.НомерСтроки КАК НомерСтроки,
	|	Строки.Калькуляция КАК Калькуляция
	|ПОМЕСТИТЬ ВтПродукция
	|ИЗ
	|	&Коллекция КАК Строки
	|ГДЕ
	|	Строки.Калькуляция <> ЗНАЧЕНИЕ(Документ.ПлановаяКалькуляция.ПустаяСсылка)
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	Номенклатура,
	|	Характеристика,
	|	Калькуляция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтПродукция.Номенклатура КАК Номенклатура,
	|	ВтПродукция.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.Ссылка, ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)) КАК ЗаказНаПроизводство,
	|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.КодСтроки, 0) КАК КодСтрокиЗаказаНаПроизводство,
	|	ЕСТЬNULL(ЗаказНаПроизводствоПродукция.Спецификация, ВтПродукция.Спецификация) КАК Спецификация,
	|	ВтПродукция.НомерСтроки КАК НомерСтроки,
	|	ВтПродукция.Калькуляция КАК Калькуляция
	|ПОМЕСТИТЬ СпецификацияВыходныеИзделия
	|ИЗ
	|	ВтПродукция КАК ВтПродукция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|		ПО ВтПродукция.Распоряжение.Распоряжение = ЗаказНаПроизводствоПродукция.Ссылка
	|			И ВтПродукция.Распоряжение.КодСтроки = ЗаказНаПроизводствоПродукция.КодСтроки
	|ГДЕ
	|	ВтПродукция.ТипСтоимости = Значение(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)
	|	ИЛИ (ВтПродукция.ТипСтоимости = Значение(Перечисление.ТипыСтоимостиВыходныхИзделий.Фиксированная)
	|	И ВтПродукция.Калькуляция.ОбъектКалькуляции = Значение(Перечисление.ОбъектыКалькуляции.Изделие))
	|	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпецификацияВыходныеИзделия.Калькуляция КАК Калькуляция,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация КАК Спецификация
	|ПОМЕСТИТЬ ВТКалькуляции
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаПроизводство,
	|	КодСтрокиЗаказаНаПроизводство,
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыходныеИзделия.Номенклатура,
	|	ВыходныеИзделия.Характеристика,
	|	СУММА(ВыходныеИзделия.Количество) КАК Количество,
	|	ВЫБОР
	|		КОГДА ВыходныеИзделия.ДоляСтоимости = 0
	|			ТОГДА 1
	|		ИНАЧЕ ВыходныеИзделия.ДоляСтоимости
	|	КОНЕЦ КАК ДоляСтоимости,
	|	ВТКалькуляции.Калькуляция КАК Калькуляция,
	|	ЛОЖЬ КАК ЛюбаяХарактеристика
	|ПОМЕСТИТЬ ВТДолиСтоимости
	|ИЗ
	|	Документ.ЗаказНаПроизводство.ВыходныеИзделия КАК ВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКалькуляции КАК ВТКалькуляции
	|			ПО ЗаказНаПроизводствоПродукция.Ссылка = ВТКалькуляции.ЗаказНаПроизводство
	|				И ЗаказНаПроизводствоПродукция.КодСтроки = ВТКалькуляции.КодСтрокиЗаказаНаПроизводство
	|		ПО ВыходныеИзделия.Ссылка = ЗаказНаПроизводствоПродукция.Ссылка
	|			И ВыходныеИзделия.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
	|ГДЕ
	|	ВТКалькуляции.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.ЗаказНаПроизводство)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыходныеИзделия.Номенклатура,
	|	ВыходныеИзделия.Характеристика,
	|	ВыходныеИзделия.ДоляСтоимости,
	|	ВТКалькуляции.Калькуляция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВыходныеИзделия.Номенклатура,
	|	ВыходныеИзделия.Характеристика,
	|	СУММА(ВыходныеИзделия.Количество),
	|	ВЫБОР
	|		КОГДА ВыходныеИзделия.ДоляСтоимости = 0
	|			ТОГДА 1
	|		ИНАЧЕ ВыходныеИзделия.ДоляСтоимости
	|	КОНЕЦ,
	|	ВТКалькуляции.Калькуляция,
	|	ВЫБОР
	|		КОГДА ВыходныеИзделия.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				И ВыходныеИзделия.Номенклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|ИЗ
	|	Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКалькуляции КАК ВТКалькуляции
	|		ПО ВыходныеИзделия.Ссылка = ВТКалькуляции.Спецификация
	|ГДЕ
	|	ВТКалькуляции.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.РесурснаяСпецификация)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыходныеИзделия.Номенклатура,
	|	ВыходныеИзделия.Характеристика,
	|	ВыходныеИзделия.ДоляСтоимости,
	|	ВТКалькуляции.Калькуляция,
	|	ВЫБОР
	|		КОГДА ВыходныеИзделия.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|				И ВыходныеИзделия.Номенклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры), ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпецификацияВыходныеИзделия.Номенклатура,
	|	СпецификацияВыходныеИзделия.Характеристика,
	|	СпецификацияВыходныеИзделия.Калькуляция.КалькуляционнаяЕдиница,
	|	1,
	|	СпецификацияВыходныеИзделия.Калькуляция,
	|	ЛОЖЬ
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|ГДЕ
	|	(СпецификацияВыходныеИзделия.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.Изделие)
	|			ИЛИ СпецификацияВыходныеИзделия.Спецификация = ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|			ИЛИ СпецификацияВыходныеИзделия.ЗаказНаПроизводство = ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДолиСтоимости.Калькуляция,
	|	СУММА(ВТДолиСтоимости.ДоляСтоимости) КАК ДоляСтоимости
	|ПОМЕСТИТЬ ВТДолиСтоимостиИтог
	|ИЗ
	|	ВТДолиСтоимости КАК ВТДолиСтоимости
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДолиСтоимости.Калькуляция
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДолиСтоимости.Номенклатура,
	|	ВТДолиСтоимости.Характеристика,
	|	ВТДолиСтоимости.Калькуляция,
	|	ВТДолиСтоимости.ЛюбаяХарактеристика,
	|	ВЫБОР
	|		КОГДА ВТДолиСтоимостиИтог.ДоляСтоимости = 0
	|				ИЛИ ВТДолиСтоимости.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВТДолиСтоимости.ДоляСтоимости * ВТДолиСтоимости.Калькуляция.СуммаПоДокументу / ВТДолиСтоимостиИтог.ДоляСтоимости / ВТДолиСтоимости.Количество
	|	КОНЕЦ КАК Цена
	|ПОМЕСТИТЬ ВТЦены
	|ИЗ
	|	ВТДолиСтоимости КАК ВТДолиСтоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДолиСтоимостиИтог КАК ВТДолиСтоимостиИтог
	|		ПО ВТДолиСтоимости.Калькуляция = ВТДолиСтоимостиИтог.Калькуляция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтПродукция.НомерСтроки,
	|	ВТЦены.Цена
	|ИЗ
	|	ВтПродукция КАК ВтПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЦены КАК ВТЦены
	|		ПО ВтПродукция.Номенклатура = ВТЦены.Номенклатура
	|			И (ВтПродукция.Характеристика = ВТЦены.Характеристика
	|				ИЛИ ВТЦены.ЛюбаяХарактеристика)
	|			И ВтПродукция.Калькуляция = ВТЦены.Калькуляция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВтПродукция.НомерСтроки,
	|	ВозвратныеОтходы.Цена
	|ИЗ
	|	ВтПродукция КАК ВтПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.ВозвратныеОтходы КАК ВозвратныеОтходы
	|		ПО ВтПродукция.Калькуляция = ВозвратныеОтходы.Ссылка
	|			И ВтПродукция.Номенклатура = ВозвратныеОтходы.Номенклатура
	|			И (ВтПродукция.Характеристика = ВозвратныеОтходы.Характеристика
	|				ИЛИ ВозвратныеОтходы.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|ГДЕ
	|	ВтПродукция.ТипСтоимости = ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Фиксированная)
	|	И ВтПродукция.Калькуляция.ОбъектКалькуляции <> ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.Изделие)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВтПродукция.НомерСтроки";

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Коллекция", Коллекция.Выгрузить(МассивСтрок));
	
	Если ПараметрыКоллекции.Свойство("Распоряжение") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Распоряжение", "Строки." + ПараметрыКоллекции.Распоряжение);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Распоряжение", "ЗНАЧЕНИЕ(Документ.МаршрутныйЛистПроизводства.ПустаяСсылка)");
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("Спецификация") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Спецификация", "Строки." + ПараметрыКоллекции.Спецификация);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Спецификация", "ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)");
	КонецЕсли;
	
	Если ПараметрыКоллекции.Свойство("ТипСтоимости") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТипСтоимости", "Строки." + ПараметрыКоллекции.ТипСтоимости);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТипСтоимости", "ЗНАЧЕНИЕ(Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается)");
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	СтруктураОтбора = Новый Структура("НомерСтроки");
	
	Выборка = Результат.Выбрать();
	
	Если МассивСтрок = Неопределено Тогда
		ТабличнаяЧасть = Коллекция;
	Иначе
		ТабличнаяЧасть = МассивСтрок;
	КонецЕсли;
	
	Для Каждого Строка Из ТабличнаяЧасть Цикл
		
		Если Не ЗначениеЗаполнено(Строка.Калькуляция) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураОтбора.НомерСтроки = Строка.НомерСтроки;
		Если Выборка.НайтиСледующий(СтруктураОтбора) Тогда
			Строка.Цена = Выборка.Цена;
			Строка.Сумма = Выборка.Цена * Строка.Количество;
			
			Если ПараметрыКоллекции.Свойство("ОчищатьВидЦены") Тогда
				Строка.ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
			КонецЕсли;
			
		Иначе
			Строка.Цена = 0;
			Строка.Сумма = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет массивы элементов формы, зависимых от способа оформления документа.
//
// Параметры:
//	ПризнакДетализации - Булево - Признак детализации статей калькуляции документа
//	МассивВсехЭлементов - Массив - Массив всех элементов, зависимых от признака детализации
//	МассивЭлементов - Массив - Массив элементов детализации
//
Процедура ИменаРеквизитовПоПризнакуДетализации(ПризнакДетализации, МассивВсехЭлементов, МассивЭлементов) Экспорт
	
	МассивВсехЭлементов = Новый Массив;
	МассивВсехЭлементов.Добавить("СтраницаВозвратныеОтходы");
	МассивВсехЭлементов.Добавить("СтраницаМатериалыИУслуги");
	МассивВсехЭлементов.Добавить("СтраницаТрудозатраты");
	МассивВсехЭлементов.Добавить("СтраницаСтатьиРасходов");
	МассивВсехЭлементов.Добавить("ГруппаИтогиПоСтатье");
	
	МассивЭлементов = Новый Массив;
	
	Если ПризнакДетализации Тогда
		МассивЭлементов.Добавить("СтраницаВозвратныеОтходы");
		МассивЭлементов.Добавить("СтраницаМатериалыИУслуги");
		МассивЭлементов.Добавить("СтраницаТрудозатраты");
		МассивЭлементов.Добавить("СтраницаСтатьиРасходов");
		МассивЭлементов.Добавить("ГруппаИтогиПоСтатье");
	Иначе
	КонецЕсли;
	
КонецПроцедуры

// Заполняет представления продукции в табличной части и реквизитах формы.
//
// Параметры:
//	ДействиеКалькуляции - ДанныеФормыКоллекция - Коллекция, в которой устанавливаются представления
//	ОбъектКалькуляции - Перечисления.ОбъектыКалькуляции - Объект калькуляции плановой калькуляции
//	НесколькоОбъектов - Булево - Признак использования нескольких объектов калькуляции
//	ПредставлениеОбщее - Строка - Текст надписи при использовании нескольких объектов калькуляции
//	ЗаголовокПереключателя - Строка - Текст надписи переключателя одного или нескольких объектов калькуляции
//
Процедура ЗаполнитьПредставленияОбъектовКалькуляции(ДействиеКалькуляции, ОбъектКалькуляции, НесколькоОбъектов = Неопределено, ПредставлениеОбщее = "", ЗаголовокПереключателя = "") Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДействиеКалькуляции.Объект КАК Объект,
	|	ДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ДействиеКалькуляции.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТДействиеКалькуляции
	|ИЗ
	|	&ДействиеКалькуляции КАК ДействиеКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДействиеКалькуляции.Объект КАК Объект,
	|	ВТДействиеКалькуляции.НомерСтроки КАК НомерСтроки,
	|	РесурсныеСпецификацииВыходныеИзделия.НомерСтроки КАК НомерСтрокиИзделия,
	|	РесурсныеСпецификацииВыходныеИзделия.ДоляСтоимости КАК ДоляСтоимости,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.Представление КАК НоменклатураПредставление,
	|	РесурсныеСпецификацииВыходныеИзделия.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	"""" КАК Номер,
	|	"""" КАК Дата,
	|	РесурсныеСпецификацииВыходныеИзделия.Ссылка КАК Спецификация
	|ИЗ
	|	ВТДействиеКалькуляции КАК ВТДействиеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РесурсныеСпецификацииВыходныеИзделия
	|		ПО ВТДействиеКалькуляции.Объект = РесурсныеСпецификацииВыходныеИзделия.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТДействиеКалькуляции.Объект,
	|	ВТДействиеКалькуляции.НомерСтроки,
	|	ЗаказНаПроизводствоПродукция.НомерСтроки,
	|	ЗаказНаПроизводствоВыходныеИзделия.ДоляСтоимости,
	|	ЗаказНаПроизводствоПродукция.Номенклатура.Представление,
	|	ЗаказНаПроизводствоПродукция.Характеристика.Представление,
	|	ЗаказНаПроизводствоПродукция.Ссылка.Номер,
	|	ЗаказНаПроизводствоПродукция.Ссылка.Дата,
	|	ЗаказНаПроизводствоПродукция.Спецификация
	|ИЗ
	|	ВТДействиеКалькуляции КАК ВТДействиеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ВыходныеИзделия КАК ЗаказНаПроизводствоВыходныеИзделия
	|			ПО ЗаказНаПроизводствоПродукция.Ссылка = ЗаказНаПроизводствоВыходныеИзделия.Ссылка
	|				И ЗаказНаПроизводствоПродукция.КлючСвязи = ЗаказНаПроизводствоВыходныеИзделия.КлючСвязиПродукция
	|				И Не ЗаказНаПроизводствоВыходныеИзделия.ПроизводитсяВПроцессе
	|		ПО ВТДействиеКалькуляции.Объект = ЗаказНаПроизводствоПродукция.Ссылка
	|			И ВТДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство = ЗаказНаПроизводствоПродукция.КодСтроки";
	
	Запрос.УстановитьПараметр("ДействиеКалькуляции", ДействиеКалькуляции.Выгрузить());
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		ДанныеОбъектовКалькуляции = Результат.Выгрузить();
		СтруктураНомерСтроки = Новый Структура("НомерСтроки");
		
		ШаблонПредставления = НСтр("ru='Изделия: %1';uk='Вироби: %1'");
		ПараметрыПредметаИсчисления = НСтр("ru='наименование';uk='найменування'") + "," + НСтр("ru='наименования';uk='найменування'") + "," + НСтр("ru='наименований';uk='найменувань'");
		
		Для Каждого Строка Из ДействиеКалькуляции Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураНомерСтроки, Строка);
			
			НайденныеСтроки = ДанныеОбъектовКалькуляции.НайтиСтроки(СтруктураНомерСтроки);
			
			Если НайденныеСтроки.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаИзделие = НайденныеСтроки[0];
			
			Если НайденныеСтроки.Количество() = 1 Тогда
				Строка.СведенияОбИзделиях = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					СтрокаИзделие.НоменклатураПредставление,
					СтрокаИзделие.ХарактеристикаПредставление);
			Иначе
				
				Строка.СведенияОбИзделиях = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонПредставления,
					СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(НайденныеСтроки.Количество(), ПараметрыПредметаИсчисления));
				
			КонецЕсли;
			
			Если ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
				
				СтруктураШапки = Новый Структура("Номер, Дата", СтрокаИзделие.Номер, СтрокаИзделие.Дата);
				
				ПредставлениеПозицииЗаказа = НСтр("ru='№%1 от %2 (строка %3)';uk='№%1 від %2 (рядок %3)'");
				
				Строка.ПредставлениеПозицииЗаказа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ПредставлениеПозицииЗаказа,
					ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(СтрокаИзделие.Номер, Ложь, Ложь),
					Формат(СтрокаИзделие.Дата, "ДЛФ=DD"),
					СтрокаИзделие.НомерСтрокиИзделия);
				
			КонецЕсли;
			
			Если Строка.Свойство("Спецификация") Тогда
				Строка.Спецификация = СтрокаИзделие.Спецификация;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НесколькоОбъектов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоОбъектов = ДействиеКалькуляции.Количество();
	
	Если ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
		ЗаголовокПереключателя = НСтр("ru='Несколько изделий';uk='Кілька виробів'");
		ШаблонПредставления = НСтр("ru='всего: %1';uk='всього: %1'");
	ИначеЕсли ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
		ЗаголовокПереключателя = НСтр("ru='Несколько спецификаций';uk='Кілька специфікацій'");
		ШаблонПредставления = НСтр("ru='всего: %1';uk='всього: %1'");
	ИначеЕсли ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		ЗаголовокПереключателя = НСтр("ru='Несколько позиций';uk='Кілька позицій'");
		ШаблонПредставления = НСтр("ru='всего: %1';uk='всього: %1'");
	КонецЕсли;
	
	Если КоличествоОбъектов = 0 И НесколькоОбъектов Тогда
		ПредставлениеОбщее = НСтр("ru='выбрать';uk='вибрати'");
	ИначеЕсли КоличествоОбъектов < 2 И Не НесколькоОбъектов Тогда
		ПредставлениеОбщее = "";
	Иначе
		ПредставлениеОбщее = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонПредставления,
			КоличествоОбъектов);
	КонецЕсли;
	
КонецПроцедуры

// Возвращает процент отклонения от норматива.
//
// Параметры:
//	Отклонение - Число - Значение отклонения
//	Норматив - Число - Значение норматива
//
// Возвращаемое значение:
//	Число - Процент отклонения
//
Функция ПроцентОтклонения(Отклонение, Норматив) Экспорт
	
	Если Не ЗначениеЗаполнено(Отклонение)
		Или Не ЗначениеЗаполнено(Норматив) Тогда
		Возврат 0;
	КонецЕсли;
	
	Возврат Отклонение * 100 / Норматив;
	
КонецФункции

// Возвращает представление позиции заказа на производство.
//
// Параметры:
//	Заказ - ДокументСсылка.ЗаказНаПроизводство - заказ на производство
//	НомерСтроки - Число - Номер строки табличной части Продукция
//	Продукция - СправочникСсылка.Номенклатура - Продукция по строке заказа
//	ПроцентВыполнения - Число - Процент выполнения по стоимости
//
// Возвращаемое значение:
//	Строка - Представление
//
Функция ПредставлениеЗаказаНапроизводство(Заказ, НомерСтроки, Продукция = Неопределено, ПроцентВыполнения = Неопределено) Экспорт
	
	Если ПроцентВыполнения <> Неопределено И Продукция <> Неопределено Тогда
		
		ШаблонПредставления = НСтр("ru='№%1 от %2 (строка %3), %4, выполнено %5%';uk='№%1 від %2 (рядок %3), %4, виконано %5%'");
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
		ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Заказ.Номер, Ложь, Ложь),
		Формат(Заказ.Дата, "ДЛФ=D"),
		НомерСтроки,
		Продукция,
		Формат(ПроцентВыполнения, "ЧЦ=15; ЧДЦ=1; ЧН=0"));
		
	Иначе
		
		ШаблонПредставления = НСтр("ru='№%1 от %2 (строка %3)';uk='№%1 від %2 (рядок %3)'");
		
		Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
		ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Заказ.Номер, Ложь, Ложь),
		Формат(Заказ.Дата, "ДЛФ=D"),
		НомерСтроки);
		
	КонецЕсли;
	
КонецФункции

// Возвращает представление этапа заказа на производство.
//
// Параметры:
//	Этап - СправочникСсылка.ЭтапПроизводства - Этап
//	Состояние - Булево - Состояние этапа (выполнен, не выполнен)
//
// Возвращаемое значение:
//	Строка - Представление
//
Функция ПредставлениеЭтапаПроизводства(Этап, Состояние = Неопределено) Экспорт
	
	Если Состояние = Неопределено Тогда
		Возврат Этап;
	Иначе
		Если Состояние Тогда
			Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 (выполнен)';uk='%1 (виконано)'"),
				Этап);
		Иначе
			Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='%1 (не выполнен)';uk='%1 (не виконано)'"),
				Этап);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Возвращает представление плановой калькуляции
//
// Параметры:
//	Калькуляция - ДокументСсылка.ПлановаяКалькуляция - документ Плановая калькуляция
//
// Возвращаемое значение:
//	Строка - Представление
//
Функция ПредставлениеПлановойКалькуляции(Калькуляция) Экспорт
	
	Шаблон = НСтр("ru='№%1 от %2';uk='№%1 від %2'");
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Калькуляция, "Номер, Дата");
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
		ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Реквизиты.Номер),
		Формат(Реквизиты.Дата, "ДФ=dd.MM.yyyy"));
		
	Возврат Представление;
	
КонецФункции

// Возвращает представление объекта калькуляции
//
// Параметры:
//	Калькуляция - ДокументСсылка.ПлановаяКалькуляция - документ Плановая калькуляция
//	ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство - заказ (объект калькуляции)
//	НомерСтроки - Число - номер строки заказа (объект калькуляции)
//	Спецификация - СправочникСсылка.РесурсныеСпецификации - спецификация (объект калькуляции)
//
// Возвращаемое значение:
//	Строка - Представление
//
Функция ПредставлениеОбъектаКалькуляции(Калькуляция, ЗаказНаПроизводство, НомерСтроки, Спецификация) Экспорт
	
	ШаблонЗаказ = НСтр("ru='Заказ №%1 от %2 (строка %3)';uk='Замовлення №%1 від %2 (рядок %3)'");
	ШаблонСпецификация = НСтр("ru='%1 (спецификация)';uk='%1 (специфікація)'");
	ШаблонИзделие = НСтр("ru='Изделие';uk='Виріб'");
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Калькуляция, "ОбъектКалькуляции");
	
	Представление = "";
	
	Если Реквизиты.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		
		РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаказНаПроизводство, "Номер, Дата");
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаказ,
			ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыЗаказа.Номер),
			Формат(РеквизитыЗаказа.Дата, "ДФ=dd.MM.yyyy"),
			НомерСтроки);
		
	ИначеЕсли Реквизиты.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСпецификация,
			Спецификация);
		
	ИначеЕсли Реквизиты.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
		
		Представление = ШаблонИзделие;
		
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Возвращает представление калькуляции и объекта калькуляции
//
// Параметры:
//	Калькуляция - ДокументСсылка.ПлановаяКалькуляция - документ Плановая калькуляция
//	ОбъектКалькуляции - ПеречислениеСсылка.ОбъектыКалькуляции - объект калькуляции
//	Спецификация - СправочникСсылка.РесурсныеСпецификации - спецификация (объект калькуляции)
//	Номенклатура - СправочникСсылка.Номенклатура - изделие
//
// Возвращаемое значение:
//	Строка - Представление
//
Функция ПредставлениеКалькуляцииИОбъектаКалькуляции(Калькуляция, ОбъектКалькуляции, Спецификация, Номенклатура) Экспорт
	
	ШаблонЗаказ        = НСтр("ru='№%1 от %2 для заказа';uk='№%1 від %2 для замовлення'");
	ШаблонСпецификация = НСтр("ru='№%1 от %2 для спецификации (%3)';uk='№%1 від %2 для специфікації (%3)'");
	ШаблонИзделие      = НСтр("ru='№%1 от %2 для изделия (%3)';uk='№%1 від %2 для виробу (%3)'");
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Калькуляция, "Номер, Дата");
	
	Представление = "";
	
	Если ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаказ,
			ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Реквизиты.Номер),
			Формат(Реквизиты.Дата, "ДФ=dd.MM.yyyy"));
		
	ИначеЕсли ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСпецификация,
			ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Реквизиты.Номер),
			Формат(Реквизиты.Дата, "ДФ=dd.MM.yyyy"),
			Спецификация);
		
	ИначеЕсли ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
		
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИзделие,
			ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(Реквизиты.Номер),
			Формат(Реквизиты.Дата, "ДФ=dd.MM.yyyy"),
			Номенклатура);
		
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

// Возвращает текст запроса для получения подходящей калькуляции.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаДействующиеКалькуляции() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПлановаяКалькуляция.Ссылка КАК Калькуляция,
	|	ПлановаяКалькуляция.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ПлановаяКалькуляцияДействиеКалькуляции.Объект ССЫЛКА Документ.ЗаказНаПроизводство
	|			ТОГДА ПлановаяКалькуляцияДействиеКалькуляции.Объект
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказНаПроизводство,
	|	ПлановаяКалькуляцияДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ВЫБОР
	|		КОГДА ПлановаяКалькуляцияДействиеКалькуляции.Объект ССЫЛКА Справочник.РесурсныеСпецификации
	|			ТОГДА ПлановаяКалькуляцияДействиеКалькуляции.Объект
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|	КОНЕЦ КАК Спецификация,
	|	ВЫБОР
	|		КОГДА ПлановаяКалькуляцияДействиеКалькуляции.Объект ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ПлановаяКалькуляцияДействиеКалькуляции.Объект
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	ПлановаяКалькуляцияДействиеКалькуляции.Характеристика КАК Характеристика,
	|	ПлановаяКалькуляция.ДатаОкончанияДействия КАК ПериодЗавершения,
	|	ПлановаяКалькуляция.ДатаНачалаДействия КАК Период,
	|	ПлановаяКалькуляция.Дата
	|ПОМЕСТИТЬ ВТВсеКалькуляции
	|ИЗ
	|	Документ.ПлановаяКалькуляция.ДействиеКалькуляции КАК ПлановаяКалькуляцияДействиеКалькуляции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция КАК ПлановаяКалькуляция
	|		ПО ПлановаяКалькуляцияДействиеКалькуляции.Ссылка = ПлановаяКалькуляция.Ссылка
	|ГДЕ
	|	ПлановаяКалькуляция.Проведен
	|	И ПлановаяКалькуляция.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПлановыхКалькуляций.Действует)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ЗаказНаПроизводство,
	|	КодСтрокиЗаказаНаПроизводство,
	|	Спецификация,
	|	Номенклатура,
	|	Характеристика,
	|	ПериодЗавершения,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.ПериодЗавершения,
	|	ВТВсеКалькуляции.Период,
	|	МАКСИМУМ(ВТВсеКалькуляции.Дата) КАК Дата
	|ПОМЕСТИТЬ ВТМаксимальныеДатыКалькуляций
	|ИЗ
	|	ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Период,
	|	ВТВсеКалькуляции.ПериодЗавершения,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период) КАК Период,
	|	10 КАК ПриоритетЗаказНаПроизводство,
	|	0 КАК ПриоритетСпецификация,
	|	0 КАК ПриоритетИзделие,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура КАК Номенклатура,
	|	ВТВсеКалькуляции.Характеристика КАК Характеристика,
	|	ВТВсеКалькуляции.Спецификация КАК Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	1 КАК ВыходноеИзделиеДоляСтоимости,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация КАК ВыходноеИзделиеСпецификация,
	|	0 КАК ВыходноеИзделиеНомерСтроки,
	|	1 КАК Кратность
	|ПОМЕСТИТЬ ПриоритетыКалькуляций
	|ИЗ
	|	ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО ВТВсеКалькуляции.ЗаказНаПроизводство = СпецификацияВыходныеИзделия.ЗаказНаПроизводство
	|			И ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство = СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство
	|			И ВТВсеКалькуляции.Организация = СпецификацияВыходныеИзделия.Организация
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|	И ВТВсеКалькуляции.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВТВсеКалькуляции.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.Организация,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	9,
	|	0,
	|	0,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	1,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	0,
	|	1
	|ИЗ
	|	ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО ВТВсеКалькуляции.ЗаказНаПроизводство = СпецификацияВыходныеИзделия.ЗаказНаПроизводство
	|			И ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство = СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство
	|ГДЕ
	|	ВТВсеКалькуляции.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|	И ВТВсеКалькуляции.ЗаказНаПроизводство <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.Организация,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	0,
	|	10,
	|	0,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	1,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	0,
	|	ВЫРАЗИТЬ(СпецификацияВыходныеИзделия.КоличествоПоЗаказу / СпецификацияВыходныеИзделия.КоличествоПоСпецификации КАК ЧИСЛО(15, 0))
	|ИЗ
	|	ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО ВТВсеКалькуляции.Спецификация = СпецификацияВыходныеИзделия.Спецификация
	|			И ВТВсеКалькуляции.Организация = СпецификацияВыходныеИзделия.Организация
	|ГДЕ
	|	ВТВсеКалькуляции.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВТВсеКалькуляции.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	ВЫРАЗИТЬ(СпецификацияВыходныеИзделия.КоличествоПоЗаказу / СпецификацияВыходныеИзделия.КоличествоПоСпецификации КАК ЧИСЛО(15, 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	0,
	|	9,
	|	0,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	1,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	0,
	|	ВЫРАЗИТЬ(СпецификацияВыходныеИзделия.КоличествоПоЗаказу / СпецификацияВыходныеИзделия.КоличествоПоСпецификации КАК ЧИСЛО(15, 0))
	|ИЗ
	|	ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО ВТВсеКалькуляции.Спецификация = СпецификацияВыходныеИзделия.Спецификация
	|ГДЕ
	|	ВТВсеКалькуляции.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВТВсеКалькуляции.Спецификация <> ЗНАЧЕНИЕ(Справочник.РесурсныеСпецификации.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	ВЫРАЗИТЬ(СпецификацияВыходныеИзделия.КоличествоПоЗаказу / СпецификацияВыходныеИзделия.КоличествоПоСпецификации КАК ЧИСЛО(15, 0))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	0,
	|	0,
	|	8,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ПО СпецификацияВыходныеИзделия.Номенклатура = ВТВсеКалькуляции.Номенклатура
	|			И СпецификацияВыходныеИзделия.Характеристика = ВТВсеКалькуляции.Характеристика
	|			И СпецификацияВыходныеИзделия.Организация = ВТВсеКалькуляции.Организация
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|	И ВТВсеКалькуляции.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Характеристика,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	0,
	|	0,
	|	7,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ПО СпецификацияВыходныеИзделия.Номенклатура = ВТВсеКалькуляции.Номенклатура
	|			И СпецификацияВыходныеИзделия.Характеристика = ВТВсеКалькуляции.Характеристика
	|ГДЕ
	|	ВТВсеКалькуляции.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Характеристика,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	0,
	|	0,
	|	6,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ПО СпецификацияВыходныеИзделия.Номенклатура = ВТВсеКалькуляции.Номенклатура
	|			И СпецификацияВыходныеИзделия.Организация = ВТВсеКалькуляции.Организация
	|ГДЕ
	|	ВТВсеКалькуляции.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|	И ВТВсеКалькуляции.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Характеристика,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Период),
	|	0,
	|	0,
	|	5,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|ИЗ
	|	СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ПО СпецификацияВыходныеИзделия.Номенклатура = ВТВсеКалькуляции.Номенклатура
	|ГДЕ
	|	ВТВсеКалькуляции.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|	И ВТВсеКалькуляции.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ВЫБОР
	|			КОГДА ВТВсеКалькуляции.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА СпецификацияВыходныеИзделия.ДатаПотребности МЕЖДУ ВТВсеКалькуляции.Период И ВТВсеКалькуляции.ПериодЗавершения
	|			ИНАЧЕ СпецификацияВыходныеИзделия.ДатаПотребности > ВТВсеКалькуляции.Период
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Характеристика,
	|	СпецификацияВыходныеИзделия.ДоляСтоимости,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.ЗаказНаПроизводство,
	|	СпецификацияВыходныеИзделия.КодСтрокиЗаказаНаПроизводство,
	|	СпецификацияВыходныеИзделия.Спецификация,
	|	СпецификацияВыходныеИзделия.НомерСтроки,
	|	СпецификацияВыходныеИзделия.КоличествоПоЗаказу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаПроизводство,
	|	КодСтрокиЗаказаНаПроизводство,
	|	Спецификация,
	|	Номенклатура,
	|	Характеристика,
	|	ВыходноеИзделиеНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПриоритетыКалькуляций.ПриоритетСпецификация) КАК ПриоритетСпецификация,
	|	МАКСИМУМ(ПриоритетыКалькуляций.ПриоритетЗаказНаПроизводство) КАК ПриоритетЗаказНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация
	|ПОМЕСТИТЬ ПриоритКалькуляцииДляСпецификации
	|ИЗ
	|	ПриоритетыКалькуляций КАК ПриоритетыКалькуляций
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриоритетыКалькуляций.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеЗаказНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеСпецификация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ПриоритетСпецификация,
	|	ПриоритетЗаказНаПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ПриоритетыКалькуляций.ПриоритетЗаказНаПроизводство) КАК ПриоритетЗаказНаПроизводство,
	|	МАКСИМУМ(ПриоритетыКалькуляций.ПриоритетСпецификация) КАК ПриоритетСпецификация,
	|	МАКСИМУМ(ПриоритетыКалькуляций.ПриоритетИзделие) КАК ПриоритетИзделие,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеДоляСтоимости,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки
	|ПОМЕСТИТЬ ПериодПриоритетнойКалькуляции
	|ИЗ
	|	ПриоритетыКалькуляций КАК ПриоритетыКалькуляций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритКалькуляцииДляСпецификации КАК ПриоритКалькуляцииДляСпецификации
	|		ПО ПриоритетыКалькуляций.ВыходноеИзделиеЗаказНаПроизводство = ПриоритКалькуляцииДляСпецификации.ВыходноеИзделиеЗаказНаПроизводство
	|			И ПриоритетыКалькуляций.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство = ПриоритКалькуляцииДляСпецификации.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И (ВЫБОР
	|				КОГДА ПриоритКалькуляцииДляСпецификации.ПриоритетЗаказНаПроизводство > 0
	|					ТОГДА ПриоритетыКалькуляций.ПриоритетЗаказНаПроизводство > 0
	|				КОГДА ПриоритКалькуляцииДляСпецификации.ПриоритетСпецификация > 0
	|					ТОГДА ПриоритетыКалькуляций.ПриоритетСпецификация > 0
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|			И ПриоритетыКалькуляций.ВыходноеИзделиеСпецификация = ПриоритКалькуляцииДляСпецификации.ВыходноеИзделиеСпецификация
	|
	|СГРУППИРОВАТЬ ПО
	|	ПриоритетыКалькуляций.ВыходноеИзделиеДоляСтоимости,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеЗаказНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеСпецификация,
	|	ПриоритетыКалькуляций.ВыходноеИзделиеНомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТВсеКалькуляции.Калькуляция) КАК Калькуляция,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.ПериодЗавершения,
	|	ВТВсеКалькуляции.Период
	|ПОМЕСТИТЬ ВТУникальныеКалькуляции
	|ИЗ
	|	ВТМаксимальныеДатыКалькуляций КАК ВТМаксимальныеДатыКалькуляций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВсеКалькуляции КАК ВТВсеКалькуляции
	|		ПО ВТМаксимальныеДатыКалькуляций.Организация = ВТВсеКалькуляции.Организация
	|			И ВТМаксимальныеДатыКалькуляций.ЗаказНаПроизводство = ВТВсеКалькуляции.ЗаказНаПроизводство
	|			И ВТМаксимальныеДатыКалькуляций.КодСтрокиЗаказаНаПроизводство = ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство
	|			И ВТМаксимальныеДатыКалькуляций.Спецификация = ВТВсеКалькуляции.Спецификация
	|			И ВТМаксимальныеДатыКалькуляций.Номенклатура = ВТВсеКалькуляции.Номенклатура
	|			И ВТМаксимальныеДатыКалькуляций.Характеристика = ВТВсеКалькуляции.Характеристика
	|			И ВТМаксимальныеДатыКалькуляций.ПериодЗавершения = ВТВсеКалькуляции.ПериодЗавершения
	|			И ВТМаксимальныеДатыКалькуляций.Период = ВТВсеКалькуляции.Период
	|			И ВТМаксимальныеДатыКалькуляций.Дата = ВТВсеКалькуляции.Дата
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВсеКалькуляции.Период,
	|	ВТВсеКалькуляции.ПериодЗавершения,
	|	ВТВсеКалькуляции.Характеристика,
	|	ВТВсеКалькуляции.Номенклатура,
	|	ВТВсеКалькуляции.Спецификация,
	|	ВТВсеКалькуляции.ЗаказНаПроизводство,
	|	ВТВсеКалькуляции.Организация,
	|	ВТВсеКалькуляции.КодСтрокиЗаказаНаПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТУникальныеКалькуляции.Калькуляция КАК Калькуляция,
	|	ВТУникальныеКалькуляции.Калькуляция.ВидЦены КАК ВидЦены,
	|	ВТУникальныеКалькуляции.Калькуляция.ДетализироватьСтатьиКалькуляции КАК ДетализироватьСтатьиКалькуляции,
	|	ПериодПриоритетнойКалькуляции.ВыходноеИзделиеДоляСтоимости,
	|	ПериодПриоритетнойКалькуляции.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ПериодПриоритетнойКалькуляции.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ПериодПриоритетнойКалькуляции.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ПериодПриоритетнойКалькуляции.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки,
	|	ВЫБОР
	|		КОГДА ВТУникальныеКалькуляции.Калькуляция.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.Изделие)
	|			ТОГДА ВЫРАЗИТЬ(ПриоритетыКалькуляций.Кратность / ВТУникальныеКалькуляции.Калькуляция.КалькуляционнаяЕдиница КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ ПриоритетыКалькуляций.Кратность
	|	КОНЕЦ КАК Кратность
	|ПОМЕСТИТЬ ДействующаяКалькуляция
	|ИЗ
	|	ПериодПриоритетнойКалькуляции КАК ПериодПриоритетнойКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПриоритетыКалькуляций КАК ПриоритетыКалькуляций
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТУникальныеКалькуляции КАК ВТУникальныеКалькуляции
	|			ПО ПриоритетыКалькуляций.Период = ВТУникальныеКалькуляции.Период
	|				И ПриоритетыКалькуляций.Организация = ВТУникальныеКалькуляции.Организация
	|				И ПриоритетыКалькуляций.Номенклатура = ВТУникальныеКалькуляции.Номенклатура
	|				И ПриоритетыКалькуляций.Характеристика = ВТУникальныеКалькуляции.Характеристика
	|				И ПриоритетыКалькуляций.Спецификация = ВТУникальныеКалькуляции.Спецификация
	|				И ПриоритетыКалькуляций.ЗаказНаПроизводство = ВТУникальныеКалькуляции.ЗаказНаПроизводство
	|				И ПриоритетыКалькуляций.КодСтрокиЗаказаНаПроизводство = ВТУникальныеКалькуляции.КодСтрокиЗаказаНаПроизводство
	|		ПО (ВЫБОР
	|				КОГДА ПериодПриоритетнойКалькуляции.ПриоритетЗаказНаПроизводство > 0
	|					ТОГДА ПериодПриоритетнойКалькуляции.ПриоритетЗаказНаПроизводство = ПриоритетыКалькуляций.ПриоритетЗаказНаПроизводство
	|				КОГДА ПериодПриоритетнойКалькуляции.ПриоритетСпецификация > 0
	|					ТОГДА ПериодПриоритетнойКалькуляции.ПриоритетСпецификация = ПриоритетыКалькуляций.ПриоритетСпецификация
	|				ИНАЧЕ ПериодПриоритетнойКалькуляции.ПриоритетИзделие = ПриоритетыКалькуляций.ПриоритетИзделие
	|			КОНЕЦ)
	|			И ПериодПриоритетнойКалькуляции.ВыходноеИзделиеЗаказНаПроизводство = ПриоритетыКалькуляций.ВыходноеИзделиеЗаказНаПроизводство
	|			И ПериодПриоритетнойКалькуляции.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство = ПриоритетыКалькуляций.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И ПериодПриоритетнойКалькуляции.ВыходноеИзделиеСпецификация = ПриоритетыКалькуляций.ВыходноеИзделиеСпецификация
	|			И ПериодПриоритетнойКалькуляции.ВыходноеИзделиеНомерСтроки = ПриоритетыКалькуляций.ВыходноеИзделиеНомерСтроки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки";
	
	ТекстЗапроса = ТекстЗапроса +
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для получения расхождений калькуляции и спецификации.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаДанныеКалькуляцийИСпецификации() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ПлановаяКалькуляцияМатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ПлановаяКалькуляцияМатериалыИУслуги.ПрименениеМатериала КАК Описание,
	|	ПлановаяКалькуляцияМатериалыИУслуги.Номенклатура КАК Номенклатура,
	|	ПлановаяКалькуляцияМатериалыИУслуги.Характеристика КАК Характеристика,
	|	ПлановаяКалькуляцияМатериалыИУслуги.Количество * ДействующаяКалькуляция.Кратность КАК Количество,
	|	ПлановаяКалькуляцияМатериалыИУслуги.Цена,
	|	ПлановаяКалькуляцияМатериалыИУслуги.Сумма * ДействующаяКалькуляция.Кратность КАК Сумма,
	|	ПлановаяКалькуляцияМатериалыИУслуги.ВидЦены,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки,
	|	ПлановаяКалькуляцияМатериалыИУслуги.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ПОМЕСТИТЬ КалькуляцияМатериальные
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.МатериалыИУслуги КАК ПлановаяКалькуляцияМатериалыИУслуги
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияМатериалыИУслуги.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация,
	|	ПлановаяКалькуляцияВозвратныеОтходы.СтатьяКалькуляции,
	|	ПлановаяКалькуляцияВозвратныеОтходы.ОписаниеИзделия,
	|	ПлановаяКалькуляцияВозвратныеОтходы.Номенклатура,
	|	ПлановаяКалькуляцияВозвратныеОтходы.Характеристика,
	|	ПлановаяКалькуляцияВозвратныеОтходы.Количество * ДействующаяКалькуляция.Кратность,
	|	ПлановаяКалькуляцияВозвратныеОтходы.Цена,
	|	ПлановаяКалькуляцияВозвратныеОтходы.Сумма * ДействующаяКалькуляция.Кратность,
	|	ПлановаяКалькуляцияВозвратныеОтходы.ВидЦены,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки,
	|	ПлановаяКалькуляцияВозвратныеОтходы.Номенклатура.ЕдиницаИзмерения
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.ВозвратныеОтходы КАК ПлановаяКалькуляцияВозвратныеОтходы
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияВозвратныеОтходы.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки,
	|	СтатьяКалькуляции,
	|	Номенклатура,
	|	Характеристика,
	|	Описание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ПлановаяКалькуляцияТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ПлановаяКалькуляцияТрудозатраты.ВидРабот КАК ВидРабот,
	|	ПлановаяКалькуляцияТрудозатраты.ВидРабот.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПлановаяКалькуляцияТрудозатраты.НазначениеРабот КАК Описание,
	|	ПлановаяКалькуляцияТрудозатраты.Расценка,
	|	ПлановаяКалькуляцияТрудозатраты.Сумма * ДействующаяКалькуляция.Кратность КАК Сумма,
	|	ПлановаяКалькуляцияТрудозатраты.Количество * ДействующаяКалькуляция.Кратность КАК Количество,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки
	|ПОМЕСТИТЬ КалькуляцияТрудозатраты
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.Трудозатраты КАК ПлановаяКалькуляцияТрудозатраты
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияТрудозатраты.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки,
	|	СтатьяКалькуляции,
	|	ВидРабот,
	|	Описание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ПлановаяКалькуляцияСтатьиРасходов.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ПлановаяКалькуляцияСтатьиРасходов.Сумма * ДействующаяКалькуляция.Кратность КАК СуммаКалькуляция,
	|	ПлановаяКалькуляцияСтатьиРасходов.СтатьяРасходов КАК Затрата,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
	|	0 КАК СуммаСпецификация,
	|	0 КАК КоличествоКалькуляция,
	|	0 КАК КоличествоСпецификация,
	|	"""" КАК Описание,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки
	|ПОМЕСТИТЬ КалькуляцияСтатьиРасходов
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.СтатьиРасходов КАК ПлановаяКалькуляцияСтатьиРасходов
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияСтатьиРасходов.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки,
	|	СтатьяКалькуляции,
	|	Затрата,
	|	Характеристика,
	|	Описание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ПлановаяКалькуляцияСтатьиКалькуляции.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ПлановаяКалькуляцияСтатьиКалькуляции.Сумма * ДействующаяКалькуляция.Кратность КАК СуммаКалькуляция,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
	|	0 КАК СуммаСпецификация,
	|	0 КАК КоличествоКалькуляция,
	|	0 КАК КоличествоСпецификация,
	|	"""" КАК Описание,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Затрата,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки
	|ПОМЕСТИТЬ КалькуляцияСтатьиКалькуляции
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.СтатьиКалькуляции КАК ПлановаяКалькуляцияСтатьиКалькуляции
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияСтатьиКалькуляции.Ссылка
	|ГДЕ
	|	ПлановаяКалькуляцияСтатьиКалькуляции.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ФиксированнымЗначением))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки,
	|	СтатьяКалькуляции,
	|	Затрата,
	|	Характеристика,
	|	Описание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСпецификацияМатериалы.Номенклатура КАК Номенклатура,
	|	ВТСпецификацияМатериалы.Характеристика КАК Характеристика,
	|	ВТСпецификацияМатериалы.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТСпецификацияМатериалы.Описание КАК Описание,
	|	ВТСпецификацияМатериалы.Количество * ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости КАК Количество,
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция,
	|	ДействующаяКалькуляция.ВидЦены,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки,
	|	ВТСпецификацияМатериалы.НачатьНеРанее,
	|	ВТСпецификацияМатериалы.ЕдиницаИзмерения
	|ПОМЕСТИТЬ СпецификацияМатериальные
	|ИЗ
	|	ВТСпецификацияМатериалы КАК ВТСпецификацияМатериалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ПО ВТСпецификацияМатериалы.КодСтрокиЗаказаНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И ВТСпецификацияМатериалы.ЗаказНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство
	|			И ВТСпецификацияМатериалы.Спецификация = ДействующаяКалькуляция.ВыходноеИзделиеСпецификация
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСпецификацияОтходы.Номенклатура,
	|	ВТСпецификацияОтходы.Характеристика,
	|	ВТСпецификацияОтходы.СтатьяКалькуляции,
	|	ВТСпецификацияОтходы.Описание,
	|	ВТСпецификацияОтходы.Количество * ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.Калькуляция,
	|	ДействующаяКалькуляция.ВидЦены,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки,
	|	ВТСпецификацияОтходы.НачатьНеРанее,
	|	ВТСпецификацияОтходы.ЕдиницаИзмерения
	|ИЗ
	|	ВТСпецификацияОтходы КАК ВТСпецификацияОтходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ПО ВТСпецификацияОтходы.КодСтрокиЗаказаНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И ВТСпецификацияОтходы.ЗаказНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство
	|			И ВТСпецификацияОтходы.Спецификация = ДействующаяКалькуляция.ВыходноеИзделиеСпецификация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки,
	|	СтатьяКалькуляции,
	|	Номенклатура,
	|	Характеристика,
	|	Описание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСпецификацияТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТСпецификацияТрудозатраты.ВидРабот КАК ВидРабот,
	|	ВТСпецификацияТрудозатраты.ЕдиницаИзмерения,
	|	ВТСпецификацияТрудозатраты.Описание КАК Описание,
	|	ВТСпецификацияТрудозатраты.Количество * ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости КАК Количество,
	|	ДействующаяКалькуляция.Калькуляция КАК Калькуляция,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки,
	|	ВТСпецификацияТрудозатраты.НачатьНеРанее
	|ПОМЕСТИТЬ СпецификацияТрудозатраты
	|ИЗ
	|	ВТСпецификацияТрудозатраты КАК ВТСпецификацияТрудозатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ПО ВТСпецификацияТрудозатраты.КодСтрокиЗаказаНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И ВТСпецификацияТрудозатраты.ЗаказНаПроизводство = ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство
	|			И ВТСпецификацияТрудозатраты.Спецификация = ДействующаяКалькуляция.ВыходноеИзделиеСпецификация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки,
	|	СтатьяКалькуляции,
	|	ВидРабот,
	|	Описание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалькуляцияМатериальные.Калькуляция,
	|	КалькуляцияМатериальные.ВыходноеИзделиеДоляСтоимости,
	|	КалькуляцияМатериальные.ВыходноеИзделиеЗаказНаПроизводство,
	|	КалькуляцияМатериальные.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	КалькуляцияМатериальные.СтатьяКалькуляции,
	|	КалькуляцияМатериальные.Номенклатура КАК Номенклатура,
	|	КалькуляцияМатериальные.Характеристика КАК Характеристика,
	|	КалькуляцияМатериальные.Описание,
	|	КалькуляцияМатериальные.ЕдиницаИзмерения,
	|	КалькуляцияМатериальные.ВидЦены КАК ВидЦены,
	|	ВЫБОР
	|		КОГДА КалькуляцияМатериальные.ВидЦены = ЗНАЧЕНИЕ(Справочник.ВидыЦен.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЦенаУказанаВручную,
	|	КалькуляцияМатериальные.Количество КАК КоличествоКалькуляция,
	|	КалькуляцияМатериальные.Цена КАК ЦенаКалькуляция,
	|	КалькуляцияМатериальные.Сумма КАК СуммаКалькуляция,
	|	ЕСТЬNULL(СпецификацияМатериальные.Количество, 0) КАК КоличествоСпецификация,
	|	0 КАК ЦенаСпецификация,
	|	0 КАК СуммаСпецификация,
	|	КалькуляцияМатериальные.ДетализироватьСтатьиКалькуляции,
	|	КалькуляцияМатериальные.ВыходноеИзделиеСпецификация,
	|	КалькуляцияМатериальные.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияМатериальные.НачатьНеРанее, ДАТАВРЕМЯ(1, 1, 1)) КАК НачатьНеРанее
	|ПОМЕСТИТЬ ВТМатериалы
	|ИЗ
	|	КалькуляцияМатериальные КАК КалькуляцияМатериальные
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацияМатериальные КАК СпецификацияМатериальные
	|		ПО КалькуляцияМатериальные.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство = СпецификацияМатериальные.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И КалькуляцияМатериальные.Номенклатура = СпецификацияМатериальные.Номенклатура
	|			И КалькуляцияМатериальные.Характеристика = СпецификацияМатериальные.Характеристика
	|			И КалькуляцияМатериальные.Описание = СпецификацияМатериальные.Описание
	|			И КалькуляцияМатериальные.СтатьяКалькуляции = СпецификацияМатериальные.СтатьяКалькуляции
	|			И КалькуляцияМатериальные.Калькуляция = СпецификацияМатериальные.Калькуляция
	|			И КалькуляцияМатериальные.ВыходноеИзделиеЗаказНаПроизводство = СпецификацияМатериальные.ВыходноеИзделиеЗаказНаПроизводство
	|			И КалькуляцияМатериальные.ВыходноеИзделиеСпецификация = СпецификацияМатериальные.ВыходноеИзделиеСпецификация
	|			И КалькуляцияМатериальные.ВыходноеИзделиеНомерСтроки = СпецификацияМатериальные.ВыходноеИзделиеНомерСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпецификацияМатериальные.Калькуляция,
	|	СпецификацияМатериальные.ВыходноеИзделиеДоляСтоимости,
	|	СпецификацияМатериальные.ВыходноеИзделиеЗаказНаПроизводство,
	|	СпецификацияМатериальные.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СпецификацияМатериальные.СтатьяКалькуляции,
	|	СпецификацияМатериальные.Номенклатура,
	|	СпецификацияМатериальные.Характеристика,
	|	СпецификацияМатериальные.Описание,
	|	СпецификацияМатериальные.ЕдиницаИзмерения,
	|	СпецификацияМатериальные.ВидЦены,
	|	ЛОЖЬ,
	|	0,
	|	0,
	|	0,
	|	СпецификацияМатериальные.Количество,
	|	0,
	|	0,
	|	СпецификацияМатериальные.ДетализироватьСтатьиКалькуляции,
	|	СпецификацияМатериальные.ВыходноеИзделиеСпецификация,
	|	СпецификацияМатериальные.ВыходноеИзделиеНомерСтроки,
	|	СпецификацияМатериальные.НачатьНеРанее
	|ИЗ
	|	СпецификацияМатериальные КАК СпецификацияМатериальные
	|		ЛЕВОЕ СОЕДИНЕНИЕ КалькуляцияМатериальные КАК КалькуляцияМатериальные
	|		ПО (КалькуляцияМатериальные.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство = СпецификацияМатериальные.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство)
	|			И (КалькуляцияМатериальные.Номенклатура = СпецификацияМатериальные.Номенклатура)
	|			И (КалькуляцияМатериальные.Характеристика = СпецификацияМатериальные.Характеристика)
	|			И (КалькуляцияМатериальные.Описание = СпецификацияМатериальные.Описание)
	|			И (КалькуляцияМатериальные.СтатьяКалькуляции = СпецификацияМатериальные.СтатьяКалькуляции)
	|			И (КалькуляцияМатериальные.Калькуляция = СпецификацияМатериальные.Калькуляция)
	|			И СпецификацияМатериальные.ВыходноеИзделиеЗаказНаПроизводство = КалькуляцияМатериальные.ВыходноеИзделиеЗаказНаПроизводство
	|			И СпецификацияМатериальные.ВыходноеИзделиеСпецификация = КалькуляцияМатериальные.ВыходноеИзделиеСпецификация
	|			И СпецификацияМатериальные.ВыходноеИзделиеНомерСтроки = КалькуляцияМатериальные.ВыходноеИзделиеНомерСтроки
	|ГДЕ
	|	КалькуляцияМатериальные.Номенклатура ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ВидЦены,
	|	НачатьНеРанее
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТМатериалы.Номенклатура КАК Номенклатура,
	|	ВТМатериалы.Характеристика КАК Характеристика,
	|	ВТМатериалы.ВидЦены КАК ВидЦены,
	|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК Период,
	|	ВТМатериалы.НачатьНеРанее КАК НачатьНеРанее
	|ПОМЕСТИТЬ ПериодыЦенНоменклатуры
	|ИЗ
	|	ВТМатериалы КАК ВТМатериалы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ВТМатериалы.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ВТМатериалы.Характеристика = ЦеныНоменклатуры.Характеристика
	|			И ВТМатериалы.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|			И ВТМатериалы.НачатьНеРанее >= ЦеныНоменклатуры.Период
	|ГДЕ
	|	ВТМатериалы.КоличествоСпецификация > 0
	|	И НЕ ВТМатериалы.ЦенаУказанаВручную
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТМатериалы.Номенклатура,
	|	ВТМатериалы.ВидЦены,
	|	ВТМатериалы.Характеристика,
	|	ВТМатериалы.НачатьНеРанее
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ВидЦены,
	|	НачатьНеРанее,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалькуляцияТрудозатраты.Калькуляция,
	|	КалькуляцияТрудозатраты.СтатьяКалькуляции,
	|	КалькуляцияТрудозатраты.ВидРабот КАК ВидРабот,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	КалькуляцияТрудозатраты.ЕдиницаИзмерения,
	|	КалькуляцияТрудозатраты.Описание,
	|	КалькуляцияТрудозатраты.Количество КАК КоличествоКалькуляция,
	|	КалькуляцияТрудозатраты.Расценка КАК ЦенаКалькуляция,
	|	КалькуляцияТрудозатраты.Сумма КАК СуммаКалькуляция,
	|	ЕСТЬNULL(СпецификацияТрудозатраты.Количество, 0) КАК КоличествоСпецификация,
	|	0 КАК ЦенаСпецификация,
	|	0 КАК СуммаСпецификация,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеДоляСтоимости,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	КалькуляцияТрудозатраты.ДетализироватьСтатьиКалькуляции,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеСпецификация,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияТрудозатраты.НачатьНеРанее, ДАТАВРЕМЯ(1, 1, 1)) КАК НачатьНеРанее
	|ПОМЕСТИТЬ ВТТрудозатраты
	|ИЗ
	|	КалькуляцияТрудозатраты КАК КалькуляцияТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацияТрудозатраты КАК СпецификацияТрудозатраты
	|		ПО КалькуляцияТрудозатраты.Калькуляция = СпецификацияТрудозатраты.Калькуляция
	|			И КалькуляцияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство = СпецификацияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство
	|			И КалькуляцияТрудозатраты.ВидРабот = СпецификацияТрудозатраты.ВидРабот
	|			И КалькуляцияТрудозатраты.Описание = СпецификацияТрудозатраты.Описание
	|			И КалькуляцияТрудозатраты.СтатьяКалькуляции = СпецификацияТрудозатраты.СтатьяКалькуляции
	|			И КалькуляцияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство = СпецификацияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство
	|			И КалькуляцияТрудозатраты.ВыходноеИзделиеСпецификация = СпецификацияТрудозатраты.ВыходноеИзделиеСпецификация
	|			И КалькуляцияТрудозатраты.ВыходноеИзделиеНомерСтроки = СпецификацияТрудозатраты.ВыходноеИзделиеНомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	КалькуляцияТрудозатраты.ВидРабот,
	|	КалькуляцияТрудозатраты.Калькуляция,
	|	КалькуляцияТрудозатраты.СтатьяКалькуляции,
	|	КалькуляцияТрудозатраты.ЕдиницаИзмерения,
	|	КалькуляцияТрудозатраты.Описание,
	|	КалькуляцияТрудозатраты.Количество,
	|	КалькуляцияТрудозатраты.Расценка,
	|	КалькуляцияТрудозатраты.Сумма,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеДоляСтоимости,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	КалькуляцияТрудозатраты.ДетализироватьСтатьиКалькуляции,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеСпецификация,
	|	КалькуляцияТрудозатраты.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияТрудозатраты.Количество, 0),
	|	ЕСТЬNULL(СпецификацияТрудозатраты.НачатьНеРанее, ДАТАВРЕМЯ(1, 1, 1))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СпецификацияТрудозатраты.Калькуляция,
	|	СпецификацияТрудозатраты.СтатьяКалькуляции,
	|	СпецификацияТрудозатраты.ВидРабот,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
	|	СпецификацияТрудозатраты.ЕдиницаИзмерения,
	|	СпецификацияТрудозатраты.Описание,
	|	0,
	|	0,
	|	0,
	|	СпецификацияТрудозатраты.Количество,
	|	0,
	|	0,
	|	СпецификацияТрудозатраты.ВыходноеИзделиеДоляСтоимости,
	|	СпецификацияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство,
	|	СпецификацияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СпецификацияТрудозатраты.ДетализироватьСтатьиКалькуляции,
	|	СпецификацияТрудозатраты.ВыходноеИзделиеСпецификация,
	|	СпецификацияТрудозатраты.ВыходноеИзделиеНомерСтроки,
	|	СпецификацияТрудозатраты.НачатьНеРанее
	|ИЗ
	|	СпецификацияТрудозатраты КАК СпецификацияТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КалькуляцияТрудозатраты КАК КалькуляцияТрудозатраты
	|		ПО (КалькуляцияТрудозатраты.Калькуляция = СпецификацияТрудозатраты.Калькуляция)
	|			И (КалькуляцияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство = СпецификацияТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство)
	|			И (КалькуляцияТрудозатраты.ВидРабот = СпецификацияТрудозатраты.ВидРабот)
	|			И (КалькуляцияТрудозатраты.Описание = СпецификацияТрудозатраты.Описание)
	|			И (КалькуляцияТрудозатраты.СтатьяКалькуляции = СпецификацияТрудозатраты.СтатьяКалькуляции)
	|			И СпецификацияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство = КалькуляцияТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство
	|			И СпецификацияТрудозатраты.ВыходноеИзделиеСпецификация = КалькуляцияТрудозатраты.ВыходноеИзделиеСпецификация
	|			И СпецификацияТрудозатраты.ВыходноеИзделиеНомерСтроки = КалькуляцияТрудозатраты.ВыходноеИзделиеНомерСтроки
	|ГДЕ
	|	КалькуляцияТрудозатраты.ВидРабот ЕСТЬ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабот,
	|	НачатьНеРанее
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТрудозатраты.ВидРабот КАК ВидРабот,
	|	ВТТрудозатраты.НачатьНеРанее КАК НачатьНеРанее,
	|	МАКСИМУМ(РасценкиРаботСотрудников.Период) КАК Период
	|ПОМЕСТИТЬ ПериодыРасценокТрудозатрат
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников КАК РасценкиРаботСотрудников
	|		ПО ВТТрудозатраты.ВидРабот = РасценкиРаботСотрудников.ВидРабот
	|			И ВТТрудозатраты.НачатьНеРанее >= РасценкиРаботСотрудников.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТТрудозатраты.ВидРабот,
	|	ВТТрудозатраты.НачатьНеРанее
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабот,
	|	НачатьНеРанее,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМатериалы.Калькуляция,
	|	ВТМатериалы.СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.Номенклатура
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Затрата,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.Описание
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Описание,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	КОНЕЦ КАК ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.КоличествоКалькуляция
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоКалькуляция,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.ЦенаКалькуляция
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ЦенаКалькуляция,
	|	ВТМатериалы.СуммаКалькуляция * ВЫБОР
	|		КОГДА ВТМатериалы.СтатьяКалькуляции.ТипЗатрат = ЗНАЧЕНИЕ(Перечисление.ТипыЗатрат.ВозвратныеОтходы)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаКалькуляция,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТМатериалы.КоличествоСпецификация
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоСпецификация,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ЦенаУказанаВручную
	|			ТОГДА ВТМатериалы.ЦенаКалькуляция
	|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|	КОНЕЦ КАК ЦенаСпецификация,
	|	ВЫБОР
	|		КОГДА ВТМатериалы.ЦенаУказанаВручную
	|			ТОГДА ВТМатериалы.ЦенаКалькуляция
	|		ИНАЧЕ ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) / ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|	КОНЕЦ * ВТМатериалы.КоличествоСпецификация * ВЫБОР
	|		КОГДА ВТМатериалы.СтатьяКалькуляции.ТипЗатрат = ЗНАЧЕНИЕ(Перечисление.ТипыЗатрат.ВозвратныеОтходы)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаСпецификация,
	|	ВТМатериалы.ВыходноеИзделиеДоляСтоимости,
	|	ВТМатериалы.ВыходноеИзделиеЗаказНаПроизводство,
	|	ВТМатериалы.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВТМатериалы.ДетализироватьСтатьиКалькуляции,
	|	ВТМатериалы.ВыходноеИзделиеСпецификация,
	|	ВТМатериалы.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки
	|ПОМЕСТИТЬ СуммыЗатрат
	|ИЗ
	|	ВТМатериалы КАК ВТМатериалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыЦенНоменклатуры КАК ПериодыЦенНоменклатуры
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|			ПО ПериодыЦенНоменклатуры.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|				И ПериодыЦенНоменклатуры.Характеристика = ЦеныНоменклатуры.Характеристика
	|				И ПериодыЦенНоменклатуры.ВидЦены = ЦеныНоменклатуры.ВидЦены
	|				И ПериодыЦенНоменклатуры.Период = ЦеныНоменклатуры.Период
	|		ПО ВТМатериалы.Номенклатура = ПериодыЦенНоменклатуры.Номенклатура
	|			И ВТМатериалы.Характеристика = ПериодыЦенНоменклатуры.Характеристика
	|			И ВТМатериалы.ВидЦены = ПериодыЦенНоменклатуры.ВидЦены
	|			И ВТМатериалы.НачатьНеРанее = ПериодыЦенНоменклатуры.НачатьНеРанее
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТТрудозатраты.Калькуляция,
	|	ВТТрудозатраты.СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.ВидРабот
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.Описание
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.ЕдиницаИзмерения
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.КоличествоКалькуляция
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.ЦенаКалькуляция
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВТТрудозатраты.СуммаКалькуляция,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ВТТрудозатраты.КоличествоСпецификация
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВТТрудозатраты.ДетализироватьСтатьиКалькуляции
	|			ТОГДА ЕСТЬNULL(РасценкиРаботСотрудников.Расценка, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ЕСТЬNULL(РасценкиРаботСотрудников.Расценка, 0) * ВТТрудозатраты.КоличествоСпецификация,
	|	ВТТрудозатраты.ВыходноеИзделиеДоляСтоимости,
	|	ВТТрудозатраты.ВыходноеИзделиеЗаказНаПроизводство,
	|	ВТТрудозатраты.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВТТрудозатраты.ДетализироватьСтатьиКалькуляции,
	|	ВТТрудозатраты.ВыходноеИзделиеСпецификация,
	|	ВТТрудозатраты.ВыходноеИзделиеНомерСтроки
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыРасценокТрудозатрат КАК ПериодыРасценокТрудозатрат
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников КАК РасценкиРаботСотрудников
	|			ПО ПериодыРасценокТрудозатрат.ВидРабот = РасценкиРаботСотрудников.ВидРабот
	|				И ПериодыРасценокТрудозатрат.Период = РасценкиРаботСотрудников.Период
	|		ПО ВТТрудозатраты.ВидРабот = ПериодыРасценокТрудозатрат.ВидРабот
	|			И ВТТрудозатраты.НачатьНеРанее = ПериодыРасценокТрудозатрат.НачатьНеРанее
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КалькуляцияСтатьиРасходов.Калькуляция,
	|	КалькуляцияСтатьиРасходов.СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА КалькуляцияСтатьиРасходов.ДетализироватьСтатьиКалькуляции
	|			ТОГДА КалькуляцияСтатьиРасходов.Затрата
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	КОНЕЦ,
	|	КалькуляцияСтатьиРасходов.Характеристика,
	|	КалькуляцияСтатьиРасходов.Описание,
	|	КалькуляцияСтатьиРасходов.ЕдиницаИзмерения,
	|	КалькуляцияСтатьиРасходов.КоличествоКалькуляция,
	|	0,
	|	КалькуляцияСтатьиРасходов.СуммаКалькуляция,
	|	КалькуляцияСтатьиРасходов.КоличествоСпецификация,
	|	0,
	|	КалькуляцияСтатьиРасходов.СуммаСпецификация,
	|	КалькуляцияСтатьиРасходов.ВыходноеИзделиеДоляСтоимости,
	|	КалькуляцияСтатьиРасходов.ВыходноеИзделиеЗаказНаПроизводство,
	|	КалькуляцияСтатьиРасходов.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	КалькуляцияСтатьиРасходов.ДетализироватьСтатьиКалькуляции,
	|	КалькуляцияСтатьиРасходов.ВыходноеИзделиеСпецификация,
	|	КалькуляцияСтатьиРасходов.ВыходноеИзделиеНомерСтроки
	|ИЗ
	|	КалькуляцияСтатьиРасходов КАК КалькуляцияСтатьиРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КалькуляцияСтатьиКалькуляции.Калькуляция,
	|	КалькуляцияСтатьиКалькуляции.СтатьяКалькуляции,
	|	КалькуляцияСтатьиКалькуляции.Затрата,
	|	КалькуляцияСтатьиКалькуляции.Характеристика,
	|	КалькуляцияСтатьиКалькуляции.Описание,
	|	КалькуляцияСтатьиКалькуляции.ЕдиницаИзмерения,
	|	КалькуляцияСтатьиКалькуляции.КоличествоКалькуляция,
	|	0,
	|	КалькуляцияСтатьиКалькуляции.СуммаКалькуляция,
	|	КалькуляцияСтатьиКалькуляции.КоличествоСпецификация,
	|	0,
	|	КалькуляцияСтатьиКалькуляции.СуммаСпецификация,
	|	КалькуляцияСтатьиКалькуляции.ВыходноеИзделиеДоляСтоимости,
	|	КалькуляцияСтатьиКалькуляции.ВыходноеИзделиеЗаказНаПроизводство,
	|	КалькуляцияСтатьиКалькуляции.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	КалькуляцияСтатьиКалькуляции.ДетализироватьСтатьиКалькуляции,
	|	КалькуляцияСтатьиКалькуляции.ВыходноеИзделиеСпецификация,
	|	КалькуляцияСтатьиКалькуляции.ВыходноеИзделиеНомерСтроки
	|ИЗ
	|	КалькуляцияСтатьиКалькуляции КАК КалькуляцияСтатьиКалькуляции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыходноеИзделиеНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыЗатрат.Калькуляция,
	|	СуммыЗатрат.СтатьяКалькуляции,
	|	СуммыЗатрат.Затрата,
	|	СуммыЗатрат.Характеристика,
	|	СуммыЗатрат.Описание,
	|	СуммыЗатрат.ЕдиницаИзмерения,
	|	СУММА(СуммыЗатрат.КоличествоКалькуляция) КАК КоличествоКалькуляция,
	|	МАКСИМУМ(СуммыЗатрат.ЦенаКалькуляция) КАК ЦенаКалькуляция,
	|	СУММА(СуммыЗатрат.СуммаКалькуляция) КАК СуммаКалькуляция,
	|	СУММА(СуммыЗатрат.КоличествоСпецификация) КАК КоличествоСпецификация,
	|	МАКСИМУМ(СуммыЗатрат.ЦенаСпецификация) КАК ЦенаСпецификация,
	|	СУММА(СуммыЗатрат.СуммаСпецификация) КАК СуммаСпецификация,
	|	СуммыЗатрат.ВыходноеИзделиеДоляСтоимости,
	|	СуммыЗатрат.ВыходноеИзделиеЗаказНаПроизводство,
	|	СуммыЗатрат.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СуммыЗатрат.ДетализироватьСтатьиКалькуляции,
	|	СуммыЗатрат.ВыходноеИзделиеСпецификация,
	|	СуммыЗатрат.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ВыходноеИзделиеНоменклатура,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ВыходноеИзделиеХарактеристика,
	|	0 КАК КоличествоФакт,
	|	0 КАК ЦенаФакт,
	|	0 КАК СуммаФакт
	|ИЗ
	|	СуммыЗатрат КАК СуммыЗатрат
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО СуммыЗатрат.ВыходноеИзделиеНомерСтроки = СпецификацияВыходныеИзделия.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыЗатрат.Калькуляция,
	|	СуммыЗатрат.СтатьяКалькуляции,
	|	СуммыЗатрат.Затрата,
	|	СуммыЗатрат.Характеристика,
	|	СуммыЗатрат.Описание,
	|	СуммыЗатрат.ЕдиницаИзмерения,
	|	СуммыЗатрат.ВыходноеИзделиеЗаказНаПроизводство,
	|	СуммыЗатрат.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СуммыЗатрат.ДетализироватьСтатьиКалькуляции,
	|	СуммыЗатрат.ВыходноеИзделиеСпецификация,
	|	СуммыЗатрат.ВыходноеИзделиеДоляСтоимости,
	|	СуммыЗатрат.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))";
	
	ТекстЗапроса = ТекстЗапроса +
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ЦеныНоменклатуры.Упаковка",
			"ЦеныНоменклатуры.Номенклатура"));
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для расчета статей, рассчитываемых по формуле.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстЗапросаДанныеДляРасчетаФормул() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция,
	|	ПлановаяКалькуляцияСтатьиКалькуляции.СтатьяКалькуляции,
	|	ПлановаяКалькуляцияСтатьиКалькуляции.Формула,
	|	ПлановаяКалькуляцияСтатьиКалькуляции.Сумма КАК Сумма,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки,
	|	ДействующаяКалькуляция.Кратность,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости
	|ПОМЕСТИТЬ СтатьиКалькуляцииДляРасчета
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.СтатьиКалькуляции КАК ПлановаяКалькуляцияСтатьиКалькуляции
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияСтатьиКалькуляции.Ссылка
	|ГДЕ
	|	ПлановаяКалькуляцияСтатьиКалькуляции.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоФормуле)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыходноеИзделиеНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействующаяКалькуляция.Калькуляция,
	|	ПлановаяКалькуляцияСтатьиРасходов.СтатьяКалькуляции,
	|	ПлановаяКалькуляцияСтатьиРасходов.Формула,
	|	ПлановаяКалькуляцияСтатьиРасходов.Сумма КАК Сумма,
	|	ДействующаяКалькуляция.ДетализироватьСтатьиКалькуляции,
	|	ДействующаяКалькуляция.ВыходноеИзделиеЗаказНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ДействующаяКалькуляция.ВыходноеИзделиеСпецификация,
	|	ДействующаяКалькуляция.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки,
	|	ДействующаяКалькуляция.Кратность,
	|	ДействующаяКалькуляция.ВыходноеИзделиеДоляСтоимости,
	|	ПлановаяКалькуляцияСтатьиРасходов.СтатьяРасходов КАК Затрата
	|ПОМЕСТИТЬ СтатьиРасходовДляРасчета
	|ИЗ
	|	ДействующаяКалькуляция КАК ДействующаяКалькуляция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлановаяКалькуляция.СтатьиРасходов КАК ПлановаяКалькуляцияСтатьиРасходов
	|		ПО ДействующаяКалькуляция.Калькуляция = ПлановаяКалькуляцияСтатьиРасходов.Ссылка
	|ГДЕ
	|	ПлановаяКалькуляцияСтатьиРасходов.РасчетПоФормуле
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВыходноеИзделиеНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СуммыЗатрат.СтатьяКалькуляции,
	|	СуммыЗатрат.СтатьяКалькуляции.Идентификатор КАК Идентификатор,
	|	СУММА(СуммыЗатрат.СуммаСпецификация) КАК Сумма,
	|	СуммыЗатрат.ВыходноеИзделиеСпецификация,
	|	СуммыЗатрат.ВыходноеИзделиеНомерСтроки,
	|	СуммыЗатрат.ВыходноеИзделиеЗаказНаПроизводство,
	|	СуммыЗатрат.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СуммыЗатрат.Калькуляция
	|ИЗ
	|	СуммыЗатрат КАК СуммыЗатрат
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммыЗатрат.СтатьяКалькуляции,
	|	СуммыЗатрат.СтатьяКалькуляции.Идентификатор,
	|	СуммыЗатрат.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СуммыЗатрат.ВыходноеИзделиеСпецификация,
	|	СуммыЗатрат.ВыходноеИзделиеЗаказНаПроизводство,
	|	СуммыЗатрат.ВыходноеИзделиеНомерСтроки,
	|	СуммыЗатрат.Калькуляция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиКалькуляцииДляРасчета.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СтатьиКалькуляцииДляРасчета.Калькуляция КАК Калькуляция,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеЗаказНаПроизводство КАК ВыходноеИзделиеЗаказНаПроизводство,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство КАК ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеСпецификация КАК ВыходноеИзделиеСпецификация,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеНомерСтроки КАК ВыходноеИзделиеНомерСтроки
	|ИЗ
	|	СтатьиКалькуляцииДляРасчета КАК СтатьиКалькуляцииДляРасчета
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтатьиРасходовДляРасчета.СтатьяКалькуляции,
	|	СтатьиРасходовДляРасчета.Калькуляция,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеЗаказНаПроизводство,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеСпецификация,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеНомерСтроки
	|ИЗ
	|	СтатьиРасходовДляРасчета КАК СтатьиРасходовДляРасчета
	|ИТОГИ ПО
	|	Калькуляция,
	|	ВыходноеИзделиеЗаказНаПроизводство,
	|	ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	ВыходноеИзделиеСпецификация,
	|	ВыходноеИзделиеНомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиРасходовДляРасчета.Калькуляция,
	|	СтатьиРасходовДляРасчета.СтатьяКалькуляции,
	|	СтатьиРасходовДляРасчета.Затрата КАК Затрата,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	"""" КАК Описание,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
	|	0 КАК КоличествоКалькуляция,
	|	0 КАК ЦенаКалькуляция,
	|	0 КАК СуммаКалькуляция,
	|	0 КАК КоличествоСпецификация,
	|	0 КАК ЦенаСпецификация,
	|	СтатьиРасходовДляРасчета.Сумма,
	|	СтатьиРасходовДляРасчета.Формула,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеДоляСтоимости,
	|	СтатьиРасходовДляРасчета.ДетализироватьСтатьиКалькуляции,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеЗаказНаПроизводство,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеСпецификация,
	|	СтатьиРасходовДляРасчета.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ВыходноеИзделиеНоменклатура,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ВыходноеИзделиеХарактеристика,
	|	0 КАК КоличествоФакт,
	|	0 КАК ЦенаФакт,
	|	0 КАК СуммаФакт
	|ИЗ
	|	СтатьиРасходовДляРасчета КАК СтатьиРасходовДляРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО СтатьиРасходовДляРасчета.ВыходноеИзделиеНомерСтроки = СпецификацияВыходныеИзделия.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиКалькуляцииДляРасчета.Калькуляция,
	|	СтатьиКалькуляцииДляРасчета.СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Затрата,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	"""" КАК Описание,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ЕдиницаИзмерения,
	|	0 КАК КоличествоКалькуляция,
	|	0 КАК ЦенаКалькуляция,
	|	0 КАК СуммаКалькуляция,
	|	0 КАК КоличествоСпецификация,
	|	0 КАК ЦенаСпецификация,
	|	СтатьиКалькуляцииДляРасчета.Сумма,
	|	СтатьиКалькуляцииДляРасчета.Формула,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеДоляСтоимости,
	|	СтатьиКалькуляцииДляРасчета.ДетализироватьСтатьиКалькуляции,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеЗаказНаПроизводство,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеСпецификация,
	|	СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеНомерСтроки,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ВыходноеИзделиеНоменклатура,
	|	ЕСТЬNULL(СпецификацияВыходныеИзделия.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК ВыходноеИзделиеХарактеристика,
	|	0 КАК КоличествоФакт,
	|	0 КАК ЦенаФакт,
	|	0 КАК СуммаФакт
	|ИЗ
	|	СтатьиКалькуляцииДляРасчета КАК СтатьиКалькуляцииДляРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ СпецификацияВыходныеИзделия КАК СпецификацияВыходныеИзделия
	|		ПО СтатьиКалькуляцииДляРасчета.ВыходноеИзделиеНомерСтроки = СпецификацияВыходныеИзделия.НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Заполняет документ по спецификации.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - Заполняемый документ
//	Спецификация - СправочникСсылка.РесурсныеСпецификации - Спецификация, по которой заполняется документ
//
Процедура ЗаполнитьПоСпецификации(Объект, Спецификация) Экспорт
	
	Объект.СтатьиКалькуляции.Очистить();
	Объект.ВозвратныеОтходы.Очистить();
	Объект.МатериалыИУслуги.Очистить();
	Объект.Трудозатраты.Очистить();
	Объект.СтатьиРасходов.Очистить();
	
	КэшированныеЗначения = Неопределено;
	
	ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(Спецификация);
	
	Если Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие И Объект.ДействиеКалькуляции.Количество() > 0 Тогда
		ДействиеКалькуляции = Объект.ДействиеКалькуляции[0];
		Если ДействиеКалькуляции.Объект = ДанныеПоНоменклатуре.Номенклатура И ЗначениеЗаполнено(ДействиеКалькуляции.Характеристика) Тогда
			ДанныеПоНоменклатуре.Характеристика = ДействиеКалькуляции.Характеристика;
		КонецЕсли;
	КонецЕсли;
	
	ТабличныеЧасти = Справочники.РесурсныеСпецификации.ДанныеСпецификацииСПолуфабрикатами(ДанныеПоНоменклатуре, Истина);
	
	Если Объект.ДействиеКалькуляции.Количество() = 0
		И Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
		НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
		НоваяСтрока.Объект = Спецификация;
	ИначеЕсли Объект.ДействиеКалькуляции.Количество() = 0
		И Объект.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие
		И ТабличныеЧасти.ВыходныеИзделия.Количество() > 0 Тогда
		НоваяСтрока = Объект.ДействиеКалькуляции.Добавить();
		ДанныеИзделия = ТабличныеЧасти.ВыходныеИзделия[0];
		НоваяСтрока.Объект = ДанныеИзделия.Номенклатура;
	КонецЕсли;
	
	ТабличныеЧасти.ВозвратныеОтходы.Свернуть("СтатьяКалькуляции, ОписаниеИзделия, Номенклатура, Характеристика, Упаковка", "Количество, КоличествоУпаковок");
	ТабличныеЧасти.МатериалыИУслуги.Свернуть("СтатьяКалькуляции, ПрименениеМатериала, Номенклатура, Характеристика, Упаковка", "Количество, КоличествоУпаковок");
	ТабличныеЧасти.Трудозатраты.Свернуть("СтатьяКалькуляции, НазначениеРабот, ВидРабот", "Количество");
	
	Объект.ВозвратныеОтходы.Загрузить(ТабличныеЧасти.ВозвратныеОтходы);
	Объект.МатериалыИУслуги.Загрузить(ТабличныеЧасти.МатериалыИУслуги);
	Объект.Трудозатраты.Загрузить(ТабличныеЧасти.Трудозатраты);
	
	СтруктураЗаполненияЦен = Новый Структура("Дата, Валюта, ВидЦены, ПоляЗаполнения",
		Объект.Дата,
		Объект.Валюта,
		Объект.ВидЦены,
		"Цена, ВидЦены");
	
	СтруктураДействий = Новый Структура("ПересчитатьСумму");
	
	ПродажиСервер.ЗаполнитьЦены(
		Объект.ВозвратныеОтходы,
		, // Массив строк или структура отбора
		СтруктураЗаполненияЦен,
		СтруктураДействий);
	
	ПродажиСервер.ЗаполнитьЦены(
		Объект.МатериалыИУслуги,
		, // Массив строк или структура отбора
		СтруктураЗаполненияЦен,
		СтруктураДействий);
	
	РассчитатьТрудозатратыПоРасценкам(Объект);
	
	ПараметрыКоллеции = Новый Структура();
	ПараметрыКоллеции.Вставить("ОчищатьВидЦены");
	
	ЗаполнитьКалькуляцииВКоллекции(
		Объект.МатериалыИУслуги,
		,
		ПараметрыКоллеции,
		Объект.Организация,
		Объект.Дата);
		
	ЗаполнитьЦеныПоКалькуляцииВКоллекции(
		Объект.МатериалыИУслуги,
		,
		ПараметрыКоллеции);
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям(Объект);
	
	Если Не Объект.ДетализироватьСтатьиКалькуляции Тогда
		
		Для Каждого Строка Из Объект.СтатьиКалькуляции Цикл
			Строка.СпособРасчета = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ФиксированнымЗначением;
		КонецЦикла;
		
		Объект.ВозвратныеОтходы.Очистить();
		Объект.МатериалыИУслуги.Очистить();
		Объект.Трудозатраты.Очистить();
		Объект.СтатьиРасходов.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет документ по спецификации.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - Заполняемый документ
//	Спецификация - СправочникСсылка.РесурсныеСпецификации - Спецификация, по которой заполняется документ
//
Процедура ЗаполнитьПоСпецификацииЗаказа(Объект, ЗаказНаПроизводство, КодСтроки) Экспорт
	
	Объект.СтатьиКалькуляции.Очистить();
	Объект.ВозвратныеОтходы.Очистить();
	Объект.МатериалыИУслуги.Очистить();
	Объект.Трудозатраты.Очистить();
	Объект.СтатьиРасходов.Очистить();
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗаказНаПроизводствоВозвратныеОтходы.Номенклатура,
	|	ЗаказНаПроизводствоВозвратныеОтходы.Характеристика,
	|	ЗаказНаПроизводствоВозвратныеОтходы.Упаковка,
	|	СУММА(ЗаказНаПроизводствоВозвратныеОтходы.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ЗаказНаПроизводствоВозвратныеОтходы.Количество) КАК Количество,
	|	ЗаказНаПроизводствоВозвратныеОтходы.СтатьяКалькуляции,
	|	ЗаказНаПроизводствоВозвратныеОтходы.ОписаниеИзделия
	|ИЗ
	|	Документ.ЗаказНаПроизводство.ВозвратныеОтходы КАК ЗаказНаПроизводствоВозвратныеОтходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|		ПО ЗаказНаПроизводствоВозвратныеОтходы.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
	|ГДЕ
	|	ЗаказНаПроизводствоПродукция.Ссылка = &Ссылка
	|	И ЗаказНаПроизводствоВозвратныеОтходы.Ссылка = &Ссылка
	|	И ЗаказНаПроизводствоПродукция.КодСтроки = &КодСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаПроизводствоВозвратныеОтходы.Номенклатура,
	|	ЗаказНаПроизводствоВозвратныеОтходы.Характеристика,
	|	ЗаказНаПроизводствоВозвратныеОтходы.Упаковка,
	|	ЗаказНаПроизводствоВозвратныеОтходы.СтатьяКалькуляции,
	|	ЗаказНаПроизводствоВозвратныеОтходы.ОписаниеИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказНаПроизводствоМатериалыИУслуги.Номенклатура,
	|	ЗаказНаПроизводствоМатериалыИУслуги.Характеристика,
	|	ЗаказНаПроизводствоМатериалыИУслуги.Упаковка,
	|	СУММА(ЗаказНаПроизводствоМатериалыИУслуги.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(ЗаказНаПроизводствоМатериалыИУслуги.Количество) КАК Количество,
	|	ЗаказНаПроизводствоМатериалыИУслуги.СтатьяКалькуляции,
	|	ЗаказНаПроизводствоМатериалыИУслуги.ПрименениеМатериала
	|ИЗ
	|	Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ЗаказНаПроизводствоМатериалыИУслуги
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|		ПО ЗаказНаПроизводствоМатериалыИУслуги.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
	|ГДЕ
	|	ЗаказНаПроизводствоПродукция.Ссылка = &Ссылка
	|	И ЗаказНаПроизводствоМатериалыИУслуги.Ссылка = &Ссылка
	|	И ЗаказНаПроизводствоПродукция.КодСтроки = &КодСтроки
	|	И НЕ ЗаказНаПроизводствоМатериалыИУслуги.ПроизводитсяВПроцессе
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаПроизводствоМатериалыИУслуги.Номенклатура,
	|	ЗаказНаПроизводствоМатериалыИУслуги.Характеристика,
	|	ЗаказНаПроизводствоМатериалыИУслуги.Упаковка,
	|	ЗаказНаПроизводствоМатериалыИУслуги.СтатьяКалькуляции,
	|	ЗаказНаПроизводствоМатериалыИУслуги.ПрименениеМатериала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ЗаказНаПроизводствоТрудозатраты.Количество) КАК Количество,
	|	ЗаказНаПроизводствоТрудозатраты.СтатьяКалькуляции,
	|	ЗаказНаПроизводствоТрудозатраты.ВидРабот,
	|	ЗаказНаПроизводствоТрудозатраты.НазначениеРабот
	|ИЗ
	|	Документ.ЗаказНаПроизводство.Трудозатраты КАК ЗаказНаПроизводствоТрудозатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|		ПО ЗаказНаПроизводствоТрудозатраты.КлючСвязиПродукция = ЗаказНаПроизводствоПродукция.КлючСвязи
	|ГДЕ
	|	ЗаказНаПроизводствоПродукция.Ссылка = &Ссылка
	|	И ЗаказНаПроизводствоТрудозатраты.Ссылка = &Ссылка
	|	И ЗаказНаПроизводствоПродукция.КодСтроки = &КодСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказНаПроизводствоТрудозатраты.СтатьяКалькуляции,
	|	ЗаказНаПроизводствоТрудозатраты.ВидРабот,
	|	ЗаказНаПроизводствоТрудозатраты.НазначениеРабот");
	
	Запрос.УстановитьПараметр("Ссылка", ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("КодСтроки", КодСтроки);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Объект.ВозвратныеОтходы.Загрузить(Результат[0].Выгрузить());
	Объект.МатериалыИУслуги.Загрузить(Результат[1].Выгрузить());
	Объект.Трудозатраты.Загрузить(Результат[2].Выгрузить());
	
	СтруктураЗаполненияЦен = Новый Структура("Дата, Валюта, ВидЦены, ПоляЗаполнения",
		Объект.Дата,
		Объект.Валюта,
		Объект.ВидЦены,
		"Цена, ВидЦены");
	
	СтруктураДействий = Новый Структура("ПересчитатьСумму");
	
	ПродажиСервер.ЗаполнитьЦены(
		Объект.ВозвратныеОтходы,
		, // Массив строк или структура отбора
		СтруктураЗаполненияЦен,
		СтруктураДействий);
	
	ПродажиСервер.ЗаполнитьЦены(
		Объект.МатериалыИУслуги,
		, // Массив строк или структура отбора
		СтруктураЗаполненияЦен,
		СтруктураДействий);
	
	РассчитатьТрудозатратыПоРасценкам(Объект);
	
		ПараметрыКоллеции = Новый Структура();
	ПараметрыКоллеции.Вставить("ОчищатьВидЦены");
	
	ЗаполнитьКалькуляцииВКоллекции(
		Объект.МатериалыИУслуги,
		,
		ПараметрыКоллеции,
		Объект.Организация,
		Объект.Дата);
		
	ЗаполнитьЦеныПоКалькуляцииВКоллекции(
		Объект.МатериалыИУслуги,
		,
		ПараметрыКоллеции);
	
	РассчитатьЗависимыеИтогиПоТабличнымЧастям(Объект);
	
	Если Не Объект.ДетализироватьСтатьиКалькуляции Тогда
		
		Для Каждого Строка Из Объект.СтатьиКалькуляции Цикл
			Строка.СпособРасчета = Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ФиксированнымЗначением;
		КонецЦикла;
		
		Объект.ВозвратныеОтходы.Очистить();
		Объект.МатериалыИУслуги.Очистить();
		Объект.Трудозатраты.Очистить();
		Объект.СтатьиРасходов.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

// Заполняет расценки в табличной части трудозатрат.
//
// Параметры:
//	Объект - ДанныеФормыСтруктура - Заполняемый документ
//	Идентификаторы - Массив - Массив идентификаторов строк
//
Процедура РассчитатьТрудозатратыПоРасценкам(Объект, Идентификаторы = Неопределено) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Трудозатраты.ВидРабот КАК ВидРабот,
	|	Трудозатраты.Количество КАК Количество,
	|	Трудозатраты.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТТрудозатраты
	|ИЗ
	|	&Трудозатраты КАК Трудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТрудозатраты.ВидРабот КАК ВидРабот,
	|	ЕСТЬNULL(Расценки.Расценка, 0) КАК Расценка
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(
	|				&Период,
	|				ВидРабот В
	|					(ВЫБРАТЬ
	|						Т.ВидРабот
	|					ИЗ
	|						ВТТрудозатраты КАК Т)) КАК Расценки
	|		ПО ВТТрудозатраты.ВидРабот = Расценки.ВидРабот");
	
	Если Идентификаторы = Неопределено Тогда
		Запрос.УстановитьПараметр("Трудозатраты", Объект.Трудозатраты.Выгрузить());
	Иначе
		Массив = Новый Массив;
		Для Каждого Идентификатор Из Идентификаторы Цикл
			Массив.Добавить(Объект.Трудозатраты.НайтиПоИдентификатору(Идентификатор));
		КонецЦикла;
		Запрос.УстановитьПараметр("Трудозатраты", Объект.Трудозатраты.Выгрузить(Массив));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период", Объект.Дата);
	Результат = Запрос.Выполнить();
	
	Расценки = Результат.Выбрать();
	
	Для Каждого Строка Из Объект.Трудозатраты Цикл
		
		Если Идентификаторы <> Неопределено И Идентификаторы.Найти(Строка.ПолучитьИдентификатор()) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Расценки.НайтиСледующий(Новый Структура("ВидРабот", Строка.ВидРабот));
		
		Строка.Расценка = Расценки.Расценка;
		Строка.Сумма = Расценки.Расценка* Строка.Количество;
		
		Расценки.Сбросить();
		
	КонецЦикла;
	
КонецПроцедуры

// Рассчитывает итоги статей калькуляции по детальным записям
// Параметры:
//	Объект - ДанныеФормыСтруктура - Заполняемый документ
//
Процедура РассчитатьЗависимыеИтогиПоТабличнымЧастям(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтатьиКалькуляции.СтатьяКалькуляции,
	|	СтатьиКалькуляции.НомерСтроки
	|ПОМЕСТИТЬ ВТСтатьиКалькуляции
	|ИЗ
	|	&СтатьиКалькуляции КАК СтатьиКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДетальныеЗаписи.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ДетальныеЗаписи.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТМатериалыИУслуги
	|ИЗ
	|	&МатериалыИУслуги КАК ДетальныеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Отходы.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Отходы.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТОтходы
	|ИЗ
	|	&Отходы КАК Отходы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Трудозатраты.СтатьяКалькуляции,
	|	Трудозатраты.Сумма
	|ПОМЕСТИТЬ ВТТрудозатраты
	|ИЗ
	|	&Трудозатраты КАК Трудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиРасходов.СтатьяКалькуляции,
	|	СтатьиРасходов.Сумма
	|ПОМЕСТИТЬ ВТСтатьиРасходов
	|ИЗ
	|	&СтатьиРасходов КАК СтатьиРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТМатериалыИУслуги.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТМатериалыИУслуги.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТДетальныеЗаписи
	|ИЗ
	|	ВТМатериалыИУслуги КАК ВТМатериалыИУслуги
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТТрудозатраты.СтатьяКалькуляции,
	|	ВТТрудозатраты.Сумма
	|ИЗ
	|	ВТТрудозатраты КАК ВТТрудозатраты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСтатьиРасходов.СтатьяКалькуляции,
	|	ВТСтатьиРасходов.Сумма
	|ИЗ
	|	ВТСтатьиРасходов КАК ВТСтатьиРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТОтходы.СтатьяКалькуляции,
	|	СУММА(-ВТОтходы.Сумма)
	|ИЗ
	|	ВТОтходы КАК ВТОтходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОтходы.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДетальныеЗаписи.СтатьяКалькуляции,
	|	СУММА(ВТДетальныеЗаписи.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ДетальныеЗаписи
	|ИЗ
	|	ВТДетальныеЗаписи КАК ВТДетальныеЗаписи
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДетальныеЗаписи.СтатьяКалькуляции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДетальныеЗаписи.СтатьяКалькуляции,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям) КАК СпособРасчета,
	|	ДетальныеЗаписи.Сумма,
	|	ЕСТЬNULL(ВТСтатьиКалькуляции.НомерСтроки, 0) КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВТСтатьиКалькуляции.НомерСтроки ЕСТЬ NULL 
	|				И ДетальныеЗаписи.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДобавитьСтроку
	|ИЗ
	|	ДетальныеЗаписи КАК ДетальныеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСтатьиКалькуляции КАК ВТСтатьиКалькуляции
	|		ПО ДетальныеЗаписи.СтатьяКалькуляции = ВТСтатьиКалькуляции.СтатьяКалькуляции";
	
	Запрос.УстановитьПараметр("СтатьиКалькуляции", Объект.СтатьиКалькуляции.Выгрузить(, "СтатьяКалькуляции, НомерСтроки"));
	Запрос.УстановитьПараметр("Отходы", Объект.ВозвратныеОтходы.Выгрузить(, "СтатьяКалькуляции, Сумма"));
	Запрос.УстановитьПараметр("МатериалыИУслуги", Объект.МатериалыИУслуги.Выгрузить(, "СтатьяКалькуляции, Сумма"));
	Запрос.УстановитьПараметр("Трудозатраты", Объект.Трудозатраты.Выгрузить(, "СтатьяКалькуляции, Сумма"));
	Запрос.УстановитьПараметр("СтатьиРасходов", Объект.СтатьиРасходов.Выгрузить(, "СтатьяКалькуляции, Сумма"));
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ОтборНомерСтроки = Новый Структура("НомерСтроки");
	
	СписокОбработанныхСтатей = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ДобавитьСтроку Тогда
			
			НоваяСтрока = Объект.СтатьиКалькуляции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка, "СтатьяКалькуляции, СпособРасчета, Сумма");
		Иначе
			
			ЗаполнитьЗначенияСвойств(ОтборНомерСтроки, Выборка);
			НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборНомерСтроки);
			
			Для Каждого Строка Из НайденныеСтроки Цикл
				Строка.СпособРасчета = Выборка.СпособРасчета;
				Строка.Сумма = Выборка.Сумма;
				Строка.СтрокаИзменена = Истина;
			КонецЦикла;
			
		КонецЕсли;
		
		СписокОбработанныхСтатей.Добавить(Выборка.СтатьяКалькуляции);
	КонецЦикла;
	
	ОтборСпособРасчета = Новый Структура("СпособРасчета", Перечисления.СпособыРасчетаЗатратПлановойКалькуляции.ПоДетальнымЗаписям);
	НайденныеСтроки = Объект.СтатьиКалькуляции.НайтиСтроки(ОтборСпособРасчета);
	Для Каждого Строка Из НайденныеСтроки Цикл
		Если СписокОбработанныхСтатей.НайтиПоЗначению(Строка.СтатьяКалькуляции) = Неопределено Тогда
			Строка.Сумма = 0;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Дополняет набор данных рассчитываемыми по формулам суммами статей расходов и статей калькуляции.
// Параметры:
//	ТаблицаСравнения - ТаблицаЗначений - Набор данных, который следует дополнить
//	ВыборкаКалькуляция - ВыборкаИзРезультатаЗапроса - Выборка с группировками: Плановая калькуляция,
//		Заказ на производство, Код строки заказа, Спецификация, Номенклатура, Характеристика
//	СтатьиКалькуляции - ТаблицаЗначений - Статьи калькуляции, которые требуется рассчитать
//	СтатьиРасходов - ТаблицаЗначений - Статьи расходов, которые требуется рассчитать
//	ИтогПоСтатьямКалькуляции - ТаблицаЗначений - Итоги по статьям калькуляции
//
Процедура ДополнитьНаборДанныхРассчитываемымиЗначениями(ТаблицаСравнения, ВыборкаКалькуляция, СтатьиКалькуляции, СтатьиРасходов, ИтогПоСтатьямКалькуляции) Экспорт
	
	ДополнительныйИтогПоСтатьямКалькуляции = ИтогПоСтатьямКалькуляции.СкопироватьКолонки("Калькуляция, СтатьяКалькуляции, ВыходноеИзделиеЗаказНапроизводство,
		|ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство, ВыходноеИзделиеСпецификация, ВыходноеИзделиеНомерСтроки, Сумма");
	
	ОтборКалькуляция = Новый Структура("Калькуляция, ВыходноеИзделиеЗаказНапроизводство, ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство, ВыходноеИзделиеСпецификация,
		|ВыходноеИзделиеНомерСтроки");
	СтруктураПроверки = Новый Структура("Калькуляция, СтатьяКалькуляции, ВыходноеИзделиеЗаказНапроизводство, ВыходноеИзделиеКодСтрокиЗаказаНаПроизводство,
		|ВыходноеИзделиеСпецификация, ВыходноеИзделиеНомерСтроки");
	
	ТребуетсяРассчитать = Новый СписокЗначений;
	
	УдалитьСтатьиРасходов = Новый Массив;
	УдалитьСтатьиКалькуляции = Новый Массив;
	
	ТаблицыДляРасчета = Новый Структура;
	
	Пока ВыборкаКалькуляция.Следующий() Цикл
		
		ВыборкаЗаказНаПроизводство = ВыборкаКалькуляция.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока ВыборкаЗаказНаПроизводство.Следующий() Цикл
			
			ВыборкаКодСтрокиЗаказаНаПроизводство = ВыборкаЗаказНаПроизводство.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			
			Пока ВыборкаКодСтрокиЗаказаНаПроизводство.Следующий() Цикл
				
				ВыборкаСпецификация = ВыборкаКодСтрокиЗаказаНаПроизводство.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
				
				Пока ВыборкаСпецификация.Следующий() Цикл
					
					ВыборкаНомерСтроки = ВыборкаСпецификация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаНомерСтроки.Следующий() Цикл
						
						ЗаполнитьЗначенияСвойств(ОтборКалькуляция, ВыборкаНомерСтроки);
						
						ВыборкаСтатья = ВыборкаНомерСтроки.Выбрать();
						
						Пока ВыборкаСтатья.Следующий() Цикл
							ТребуетсяРассчитать.Добавить(ВыборкаСтатья.СтатьяКалькуляции);
						КонецЦикла;
						
						Пока ТребуетсяРассчитать.Количество() > 0 Цикл
							
							ТекущиеСтатьиРасходов = СтатьиРасходов.НайтиСтроки(ОтборКалькуляция);
							ТекущиеСтатьиКалькуляции = СтатьиКалькуляции.НайтиСтроки(ОтборКалькуляция);
							
							ТаблицыДляРасчета.Вставить("СтатьиРасходов", ТекущиеСтатьиРасходов);
							ТаблицыДляРасчета.Вставить("СтатьиКалькуляции", ТекущиеСтатьиКалькуляции);
							
							Для Каждого ТаблицаРасчета Из ТаблицыДляРасчета Цикл
								
								УдалитьСтатьиРасходов.Очистить();
								УдалитьСтатьиКалькуляции.Очистить();
								
								Для Каждого СтрокаРасчета Из ТаблицаРасчета.Значение Цикл
									
									МассивВлияющихСтрок = ИзвлечьБазовыеСтрокиИзФормулы(ИтогПоСтатьямКалькуляции,
										ОтборКалькуляция,
										СтрокаРасчета.Формула);
									
									Для Каждого ВлияющаяСтрока Из МассивВлияющихСтрок Цикл
										Если ТребуетсяРассчитать.НайтиПоЗначению(ВлияющаяСтрока.СтатьяКалькуляции) <> Неопределено Тогда
											Продолжить;
										КонецЕсли;
									КонецЦикла;
									
									РассчитатьСтроку(СтрокаРасчета,
										МассивВлияющихСтрок,
										ТаблицаСравнения,
										ДополнительныйИтогПоСтатьямКалькуляции,
										ОтборКалькуляция);
									
									Если ТаблицаРасчета.Ключ = "СтатьиРасходов" Тогда
										УдалитьСтатьиРасходов.Добавить(СтрокаРасчета);
										
										ЗаполнитьЗначенияСвойств(СтруктураПроверки, СтрокаРасчета);
										ПроверкаСтатейКалькуляции = СтатьиРасходов.НайтиСтроки(СтруктураПроверки);
										Если ПроверкаСтатейКалькуляции.Количество() = 1 Тогда
											ТребуетсяРассчитать.Удалить(ТребуетсяРассчитать.НайтиПоЗначению(СтрокаРасчета.СтатьяКалькуляции));
										КонецЕсли;
									Иначе
										УдалитьСтатьиКалькуляции.Добавить(СтрокаРасчета);
										ТребуетсяРассчитать.Удалить(ТребуетсяРассчитать.НайтиПоЗначению(СтрокаРасчета.СтатьяКалькуляции));
									КонецЕсли;
									
								КонецЦикла;
								
								Для Каждого Строка Из УдалитьСтатьиРасходов Цикл
									СтатьиРасходов.Удалить(Строка);
								КонецЦикла;
								
								Для Каждого Строка Из УдалитьСтатьиКалькуляции Цикл
									СтатьиКалькуляции.Удалить(Строка);
								КонецЦикла;
								
							КонецЦикла;
							
						КонецЦикла;
					КонецЦикла;
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает обязательные настройки отчетов СравнениеКалькуляцииИФактическихЗатрат и ОтклонениеЗаказаФактаОтКалькуляции.
// Параметры:
//	КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компoновщик для редактирования настроек отчета
//	ПользовательскиеНастройкиМодифицированы - Булево - признак модификации пользовательских настроек
//
Процедура УстановитьОбязательныеНастройкиОтчетов(КомпоновщикНастроек, ПользовательскиеНастройкиМодифицированы) Экспорт
	
	ВалютаУправленческогоУчета      = Константы.ВалютаУправленческогоУчета.Получить();
	ВалютаРегламентированногоУчета  = Константы.ВалютаРегламентированногоУчета.Получить();
	ВалютаПлановойСтоимости         = Константы.ВалютаПлановойСебестоимостиПродукции.Получить();
	ВидЦеныПлановойСтоимости        = Константы.ВидЦеныПлановойСтоимостиМатериаловРабот.Получить();
	
	Если Не ЗначениеЗаполнено(ВидЦеныПлановойСтоимости) Тогда
		ТекстИсключения = НСтр("ru='Не задан плановый вид цен материалов. Вид цен устанавливается в панели администрирования (раздел Производство).';uk='Не заданий плановий вид цін матеріалів. Вид цін встановлюється в панелі адміністрування (розділ Виробництво).'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	ЦенаВключаетНДС                 = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦеныПлановойСтоимости, "ЦенаВключаетНДС");
	ПараметрПериодОтчета            = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПериодОтчета");
	
	Если ПараметрПериодОтчета <> Неопределено Тогда
		ДатаОтчета = ПараметрПериодОтчета.Значение.ДатаОкончания;
	Иначе
		ДатаОтчета = ТекущаяДатаСеанса();
	КонецЕсли;
	
	// Отбор по периоду этапов
	Если ПараметрПериодОтчета = Неопределено Тогда
		ОтборПоПериодуЭтапов = Ложь;
	Иначе
		ОтборПоПериодуЭтапов = ПараметрПериодОтчета.Использование;
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ОтборПоПериодуЭтапов", ОтборПоПериодуЭтапов);
	
	ПараметрДанныеОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДанныеОтчета");
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ЦенаВключаетНДС", ЦенаВключаетНДС);
	
	Если Не ЗначениеЗаполнено(ПараметрДанныеОтчета.Значение) Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДанныеОтчета", 1);
		ПользовательскиеНастройкиМодифицированы = Истина;
	КонецЕсли;
	
	// 1 - Упр
	// 2 - План
	// 3 - Регл
	
	Если ПараметрДанныеОтчета.Значение = 1 Тогда
		
		ЗначениеПараметра = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1; НДС: %2';uk='%1; ПДВ: %2'"),
		ВалютаУправленческогоУчета,
		?(ЦенаВключаетНДС, НСтр("ru='Включен';uk='Включений'"), НСтр("ru='Не включен';uk='Не включений'")));
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", ЗначениеПараметра);
		
		
		Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(ВалютаПлановойСтоимости,
			ВалютаПлановойСтоимости,
			ДатаОтчета);
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КоэфПересчетаПлан", Коэффициенты.КоэффициентПересчетаВВалютуУпр);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КоэфПересчетаФакт", 1);
		
	ИначеЕсли ПараметрДанныеОтчета.Значение = 2 Тогда
		
		ЗначениеПараметра = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1; НДС: %2';uk='%1; ПДВ: %2'"),
		ВалютаПлановойСтоимости,
		?(ЦенаВключаетНДС, НСтр("ru='Включен';uk='Включений'"), НСтр("ru='Не включен';uk='Не включений'")));
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", ЗначениеПараметра);
		
		Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(ВалютаУправленческогоУчета,
		ВалютаПлановойСтоимости,
		ДатаОтчета);
		
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КоэфПересчетаПлан", 1);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КоэфПересчетаФакт", Коэффициенты.КоэффициентПересчетаВВалютуВзаиморасчетов);
		
	ИначеЕсли ПараметрДанныеОтчета.Значение = 3 Тогда
		
		ЗначениеПараметра = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='%1; НДС: %2';uk='%1; ПДВ: %2'"),
		ВалютаРегламентированногоУчета,
		?(ЦенаВключаетНДС, НСтр("ru='Включен';uk='Включений'"), НСтр("ru='Не включен';uk='Не включений'")));
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", ЗначениеПараметра);
		
		Коэффициенты = РаботаСКурсамивалютУТ.ПолучитьКоэффициентыПересчетаВалюты(ВалютаПлановойСтоимости,
		ВалютаПлановойСтоимости,
		ДатаОтчета);
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КоэфПересчетаПлан", Коэффициенты.КоэффициентПересчетаВВалютуРегл);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "КоэфПересчетаФакт", 1);
		
	КонецЕсли;
	
	ПользовательскиеНастройкиМодифицированы = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьСтроку(Основание, МассивВлияющихСтатей, ТаблицаСравнения, ДополнительныйИтогПоСтатьямКалькуляции, СтруктураОтбора)
	
	Результат = 0;
	Формула = Основание.Формула;
	СтруктураОтбора.Вставить("СтатьяКалькуляции");
	
	Для Каждого Строка Из МассивВлияющихСтатей Цикл
		
		СтруктураОтбора.СтатьяКалькуляции = Строка.СтатьяКалькуляции;
		
		ДополнительныеИтоги = ДополнительныйИтогПоСтатьямКалькуляции.НайтиСтроки(СтруктураОтбора);
		
		Сумма = Строка.Сумма;
		
		Для Каждого ДополнительныйИтог Из ДополнительныеИтоги Цикл
			Сумма = Сумма + ДополнительныйИтог.Сумма;
		КонецЦикла;
		
		Формула = СтрЗаменить(Формула, "["+ Строка.Идентификатор + "]", Формат(Сумма, "ЧРД=.; ЧН=0; ЧГ="));
		
	КонецЦикла;
	
	Попытка
		Результат = Вычислить(Формула);
	Исключение
	КонецПопытки;
	
	Если Результат > 0 Тогда
		
		НоваяСтрока = ТаблицаСравнения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Основание);
		НоваяСтрока.СуммаСпецификация = Результат;
		
		НоваяСтрока = ДополнительныйИтогПоСтатьямКалькуляции.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Основание);
		НоваяСтрока.Сумма = Результат;
		
	КонецЕсли;
	
	СтруктураОтбора.Удалить("СтатьяКалькуляции");
	
КонецПроцедуры

Функция ИзвлечьБазовыеСтрокиИзФормулы(ИтогПоСтатьямКалькуляции, СтруктураОтбора, Формула)
	
	Результат = Новый Массив;
	
	СтруктураОтбора.Вставить("Идентификатор");
	
	МассивЭлементов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Формула, "[");
	
	Для Индекс = 1 По МассивЭлементов.Количество() - 1 Цикл
		
		Если ЗначениеЗаполнено(МассивЭлементов[Индекс]) Тогда
			
			ОкончаниеИдентификатора = СтрНайти(МассивЭлементов[Индекс], "]");
			
			Если ОкончаниеИдентификатора > 0 Тогда
				
				БазоваяСтатьяИдентификатор = Лев(МассивЭлементов[Индекс], ОкончаниеИдентификатора - 1);
				СтруктураОтбора.Идентификатор = БазоваяСтатьяИдентификатор;
				
				БазоваяСтатья = ИтогПоСтатьямКалькуляции.НайтиСтроки(СтруктураОтбора);
				Если БазоваяСтатья.Количество() > 0 Тогда
					Результат.Добавить(БазоваяСтатья[0]);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураОтбора.Удалить("Идентификатор");
	
	Возврат Результат;
	
КонецФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Чтение", Метаданные.Документы.ПлановаяКалькуляция) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ПлановаяКалькуляция";
		КомандаПечати.Представление = НСтр("ru='Плановая калькуляция';uk='Планова калькуляція'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПлановаяКалькуляция") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм, 
			"ПлановаяКалькуляция", 
			НСтр("ru='Плановая калькуляция';uk='Планова калькуляція'"),
			ПечатнаяФормаПлановойКалькуляции(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода),
			,
			"Документ.ПлановаяКалькуляция.ПФ_MXL_ПлановаяКалькуляция",
			,
			Истина // ЭтоМногоязычнаяПечатнаяФорма
			);
	КонецЕсли;
	
КонецПроцедуры

Функция ПечатнаяФормаПлановойКалькуляции(МассивОбъектов, ОбъектыПечати, ПараметрыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПлановаяКалькуляция.ПФ_MXL_ПлановаяКалькуляция", КодЯзыкаПечать);
	
	СтруктураПараметров = Новый Структура;
	ШаблонПредставленияОбъекта = НСтр("ru='%1 (строка %2)';uk='%1 (рядок %2)'",КодЯзыкаПечать);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Номер,
	|	Реквизиты.Дата,
	|	Реквизиты.КалькуляционнаяЕдиница,
	|	Реквизиты.ОбъектКалькуляции КАК ОбъектКалькуляции,
	|	Реквизиты.ДатаНачалаДействия,
	|	Реквизиты.ДатаОкончанияДействия,
	|	Реквизиты.Статус,
	|	Реквизиты.Валюта,
	|	Реквизиты.Организация,
	|	Реквизиты.Представление,
	|	ВЫБОР Реквизиты.ОбъектКалькуляции
	|		КОГДА ЗНАЧЕНИЕ(ПЕречисление.ОбъектыКалькуляции.Изделие)
	|			ТОГДА ""Изделие""
	|		КОГДА ЗНАЧЕНИЕ(ПЕречисление.ОбъектыКалькуляции.РесурснаяСпецификация)
	|			ТОГДА ""Спецификация""
	|		ИНАЧЕ ""ПозицияЗаказа""
	|	КОНЕЦ КАК ЗаголовокОбласти,
	|	Реквизиты.Ответственный.ФизическоеЛицо КАК ФизЛицо,
	|	Реквизиты.ВыводитьИерархиюСтатейКалькуляции КАК ВыводитьИерархию
	|ИЗ
	|	Документ.ПлановаяКалькуляция КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДействиеКалькуляции.Объект КАК Объект,
	|	ДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство КАК КодСтрокиЗаказаНаПроизводство,
	|	ЗаказНаПроизводствоПродукция.НомерСтроки КАК НомерСтроки,
	|	ЗаказНаПроизводствоВыходныеИзделия.Номенклатура КАК Номенклатура,
	|	ЗаказНаПроизводствоВыходныеИзделия.Характеристика КАК Характеристика,
	|	ЗаказНаПроизводствоВыходныеИзделия.ДоляСтоимости,
	|	ЗаказНаПроизводствоВыходныеИзделия.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЗаказНаПроизводствоВыходныеИзделия.Количество,
	|	ДействиеКалькуляции.Ссылка КАК Ссылка,
	|	ЗаказНаПроизводствоВыходныеИзделия.Номенклатура.Представление,
	|	ЗаказНаПроизводствоВыходныеИзделия.Характеристика.Представление,
	|	ЗаказНаПроизводствоВыходныеИзделия.Номенклатура.Код КАК НоменклатураКод,
	|	ЗаказНаПроизводствоВыходныеИзделия.Номенклатура.Артикул КАК НоменклатураАртикул,
	|	ДействиеКалькуляции.Объект.Номер КАК Номер,
	|	ДействиеКалькуляции.Объект.Дата КАК Дата,
	|	ЗаказНаПроизводствоВыходныеИзделия.Количество КАК КалькуляционнаяЕдиница
	|ИЗ
	|	Документ.ПлановаяКалькуляция.ДействиеКалькуляции КАК ДействиеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводствоПродукция
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ВыходныеИзделия КАК ЗаказНаПроизводствоВыходныеИзделия
	|			ПО ЗаказНаПроизводствоПродукция.Ссылка = ЗаказНаПроизводствоВыходныеИзделия.Ссылка
	|				И ЗаказНаПроизводствоПродукция.КлючСвязи = ЗаказНаПроизводствоВыходныеИзделия.КлючСвязиПродукция
	|		ПО ДействиеКалькуляции.Объект = ЗаказНаПроизводствоПродукция.Ссылка
	|			И ДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство = ЗаказНаПроизводствоПродукция.КодСтроки
	|ГДЕ
	|	ДействиеКалькуляции.Ссылка В(&МассивДокументов)
	|	И ДействиеКалькуляции.Ссылка.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.ЗаказНаПроизводство)
	|	И НЕ ЗаказНаПроизводствоВыходныеИзделия.ПроизводитсяВПроцессе
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДействиеКалькуляции.Объект,
	|	ДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ДействиеКалькуляции.НомерСтроки,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура,
	|	РесурсныеСпецификацииВыходныеИзделия.Характеристика,
	|	РесурсныеСпецификацииВыходныеИзделия.ДоляСтоимости,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.ЕдиницаИзмерения,
	|	РесурсныеСпецификацииВыходныеИзделия.Количество,
	|	ДействиеКалькуляции.Ссылка,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.Представление,
	|	РесурсныеСпецификацииВыходныеИзделия.Характеристика.Представление,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.Код,
	|	РесурсныеСпецификацииВыходныеИзделия.Номенклатура.Артикул,
	|	"""",
	|	"""",
	|	РесурсныеСпецификацииВыходныеИзделия.Количество
	|ИЗ
	|	Документ.ПлановаяКалькуляция.ДействиеКалькуляции КАК ДействиеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК РесурсныеСпецификацииВыходныеИзделия
	|		ПО ДействиеКалькуляции.Объект = РесурсныеСпецификацииВыходныеИзделия.Ссылка
	|ГДЕ
	|	ДействиеКалькуляции.Ссылка В(&МассивДокументов)
	|	И ДействиеКалькуляции.Ссылка.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.РесурснаяСпецификация)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДействиеКалькуляции.Объект,
	|	ДействиеКалькуляции.КодСтрокиЗаказаНаПроизводство,
	|	ДействиеКалькуляции.НомерСтроки,
	|	ДействиеКалькуляции.Объект,
	|	ДействиеКалькуляции.Характеристика,
	|	"""",
	|	ДействиеКалькуляции.Объект.ЕдиницаИзмерения,
	|	ДействиеКалькуляции.Ссылка.КалькуляционнаяЕдиница,
	|	ДействиеКалькуляции.Ссылка,
	|	ДействиеКалькуляции.Объект.Представление,
	|	ДействиеКалькуляции.Характеристика.Представление,
	|	ДействиеКалькуляции.Объект.Код,
	|	ДействиеКалькуляции.Объект.Артикул,
	|	"""",
	|	"""",
	|	ДействиеКалькуляции.Ссылка.КалькуляционнаяЕдиница
	|ИЗ
	|	Документ.ПлановаяКалькуляция.ДействиеКалькуляции КАК ДействиеКалькуляции
	|ГДЕ
	|	ДействиеКалькуляции.Ссылка В(&МассивДокументов)
	|	И ДействиеКалькуляции.Ссылка.ОбъектКалькуляции = ЗНАЧЕНИЕ(Перечисление.ОбъектыКалькуляции.Изделие)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки,
	|	КодСтрокиЗаказаНаПроизводство,
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Калькуляция.Ссылка КАК Ссылка,
	|	СтатьиКалькуляции.Ссылка КАК СтатьяКалькуляции,
	|	ЕСТЬNULL(Калькуляция.Сумма, 0) КАК Сумма,
	|	СтатьиКалькуляции.РеквизитДопУпорядочивания КАК Порядок
	|ИЗ
	|	Документ.ПлановаяКалькуляция.СтатьиКалькуляции КАК Калькуляция
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтатьиКалькуляции КАК СтатьиКалькуляции
	|		ПО Калькуляция.СтатьяКалькуляции = СтатьиКалькуляции.Ссылка
	|ГДЕ
	|	Калькуляция.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Порядок
	|ИТОГИ
	|	СУММА(Сумма)
	|ПО
	|	Ссылка,
	|	СтатьяКалькуляции ИЕРАРХИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Калькуляция.Ссылка КАК Ссылка,
	|	Калькуляция.НомерСтроки КАК НомерСтроки,
	|	Калькуляция.Сумма КАК Сумма,
	|	Калькуляция.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВЫБОР
	|		КОГДА Калькуляция.Ссылка.ВыводитьИерархиюСтатейКалькуляции
	|			ТОГДА Калькуляция.СтатьяКалькуляции.РеквизитДопУпорядочивания
	|		ИНАЧЕ Калькуляция.НомерСтроки
	|	КОНЕЦ КАК Порядок
	|ИЗ
	|	Документ.ПлановаяКалькуляция.СтатьиКалькуляции КАК Калькуляция
	|ГДЕ
	|	Калькуляция.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	Порядок";

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПлановаяКалькуляция";
	
	МассивРезультатов  = Запрос.ВыполнитьПакет();
	
	ДанныеПечати = МассивРезультатов[0].Выбрать();
	
	ДействиеКалькуляции = МассивРезультатов[1].Выгрузить();
	ДействиеКалькуляции.Индексы.Добавить("Ссылка");
	
	КалькуляцияИерархия = МассивРезультатов[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КалькуляцияСписок = МассивРезультатов[3].Выгрузить();
	КалькуляцияСписок.Индексы.Добавить("Ссылка");
	
	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеПечати.Следующий() Цикл
		
		ВыводитьИмяТаблиц = Истина;
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент    = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
	
		// Выводим общие реквизиты шапки
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
		ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
		ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru='Плановая калькуляция';uk='Планова калькуляція'",КодЯзыкаПечать), КодЯзыкаПечать);
		ОбластьМакета.Параметры.ТекстЗаголовка = ТекстЗаголовка;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если ДанныеПечати.Статус = Перечисления.СтатусыПлановыхКалькуляций.Действует Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_Действует");
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_ВРазработке");
		КонецЕсли;
		
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если ЗначениеЗаполнено(ДанныеПечати.Организация) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_Организация");
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_ОбъектКалькуляции");
		ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);
		ВыборкаДействиеКалькуляции = ДействиеКалькуляции.Скопировать(СтруктураПоиска);
		
		Если ВыборкаДействиеКалькуляции.Количество() = 1 Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Заголовок_" + ДанныеПечати.ЗаголовокОбласти);
			
			Строка = ВыборкаДействиеКалькуляции[0];
			ОбластьМакета.Параметры.ПредставлениеИзделия = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
					Строка.НоменклатураПредставление, 
					Строка.ХарактеристикаПредставление);
			ОбластьМакета.Параметры.КалькуляционнаяЕдиница = "" + Строка.КалькуляционнаяЕдиница + " " + Строка.ЕдиницаИзмерения;
			
			Если ДанныеПечати.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
				ОбластьМакета.Параметры.ПредставлениеОбъекта = Строка.Объект;
			ИначеЕсли ДанныеПечати.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.ЗаказНаПроизводство Тогда
				ОбластьМакета.Параметры.ПредставлениеОбъекта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонПредставленияОбъекта,
					ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(Строка,, КодЯзыкаПечать),
					Строка.НомерСтроки);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
			ВыводитьИмяТаблиц = Ложь;
		Иначе
			
			ОбластьИмяТаблицы       = Макет.ПолучитьОбласть("ИмяТаб_"   + ДанныеПечати.ЗаголовокОбласти);
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаб_" + ДанныеПечати.ЗаголовокОбласти + ?(ВыводитьКоды, "_Код", ""));
			ОбластьПодвалТаблицы    = Макет.ПолучитьОбласть("Подвал_"   + ДанныеПечати.ЗаголовокОбласти);
			
			Если ВыводитьКоды Тогда
				ОбластьЗаголовокТаблицы.Параметры.ИмяКолонкиКодов = КолонкаКодов;
			КонецЕсли; 
			
			Если ДанныеПечати.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
				ОбластьКалькуляционнаяЕдиница       = Макет.ПолучитьОбласть("Заголовок_КалькуляционнаяЕдиница");
				ОбластьКалькуляционнаяЕдиница.Параметры.КалькуляционнаяЕдиница = "" + ДанныеПечати.КалькуляционнаяЕдиница + " " + ВыборкаДействиеКалькуляции[0].ЕдиницаИзмерения;
				ТабличныйДокумент.Вывести(ОбластьКалькуляционнаяЕдиница);
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьИмяТаблицы);
			ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
			
			Если ДанныеПечати.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.Изделие Тогда
				
				ОбластьСтрока = Макет.ПолучитьОбласть("Строка_"   + ДанныеПечати.ЗаголовокОбласти + ?(ВыводитьКоды, "_Код", ""));
				
				Для Каждого Строка Из ВыборкаДействиеКалькуляции Цикл
					
					СтруктураПараметров.Очистить();
					
					СтруктураПараметров.Вставить("НомерСтроки", Строка.НомерСтроки);
					
					СтруктураПараметров.Вставить("НоменклатураПредставление", НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
						Строка.НоменклатураПредставление, Строка.ХарактеристикаПредставление));
						
					СтруктураПараметров.Вставить("ЕдиницаИзмерения", Строка.ЕдиницаИзмерения);
						
					Если ВыводитьКоды Тогда
						СтруктураПараметров.Вставить("Артикул", Строка["Номенклатура" + КолонкаКодов]);
					КонецЕсли;
					
					ОбластьСтрока.Параметры.Заполнить(СтруктураПараметров);
					ТабличныйДокумент.Вывести(ОбластьСтрока);
					
				КонецЦикла;
				
			ИначеЕсли ДанныеПечати.ОбъектКалькуляции = Перечисления.ОбъектыКалькуляции.РесурснаяСпецификация Тогда
				
				ОбластьСтрока1 = Макет.ПолучитьОбласть("Строка_"   + ДанныеПечати.ЗаголовокОбласти);
				ОбластьСтрока2 = Макет.ПолучитьОбласть("Строка_"   + ДанныеПечати.ЗаголовокОбласти + "Изделие" + ?(ВыводитьКоды, "_Код", ""));
				
				ТекущаяСпецификация = Справочники.РесурсныеСпецификации.ПустаяСсылка();
				
				Для Каждого Строка Из ВыборкаДействиеКалькуляции Цикл
					
					Если Строка.Объект <> ТекущаяСпецификация Тогда
						ОбластьСтрока1.Параметры.Спецификация = Строка.Объект;
						ОбластьСтрока1.Параметры.НомерСтроки = Строка.НомерСтроки;
						ТабличныйДокумент.Вывести(ОбластьСтрока1);
						ТекущаяСпецификация = Строка.Объект;
					КонецЕсли;
					
					НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
						Строка.НоменклатураПредставление, 
						Строка.ХарактеристикаПредставление);
						
					ОбластьСтрока2.Параметры.Заполнить(Строка);
					ОбластьСтрока2.Параметры.НоменклатураПредставление = НоменклатураПредставление;
					Если ВыводитьКоды Тогда
						ОбластьСтрока2.Параметры.Артикул =Строка["Номенклатура" + КолонкаКодов];
					КонецЕсли;
					
					ТабличныйДокумент.Вывести(ОбластьСтрока2);
					
				КонецЦикла;
				
			Иначе
				
				ОбластьСтрока1 = Макет.ПолучитьОбласть("Строка_"   + ДанныеПечати.ЗаголовокОбласти);
				ОбластьСтрока2 = Макет.ПолучитьОбласть("Строка_"   + ДанныеПечати.ЗаголовокОбласти + "Изделие" + ?(ВыводитьКоды, "_Код", ""));
				
				ТекущийЗаказ = Документы.ЗаказНаПроизводство.ПустаяСсылка();
				ТекущийКодСтроки = -1;
				
				НомерСтроки = 1;
				
				Для Каждого Строка Из ВыборкаДействиеКалькуляции Цикл
					
					Если Строка.Объект <> ТекущийЗаказ Или Строка.КодСтрокиЗаказаНаПроизводство <> ТекущийКодСтроки Тогда
						ОбластьСтрока1.Параметры.ЗаказНаПроизводство = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонПредставленияОбъекта,
							ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(Строка,, КодЯзыкаПечать),
							Строка.НомерСтроки);
						ОбластьСтрока1.Параметры.НомерСтроки = НомерСтроки;
						ТабличныйДокумент.Вывести(ОбластьСтрока1);
						ТекущийЗаказ = Строка.Объект;
						ТекущийКодСтроки = Строка.КодСтрокиЗаказаНаПроизводство;
						НомерСтроки = НомерСтроки + 1;
					КонецЕсли;
					
					НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
						Строка.НоменклатураПредставление, 
						Строка.ХарактеристикаПредставление);
						
					ОбластьСтрока2.Параметры.Заполнить(Строка);
					
					ОбластьСтрока2.Параметры.НоменклатураПредставление = НоменклатураПредставление;
					Если ВыводитьКоды Тогда
						ОбластьСтрока2.Параметры.Артикул =Строка["Номенклатура" + КолонкаКодов];
					КонецЕсли;
					
					ТабличныйДокумент.Вывести(ОбластьСтрока2);
					
				КонецЦикла;
				
			КонецЕсли;
			
			ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
		КонецЕсли;
		
		ОбластьИмяТаблицы       = Макет.ПолучитьОбласть("ИмяТаб_Калькуляция");
		ОбластьПодвалТаблицы    = Макет.ПолучитьОбласть("Подвал_Калькуляция");
		ОбластьИтог             = Макет.ПолучитьОбласть("Итоги_Калькуляция");
		ОбластьПодписи          = Макет.ПолучитьОбласть("Подписи");
		
		Если ДанныеПечати.ВыводитьИерархию Тогда
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаб_КалькуляцияИерархия");
			ОбластьСтрокаГруппы     = Макет.ПолучитьОбласть("Строка_ИерархияГруппа");
			ОбластьСтрокаЭлемента   = Макет.ПолучитьОбласть("Строка_ИерархияЭлемент");
		Иначе
			ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаб_КалькуляцияСписок");
			ОбластьСтрокаГруппы     = Макет.ПолучитьОбласть("Строка_Элемент");
			ОбластьСтрокаЭлемента   = Макет.ПолучитьОбласть("Строка_Элемент");
		КонецЕсли;
		
		Если ВыводитьИмяТаблиц Тогда
			ТабличныйДокумент.Вывести(ОбластьИмяТаблицы);
		КонецЕсли;
		
		ОбластьЗаголовокТаблицы.Параметры.Валюта = ДанныеПечати.Валюта;
		ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);
		
		Если ДанныеПечати.ВыводитьИерархию Тогда
			ТабличныйДокумент.НачатьАвтогруппировкуСтрок();
			КалькуляцияИерархия.НайтиСледующий(СтруктураПоиска);
			Итого = КалькуляцияИерархия.Сумма;
			Калькуляция = КалькуляцияИерархия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "СтатьяКалькуляции");
			Отступ = 0;
			ВывестиСтрокуКалькуляции(ТабличныйДокумент, ОбластьСтрокаГруппы, ОбластьСтрокаЭлемента, Калькуляция, Итого, Отступ);
			ТабличныйДокумент.ЗакончитьАвтогруппировкуСтрок();
			КалькуляцияИерархия.Сбросить();
		Иначе
			
			Калькуляция = КалькуляцияСписок.Скопировать(СтруктураПоиска);
			Итого = Калькуляция.Итог("Сумма");
			
			Для Каждого Строка Из Калькуляция Цикл
				ОбластьСтрокаЭлемента.Параметры.Заполнить(Строка);
				ОбластьСтрокаЭлемента.Параметры.УдельныйВес = 100 * Строка.Сумма / Итого;
				ТабличныйДокумент.Вывести(ОбластьСтрокаЭлемента);
			КонецЦикла;
			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
		
		ОбластьИтог.Параметры.Всего = Итого;
		ОбластьИтог.Параметры.Валюта = ДанныеПечати.Валюта;
		ТабличныйДокумент.Вывести(ОбластьИтог);
		
		ОбластьПодписи.Параметры.Ответственный =  ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.ФизЛицо, ДанныеПечати.Дата);
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Процедура ВывестиСтрокуКалькуляции(ТабличныйДокумент, ОбластьСтрокаГруппы, ОбластьСтрокаЭлемента, Выборка, Итого, Отступ)
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
			
			ОбластьСтрокаГруппы.Параметры.Заполнить(Выборка);
			ОбластьСтрокаГруппы.Параметры.УдельныйВес = 100 * Выборка.Сумма / Итого;
			ОбластьМакета = ТабличныйДокумент.Вывести(ОбластьСтрокаГруппы, Отступ);
			ТабличныйДокумент.Область(ОбластьМакета.Верх,2).Отступ = Отступ;
			
			ВыборкаДетальныеЗаписи = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "СтатьяКалькуляции");
			ВывестиСтрокуКалькуляции(ТабличныйДокумент, ОбластьСтрокаГруппы, ОбластьСтрокаЭлемента, ВыборкаДетальныеЗаписи, Итого, Отступ + 1);
			
		Иначе
			
			ОбластьСтрокаЭлемента.Параметры.Заполнить(Выборка);
			ОбластьСтрокаЭлемента.Параметры.УдельныйВес = 100 * Выборка.Сумма / Итого;
			
			ОбластьМакета = ТабличныйДокумент.Вывести(ОбластьСтрокаЭлемента, Отступ);
			ТабличныйДокумент.Область(ОбластьМакета.Верх,1+1).Отступ = Отступ;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли