#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
//++ НЕ УТ
#Область ПроведениеПоРеглУчетуУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
#Область ТекстПогрешностьРасчетаСебестоимостиТовары // (Дт 9X :: Кт 28)

	ТекстПогрешностьРасчетаСебестоимостиТовары = "
	|ВЫБРАТЬ //// Погрешность расчета себестоимости (Дт 9X :: Кт 28)
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ВЫБОР КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) ТОГДА
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.ПередачаНаКомиссию)
	|	КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.НаСкладе)
	|	КОНЕЦ КАК ВидСчетаКт,
	|	Строки.ГруппаФинансовогоУчета КАК АналитикаУчетаКт,
	|	ВЫБОР КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Строки.Склад
	|	КОНЕЦ КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Склад.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ВЫБОР КОГДА Строки.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	// |		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ТоварыКОформлениюОтчетовКомитенту)
	|		ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ВычетыИзДохода)
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетКт,
	|
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ВыручкаОтПродаж)
	|	ИНАЧЕ
	|		Строки.Номенклатура
	|	КОНЕЦ КАК СубконтоКт1,
	|	ВЫБОР КОГДА Строки.Склад ССЫЛКА Справочник.Партнеры ТОГДА
	|		Строки.ГруппаФинансовогоУчета
	|	ИНАЧЕ
	|		Строки.Склад
	|	КОНЕЦ КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Погрешность расчета себестоимости (товары)"" КАК Содержание
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Аналитика.Номенклатура КАК Номенклатура,
	|			Аналитика.Склад КАК Склад,
	|			ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|				Движения.ВидЗапасов.ГруппаФинансовогоУчета
	|			ИНАЧЕ
	|				Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|			КОНЕЦ КАК ГруппаФинансовогоУчета,
	|			Назначения.НаправлениеДеятельности КАК НаправленияДеятельности,
	|			Движения.СтатьяРасходовСписания КАК СтатьяРасходов,
	|			Движения.РазделУчета,
	|			ЕСТЬNULL(Комиссия.Контрагент, Движения.ВидЗапасов.Контрагент) КАК Контрагент,
	|			СУММА(Движения.СтоимостьРегл) КАК Сумма
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Движения
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО
	|				Движения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ
	|				Справочник.ВидыЗапасов КАК ВидыЗапасов
	|			ПО
	|				Движения.ВидЗапасов = ВидыЗапасов.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ
	|				Справочник.Назначения КАК Назначения
	|			ПО
	|				ВидыЗапасов.Назначение = Назначения.Ссылка
	|
	|			ЛЕВОЕ СОЕДИНЕНИЕ Комиссия КАК Комиссия
	|				ПО Комиссия.Организация = Движения.Организация
	|				И Комиссия.АналитикаУчетаНоменклатуры = Движения.АналитикаУчетаНоменклатуры
	|				И Комиссия.ВидЗапасов = Движения.ВидЗапасов
	|				И Комиссия.РазделУчета = Движения.РазделУчета
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.РазделУчета В (
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах),
	|				ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию))
	|			И Движения.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|			И Движения.ДокументДвижения В (НЕОПРЕДЕЛЕНО, &Ссылка)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Движения.РазделУчета,
	|			Аналитика.Номенклатура,
	|			Аналитика.Склад,
	|			ЕСТЬNULL(Комиссия.Контрагент, Движения.ВидЗапасов.Контрагент),
	|			ВЫБОР КОГДА &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|				Движения.ВидЗапасов.ГруппаФинансовогоУчета
	|			ИНАЧЕ
	|				Аналитика.Номенклатура.ГруппаФинансовогоУчета
	|			КОНЕЦ,
	|			Назначения.НаправлениеДеятельности,
	|			Движения.СтатьяРасходовСписания
	|		) КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.Сумма <> 0
	|";
#КонецОбласти

#Область ТекстПогрешностьРасчетаСебестоимостиПереработка // (Дт 9X :: Кт 206)

	ТекстПогрешностьРасчетаСебестоимостиПереработка = "
	|ВЫБРАТЬ //// Погрешность расчета себестоимости (Дт 9X :: Кт 206)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.МатериалыПереданныеВПереработку) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	Строки.Номенклатура КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Погрешность расчета себестоимости (переработка)"" КАК Содержание
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Аналитика.Номенклатура КАК Номенклатура,
	|			Движения.СтатьяРасходовСписания КАК СтатьяРасходов,
	|			СУММА(Движения.СтоимостьРегл) КАК Сумма
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Движения
	|
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|			ПО
	|				Движения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеПереработчику)
	|			И Движения.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|			И Движения.ДокументДвижения В (НЕОПРЕДЕЛЕНО, &Ссылка)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Аналитика.Номенклатура,
	|			Движения.СтатьяРасходовСписания
	|		) КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.Сумма <> 0
	|";
#КонецОбласти
	
#Область ТекстПогрешностьРасчетаСебестоимостиНЗП // (Дт 9X :: Кт 23)

	ТекстПогрешностьРасчетаСебестоимостиНЗП = "
	|ВЫБРАТЬ //// Погрешность расчета себестоимости (Дт 9X :: Кт 23)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Строки.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости) КАК АналитикаУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости) КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Строки.МестоУчетаКт КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Строки.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Погрешность расчета себестоимости (НЗП)"" КАК Содержание
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК Операция
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Аналитика.Склад КАК МестоУчетаКт,
	|			ЕСТЬNULL(Переработка.Подразделение, Аналитика.Склад) КАК Подразделение,
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|			СУММА(Движения.СтоимостьРегл) КАК Сумма
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Движения
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|				ПО Движения.АналитикаУчетаНоменклатуры = Аналитика.КлючАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ Переработка КАК Переработка
	|				ПО Переработка.Организация = Движения.Организация
	|				И Переработка.АналитикаУчетаНоменклатуры = Движения.АналитикаУчетаНоменклатуры
	|				И Переработка.ВидЗапасов = Движения.ВидЗапасов
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			И Движения.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|			И Движения.ДокументДвижения В (НЕОПРЕДЕЛЕНО, &Ссылка)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Аналитика.Склад,
	|			ЕСТЬNULL(Переработка.Подразделение, Аналитика.Склад),
	|			Движения.СтатьяРасходовСписания
	|
	|		ОБЪЕДИНИТЬ ВСЕ
	|
	|		ВЫБРАТЬ
	|			Движения.Организация КАК Организация,
	|			Движения.Подразделение КАК МестоУчетаКт,
	|			Движения.Подразделение КАК Подразделение,
	|			Движения.СтатьяРасходов КАК СтатьяРасходов,
	|			СУММА(Движения.СуммаРегл) КАК Сумма
	|		ИЗ
	|			РегистрНакопления.ПрочиеРасходы КАК Движения
	|		ГДЕ
	|			Движения.Регистратор = &Ссылка
	|			И Движения.Активность
	|			И Движения.СтатьяРасходов.ВариантРаспределенияРасходов =
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			И Движения.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|			И Движения.СтатьяРасходов <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|
	|		СГРУППИРОВАТЬ ПО
	|			Движения.Организация,
	|			Движения.Подразделение,
	|			Движения.СтатьяРасходов
	|
	|		) КАК Строки
	|	ПО
	|		ИСТИНА
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Строки.Сумма <> 0
	|";
#КонецОбласти
	
	Возврат ТекстПогрешностьРасчетаСебестоимостиТовары
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПогрешностьРасчетаСебестоимостиНЗП
		+ "ОБЪЕДИНИТЬ ВСЕ" + ТекстПогрешностьРасчетаСебестоимостиПереработка;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламентированном учете.
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт

	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	МАКСИМУМ(Движения.Подразделение) КАК Подразделение
	|ПОМЕСТИТЬ
	|	Переработка
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Погрешность
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Погрешность.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Организация = Погрешность.Организация
	|		И Движения.АналитикаУчетаНоменклатуры = Погрешность.АналитикаУчетаНоменклатуры
	|		И Движения.РазделУчета = Погрешность.РазделУчета
	|		И Движения.ВидЗапасов = Погрешность.ВидЗапасов
	|ГДЕ
	|	Погрешность.Регистратор = &Ссылка
	|	И Погрешность.Активность
	|	И Погрешность.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	И Погрешность.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И Аналитика.Склад ССЫЛКА Справочник.Партнеры
	|	И Движения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Движения.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов
	|;
	|ВЫБРАТЬ
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	Погрешность.РазделУчета,
	|	МАКСИМУМ(АналитикаКомиссионера.Контрагент) КАК Контрагент
	|ПОМЕСТИТЬ
	|	Комиссия
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Погрешность
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО Аналитика.КлючАналитики = Погрешность.АналитикаУчетаНоменклатуры
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СебестоимостьТоваров КАК Движения
	|		ПО Движения.Организация = Погрешность.Организация
	|		И Движения.АналитикаУчетаНоменклатуры = Погрешность.АналитикаУчетаНоменклатуры
	|		И Движения.РазделУчета = Погрешность.РазделУчета
	|		И Движения.ВидЗапасов = Погрешность.ВидЗапасов
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаКомиссионера
	|		ПО АналитикаКомиссионера.КлючАналитики = Движения.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	Погрешность.Регистратор = &Ссылка
	|	И Погрешность.Активность
	|	И Погрешность.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	И Погрешность.СтатьяРасходовСписания = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПогрешностьРасчетаСебестоимости)
	|	И Аналитика.Склад ССЫЛКА Справочник.Партнеры
	|	И Движения.Период МЕЖДУ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) И КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)
	|	И Движения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|СГРУППИРОВАТЬ ПО
	|	Погрешность.Организация,
	|	Погрешность.АналитикаУчетаНоменклатуры,
	|	Погрешность.ВидЗапасов,
	|	Погрешность.РазделУчета
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти
//-- НЕ УТ
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт
	
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);
	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНаОсновании

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РасчетСебестоимостиТоваров) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РасчетСебестоимостиТоваров.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РасчетСебестоимостиТоваров);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


Функция ПолноеИмяДокумента()
    Возврат "Документ.РасчетСебестоимостиТоваров";
КонецФункции 

Процедура СоздатьЗаданияПерерасчетаКомиссии_ДанныеДляОбновления(Параметры) Экспорт
	
	// Для документов "Расчет себестоимости товаров" 
    // необходим перерасчет себестоимости по прошедшим периодам 
	// для комиссионных товаров и давальческих материалов.
    // сам перерасчет себестоимости в обработчике не происходит, 
    // формируются только задания для перерасчета - записи регистра сведений ЗаданияКРасчетуСебестоимости
    
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(СебестоимостьТоваров.Период, Месяц) КАК Месяц,
	|	СебестоимостьТоваров.Организация КАК Организация
	|ПОМЕСТИТЬ Периоды
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров.Обороты(
	|			,
	|			,
	|			Месяц,
	|			РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию)
	|				ИЛИ РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)) КАК СебестоимостьТоваров
	|;
	|
	|ВЫБРАТЬ
	|	РасчетСебестоимости.Ссылка
	|ИЗ
	|	Документ.РасчетСебестоимостиТоваров КАК РасчетСебестоимости
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК Строки
	|		ПО (Строки.Ссылка = РасчетСебестоимости.Ссылка)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Периоды КАК Периоды
	|		ПО (Периоды.Месяц = НАЧАЛОПЕРИОДА(РасчетСебестоимости.Дата, МЕСЯЦ))
	|			И (Периоды.Организация = Строки.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК Стоимости
	|		ПО (Стоимости.Регистратор = РасчетСебестоимости.Ссылка)
	|			И (Стоимости.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаКомиссию), ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.МатериалыПринятыеВПереработку)))
	|ГДЕ
	|	РасчетСебестоимости.Проведен
	|	И Стоимости.Регистратор ЕСТЬ NULL";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
    ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
        Параметры, 
        Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка")
    );	
	
КонецПроцедуры

Процедура СоздатьЗаданияПерерасчетаКомиссии(Параметры) Экспорт
    
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуСсылокДляОбработки(
        Параметры.Очередь, 
        ПолноеИмяДокумента(), 
        МенеджерВременныхТаблиц
    );
    
    Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
    КонецЕсли;
    
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
    
	ЗапросПоДокументам = Новый Запрос;
    ЗапросПоДокументам.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ЗапросПоДокументам.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|	ДокументыКОбработке.Ссылка КАК Документ,
    |   НАЧАЛОПЕРИОДА(РасчетСебестоимостиТоваровОрганизации.Ссылка.Дата, МЕСЯЦ) КАК Месяц,
    |   РасчетСебестоимостиТоваровОрганизации.Организация КАК Организация
	|ИЗ
    |   ВТДляОбработки КАК ДокументыКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК РасчетСебестоимостиТоваровОрганизации
	|		    ПО РасчетСебестоимостиТоваровОрганизации.Ссылка = ДокументыКОбработке.Ссылка
	|";
    
    ЗапросПоДокументам.Текст = СтрЗаменить(ЗапросПоДокументам.Текст, "ВТДляОбработки", Результат.ИмяВременнойТаблицы);
    
    Выборка = ЗапросПоДокументам.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка	
			
            РегистрыСведений.ЗаданияКРасчетуСебестоимости.СоздатьЗаписьРегистра(Выборка.Месяц, Выборка.Документ, Выборка.Организация);
            
            ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Выборка.Документ);
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
            ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(Выборка.Документ);
										
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
        Параметры.Очередь, 
        ПолноеИмяДокумента()
    );

КонецПроцедуры


#КонецОбласти

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
