#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

	
#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Заявление
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.МенеджерПечати = "Документ.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ";
	КомандаПечати.Идентификатор = "ПФ_MXL_UK_Заявление";
	КомандаПечати.Представление = НСтр("ru='Заявление';uk='Заява'");
	КомандаПечати.Порядок = 10;
	КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;
	
КонецПроцедуры

// Формирует печатные формы
//
// Параметры:
//  (входные)
//    МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//    ПараметрыПечати - Структура - дополнительные настройки печати;
//  (выходные)
//   КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы.
//   ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                             представление - имя области в которой был выведен объект;
//   ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов.
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПФ_MXL_UK_Заявление") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,	
			"ПФ_MXL_UK_Заявление",
			НСтр("ru='Заявление';uk='Заява'"), 
			ПечатьЗаявления(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)); 
	КонецЕсли;
						
КонецПроцедуры								
	
Функция ПечатьЗаявления(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ_Заявление";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.ПФ_MXL_UK_Заявление");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Номер,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Дата,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Организация,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник.ФИО КАК ФИО,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник.КодПоДРФО КАК КодПоДРФО,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Льгота.Код КАК Льгота,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.ДокументПодтверждающийПравоНаЛьготу КАК Документ,
		|	КадроваяИсторияСотрудников.Должность
		|ИЗ
		|	Документ.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.ЛичныеЛьготы КАК ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&МоментВремени, ВидСобытия <> &Увольнение) КАК КадроваяИсторияСотрудников
		|		ПО ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник = КадроваяИсторияСотрудников.ФизическоеЛицо
		|			И ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Организация = КадроваяИсторияСотрудников.ГоловнаяОрганизация
		|ГДЕ
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Номер,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Дата,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Организация,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник.ФИО КАК ФИО,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник.КодПоДРФО КАК КодПоДРФО,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Льгота.Код КАК Льгота,
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.ДокументПодтверждающийПравоНаЛьготу КАК Документ,
		|	КадроваяИсторияСотрудников.Должность
		|ИЗ
		|	Документ.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.ЛьготыНаДетей КАК ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КадроваяИсторияСотрудников.СрезПоследних(&МоментВремени, ВидСобытия <> &Увольнение) КАК КадроваяИсторияСотрудников
		|		ПО ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Сотрудник = КадроваяИсторияСотрудников.ФизическоеЛицо
		|			И ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка.Организация = КадроваяИсторияСотрудников.ГоловнаяОрганизация
		|
		|ГДЕ
		|	ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		Запрос.УстановитьПараметр("Увольнение", Перечисления.ВидыКадровыхСобытий.Увольнение);
		Запрос.УстановитьПараметр("МоментВремени", Ссылка.МоментВремени());
		Выборка = Запрос.Выполнить().Выбрать();
		
		Льгота = "";
		НомерДокумента = 1;
		ОбластьЗаявление = Неопределено;
		ПерваяЛьгота = Истина;
		НомерСтрокиНачало = 0;
				
		Пока Выборка.Следующий() Цикл
			
			
			Если Льгота <> Выборка.Льгота Тогда
				Льгота = Выборка.Льгота;
				НомерДокумента = 1;
				
				Если Не ПерваяЛьгота Тогда
					ТабличныйДокумент.Присоединить(ОбластьЗаявление);
						
					// В табличном документе необходимо задать имя области, в которую был 
					// выведен объект. Нужно для возможности печати покомплектно.
					УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
				Иначе
					ПерваяЛьгота = Ложь;
				КонецЕсли;
				
				// Документы нужно выводить на разных страницах.
				Если Не ПервыйДокумент Тогда
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				Иначе
					ПервыйДокумент = Ложь;
				КонецЕсли;
				
				
				// Запомним номер строки, с которой начали выводить текущий документ.
				НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
				
				ОбластьШапка1 = Макет.ПолучитьОбласть("Шапка1");
				ОбластьШапка1.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьШапка1);
			
				ОбластьДРФО = Макет.ПолучитьОбласть("ДРФО");
				ТабличныйДокумент.Присоединить(ОбластьДРФО);
				Счетчик = 1; 
				Пока Счетчик < 11 Цикл
					ОбластьДРФО = Макет.ПолучитьОбласть("КодПоДРФО" + Строка(Счетчик));	
					КодПоДРФО = Сред(Выборка.КодПоДРФО, Счетчик, 1);
					ОбластьДРФО.Параметры.Номер = КодПоДРФО;
					ТабличныйДокумент.Присоединить(ОбластьДРФО);
					Счетчик = Счетчик + 1;
				КонецЦикла;
				
				ОбластьШапка2 = Макет.ПолучитьОбласть("Шапка2");
				Областьшапка2.Параметры.Заполнить(Выборка);
				ТабличныйДокумент.Присоединить(ОбластьШапка2);		
				
				ОбластьЗаявление = Макет.ПолучитьОбласть("ПрименениеЛьготы");
				КодЛьготы = СтрЗаменить(Выборка.Льгота,"ВР","");
				ОбластьЗаявление.Параметры.Норма = КодЛьготы;
				ОбластьЗаявление.Параметры["Документ"+НомерДокумента] = Выборка.Документ;
				
				
			Иначе
				НомерДокумента = Мин(НомерДокумента + 1,5);
				
				ОбластьЗаявление.Параметры["Документ"+НомерДокумента] = Выборка.Документ;
				
			КонецЕсли;
			
			
		КонецЦикла;	
		
		Если НомерСтрокиНачало > 0 Тогда
			ТабличныйДокумент.Присоединить(ОбластьЗаявление);
					
			// В табличном документе необходимо задать имя области, в которую был 
			// выведен объект. Нужно для возможности печати покомплектно.
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		КонецЕсли;
		
	КонецЦикла;	
						
	Возврат ТабличныйДокумент;
	
КонецФункции

	
#КонецОбласти

Функция КонфликтующиеРегистраторы(Регистратор, Месяц, Сотрудник, МассивЛьгот) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|
		|ВЫБРАТЬ
		|	ЛьготыНаДетейНДФЛ.Регистратор,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЛьготыНаДетейНДФЛ.Регистратор)
		|ИЗ
		|	РегистрСведений.ЛьготыНаДетейНДФЛ КАК ЛьготыНаДетейНДФЛ
		|ГДЕ
		|	ЛьготыНаДетейНДФЛ.МесяцРегистрации = &Период
		|	И ЛьготыНаДетейНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
		|	И ЛьготыНаДетейНДФЛ.Льгота В(&Льготы)
		|	И ЛьготыНаДетейНДФЛ.Регистратор <> &Регистратор
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛьготыФизическихЛицНДФЛ.Регистратор,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЛьготыФизическихЛицНДФЛ.Регистратор)
		|ИЗ
		|	РегистрСведений.ЛьготыФизическихЛицНДФЛ КАК ЛьготыФизическихЛицНДФЛ
		|ГДЕ
		|	ЛьготыФизическихЛицНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
		|	И ЛьготыФизическихЛицНДФЛ.Период = &Период
		|	И ЛьготыФизическихЛицНДФЛ.Регистратор <> &Регистратор";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо",	Сотрудник);
	Запрос.УстановитьПараметр("Период",			Месяц);
	Запрос.УстановитьПараметр("Регистратор",	Регистратор);
	Запрос.УстановитьПараметр("Льготы",	МассивЛьгот);
	
	Возврат Запрос;
	
КонецФункции

#КонецОбласти

#КонецЕсли