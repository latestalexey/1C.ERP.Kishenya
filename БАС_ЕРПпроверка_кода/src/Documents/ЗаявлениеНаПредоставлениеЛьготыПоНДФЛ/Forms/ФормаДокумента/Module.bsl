
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ЗначенияДляЗаполнения = Новый Структура("Месяц, Организация, Ответственный", 
		"Объект.Месяц",
		"Объект.Организация",
		"Объект.Ответственный");
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ПрочитатьЛьготыПредыдущегоМесяца();
		
		ПриПолученииДанныхНаСервере();
		
	Иначе
		
		ЛьготыНаДетейБылиИзменены = Истина;
		
	КонецЕсли;
	
	Если Параметры.ИзФормыСотрудника Тогда
		Элементы.Организация.ТолькоПросмотр		= Истина;
		Элементы.ФизическоеЛицо.ТолькоПросмотр	= Истина;
	КонецЕсли;
	
	Элементы.ЛьготыНаДетей.Доступность = Объект.ИзменитьЛьготыНаДетей;
	Элементы.ЛичныеЛьготы.Доступность = Объект.ИзменитьЛичнуюЛьготу;
	
	УчетНДФЛФормыВнутренний.УстановитьПараметрыВыбораСотрудниковВДокументахПредоставленияЛьгот(ЭтаФорма, "ФизическоеЛицо");
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Сообщения = ПолучитьСообщенияПользователю(Ложь);
	Для Каждого Сообщение Из Сообщения Цикл
		Если СтрНайти(Сообщение.Поле, "ЛьготыНаДетей") <> 0 И СтрНайти(Сообщение.Поле, "ДействуетДо") <> 0 Тогда
			Сообщение.Поле			= Сообщение.Поле + "Строкой";
			
		ИначеЕсли Сообщение.Поле = "Месяц" Тогда
			Сообщение.ПутьКДанным	= Сообщение.Поле + "Строкой";
			Сообщение.Поле			= "";
			
		КонецЕсли;
		
		Отказ = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьОписаниеЛьготНаДетей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененыЛьготы", Объект.Сотрудник, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	ПрочитатьЛьготыПредыдущегоМесяца();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформацияОВозможностиПроведенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОтменаЗаявления" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОтменитьЗаявлениеОПрекращенииЛьгот();
		
		ПоказатьПредупреждение(, НСтр("ru='Заявление о прекращении предоставления льгот отменено';uk='Заяву про припинення надання пільг скасовано'"));
		
	КонецЕсли; 
	
КонецПроцедуры

#Область РедактированиеМесяцаСтрокой

&НаКлиенте
Процедура МесяцНачисленияСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(ЭтаФорма, "Объект.Месяц", "МесяцСтрокой", Модифицированность);
	
	ПрочитатьЛьготыПредыдущегоМесяца();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("МесяцСтрокойНачалоВыбораЗавершение", ЭтотОбъект);
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, ЭтаФорма, "Объект.Месяц", "МесяцСтрокой", , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцСтрокойНачалоВыбораЗавершение(ЗначениеВыбрано, ДополнительныеПараметры) Экспорт 
	
	ПрочитатьЛьготыПредыдущегоМесяца();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(ЭтаФорма, "Объект.Месяц", "МесяцСтрокой", Направление, Модифицированность);
	
	ПрочитатьЛьготыПредыдущегоМесяца();
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МесяцНачисленияСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
	ПрочитатьЛьготыПредыдущегоМесяца();
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТабличногоПоляЛьготыНаДетей

&НаКлиенте
Процедура ЛьготыНаДетейПриИзменении(Элемент)
	
	ЛьготыНаДетейБылиИзменены = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейДействуетДоСтрокойПриИзменении(Элемент)
	
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.ЛьготыНаДетей.ТекущиеДанные, "ДействуетДо", "ДействуетДоСтрокой", Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейДействуетДоСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.ЛьготыНаДетей.ТекущиеДанные, "ДействуетДо", "ДействуетДоСтрокой");
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейДействуетДоСтрокойРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.ЛьготыНаДетей.ТекущиеДанные, "ДействуетДо", "ДействуетДоСтрокой", Направление, Модифицированность);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейДействуетДоСтрокойАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейДействуетДоСтрокойОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		Элемент.ТекущиеДанные.ДействуетДо = НачалоМесяца(КонецГода(Объект.Дата));
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.ЛьготыНаДетей, "ДействуетДо", "ДействуетДоСтрокой");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТабличногоПоляЛичныеЛьготы

&НаКлиенте
Процедура ЛичныеЛьготыПриИзменении(Элемент)
	
	ЛичныеЛьготыБылиИзменены = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЛьготыНаДетейПриИзменении(Элемент)
	
	Элементы.ЛьготыНаДетей.Доступность		= Объект.ИзменитьЛьготыНаДетей;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыНаДетейЛьготаПриИзменении(Элемент)
	
	Элементы.ЛьготыНаДетей.ТекущиеДанные.ЛьготаИнфо = УчетНДФЛВызовСервера.ПолучитьОписаниеЛьготы(Элементы.ЛьготыНаДетей.ТекущиеДанные.Льгота);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЛичныеЛьготыПриИзменении(Элемент)
	
	Элементы.ЛичныеЛьготы.Доступность		= Объект.ИзменитьЛичнуюЛьготу;

КонецПроцедуры


#КонецОбласти


#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "Объект.Месяц", "МесяцСтрокой");
	
	ОбновитьОписаниеЛьготНаДетей();
	ОбновитьОписаниеЛичныхЛьгот();
	УстановитьИнформационнуюНадписьОВозможностиПроведения();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеЛьготНаДетей()
	
	Для Каждого Строка Из Объект.ЛьготыНаДетей Цикл
		Строка.ЛьготаИнфо = УчетНДФЛВызовСервера.ПолучитьОписаниеЛьготы(Строка.Льгота);
	КонецЦикла;
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(Объект.ЛьготыНаДетей, "ДействуетДо", "ДействуетДоСтрокой");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеЛичныхЛьгот()
	
	Для Каждого Строка Из Объект.ЛичныеЛьготы Цикл
		Строка.ЛьготаИнфо = УчетНДФЛВызовСервера.ПолучитьОписаниеЛьготы(Строка.Льгота);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЛьготыПредыдущегоМесяца()
	
	Если ЛьготыНаДетейБылиИзменены Или ЛичныеЛьготыБылиИзменены Или Объект.Сотрудник.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Месяц",			Объект.Месяц);
	Запрос.УстановитьПараметр("ФизическоеЛицо",	Объект.Сотрудник);
	Запрос.УстановитьПараметр("Ссылка",			Объект.Ссылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛьготыФизическихЛицНДФЛСрезПоследних.Льгота
	|ИЗ
	|	РегистрСведений.ЛьготыФизическихЛицНДФЛ.СрезПоследних(
	|			&Месяц,
	|			Регистратор <> &Ссылка
	|				И ФизическоеЛицо = &ФизическоеЛицо) КАК ЛьготыФизическихЛицНДФЛСрезПоследних";
	Выборка = Запрос.Выполнить().Выбрать();
	Объект.ЛичныеЛьготы.Очистить();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Объект.ЛичныеЛьготы.Добавить(), Выборка);
	КонецЦикла;
	Объект.ИзменитьЛичнуюЛьготу = Не Объект.ИзменитьЛичнуюЛьготу Или Объект.ЛичныеЛьготы.Количество() <> 0;
	
	Элементы.ЛичныеЛьготы.Доступность		= Объект.ИзменитьЛичнуюЛьготу;
	
	ОбновитьОписаниеЛичныхЛьгот();
	
	Запрос.УстановитьПараметр("ПредыдущийМесяц",	ДобавитьМесяц(Объект.Месяц, -1));
	Запрос.УстановитьПараметр("КонецГода",			НачалоМесяца(КонецГода(Объект.Месяц)));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЛьготыНаДетейНДФЛ.ФизическоеЛицо,
	|	МАКСИМУМ(ЛьготыНаДетейНДФЛ.МесяцРегистрации) КАК МесяцРегистрации
	|ПОМЕСТИТЬ ВТЛьготыНаДетейНДФЛСрезПоследних
	|ИЗ
	|	РегистрСведений.ЛьготыНаДетейНДФЛ КАК ЛьготыНаДетейНДФЛ
	|ГДЕ
	|	ЛьготыНаДетейНДФЛ.МесяцРегистрации < &Месяц
	|	И ЛьготыНаДетейНДФЛ.ФизическоеЛицо = &ФизическоеЛицо
	|
	|СГРУППИРОВАТЬ ПО
	|	ЛьготыНаДетейНДФЛ.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЛьготыНаДетейНДФЛ.Льгота,
	|	ВЫБОР
	|		КОГДА ЛьготыНаДетейНДФЛ.ДействуетДо = &ПредыдущийМесяц
	|			ТОГДА &КонецГода
	|		ИНАЧЕ ЛьготыНаДетейНДФЛ.ДействуетДо
	|	КОНЕЦ КАК ДействуетДо,
	|	ЛьготыНаДетейНДФЛ.КоличествоДетей
	|ИЗ
	|	РегистрСведений.ЛьготыНаДетейНДФЛ КАК ЛьготыНаДетейНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЛьготыНаДетейНДФЛСрезПоследних КАК ЛьготыНаДетейНДФЛСрезПоследних
	|		ПО ЛьготыНаДетейНДФЛ.МесяцРегистрации = ЛьготыНаДетейНДФЛСрезПоследних.МесяцРегистрации
	|			И ЛьготыНаДетейНДФЛ.ФизическоеЛицо = ЛьготыНаДетейНДФЛСрезПоследних.ФизическоеЛицо
	|ГДЕ
	|	ЛьготыНаДетейНДФЛ.КоличествоДетей > 0
	|	И ЛьготыНаДетейНДФЛ.ДействуетДо >= &ПредыдущийМесяц
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЛьготыНаДетейНДФЛ.Льгота.Код,
	|	ДействуетДо";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Объект.ЛьготыНаДетей.Очистить();
	Пока Выборка.Следующий() Цикл
		Для Номер = 1 По Выборка.КоличествоДетей Цикл
			ЗаполнитьЗначенияСвойств(Объект.ЛьготыНаДетей.Добавить(), Выборка);
		КонецЦикла;
	КонецЦикла;
	Объект.ИзменитьЛьготыНаДетей = Не Объект.ИзменитьЛьготыНаДетей Или Объект.ЛьготыНаДетей.Количество() <> 0;
	
	Элементы.ЛьготыНаДетей.Доступность		= Объект.ИзменитьЛьготыНаДетей;
	
	ОбновитьОписаниеЛьготНаДетей();
	УстановитьИнформационнуюНадписьОВозможностиПроведения();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьИнформационнуюНадписьОВозможностиПроведения()
	
	ИнформацияОВозможностиПроведения = "";
	
	МассивЛьгот = Новый Массив;
	Для Каждого СтрокаЛьготы Из Объект.ЛьготыНаДетей Цикл
		МассивЛьгот.Добавить(СтрокаЛьготы.Льгота);
	КонецЦикла;
	
	Запрос = Документы.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.КонфликтующиеРегистраторы(
		Объект.Ссылка, Объект.Месяц, Объект.Сотрудник, МассивЛьгот);
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ИнформацияОВозможностиПроведения = Новый ФорматированнаяСтрока(
			НСтр("ru='Документ не может быть проведен. На';uk='Документ не може бути проведений. На'") + " ",
			НРег(Формат(Объект.Месяц, "ДФ='ММММ гггг'")) + " ",
			НСтр("ru='уже введено заявление';uk='вже введено заяву'")
		);
			
		Если ТипЗнч(Выборка.Регистратор) = Тип("ДокументСсылка.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ") Тогда
			
			ИнформацияОВозможностиПроведения = Новый ФорматированнаяСтрока(
				ИнформацияОВозможностиПроведения,
				" " + НСтр("ru='на льготы';uk='на пільги'"),
				" (",
				Новый ФорматированнаяСтрока(Выборка.РегистраторПредставление, , , , ПолучитьНавигационнуюСсылку(Выборка.Регистратор)),
				")"
			);
			
		Иначе
			
			ИнформацияОВозможностиПроведения = Новый ФорматированнаяСтрока(
				ИнформацияОВозможностиПроведения,
				" " + НСтр("ru='о прекращении льгот';uk='про припинення пільг'")
			);
			
			ОрганизацияРегистратора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Регистратор, "Организация");
			Если Объект.Организация = ОрганизацияРегистратора Тогда

				ИнформацияОВозможностиПроведения = Новый ФорматированнаяСтрока(
					ИнформацияОВозможностиПроведения,
					" (",
					Новый ФорматированнаяСтрока(Выборка.РегистраторПредставление, , , , ПолучитьНавигационнуюСсылку(Выборка.Регистратор)),
					")"
				);
				
			Иначе
				
				ИнформацияОВозможностиПроведения = Новый ФорматированнаяСтрока(
					ИнформацияОВозможностиПроведения,
					" " + НСтр("ru='в другой организации. Если необходимо перенести применение льгот в';uk='в іншій організації. Якщо необхідно перенести застосування пільг в'") + " ",
					Строка(Объект.Организация) + ", ",
					Новый ФорматированнаяСтрока("отмените заявление", , , , "ОтменаЗаявления"),
					" " + НСтр("ru='о прекращении льгот и проведите это заявление';uk='про припинення пільг і проведіть цю заяву'") + "."
				);
				
			КонецЕсли;
			
		КонецЕсли; 
		
		
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ИнформацияОВозможностиПроведения",
		"Заголовок",
		ИнформацияОВозможностиПроведения
	);
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьЗаявлениеОПрекращенииЛьгот()
	
	МассивЛьгот = Новый Массив;
	Для Каждого СтрокаЛьготы Из Объект.ЛьготыНаДетей Цикл
		МассивЛьгот.Добавить(СтрокаЛьготы.Льгота);
	КонецЦикла;
	
	Для Каждого СтрокаЛьготы Из Объект.ЛичныеЛьготы Цикл
		МассивЛьгот.Добавить(СтрокаЛьготы.Льгота);
	КонецЦикла;
	
	Запрос = Документы.ЗаявлениеНаПредоставлениеЛьготыПоНДФЛ.КонфликтующиеРегистраторы(
		Объект.Ссылка, Объект.Месяц, Объект.Сотрудник, МассивЛьгот);
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Заявление = Выборка.Регистратор.ПолучитьОбъект();
		Заявление.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		
	КонецЕсли;
	
	УстановитьИнформационнуюНадписьОВозможностиПроведения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛичныеЛьготыЛьготаПриИзменении(Элемент)
	
	Элементы.ЛичныеЛьготы.ТекущиеДанные.ЛьготаИнфо = УчетНДФЛВызовСервера.ПолучитьОписаниеЛьготы(Элементы.ЛичныеЛьготы.ТекущиеДанные.Льгота);

КонецПроцедуры

#КонецОбласти
