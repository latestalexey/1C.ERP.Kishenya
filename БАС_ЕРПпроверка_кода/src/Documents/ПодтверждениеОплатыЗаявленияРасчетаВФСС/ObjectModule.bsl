#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

	
#Область ПрограммныйИнтерфейс

#Область ПроцедурыИФункцииЗаполненияДокумента

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическиеЛица.ФизическоеЛицо");
	
КонецПроцедуры
// Подсистема "Управление доступом".

#КонецОбласти

#КонецОбласти
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаявлениеРасчетВФСС") Тогда
		ЗаполнитьРеквизитыПоОснованию(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Проведение документа
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// регистр ОплатаПособийСоциальногоСтрахования
	Движения.ОплатаПособийСоциальногоСтрахования.Записывать = Истина;
	Для Каждого ТекСтрокаРасчетыСФондами Из РасчетыСФондами Цикл
		Если НЕ ТекСтрокаРасчетыСФондами.Отклонено Тогда
			Движение = Движения.ОплатаПособийСоциальногоСтрахования.Добавить();
			Движение.Организация = Организация;
			Движение.ДокументОснование = ТекСтрокаРасчетыСФондами.ДокументОснование;
		КонецЕсли;
	КонецЦикла;
	
	// регистр РасчетыСФондамиПоПособиям Расход
	Движения.РасчетыСФондамиПоПособиям.Записывать = Истина;
	Для Каждого ТекСтрокаРасчетыСФондами Из РасчетыСФондами Цикл
		Движение = Движения.РасчетыСФондамиПоПособиям.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.Месяц = ТекСтрокаРасчетыСФондами.Месяц;
		Движение.Заявление = ЗаявлениеРасчетВФСС;
		Движение.ФондСоциальногоСтрахования = ТекСтрокаРасчетыСФондами.ФондСоциальногоСтрахования;
		Движение.СтатьяРасчетов = ТекСтрокаРасчетыСФондами.СтатьяРасчетов;
		Движение.Сотрудник = ТекСтрокаРасчетыСФондами.Сотрудник;
		Движение.ДокументОснование = ТекСтрокаРасчетыСФондами.ДокументОснование;
		
		Если НЕ ТекСтрокаРасчетыСФондами.Отклонено Тогда
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Сумма = ТекСтрокаРасчетыСФондами.Сумма;
			Движение.КоличествоДней = ТекСтрокаРасчетыСФондами.КоличествоДней;
			Движение.СуммаЛьготыЧернобыльцы = ТекСтрокаРасчетыСФондами.СуммаЛьготЧернобыльцы;
		Иначе
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Сумма = - ТекСтрокаРасчетыСФондами.Сумма;
			Движение.КоличествоДней = - ТекСтрокаРасчетыСФондами.КоличествоДней;
			Движение.СуммаЛьготыЧернобыльцы = - ТекСтрокаРасчетыСФондами.СуммаЛьготЧернобыльцы;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыПоОснованию(ДокументОснование)
	
	РеквизитыДокументаОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОснование, "Организация, ФондСоциальногоСтрахования");

	Организация = РеквизитыДокументаОснования.Организация;
	ФондСоциальногоСтрахования = РеквизитыДокументаОснования.ФондСоциальногоСтрахования;
	ЗаявлениеРасчетВФСС = ДокументОснование;

КонецПроцедуры

#КонецОбласти


#КонецЕсли

