
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДоступенРасчетОтпусков = Пользователи.РолиДоступны("ДобавлениеИзменениеНачисленнойЗарплатыРасширенная");
	ДоступноОформлениеОтпусков = ДоступенРасчетОтпусков ИЛИ Пользователи.РолиДоступны("ДобавлениеИзменениеОтпусков");
	
	Если Параметры.Ключ.Пустая() Тогда
	
		// Заполнение нового документа.
		ЗначенияДляЗаполнения = Новый Структура("Организация, Ответственный, Руководитель, ДолжностьРуководителя",
			"Объект.Организация", "Объект.Ответственный", "Объект.Руководитель", "Объект.ДолжностьРуководителя");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		ИнициализироватьФорму();
		ПриПолученииДанныхНаСервере();
		
		УстановитьИнформационнуюНадпись(ЭтаФорма);
		
	КонецЕсли; 
		
	// Обработчик подсистемы "Дополнительные отчеты и обработки".
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Обработчик подсистемы "Печать".
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужбаФормы");
		Модуль.УстановитьПараметрыВыбораСотрудников(ЭтаФорма, "СотрудникиСотрудник");
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ЗаписанДокументОтпуск" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ЗаполнитьРасчетныеДокументы();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ИнициализироватьФорму();
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НеРассчитанныеСтрокиОтпусков = Объект.Сотрудники.НайтиСтроки(Новый Структура("Рассчитан", Ложь));
	Для каждого НеРассчитаннаяСтрока Из НеРассчитанныеСтрокиОтпусков Цикл
		
		Если ЗначениеЗаполнено(НеРассчитаннаяСтрока.Отпуск) Тогда
			
			Если НеРассчитаннаяСтрока.ВидОтпуска <> НеРассчитаннаяСтрока.ВидОтпускаПрежний
				ИЛИ НеРассчитаннаяСтрока.ДатаНачала <> НеРассчитаннаяСтрока.ДатаНачалаПрежняя
				ИЛИ НеРассчитаннаяСтрока.ДатаОкончания <> НеРассчитаннаяСтрока.ДатаОкончанияПрежняя
				ИЛИ НеРассчитаннаяСтрока.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск <> НеРассчитаннаяСтрока.НачалоПериодаЗаКоторыйПредоставляетсяОтпускПрежний
				ИЛИ НеРассчитаннаяСтрока.КонецПериодаЗаКоторыйПредоставляетсяОтпуск <> НеРассчитаннаяСтрока.КонецПериодаЗаКоторыйПредоставляетсяОтпускПрежний
				ИЛИ НеРассчитаннаяСтрока.Основание <> НеРассчитаннаяСтрока.ОснованиеПрежнее Тогда
				
				ПараметрыЗаполнения = ОбщиеДанныеЗаполнения(Объект, НеРассчитаннаяСтрока.Сотрудник, НеРассчитаннаяСтрока.Отпуск);
				ПараметрыЗаполнения.Вставить("ДанныеОтпусков", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеЗаполненияСтроки(НеРассчитаннаяСтрока)));
				
				РасчетныйДокументПоПараметрыЗаполнения(НеРассчитаннаяСтрока.Сотрудник, ПараметрыЗаполнения);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписанДокументОтпускаСотрудников", , ЭтаФорма);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
    ЗарплатаКадрыКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Сотрудники

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СотрудникиОтпуск" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		Если Модифицированность Или Не ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
			
			Оповещение = Новый ОписаниеОповещения("СотрудникиВыборЗавершение", ЭтотОбъект);
			ДобавитьОтпускКДокументу(Оповещение);
			ОповеститьОЗаписиДокументаОтпуск();
			
		Иначе 
			СотрудникиВыборЗавершение(ТекущиеДанные, Неопределено);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыборЗавершение(ТекущиеДанные, ДополнительныеПараметры) Экспорт 
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.Отпуск);
		ПараметрыОткрытия.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
		ОткрытьФорму("Документ.Отпуск.ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ТекущиеДанные.Рассчитан Тогда
		
		Если Не ДоступенРасчетОтпусков Тогда
			
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сотруднику %1 уже начислены отпускные и оформлено отсутствие.';uk='Співробітникові %1 вже нараховані відпускні та оформлено відсутність.'"),
					ТекущиеДанные.Сотрудник);
					
			Если ЗначениеЗаполнено(ТекущиеДанные.ОтпускРассчитал) Тогда
				ТекстПредупрежденияДополнительно = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru='Для отмены начислений обратитесь к расчетчику, работающему под именем ""%1"".';uk='Для скасування нарахувань зверніться до розраховувача, що працює під назвою ""%1"".'"),
					ТекущиеДанные.ОтпускРассчитал);
			Иначе
				ТекстПредупрежденияДополнительно = НСтр("ru='Для отмены начислений обратитесь к расчетчику.';uk='Для скасування нарахувань зверніться до розраховувача.'");
			КонецЕсли;
			
			Отказ = Истина;
			ПоказатьПредупреждение(, ТекстПредупреждения + Символы.ПС + ТекстПредупрежденияДополнительно);
					
		Иначе
	
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Сотруднику %1 уже начислены отпускные и оформлено отсутствие.
                    |Для удаления этой строки необходимо также отменить выполненное начисление.
                    |Отменить начисления?'
                    |;uk='Співробітникові %1 вже нараховані відпускні та оформлено відсутність.
                    |Для видалення цього рядка необхідно також скасувати виконане нарахування.
                    |Скасувати нарахування?'"),
					ТекущиеДанные.Сотрудник);
					
			Оповещение = Новый ОписаниеОповещения("СотрудникиПередУдалениемЗавершение", ЭтотОбъект);		
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
				
			Отказ = Истина;
		
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		
		Если ТекущиеДанные.Проведен Тогда
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Отсутствие сотрудника %1 уже оформлено.
                    |Для удаления этой строки необходимо также отменить оформление отсутствия на работе.
                    |Отменить оформленное отсутствие?'
                    |;uk='Відсутність співробітника %1 вже оформлено.
                    |Для вилучення цього рядка необхідно також скасувати оформлення відсутності на роботі.
                    |Скасувати оформлену відсутність?'"),
					ТекущиеДанные.Сотрудник);
					
			Оповещение = Новый ОписаниеОповещения("СотрудникиПередУдалениемЗавершение", ЭтотОбъект);		
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
			
		Иначе
			СотрудникиПередУдалениемЗавершение(КодВозвратаДиалога.Да)
		КонецЕсли;
			
		Отказ = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПослеУдаления(Элемент)
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалениемЗавершение(Ответ, ДополнительныеПараметры = Неопределено) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.Сотрудники.ТекущаяСтрока;
	
	Отказ = Ложь;
	ОтменитьДокумент(ТекущаяСтрока, Отказ);
	
	Если Не Отказ Тогда 
		Объект.Сотрудники.Удалить(ТекущаяСтрока);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		
		Элемент.ТекущиеДанные.ВидОтпуска = ПредопределенноеЗначение("Справочник.ВидыОтпусков.ПустаяСсылка");
		Элемент.ТекущиеДанные.ОтпускЯвляетсяЕжегодным = Ложь;
		Элемент.ТекущиеДанные.ДатаНачала = '00010101';
		Элемент.ТекущиеДанные.ДатаОкончания = '00010101';
		Элемент.ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		Элемент.ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		Элемент.ТекущиеДанные.Рассчитан = Ложь;
		Элемент.ТекущиеДанные.Проведен = Ложь;
		Элемент.ТекущиеДанные.Отпуск = ПредопределенноеЗначение("Документ.Отпуск.ПустаяСсылка");
		Элемент.ТекущиеДанные.ОтпускРассчитал = ПредопределенноеЗначение("Справочник.Пользователи.ПустаяСсылка");
		
		СотрудникиСотрудникПриИзмененииНаСервере();
		
		ТекущийЭлемент = Элементы.СотрудникиВидОтпуска;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьИнформационнуюНадпись(ЭтаФорма);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	
	СотрудникиСотрудникПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВидОтпускаПриИзменении(Элемент)
	
	СотрудникиВидОтпускаПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиДатаНачалаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) 
		И НЕ ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания)
		И ТекущиеДанные.КоличествоДней > 0 Тогда
		
		ТекущиеДанные.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(ТекущиеДанные.Сотрудник, ТекущиеДанные.ДатаНачала, ТекущиеДанные.КоличествоДней, ТекущиеДанные.ОтпускЯвляетсяЕжегодным);
		
	КонецЕсли;
	
	ЗаполнитьКраткосрочныйТрудовойДоговорСотрудника(ТекущиеДанные.ПолучитьИдентификатор());
	
	ПриИзмененииПериодаОтпуска();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиДатаОкончанияПриИзменении(Элемент)
	
	ПриИзмененииПериодаОтпуска();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиКоличествоДнейПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если  ТекущиеДанные.КоличествоДней > 0 И ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) Тогда
		ТекущиеДанные.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(ТекущиеДанные.Сотрудник, ТекущиеДанные.ДатаНачала, ТекущиеДанные.КоличествоДней, ТекущиеДанные.ОтпускЯвляетсяЕжегодным);
	КонецЕсли;
	
	ПриИзмененииПериодаОтпуска();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиКоличествоДнейКомпенсацииПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.КоличествоДнейКомпенсации <> 0 Тогда
		СотрудникиКоличествоДнейКомпенсацииПриИзмененииНаСервере();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаПодбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура СформироватьРасчетныеДокументы(Команда)
	
	СформироватьРасчетныеДокументыНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПравильностьОформленныхОтпусков(Команда)
	
	ДанныеОРасхождениях = ДанныеОРасхожденияхСУчетом();
	Если ДанныеОРасхождениях.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Документ оформлен правильно';uk='Документ оформлено правильно'"));
	Иначе
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ДанныеОРасхождениях", ДанныеОРасхождениях);
		ПараметрыОткрытия.Вставить("ДатаОстатков", Объект.Дата);
		
		ОткрытьФорму("Документ.ОтпускаСотрудников.Форма.ФормаПроверкиПравильностиОформления", ПараметрыОткрытия, ЭтаФорма);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ТекстСообщения = НСтр("ru='Подбор возможен только при указанной организации';uk='Підбір можливий тільки при зазначеній організації'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Объект.Организация");
		Возврат;
	КонецЕсли;	
	
	ПараметрыОткрытия = Неопределено;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ГосударственнаяСлужбаКлиент");
		Модуль.УточнитьПараметрыОткрытияФормыВыбораСотрудников(ПараметрыОткрытия);
	КонецЕсли; 
		
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.Сотрудники,
		Объект.Организация,
		,
		Объект.Дата,
		Объект.Дата,
		,
		АдресСпискаПодобранныхСотрудников(),
		ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ЗаполнитьВторичныеДанныеСотрудниковВФорме();
	ЗаполнитьРасчетныеДокументы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПрежниеЗначения()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Для каждого СтрокаСотрудники Из Объект.Сотрудники Цикл
			ЗаполнитьПрежниеЗначеннияСтроки(СтрокаСотрудники);
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПрежниеЗначеннияСтроки(СтрокаСотрудники)
	
	Если ЗначениеЗаполнено(СтрокаСотрудники.Отпуск) Тогда
		СтрокаСотрудники.ВидОтпускаПрежний = СтрокаСотрудники.ВидОтпуска;
		СтрокаСотрудники.ДатаНачалаПрежняя = СтрокаСотрудники.ДатаНачала;
		СтрокаСотрудники.ДатаОкончанияПрежняя = СтрокаСотрудники.ДатаОкончания;
		СтрокаСотрудники.НачалоПериодаЗаКоторыйПредоставляетсяОтпускПрежний = СтрокаСотрудники.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск;
		СтрокаСотрудники.КонецПериодаЗаКоторыйПредоставляетсяОтпускПрежний = СтрокаСотрудники.КонецПериодаЗаКоторыйПредоставляетсяОтпуск;
		СтрокаСотрудники.ОснованиеПрежнее = СтрокаСотрудники.Основание;
	КонецЕсли; 
			
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	УстанавливаемыеПараметры = Новый Структура;
	УстанавливаемыеПараметры.Вставить("Организация", Объект.Организация);
	
	УстановитьПараметрыФункциональныхОпцийФормы(УстанавливаемыеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ЗначенияДляЗаполнения = Новый Структура("Организация, Руководитель, ДолжностьРуководителя",
		"Объект.Организация", "Объект.Руководитель", "Объект.ДолжностьРуководителя");
	ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Организация"))
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиСотрудникПриИзмененииНаСервере()
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	Если ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные.ВидОтпуска = Справочники.ВидыОтпусков.ПустаяСсылка();
		ТекущиеДанные.ДатаНачала = '00010101';
		ТекущиеДанные.ДатаОкончания = '00010101';
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.КоличествоДней = 0;
		ТекущиеДанные.КоличествоДнейКомпенсации = 0;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
			ЗаполнитьСтрокуСотрудника(ТекущиеДанные);
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПодбораНаСервере(Сотрудники)
	
	Для Каждого Сотрудник Из Сотрудники Цикл
		
		СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
		
		Если СтрокиПоСотруднику.Количество() = 0 Тогда
			НоваяСтрока = Объект.Сотрудники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
			
			ЗаполнитьСтрокуСотрудника(НоваяСтрока);
			
			ЭтаФорма.Модифицированность = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокуСотрудника(ТекущиеДанные)
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ВидОтпуска) Тогда
		ТекущиеДанные.ВидОтпуска = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной");
	КонецЕсли;
	
	ДанныеПоОтпускамСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ТекущиеДанные.Сотрудник));
	ДанныеПоОтпускамСотрудника.Удалить(ДанныеПоОтпускамСотрудника.Найти(ТекущиеДанные));
	
	НачалоПериодаОтпуска = '00010101';
	ОкончаниеПериодаОтпуска = '00010101';
	МаксимальнаяДатаОкончания = '00010101';
	Для каждого ДанныеОтпуска Из ДанныеПоОтпускамСотрудника Цикл
		
		Если МаксимальнаяДатаОкончания < ДанныеОтпуска.ДатаОкончания Тогда
			МаксимальнаяДатаОкончания = КонецДня(ДанныеОтпуска.ДатаОкончания) + 1;
		КонецЕсли; 
		
	КонецЦикла;
	
	ТекущиеДанные.ДатаНачала = МаксимальнаяДатаОкончания;
	
	ЗаполнитьКраткосрочныйТрудовойДоговорСотрудника(ТекущиеДанные.ПолучитьИдентификатор());
	
	ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВторичныеДанныеСотрудниковВФорме()
	
	ЗаполнитьКраткосрочныйТрудовойДоговорСотрудника();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКраткосрочныйТрудовойДоговорСотрудника(ТекущаяСтрока = Неопределено)
	
	Если ТекущаяСтрока = Неопределено Тогда
		Строки = Объект.Сотрудники;
	Иначе
		Строки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока));
	КонецЕсли;
	
	ТаблицаСотрудников = Новый ТаблицаЗначений;
	ТаблицаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаСотрудников.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Для каждого Строка Из Строки Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТаблицаСотрудников.Добавить();
		НоваяСтрока.Сотрудник = Строка.Сотрудник;
		НоваяСтрока.Период = Строка.ДатаНачала;
	КонецЦикла;
	
	ТрудовыеДоговоры = ОстаткиОтпусков.КраткосрочныеТрудовыеДоговорыСотрудников(ТаблицаСотрудников);
	
	Отбор = Новый Структура("Сотрудник,Период");
	Для каждого Строка Из Строки Цикл  
		Если НЕ ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			Продолжить;
		КонецЕсли;
		Отбор.Сотрудник  = Строка.Сотрудник;
		Отбор.Период = Строка.ДатаНачала;
		Строка.КраткосрочныйТрудовойДоговорСотрудника = Ложь;
		НайденныеСтроки = ТрудовыеДоговоры.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда
			Строка.КраткосрочныйТрудовойДоговорСотрудника = НайденныеСтроки[0].КраткосрочныйТрудовойДоговор;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ПриИзмененииПериодаОтпуска()
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сотрудник)
		И ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания)
		И ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала) 
		И ТекущиеДанные.ДатаНачала <= ТекущиеДанные.ДатаОкончания Тогда
		
		РассчитатьПараметрыПериодаОтпуска();
		
	Иначе
		ТекущиеДанные.КоличествоДней = 0;
		ТекущиеДанные.КоличествоДнейКомпенсации = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьПараметрыПериодаОтпуска()
	УстановитьКоличествоДнейНаСервере();
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	Если ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные); 
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРасчетныеДокументы()
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Запрос.Текст =
	 	"ВЫБРАТЬ
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска,
	 	|	ОтпускаСотрудниковСотрудники.ДатаНачала,
	 	|	ОтпускаСотрудниковСотрудники.ДатаОкончания
	 	|ПОМЕСТИТЬ ВТОтпускаСотрудниковДокумента
	 	|ИЗ
	 	|	Документ.ОтпускаСотрудников.Сотрудники КАК ОтпускаСотрудниковСотрудники
	 	|ГДЕ
	 	|	ОтпускаСотрудниковСотрудники.Ссылка = &Ссылка
	 	|;
	 	|
	 	|////////////////////////////////////////////////////////////////////////////////
	 	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска,
	 	|	МАКСИМУМ(Отпуск.Ссылка) КАК Отпуск
	 	|ПОМЕСТИТЬ ВТПроведенныеОтпуска
	 	|ИЗ
	 	|	ВТОтпускаСотрудниковДокумента КАК ОтпускаСотрудниковСотрудники
	 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск КАК Отпуск
	 	|		ПО ОтпускаСотрудниковСотрудники.Сотрудник = Отпуск.Сотрудник
	 	|			И ОтпускаСотрудниковСотрудники.ДатаНачала = Отпуск.ДатаНачалаОсновногоОтпуска
	 	|			И ОтпускаСотрудниковСотрудники.ДатаОкончания = Отпуск.ДатаОкончанияОсновногоОтпуска
	 	|			И (ОтпускаСотрудниковСотрудники.ВидОтпуска = ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной))
	 	|			И (Отпуск.ПредоставитьОсновнойОтпуск
	 	|					И Отпуск.КоличествоДнейОсновногоОтпуска > 0
	 	|				ИЛИ Отпуск.ПредоставитьКомпенсациюОсновногоОтпуска
	 	|					И Отпуск.КоличествоДнейКомпенсацииОсновногоОтпуска > 0)
	 	|			И (Отпуск.Проведен)
	 	|
	 	|СГРУППИРОВАТЬ ПО
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска
	 	|
	 	|ОБЪЕДИНИТЬ ВСЕ
	 	|
	 	|ВЫБРАТЬ
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска,
	 	|	МАКСИМУМ(ОтпускДополнительныеОтпуска.Ссылка)
	 	|ИЗ
	 	|	ВТОтпускаСотрудниковДокумента КАК ОтпускаСотрудниковСотрудники
	 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
	 	|		ПО ОтпускаСотрудниковСотрудники.Сотрудник = ОтпускДополнительныеОтпуска.Ссылка.Сотрудник
	 	|			И ОтпускаСотрудниковСотрудники.ВидОтпуска = ОтпускДополнительныеОтпуска.ВидОтпуска
	 	|			И ОтпускаСотрудниковСотрудники.ДатаНачала = ОтпускДополнительныеОтпуска.ДатаНачала
	 	|			И ОтпускаСотрудниковСотрудники.ДатаОкончания = ОтпускДополнительныеОтпуска.ДатаОкончания
	 	|			И (ОтпускДополнительныеОтпуска.Ссылка.ПредоставитьДополнительныйОтпуск)
	 	|			И (ОтпускДополнительныеОтпуска.Ссылка.Проведен)
	 	|
	 	|СГРУППИРОВАТЬ ПО
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска
	 	|;
	 	|
	 	|////////////////////////////////////////////////////////////////////////////////
	 	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска,
	 	|	МАКСИМУМ(Отпуск.Ссылка) КАК Отпуск
	 	|ПОМЕСТИТЬ ВТНеПроведенныеОтпуска
	 	|ИЗ
	 	|	ВТОтпускаСотрудниковДокумента КАК ОтпускаСотрудниковСотрудники
	 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск КАК Отпуск
	 	|		ПО ОтпускаСотрудниковСотрудники.Сотрудник = Отпуск.Сотрудник
	 	|			И ОтпускаСотрудниковСотрудники.ДатаНачала = Отпуск.ДатаНачалаОсновногоОтпуска
	 	|			И ОтпускаСотрудниковСотрудники.ДатаОкончания = Отпуск.ДатаОкончанияОсновногоОтпуска
	 	|			И (ОтпускаСотрудниковСотрудники.ВидОтпуска = ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной))
	 	|			И (Отпуск.ПредоставитьОсновнойОтпуск
	 	|					И Отпуск.КоличествоДнейОсновногоОтпуска > 0
	 	|				ИЛИ Отпуск.ПредоставитьКомпенсациюОсновногоОтпуска
	 	|					И Отпуск.КоличествоДнейКомпенсацииОсновногоОтпуска > 0)
	 	|			И (НЕ Отпуск.Проведен)
	 	|			И (НЕ Отпуск.ПометкаУдаления)
	 	|
	 	|СГРУППИРОВАТЬ ПО
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска
	 	|
	 	|ОБЪЕДИНИТЬ ВСЕ
	 	|
	 	|ВЫБРАТЬ
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска,
	 	|	МАКСИМУМ(ОтпускДополнительныеОтпуска.Ссылка)
	 	|ИЗ
	 	|	ВТОтпускаСотрудниковДокумента КАК ОтпускаСотрудниковСотрудники
	 	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
	 	|		ПО ОтпускаСотрудниковСотрудники.Сотрудник = ОтпускДополнительныеОтпуска.Ссылка.Сотрудник
	 	|			И ОтпускаСотрудниковСотрудники.ВидОтпуска = ОтпускДополнительныеОтпуска.ВидОтпуска
	 	|			И ОтпускаСотрудниковСотрудники.ДатаНачала = ОтпускДополнительныеОтпуска.ДатаНачала
	 	|			И ОтпускаСотрудниковСотрудники.ДатаОкончания = ОтпускДополнительныеОтпуска.ДатаОкончания
	 	|			И (ОтпускДополнительныеОтпуска.Ссылка.ПредоставитьДополнительныйОтпуск)
	 	|			И (НЕ ОтпускДополнительныеОтпуска.Ссылка.Проведен)
	 	|			И (НЕ ОтпускДополнительныеОтпуска.Ссылка.ПометкаУдаления)
	 	|
	 	|СГРУППИРОВАТЬ ПО
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ОтпускаСотрудниковСотрудники.Сотрудник,
	 	|	ОтпускаСотрудниковСотрудники.ВидОтпуска
	 	|;
	 	|
	 	|////////////////////////////////////////////////////////////////////////////////
	 	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	 	|	ОтпускаСотрудниковСотрудники.НомерСтроки,
	 	|	ВЫБОР
	 	|		КОГДА НЕ ПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА ПроведенныеОтпуска.Отпуск
	 	|		КОГДА НЕ НеПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА НеПроведенныеОтпуска.Отпуск
	 	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.отпуск.ПустаяСсылка)
	 	|	КОНЕЦ КАК Отпуск,
	 	|	ВЫБОР
	 	|		КОГДА НЕ ПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА ВЫБОР
	 	|					КОГДА ПроведенныеОтпуска.Отпуск.Проведен
	 	|						ТОГДА ПроведенныеОтпуска.Отпуск.ДокументРассчитан
	 	|					ИНАЧЕ ЛОЖЬ
	 	|				КОНЕЦ
	 	|		КОГДА НЕ НеПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА ВЫБОР
	 	|					КОГДА НеПроведенныеОтпуска.Отпуск.Проведен
	 	|						ТОГДА НеПроведенныеОтпуска.Отпуск.ДокументРассчитан
	 	|					ИНАЧЕ ЛОЖЬ
	 	|				КОНЕЦ
	 	|		ИНАЧЕ ЛОЖЬ
	 	|	КОНЕЦ КАК Рассчитан,
	 	|	ВЫБОР
	 	|		КОГДА НЕ ПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА ПроведенныеОтпуска.Отпуск.Проведен
	 	|		КОГДА НЕ НеПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА НеПроведенныеОтпуска.Отпуск.Проведен
	 	|		ИНАЧЕ ЛОЖЬ
	 	|	КОНЕЦ КАК Проведен,
	 	|	ВЫБОР
	 	|		КОГДА НЕ ПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА ПроведенныеОтпуска.Отпуск.Рассчитал
	 	|		КОГДА НЕ НеПроведенныеОтпуска.Отпуск ЕСТЬ NULL 
	 	|			ТОГДА НеПроведенныеОтпуска.Отпуск.Рассчитал
	 	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	 	|	КОНЕЦ КАК Рассчитал
	 	|ИЗ
	 	|	ВТОтпускаСотрудниковДокумента КАК ОтпускаСотрудниковСотрудники
	 	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПроведенныеОтпуска КАК ПроведенныеОтпуска
	 	|		ПО ОтпускаСотрудниковСотрудники.НомерСтроки = ПроведенныеОтпуска.НомерСтроки
	 	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеПроведенныеОтпуска КАК НеПроведенныеОтпуска
	 	|		ПО ОтпускаСотрудниковСотрудники.НомерСтроки = НеПроведенныеОтпуска.НомерСтроки";
		
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
	ТаблицаОтпусков = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаТаблицыОтпусков Из ТаблицаОтпусков Цикл
		
		СтрокиОтпусков =  Объект.Сотрудники.НайтиСтроки(Новый Структура("НомерСтроки", СтрокаТаблицыОтпусков.НомерСтроки));
		Если СтрокиОтпусков.Количество() > 0 Тогда
			СтрокиОтпусков[0].Отпуск = СтрокаТаблицыОтпусков.Отпуск;
			СтрокиОтпусков[0].Рассчитан = СтрокаТаблицыОтпусков.Рассчитан;
			СтрокиОтпусков[0].Проведен = СтрокаТаблицыОтпусков.Проведен;
			СтрокиОтпусков[0].ОтпускРассчитал = СтрокаТаблицыОтпусков.Рассчитал;
		КонецЕсли; 
		
	КонецЦикла;
	
	ЗаполнитьПризнакиЕжегодногоОтпуска();
	ЗаполнитьПрежниеЗначения();
	
	УстановитьИнформационнуюНадпись(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПризнакиЕжегодногоОтпуска()
	
	ВидыОтпусков = Объект.Сотрудники.Выгрузить(Новый Структура("ОтпускЯвляетсяЕжегодным", Ложь)).ВыгрузитьКолонку("ВидОтпуска");
	
	ДанныеВидовОтпусков = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВидыОтпусков, "ОтпускЯвляетсяЕжегодным");
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		Если НЕ СтрокаСотрудника.ОтпускЯвляетсяЕжегодным Тогда
			СтрокаСотрудника.ОтпускЯвляетсяЕжегодным = ДанныеВидовОтпусков.Получить(СтрокаСотрудника.ВидОтпуска);			
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОбщиеДанныеЗаполнения(Объект, Сотрудник, ДокументОтпуск)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Действие", "Заполнить");
	ПараметрыЗаполнения.Вставить("ДокументОтпуск", ДокументОтпуск);
	ПараметрыЗаполнения.Вставить("Организация", Объект.Организация);
	ПараметрыЗаполнения.Вставить("Сотрудник", Сотрудник);
	ПараметрыЗаполнения.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыЗаполнения.Вставить("Руководитель", Объект.Руководитель);
	ПараметрыЗаполнения.Вставить("ДолжностьРуководителя", Объект.ДолжностьРуководителя);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДанныеЗаполненияСтроки(СтрокаОтпуска)
	
	СтруктураОтпуска = Новый Структура;
		
	СтруктураОтпуска.Вставить("ВидОтпуска", СтрокаОтпуска.ВидОтпуска);
	СтруктураОтпуска.Вставить("ДатаНачала", СтрокаОтпуска.ДатаНачала);
	СтруктураОтпуска.Вставить("ДатаОкончания", СтрокаОтпуска.ДатаОкончания);
	СтруктураОтпуска.Вставить("КоличествоДней", СтрокаОтпуска.КоличествоДней);
	СтруктураОтпуска.Вставить("КоличествоДнейКомпенсации", СтрокаОтпуска.КоличествоДнейКомпенсации);
	СтруктураОтпуска.Вставить("НачалоПериодаЗаКоторыйПредоставляетсяОтпуск", СтрокаОтпуска.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск);
	СтруктураОтпуска.Вставить("КонецПериодаЗаКоторыйПредоставляетсяОтпуск", СтрокаОтпуска.КонецПериодаЗаКоторыйПредоставляетсяОтпуск);
	СтруктураОтпуска.Вставить("Основание", СтрокаОтпуска.Основание);
	СтруктураОтпуска.Вставить("ВидОтпускаПрежний", СтрокаОтпуска.ВидОтпускаПрежний);
	СтруктураОтпуска.Вставить("ИндексСтрокиДокумента", СтрокаОтпуска.НомерСтроки - 1);
	
	Возврат СтруктураОтпуска;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОтпускКДокументу(ОповещениеЗавершения = Неопределено)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗаписатьДокумент", Истина);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru='Оформить отпуск можно только после записи этого документа.
                            |Записать этот документ?'
                            |;uk='Оформити відпустку можна тільки після запису цього документа.
                            |Записати цей документ?'");
							
		Оповещение = Новый ОписаниеОповещения("ДобавитьОтпускКДокументуЗавершение", ЭтотОбъект, ДополнительныеПараметры);					
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
			
	Иначе
		
		ДополнительныеПараметры.ЗаписатьДокумент = Ложь;
		ДобавитьОтпускКДокументуЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтпускКДокументуЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗаписатьДокумент И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Отпуск) Тогда
		
		ПараметрыЗаполненияСтроки = ПараметрыЗаполнения(ТекущиеДанные.Сотрудник, Элементы.Сотрудники.ТекущаяСтрока);
		Если ПараметрыЗаполненияСтроки.Количество() > 0 Тогда
			РезультатЗаполнения = РасчетныйДокументПоПараметрыЗаполнения(ТекущиеДанные.Сотрудник, ПараметрыЗаполненияСтроки[0]);
			Если РезультатЗаполнения <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ТекущиеДанные, РезультатЗаполнения);
				ЗаполнитьПрежниеЗначеннияСтроки(ТекущиеДанные);
			КонецЕсли; 
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасчетныеДокументыНаКлиенте() Экспорт
	
	ДополнительныеПараметры = Новый Структура("ЗаписатьДокумент", Истина);
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru='Оформить отпуска можно только после записи этого документа.
                            |Записать этот документ?'
                            |;uk='Оформити відпустки можна тільки після запису цього документа.
                            |Записати цей документ?'");
							
		Оповещение = Новый ОписаниеОповещения("СформироватьРасчетныеДокументыНаКлиентеЗавершение", ЭтотОбъект, ДополнительныеПараметры);					
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Отмена);
			
	Иначе 
		
		ДополнительныеПараметры.ЗаписатьДокумент = Ложь;
		СформироватьРасчетныеДокументыНаКлиентеЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура СформироватьРасчетныеДокументыНаКлиентеЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗаписатьДокумент И Не Записать() Тогда
		Возврат;
	КонецЕсли; 
	
	ОчиститьСообщения();
	
	СформироватьРасчетныеДокументыНаСервере();
	ПоказатьПредупреждение(, НСтр("ru='Оформление отпусков завершено';uk='Оформлення відпусток завершено'"));
	ОповеститьОЗаписиДокументаОтпуск();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоДнейНаСервере()
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	
	ОписаниеВидаОтпуска = Документы.Отпуск.ОписаниеВидаОтпуска(ТекущиеДанные.ВидОтпуска);
	ТекущиеДанные.КоличествоДней = УчетРабочегоВремени.ДлительностьИнтервала(ТекущиеДанные.Сотрудник, ТекущиеДанные.ДатаНачала, ТекущиеДанные.ДатаОкончания, ОписаниеВидаОтпуска.СпособРасчетаПоКалендарнымДням, ОписаниеВидаОтпуска.ЕжегодныйОтпуск);
	
	ДатаОкончания = ТекущиеДанные.ДатаОкончания;
	
	СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ТекущиеДанные.Сотрудник));
	ИндексСтрокиТекущегоОтпуска = СтрокиПоСотруднику.Найти(ТекущиеДанные);
	Если ИндексСтрокиТекущегоОтпуска <> Неопределено Тогда
		
		Для ИндексСтроки = ИндексСтрокиТекущегоОтпуска + 1 По СтрокиПоСотруднику.Количество() - 1 Цикл
			
			ДанныеСтроки = СтрокиПоСотруднику[ИндексСтроки];
			Если ДанныеСтроки.ДатаНачала <= ДатаОкончания Тогда
				
				ДанныеСтроки.ДатаНачала = КонецДня(ДатаОкончания) + 1;
				ДанныеСтроки.ДатаОкончания = ОстаткиОтпусковКлиентСервер.ДатаОкончанияОтпуска(ДанныеСтроки.Сотрудник, ДанныеСтроки.ДатаНачала, ДанныеСтроки.КоличествоДней, ДанныеСтроки.ОтпускЯвляетсяЕжегодным);
				
				ДатаОкончания = ДанныеСтроки.ДатаОкончания;
			Иначе
				Прервать;
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьИнформационнуюНадпись(Форма)
	
	КоличествоСтрок = Форма.Объект.Сотрудники.Количество();
	
	НеОформленные = Форма.Объект.Сотрудники.НайтиСтроки(Новый Структура("Отпуск", ПредопределенноеЗначение("Документ.Отпуск.ПустаяСсылка")));
	КоличествоНеОформленных = НеОформленные.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяПусто";
	ИначеЕсли КоличествоНеОформленных = КоличествоСтрок Тогда
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяВсеНеОформлены";
	ИначеЕсли КоличествоНеОформленных > 0 Тогда
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяОформленыНеВсе";
	Иначе
		ИмяИнформационнойСтраницы = "ГруппаИнформационнаяВсеОформлены";
	КонецЕсли; 
	
	ТекущаяСтраница = Форма.Элементы.Найти(ИмяИнформационнойСтраницы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ГруппаИнформация",
		"ТекущаяСтраница",
		ТекущаяСтраница);
	
КонецПроцедуры

&НаСервере
Процедура СотрудникиКоличествоДнейКомпенсацииПриИзмененииНаСервере()
	
	Если Элементы.Сотрудники.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
		ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные)
	
	ТекущиеДанные.ОтпускЯвляетсяЕжегодным = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущиеДанные.ВидОтпуска, "ОтпускЯвляетсяЕжегодным");
	Если ТекущиеДанные.ОтпускЯвляетсяЕжегодным 
		И (ЗначениеЗаполнено(ТекущиеДанные.ДатаНачала)
			И ЗначениеЗаполнено(ТекущиеДанные.ДатаОкончания)
			 Или ЗначениеЗаполнено(ТекущиеДанные.КоличествоДнейКомпенсации)) Тогда
			 
		СтруктураПараметров = ОстаткиОтпусков.ПараметрыПолученияРабочегоПериодаОтпуска();
		СтруктураПараметров.Сотрудник = ТекущиеДанные.Сотрудник;
		СтруктураПараметров.ТекущийРегистратор = Объект.Ссылка;
		СтруктураПараметров.ВидОтпуска = ТекущиеДанные.ВидОтпуска;
		СтруктураПараметров.ДатаНачала = ТекущиеДанные.ДатаНачала;
		СтруктураПараметров.ДатаОкончания = ТекущиеДанные.ДатаОкончания;
		СтруктураПараметров.ДатаКомпенсации = ?(ЗначениеЗаполнено(Объект.Дата), Началомесяца(Объект.Дата), НачалоМесяца(ТекущаяДатаСеанса()));
		СтруктураПараметров.КоличествоДнейКомпенсации = ТекущиеДанные.КоличествоДнейКомпенсации;
		ПериодОсновногоОтпуска = ОстаткиОтпусков.РабочийПериодОтпуска(СтруктураПараметров);
		
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск	= ПериодОсновногоОтпуска.РабочийГодС;
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск	= ПериодОсновногоОтпуска.РабочийГодПо;
		
	Иначе
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура СформироватьРасчетныеДокументыНаСервере()
	
	НеРассчитанныеСотрудники = Объект.Сотрудники.Выгрузить(Новый Структура("Отпуск", Документы.Отпуск.ПустаяСсылка()), "Сотрудник");
	НеРассчитанныеСотрудники.Свернуть("Сотрудник");
	
	Для каждого СтрокаСотрудника Из НеРассчитанныеСотрудники Цикл
		
		ПараметрыЗаполненияДокументов = ПараметрыЗаполнения(СтрокаСотрудника.Сотрудник);
		Для каждого ПараметрЗаполненияДокумента Из ПараметрыЗаполненияДокументов Цикл
			РасчетныйДокументПоПараметрыЗаполнения(СтрокаСотрудника.Сотрудник, ПараметрЗаполненияДокумента);
		КонецЦикла;
		
	КонецЦикла;
	
	ЗаполнитьРасчетныеДокументы();
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗаполнения(Сотрудник, ИдентификаторСтрокиСотрудника = Неопределено)
	
	ПараметрыЗаполненияДокументов = Новый Массив;
	
	// Формирования массива обрабатываемых строк.
	Если ИдентификаторСтрокиСотрудника = Неопределено Тогда
		СтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник,Отпуск", Сотрудник, Документы.Отпуск.ПустаяСсылка()));
	Иначе
		
		СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтрокиСотрудника);
		Если СтрокаСотрудника <> Неопределено Тогда
			СтрокиСотрудника = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаСотрудника);
		КонецЕсли; 
		
	КонецЕсли;
	
	Если СтрокиСотрудника.Количество() > 0 Тогда
		
		// Формирования списка уже оформленных документов Отпуск.
		ВсеСтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
		
		ДокументыОтпуск = Новый Массив;
		Для каждого СтрокаСотрудника Из ВсеСтрокиСотрудника Цикл
			
			Если НЕ ЗначениеЗаполнено(СтрокаСотрудника.Отпуск) ИЛИ СтрокаСотрудника.Рассчитан Тогда
				Продолжить;
			КонецЕсли;
			
			ДокументыОтпуск.Добавить(СтрокаСотрудника.Отпуск);
			
		КонецЦикла;
		
		// Получение данных оформленных документов.
		ИменаРеквизитовДокумента = "ДатаНачалаПериодаОтсутствия,ДатаОкончанияПериодаОтсутствия,Проведен";
		ДанныеДокументов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ДокументыОтпуск, ИменаРеквизитовДокумента);
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ОтпускДополнительныеОтпуска.Ссылка,
			|	ОтпускДополнительныеОтпуска.ВидОтпуска
			|ИЗ
			|	Документ.Отпуск.ДополнительныеОтпуска КАК ОтпускДополнительныеОтпуска
			|ГДЕ
			|	ОтпускДополнительныеОтпуска.Ссылка В(&ДокументыОтпуск)
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	Отпуск.Ссылка,
			|	ЗНАЧЕНИЕ(Справочник.ВидыОтпусков.Основной)
			|ИЗ
			|	Документ.Отпуск КАК Отпуск
			|ГДЕ
			|	Отпуск.Ссылка В(&ДокументыОтпуск)
			|	И Отпуск.ПредоставитьОсновнойОтпуск";
			
		Запрос.УстановитьПараметр("ДокументыОтпуск", ДокументыОтпуск);
		ВидыОтпусковДокументов = Запрос.Выполнить().Выгрузить();
			
		// Отнесение обрабатываемых строк к оформленному документу.
		Для каждого СтрокаСотрудника Из СтрокиСотрудника Цикл
			
			ПодходящийДокумент = Неопределено;
			Для каждого ДанныеДокумента Из ДанныеДокументов Цикл
				
				Если ВидыОтпусковДокументов.НайтиСтроки(Новый Структура("Ссылка,ВидОтпуска", ДанныеДокумента.Ключ, СтрокаСотрудника.ВидОтпуска)).Количество() > 0 Тогда
					Продолжить;
				КонецЕсли;
				
				ЗначенияРеквизитовДокумента = ДанныеДокумента.Значение;
				Если (КонецДня(ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия) + 1 = СтрокаСотрудника.ДатаНачала
					ИЛИ КонецДня(СтрокаСотрудника.ДатаОкончания) + 1 = ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия) Тогда
					
					ПодходящийДокумент = ДанныеДокумента.Ключ;
					Если ЗначенияРеквизитовДокумента.Проведен = Истина Тогда
						Прервать;
					КонецЕсли; 
					
				КонецЕсли;
				
			КонецЦикла;
			
			// Обновление данных оформленного документа, если подходящего документа нет
			// отпуск относится к пустому документу.
			ЗначенияРеквизитовДокумента = ДанныеДокументов.Получить(ПодходящийДокумент);
			Если ЗначенияРеквизитовДокумента = Неопределено Тогда
				
				ЗначенияРеквизитовДокумента = Новый Структура(ИменаРеквизитовДокумента);
				
				ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия = СтрокаСотрудника.ДатаНачала;
				ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия = СтрокаСотрудника.ДатаОкончания;
				
			Иначе
				
				Если КонецДня(ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия) + 1 = СтрокаСотрудника.ДатаНачала Тогда
					ЗначенияРеквизитовДокумента.ДатаОкончанияПериодаОтсутствия = СтрокаСотрудника.ДатаОкончания;
				ИначеЕсли КонецДня(СтрокаСотрудника.ДатаОкончания) + 1 = ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия Тогда
					ЗначенияРеквизитовДокумента.ДатаНачалаПериодаОтсутствия = СтрокаСотрудника.ДатаНачала;
				КонецЕсли; 
				
			КонецЕсли;
			
			Если ЗначенияРеквизитовДокумента.Свойство("МассивСтрокДокумента") Тогда
				МассивСтрокДокумента = ЗначенияРеквизитовДокумента.МассивСтрокДокумента;
			Иначе
				МассивСтрокДокумента = Новый Массив;
			КонецЕсли; 
			
			МассивСтрокДокумента.Добавить(СтрокаСотрудника);
			ЗначенияРеквизитовДокумента.Вставить("МассивСтрокДокумента", МассивСтрокДокумента);
			
			// Если подходящего документа не было найдено, создадим уникальный ключ
			// для списка связанных датами строк.
			Если ПодходящийДокумент = Неопределено Тогда
				ПодходящийДокумент = Новый УникальныйИдентификатор();
			КонецЕсли; 
			ДанныеДокументов.Вставить(ПодходящийДокумент, ЗначенияРеквизитовДокумента);
			
		КонецЦикла;
		
		// Формирование списка параметров заполнения документов Отпуск.
		Для каждого ДанныеДокумента Из ДанныеДокументов Цикл
			
			Если НЕ ДанныеДокумента.Значение.Свойство("МассивСтрокДокумента") Тогда
				Продолжить;
			КонецЕсли; 
			
			Если ТипЗнч(ДанныеДокумента.Ключ) = Тип("ДокументСсылка.Отпуск") Тогда
				ДокументОтпуск = ДанныеДокумента.Ключ;
			Иначе
				ДокументОтпуск = Документы.Отпуск.ПустаяСсылка();
			КонецЕсли;
			
			ПараметрыЗаполнения = ОбщиеДанныеЗаполнения(Объект, Сотрудник, ДокументОтпуск);
			
			ДанныеОтпусков = Новый Массив;
			Для каждого СтрокаДокумента Из ДанныеДокумента.Значение.МассивСтрокДокумента Цикл
				ДанныеОтпусков.Добавить(ДанныеЗаполненияСтроки(СтрокаДокумента));
			КонецЦикла;
			
			ПараметрыЗаполнения.Вставить("ДанныеОтпусков", ДанныеОтпусков);
			
			ПараметрыЗаполненияДокументов.Добавить(ПараметрыЗаполнения);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПараметрыЗаполненияДокументов;
	
КонецФункции

&НаСервере
Функция РасчетныйДокументПоПараметрыЗаполнения(Сотрудник, ПараметрыЗаполнения)
	
	РезультатЗаполнения = Новый Структура("Рассчитан,Проведен,ОтпускРассчитал,Отпуск", Ложь, Ложь);
	УстановитьПривилегированныйРежим(Истина);
	
	ПодходящийДокумент = ПараметрыЗаполнения.ДокументОтпуск;
	
	Если ЗначениеЗаполнено(ПодходящийДокумент) Тогда
		ДокументОтпуск = ПодходящийДокумент.ПолучитьОбъект();
	Иначе
		ДокументОтпуск = Документы.Отпуск.СоздатьДокумент();
	КонецЕсли;
	
	ДокументОтпуск.Заполнить(ПараметрыЗаполнения);
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьМногофункциональностьДокументовЗарплатаКадры") Тогда
		ДокументОтпуск.ДокументРассчитан = Ложь;
	КонецЕсли; 
	
	Отказ = Ложь;
	Документы.Отпуск.ПроверитьРаботающих(ДокументОтпуск, Отказ);
	
	Если НЕ Отказ Тогда
		
		ДокументОтпуск.Записать(РежимЗаписиДокумента.Запись);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьМногофункциональностьДокументовЗарплатаКадры") Тогда
			
			Попытка
				
				Если НЕ ТранзакцияАктивна() ИЛИ ДокументОтпуск.Проведен Тогда
					ДокументОтпуск.Записать(РежимЗаписиДокумента.Проведение);
				КонецЕсли; 
				
			Исключение
				Инфо = ИнформацияОбОшибке();
				ВызватьИсключение НСтр("ru='Не удалось записать ';uk='Не вдалося записати '") + Строка(ДокументОтпуск.Ссылка);
			КонецПопытки;
		
		КонецЕсли;
		
	Иначе
		
		СообщенияПроверкиЗаполнения = ПолучитьСообщенияПользователю(Истина);
		Если СообщенияПроверкиЗаполнения <> Неопределено Тогда
			
			Для каждого СообщениеПроверки Из СообщенияПроверкиЗаполнения Цикл
				
				Если СообщениеПроверки.Поле = "Сотрудник" Тогда
					СообщениеПроверки.Поле = "Сотрудники[" + ПараметрыЗаполнения.ДанныеОтпусков[0].ИндексСтрокиДокумента + "]." + СообщениеПроверки.Поле;
				КонецЕсли;
				
				СообщениеПроверки.Сообщить();
				
			КонецЦикла;
			
		КонецЕсли; 
		
	КонецЕсли; 
			
	РезультатЗаполнения.Вставить("Отпуск", ДокументОтпуск.Ссылка);
	РезультатЗаполнения.Вставить("Рассчитан", ДокументОтпуск.ДокументРассчитан И ДокументОтпуск.Проведен);
	РезультатЗаполнения.Вставить("Проведен", ДокументОтпуск.Проведен);
	РезультатЗаполнения.Вставить("ОтпускРассчитал", ДокументОтпуск.Рассчитал);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗаполнения;
	
КонецФункции

&НаСервере
Функция ДанныеОРасхожденияхСУчетом()
	
	Расхождения = Новый Массив;
	
	ТаблицаСотрудников = Объект.Сотрудники.Выгрузить(, "Сотрудник");
	ТаблицаСотрудников.Свернуть("Сотрудник");
	Для каждого СтрокСотрудника Из ТаблицаСотрудников Цикл
		
		СтрокиПоСотруднику = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник,ОтпускЯвляетсяЕжегодным", СтрокСотрудника.Сотрудник, Истина));
		Если СтрокиПоСотруднику.Количество() > 0 Тогда
			
			ОстаткиОтпуска = ОстаткиОтпусков.ОстатокОтпускаСотрудникаНаДату(СтрокСотрудника.Сотрудник, Объект.Дата);
			
			Для каждого СтрокаПоСотруднику Из СтрокиПоСотруднику Цикл
				
				СтрокаСоответствует = Неопределено;
				Для каждого ОстатокОтпуска Из ОстаткиОтпуска.ОстаткиВРазрезеРабочихЛетИВидовОтпусков Цикл
					
					Если СтрокаПоСотруднику.ВидОтпуска = ОстатокОтпуска.ВидЕжегодногоОтпуска Тогда
						
						СтрокаСоответствует = Истина;
						Если СтрокаПоСотруднику.КоличествоДней <> ОстатокОтпуска.ОсталосьДней Тогда
							
							СтрокаСоответствует = Ложь;
							
							ОписаниеОшибки = Новый Структура("Сотрудник,ВидОтпуска");
							ЗаполнитьЗначенияСвойств(ОписаниеОшибки, СтрокаПоСотруднику);
							
							Если СтрокаПоСотруднику.КоличествоДней > ОстатокОтпуска.ОсталосьДней Тогда
								
								ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Предоставляется отпуск на %1 дн. больше, чем осталось по данным учета';uk='Надається відпустка на %1 дн. більше, ніж залишилося за даними обліку'"),
									СтрокаПоСотруднику.КоличествоДней - ОстатокОтпуска.ОсталосьДней);
									
							Иначе
									
								ПредставлениеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='По данным учета остаток дней отпуска на %1 дн. больше';uk='За даними обліку залишок днів відпустки на %1 дн. більше'"),
									ОстатокОтпуска.ОсталосьДней - СтрокаПоСотруднику.КоличествоДней);
								
							КонецЕсли;
							
							ОписаниеОшибки.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
							
							Расхождения.Добавить(ОписаниеОшибки);
							
						КонецЕсли; 
						
						Если СтрокаПоСотруднику.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск <> ОстатокОтпуска.РабочийГодС 
							ИЛИ СтрокаПоСотруднику.КонецПериодаЗаКоторыйПредоставляетсяОтпуск <> ОстатокОтпуска.РабочийГодПо Тогда
							
							ОписаниеОшибки = Новый Структура("Сотрудник,ВидОтпуска");
							ЗаполнитьЗначенияСвойств(ОписаниеОшибки, СтрокаПоСотруднику);
							
							ПредставлениеОшибки = НСтр("ru='Не верно указан период, за который предоставляется отпуск';uk='Не вірно вказаний період, за який надається відпустка'");
							
							ОписаниеОшибки.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
							
							Расхождения.Добавить(ОписаниеОшибки);
							
						КонецЕсли; 
						
						Прервать;
						
					КонецЕсли; 
					
				КонецЦикла;
				
				Если СтрокаСоответствует = Неопределено Тогда
					
					ОписаниеОшибки = Новый Структура("Сотрудник,ВидОтпуска");
					ЗаполнитьЗначенияСвойств(ОписаниеОшибки, СтрокаПоСотруднику);
					
					ПредставлениеОшибки = НСтр("ru='Не найдено остатков отпуска';uk='Не знайдено залишків відпустки'");
					
					ОписаниеОшибки.Вставить("ПредставлениеОшибки", ПредставлениеОшибки);
					
					Расхождения.Добавить(ОписаниеОшибки);
							
				КонецЕсли; 
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Расхождения;
	
КонецФункции

&НаСервере
Процедура ОтменитьДокумент(ИдентификаторТекущейСтроки, Отказ)
	
	Если ИдентификаторТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	
	Попытка
		
		ДокументОбъект = ТекущиеДанные.Отпуск.ПолучитьОбъект();
		
		Если ТекущиеДанные.ВидОтпуска = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыОтпусков.Основной") Тогда
			
			ДокументОбъект.ПредоставитьОсновнойОтпуск = Ложь;
			ДокументОбъект.ДатаНачалаОсновногоОтпуска = '00010101';
			ДокументОбъект.ДатаОкончанияОсновногоОтпуска = '00010101';
			ДокументОбъект.КоличествоДнейОсновногоОтпуска = 0;
			ДокументОбъект.КоличествоДнейКомпенсацииОсновногоОтпуска = 0;
			
		Иначе
			
			СтрокиДополнительных = ДокументОбъект.ДополнительныеОтпуска.НайтиСтроки(Новый Структура("ВидОтпуска", ТекущиеДанные.ВидОтпуска));
			Для каждого СтрокаДопОтпуска Из СтрокиДополнительных Цикл
				ДокументОбъект.ДополнительныеОтпуска.Удалить(СтрокаДопОтпуска);
			КонецЦикла;
			
			Если ДокументОбъект.ДополнительныеОтпуска.Количество() = 0 Тогда
				ДокументОбъект.ПредоставитьДополнительныйОтпуск = Ложь;
			КонецЕсли; 
				
		КонецЕсли; 
		
		Если НЕ ДокументОбъект.ПредоставитьОсновнойОтпуск И НЕ ДокументОбъект.ПредоставитьДополнительныйОтпуск Тогда
			ДокументОбъект.ПометкаУдаления = Истина;
		КонецЕсли;
		
		ДокументОбъект.ДокументРассчитан = ЛОжь;
		
		ДокументОбъект.Записать(?(ДокументОбъект.Проведен, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписиДокумента.Запись));
		
	Исключение
		
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не удалось отменить отпуск. ';uk='Не вдалося скасувати відпустку. '") + ИнформацияОбОшибке().Описание, , , ,Отказ);
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьФорму()
	
	УстановитьФункциональныеОпцииФормы();
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);

КонецПроцедуры

&НаСервере
Процедура СотрудникиВидОтпускаПриИзмененииНаСервере()
	
	ТекущиеДанные = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.Сотрудники.ТекущаяСтрока);
	Если НЕ ЗначениеЗаполнено(ТекущиеДанные.ВидОтпуска) Тогда
		
		ТекущиеДанные.ОтпускЯвляетсяЕжегодным = Ложь;
		ТекущиеДанные.НачалоПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
		ТекущиеДанные.КонецПериодаЗаКоторыйПредоставляетсяОтпуск = '00010101';
	Иначе
		ЗаполнитьПериодыПредоставляемогоОтпуска(ТекущиеДанные);
	КонецЕсли; 
	
	УстановитьКоличествоДнейНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОЗаписиДокументаОтпуск()
	
	Оповестить ("Запись_Отпуск");
	
КонецПроцедуры

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Массив = Новый Массив;
	Массив.Добавить("Объект.Сотрудники");
	Возврат Массив
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",		НСтр("ru='организации';uk='організації'")));
	Возврат Массив
КонецФункции

#КонецОбласти
