
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ДоступныеКонтексты = "БУ";
	//++ НЕ УТКА
	ДоступныеКонтексты = ДоступныеКонтексты + ", МФУ";
	//-- НЕ УТКА
	ПараметрыФормы = Новый Структура(
		"Контекст, ДатаСведений, ТекущийРегистратор",
		ДоступныеКонтексты,
		ТекущаяДатаСеанса(),
		Документы.ПринятиеКУчетуНМА.ПустаяСсылка()
	);
	
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);
	
	ЗаполнитьПоКонтексту(ПараметрыФормы.Контекст);
	Список.Параметры.УстановитьЗначениеПараметра("ТекущийРегистратор", ПараметрыФормы.ТекущийРегистратор);
	Список.Параметры.УстановитьЗначениеПараметра("ДатаСведений", ПараметрыФормы.ДатаСведений);
	
	Если Параметры.Свойство("Отбор") И Параметры.Отбор.Количество()<>0 Тогда
		
		ЗаполнитьБыстрыеОтборы(Параметры.Отбор);
		
	КонецЕсли;
	
	Если Параметры.Свойство("ЗакрыватьПриВыборе") И Параметры.ЗакрыватьПриВыборе=Ложь Тогда
		Элементы.Список.МножественныйВыбор = Истина;
	КонецЕсли;
	
	Колонки = КолонкиПоКонтекстам();
	Для Каждого КлючИЗначениеКонтекст Из Колонки Цикл
		Признак = (КлючИЗначениеКонтекст.Ключ = ОтборКонтекст);
		Для Каждого КлючИЗначениеКолонка Из КлючИЗначениеКонтекст.Значение Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, КлючИЗначениеКолонка.Ключ, "Видимость", Признак);
		КонецЦикла;
	КонецЦикла;
	
	ВводНаОсновании.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюСоздатьНаОсновании);
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	МенюОтчеты.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюОтчеты);
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма, "ФормаСписка");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборКонтекстПриИзменении(Элемент)
	
	Для Каждого ЭлементФормы Из Элементы.СтраницыОтбор.ТекущаяСтраница.ПодчиненныеЭлементы Цикл
		Если Не ЭлементФормы.ТолькоПросмотр Тогда
			ЭтаФорма[ЭлементФормы.Имя] = Неопределено;
			ОтборПриИзменении(ЭлементФормы);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.СтраницыОтбор.ТекущаяСтраница = Элементы["Страница" + ОтборКонтекст];
	
	Колонки = КолонкиПоКонтекстам();
	Для Каждого КлючИЗначениеКонтекст Из Колонки Цикл
		Признак = (КлючИЗначениеКонтекст.Ключ = ОтборКонтекст);
		Для Каждого КлючИЗначениеКолонка Из КлючИЗначениеКонтекст.Значение Цикл
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, КлючИЗначениеКолонка.Ключ, "Видимость", Признак);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

#Область ОтборБУ

&НаКлиенте
Процедура ОтборБУСостояниеПриИзменении(Элемент)
	ОтборПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОтборБУОрганизацияПриИзменении(Элемент)
	ОтборПриИзменении(Элемент);
КонецПроцедуры


#КонецОбласти

#Область ОтборМФУ

&НаКлиенте
Процедура ОтборМФУСостояниеПриИзменении(Элемент)
	ОтборПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОтборМФУОрганизацияПриИзменении(Элемент)
	ОтборПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОтборМФУПодразделениеПриИзменении(Элемент)
	ОтборПриИзменении(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ОтборМФУГруппаНМАПриИзменении(Элемент)
	ОтборПриИзменении(Элемент);
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СтандатрныеПодсистемы

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуСоздатьНаОсновании(Команда)
	
	ВводНаОснованииКлиент.ВыполнитьПодключаемуюКомандуСоздатьНаОсновании(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтотОбъект, Элементы.Список);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция КолонкиПоКонтекстам()
	
	Результат = Новый Структура;
	
	Результат.Вставить("БУ", Новый Структура);
	Результат.БУ.Вставить("БУСостояние");
	Результат.БУ.Вставить("БУОрганизация");
	Результат.БУ.Вставить("БУПодразделение");
	Результат.БУ.Вставить("БУМОЛ");
	 
	//++ НЕ УТКА
	Результат.Вставить("МФУ", Новый Структура);
	Результат.МФУ.Вставить("МФУСостояние");
	Результат.МФУ.Вставить("МФУОрганизация");
	Результат.МФУ.Вставить("МФУПодразделение");
	Результат.МФУ.Вставить("МФУГруппаНМА");
	//-- НЕ УТКА
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		СтрЗаменить(Элемент.Имя, "Отбор", ""),
		ЭтаФорма[Элемент.Имя],
		ВидСравненияКомпоновкиДанных.Равно,,
		ЗначениеЗаполнено(ЭтаФорма[Элемент.Имя]));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоКонтексту(Знач ДоступныеКонтексты)
	
	СтруктураДоступныеКонтексты = Новый Структура(ДоступныеКонтексты);
	
	Элементы.ОтборКонтекст.СписокВыбора.Очистить();
	
	Если СтруктураДоступныеКонтексты.Свойство("БУ") Тогда
		Если ПравоДоступа("Чтение", Метаданные.Документы.ПринятиеКУчетуНМА) Тогда
			Элементы.ОтборКонтекст.СписокВыбора.Добавить("БУ", НСтр("ru='Бухгалтерский учет';uk='Бухоблік'"));
		Иначе
			СтруктураДоступныеКонтексты.Удалить("БУ");
		КонецЕсли;
	КонецЕсли;
	//++ НЕ УТКА
	Если СтруктураДоступныеКонтексты.Свойство("МФУ") Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет")
		 И ПравоДоступа("Чтение", Метаданные.Документы.ПринятиеКУчетуНМАМеждународныйУчет) Тогда
			Элементы.ОтборКонтекст.СписокВыбора.Добавить("МФУ", НСтр("ru='Международный учет';uk='Міжнародний облік'"));
		Иначе
			СтруктураДоступныеКонтексты.Удалить("МФУ");
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТКА
	
	// Контекст заданный в строке первым - является значением по умолчанию
	Для Каждого КлючИЗначение Из СтруктураДоступныеКонтексты Цикл
		ОтборКонтекст = КлючИЗначение.Ключ;
		Прервать;
	КонецЦикла;
	
	// Текущая страница отбора
	Элементы.СтраницыОтбор.ТекущаяСтраница = Элементы["Страница" + ОтборКонтекст];
	
	// Если вариант контекста один - можно не показывать переключатель
	Элементы.ОтборКонтекст.Видимость = (СтруктураДоступныеКонтексты.Количество()>1);
	
	ЗаполнитьТекстЗапросаДинамическогоСписка(СтруктураДоступныеКонтексты);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБыстрыеОтборы(Знач УстановленныйОтбор)
	
	ДоступныеОтборы = "ОтборБУСостояние, ОтборБУОрганизация";
	
	//++ НЕ УТКА
	ДоступныеОтборы = ДоступныеОтборы + ",ОтборМФУСостояние, ОтборМФУОрганизация, ОтборМФУПодразделение, ОтборМФУГруппаНМА";
	//-- НЕ УТКА
	СтруктураОтборов = Новый Структура(ДоступныеОтборы);
	
	Для Каждого КлючИЗначение Из УстановленныйОтбор Цикл
		
		Если СтруктураОтборов.Свойство("Отбор" + КлючИЗначение.Ключ) Тогда
			
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") Или ТипЗнч(КлючИЗначение.Значение) = Тип("ФиксированныйМассив") Тогда
				Если КлючИЗначение.Значение.Количество() = 1 Тогда
					ЭтаФорма["Отбор" + КлючИЗначение.Ключ] = КлючИЗначение.Значение[0];
				Иначе
					
					ПодсказкаВвода = "";
					Если КлючИЗначение.Значение.Количество() = 0 Тогда
						ПодсказкаВвода = НСтр("ru='<не указано>';uk='<не вказано>'");
					Иначе
						Для Каждого Элемент Из КлючИЗначение.Значение Цикл
							ПодсказкаВвода = ПодсказкаВвода + ", " + Строка(Элемент);
						КонецЦикла;
					КонецЕсли;
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отбор" + КлючИЗначение.Ключ, "ПодсказкаВвода", Сред(ПодсказкаВвода, 3));
					
				КонецЕсли;
			ИначеЕсли Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отбор" + КлючИЗначение.Ключ, "ПодсказкаВвода", НСтр("ru='<не указано>';uk='<не вказано>'"));
			Иначе
				ЭтаФорма["Отбор" + КлючИЗначение.Ключ] = КлючИЗначение.Значение;
			КонецЕсли;
			
			// Скрыть элемент быстрого отбора
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Отбор" + КлючИЗначение.Ключ, "ТолькоПросмотр", Истина);
			
		КонецЕсли;
		
		// Скрыть колонку динамического списка
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, КлючИЗначение.Ключ, "Видимость", Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекстЗапросаДинамическогоСписка(СтруктураДоступныеКонтексты)
	
	ТексЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаСправочника.Ссылка,
	|	ТаблицаСправочника.ПометкаУдаления,
	|	ТаблицаСправочника.Родитель,
	|	ТаблицаСправочника.ЭтоГруппа,
	|	ТаблицаСправочника.Код,
	|	ТаблицаСправочника.Наименование,
	|	ТаблицаСправочника.НаименованиеПолное,
	|	ТаблицаСправочника.Предопределенный,
	|	ТаблицаСправочника.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаСправочника.ИмяПредопределенныхДанных%1
	|ИЗ
	|	Справочник.НематериальныеАктивы КАК ТаблицаСправочника%2";
	
	ПоляВЗапрос = "";
	ТаблицыВЗапрос = "";
	
	Если Не СтруктураДоступныеКонтексты.Свойство("БУ") Тогда
		
		ПоляВЗапрос = ПоляВЗапрос + ",
		|	
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК БУОрганизация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК БУПодразделение,
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК БУМОЛ,		
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету) КАК БУСостояние
		|	";
		
		
	Иначе
		
		ПоляВЗапрос = ПоляВЗапрос + ",
		|	
		|	ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК БУОрганизация,
		|	ЕСТЬNULL(МестонахождениеНМАБухгалтерскийУчетСрезПоследних.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК БУПодразделение,
		|	ЕСТЬNULL(МестонахождениеНМАБухгалтерскийУчетСрезПоследних.МОЛ, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК БУМОЛ,
		|	ЕСТЬNULL(СостоянияНМАОрганизацийСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)) КАК БУСостояние
		|	";
		ТаблицыВЗапрос = ТаблицыВЗапрос + "
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(&ДатаСведений, Регистратор <> &ТекущийРегистратор) КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
		|			ПО ТаблицаСправочника.Ссылка = ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеНМАБухгалтерскийУчет.СрезПоследних(&ДатаСведений, Регистратор <> &ТекущийРегистратор) КАК МестонахождениеНМАБухгалтерскийУчетСрезПоследних
		|			ПО ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив = МестонахождениеНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
		|			И ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Организация = МестонахождениеНМАБухгалтерскийУчетСрезПоследних.Организация
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияНМАОрганизаций.СрезПоследних(&ДатаСведений, Регистратор <> &ТекущийРегистратор) КАК СостоянияНМАОрганизацийСрезПоследних
		|			ПО ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив = СостоянияНМАОрганизацийСрезПоследних.НематериальныйАктив
		|			И ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.Организация = СостоянияНМАОрганизацийСрезПоследних.Организация";
		
	КонецЕсли;
	
	Если Не СтруктураДоступныеКонтексты.Свойство("МФУ") Тогда
		
		ПоляВЗапрос = ПоляВЗапрос + ",
		|	
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету) КАК МФУСостояние,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК МФУОрганизация,
		|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК МФУПодразделение,
		|	"""" КАК МФУГруппаНМА";
		
	//++ НЕ УТКА
	Иначе
		
		ПоляВЗапрос = ПоляВЗапрос + ",
		|	
		|	ЕСТЬNULL(НематериальныеАктивыМеждународныйУчетУчетСрезПоследних.Состояние, ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)) КАК МФУСостояние,
		|	ЕСТЬNULL(НематериальныеАктивыМеждународныйУчетУчетСрезПоследних.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК МФУОрганизация,
		|	ЕСТЬNULL(НематериальныеАктивыМеждународныйУчетУчетСрезПоследних.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК МФУПодразделение,
		|	ЕСТЬNULL(ТаблицаСправочника.ГруппаНМАМеждународныйУчет, ЗНАЧЕНИЕ(Справочник.ГруппыНМАМеждународныйУчет.ПустаяСсылка)) КАК МФУГруппаНМА";
		
		ТаблицыВЗапрос = ТаблицыВЗапрос + "
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НематериальныеАктивыМеждународныйУчет.СрезПоследних(&ДатаСведений, Регистратор <> &ТекущийРегистратор) КАК НематериальныеАктивыМеждународныйУчетУчетСрезПоследних
		|			ПО ТаблицаСправочника.Ссылка = НематериальныеАктивыМеждународныйУчетУчетСрезПоследних.НематериальныйАктив";
		
	//-- НЕ УТКА
	КонецЕсли;
	
	
	Список.ТекстЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТексЗапроса,
		ПоляВЗапрос,
		ТаблицыВЗапрос);
	
КонецПроцедуры

#КонецОбласти
