#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения.Свойство("Родитель") И ЗначениеЗаполнено(ДанныеЗаполнения.Родитель) Тогда
		РодительГруппа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Родитель, "ЭтоГруппаШаблонов");
		Если Не РодительГруппа Тогда
			ДанныеЗаполнения.Родитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.Родитель, "Родитель");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппаШаблонов Тогда
		ПроверяемыеРеквизиты.Очистить();
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	ИсточникДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Операция, "ИсточникДанных");
	ПоказателиВВалюте = МеждународныйУчетСерверПовтИсп.ПоказателиВВалюте(ИсточникДанных);
	
	Если НЕ МеждународныйУчетОбщегоНазначения.СчетВалютный(СчетДебетаПоУмолчанию) 
		ИЛИ ПоказателиВВалюте = Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникСуммыВВалютеДт");
	КонецЕсли;
	
	Если НЕ МеждународныйУчетОбщегоНазначения.СчетВалютный(СчетКредитаПоУмолчанию)
		ИЛИ ПоказателиВВалюте = Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникСуммыВВалютеКт");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ИндексКартинки = Справочники.ШаблоныПроводокДляМеждународногоУчета.ИндексКартинки(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли; 
	
	Если ПометкаУдаления Тогда
		НаборЗаписей = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ШаблонПроводки.Установить(Ссылка);
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли