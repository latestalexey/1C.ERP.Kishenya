
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	ЗаполнитьПолеВидовАналитик();
	
	КонтрольЛимитов = ПолучитьФункциональнуюОпцию("КонтролироватьПревышениеЛимитовРасходаДенежныхСредств");
	Элементы.ГруппаЛимиты.Видимость = КонтрольЛимитов;
	
	ЭтаФорма.КлючСохраненияПоложенияОкна = "ПравилаЛимитовПоДаннымБюджетирования.Форма.Лимиты_" + КонтрольЛимитов;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ИспользуемыеАналитики.Очистить();
	Для Каждого ЭлементСписка из ИспользуемыеАналитики Цикл
		Если Не ЭлементСписка.Пометка Тогда
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(ЭлементСписка.Значение) = Тип("Строка") Тогда
			НоваяСтрока = ТекущийОбъект.ИспользуемыеАналитики.Добавить();
			НоваяСтрока.ИмяИзмерения = ЭлементСписка.Значение;
		Иначе
			НоваяСтрока = ТекущийОбъект.ИспользуемыеАналитики.Добавить();
			НоваяСтрока.ВидАналитики = ЭлементСписка.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатьяПоказательБюджетаПриИзменении(Элемент)
	
	СтатьяПоказательБюджетаПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТребуемыеРеквизитыСтатьи()
	
	Возврат "ВидАналитики1, ВидАналитики2, ВидАналитики3, ВидАналитики4, ВидАналитики5, ВидАналитики6,
						|ВидАналитики1.Наименование, ВидАналитики2.Наименование, ВидАналитики3.Наименование,
						|ВидАналитики4.Наименование, ВидАналитики5.Наименование, ВидАналитики6.Наименование";
	
КонецФункции

&НаСервере
Процедура СтатьяПоказательБюджетаПриИзмененииНаСервере()
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СтатьяБюджета, 
												"Наименование, " + ТребуемыеРеквизитыСтатьи());
	
	Объект.Наименование = Реквизиты.Наименование;
	ЗаполнитьПолеВидовАналитик(Реквизиты);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПолеВидовАналитик(Реквизиты = Неопределено)
	
	ИспользуемыеАналитики.Очистить();
	
	Если Не ЗначениеЗаполнено(Объект.СтатьяБюджета) Тогда
		Возврат;
	КонецЕсли;
	
	Если Реквизиты = Неопределено Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СтатьяБюджета, ТребуемыеРеквизитыСтатьи());
	КонецЕсли;
	
	РеквизитыМодели = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец, 
											"БюджетыПоПодразделениям, БюджетыПоОрганизациям");
	
	Если РеквизитыМодели.БюджетыПоПодразделениям Тогда
		ИспользуемыеАналитики.Добавить("Подразделение", "Подразделение", 
				Объект.ИспользуемыеАналитики.НайтиСтроки(Новый Структура("ИмяИзмерения", "Подразделение")).Количество());
	КонецЕсли;
	
	Если РеквизитыМодели.БюджетыПоОрганизациям Тогда
		ИспользуемыеАналитики.Добавить("Организация", "Организация", 
				Объект.ИспользуемыеАналитики.НайтиСтроки(Новый Структура("ИмяИзмерения", "Организация")).Количество());
	КонецЕсли;
	
	Для Сч = 1 по 6 Цикл
		ВидАналитики = Реквизиты["ВидАналитики" + Сч];
		Если Не ЗначениеЗаполнено(ВидАналитики) Тогда
			Продолжить;
		КонецЕсли;
		ИспользуемыеАналитики.Добавить(ВидАналитики, Реквизиты["ВидАналитики" + Сч + "Наименование"], 
				Объект.ИспользуемыеАналитики.НайтиСтроки(Новый Структура("ВидАналитики", ВидАналитики)).Количество());
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

