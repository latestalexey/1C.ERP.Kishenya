
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	
	Если НЕ Объект.СодержитВложенныйМаршрут Тогда
		// Реквизит может быть другим при смене вида операции
		Объект.СодержитВложенныйМаршрут = Истина;
		Модифицированность = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВложенныйМаршрутПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ВложенныйМаршрут) И НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = ОбщегоНазначенияУТВызовСервера.ЗначениеРеквизитаОбъекта(Объект.ВложенныйМаршрут, "Представление");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоСтатусуСервер()
	
	Если НЕ Объект.Владелец.Пустая() Тогда
		СтатусВладельца = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Владелец, "Статус");
		ДоступностьРеквизитов = (СтатусВладельца <> Перечисления.СтатусыМаршрутныхКарт.Действует
								И СтатусВладельца <> Перечисления.СтатусыМаршрутныхКарт.Закрыта
								И НЕ ТолькоПросмотр);
								
	Иначе
		ДоступностьРеквизитов = Ложь;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	// Элементы управления шапки
	МассивЭлементов.Добавить("НомерОперации");
	МассивЭлементов.Добавить("НомерСледующейОперации");
	МассивЭлементов.Добавить("Количество");
	МассивЭлементов.Добавить("ЕдиницаИзмерения");
	МассивЭлементов.Добавить("ВложенныйМаршрут");
	МассивЭлементов.Добавить("Владелец");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", НЕ ДоступностьРеквизитов)
	
КонецПроцедуры

#КонецОбласти
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

