
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыРасширенныйФормы.ИнициализироватьМеханизмПересчетаДокументаПриРедактировании(ЭтаФорма);
	
	// Подсистема "Охрана труда"
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		МодульОхранаТруда = ОбщегоНазначения.ОбщийМодуль("ОхранаТрудаФормы");
		КлассыУсловийТрудаДоступныДляИзменения = МодульОхранаТруда.КлассыУсловийТрудаДоступныДляИзменения();
	КонецЕсли;
	
	ВнешниеДанные = Ложь;
	СуществующаяПозиция = Ложь;
	ЗаголовокФормы = "";
	
	СозданиеНовой = Параметры.Ключ.Пустая();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.УправленческаяПозицияПриСозданииНаСервере(ЭтотОбъект, Параметры, СозданиеНовой);
	КонецЕсли;
	
	Если СозданиеНовой И НЕ Параметры.Свойство("АдресДанныхПозицииВХранилище") Тогда
		
		СсылкаНаОбъект = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка).ПолучитьСсылку();
		
		Если Не ЗначениеЗаполнено(Объект.Подразделение)
			И Параметры.ЗначенияЗаполнения.Свойство("Родитель") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Родитель) Тогда
			
			Объект.Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначенияЗаполнения.Родитель, "Подразделение");
			
		КонецЕсли; 
		
		ЗапомнитьЗначенияФункциональныхОпцийИНабораПрав();
		
		ОрганизацияФормы = Объект.Владелец;
		ДополнитьФорму();
		
		ЗначенияДляЗаполнения = Новый Структура;
		ЗначенияДляЗаполнения.Вставить("Организация",	"Объект.Владелец");
		ЗначенияДляЗаполнения.Вставить("ГрафикРаботы",	"Объект.ГрафикРаботыСотрудников");
		
		ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
		
		Если Не ЗначениеЗаполнено(Объект.ДатаУтверждения) Тогда
			УстановитьПодходящуюДатуУтверждения();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ТекущийОбъект = РеквизитФормыВЗначение("Объект");
			ПоместитьДанныеОбъектаВФорму(ТекущийОбъект, Параметры.ЗначениеКопирования);
		КонецЕсли;
		
	Иначе
		
		Если Параметры.Свойство("АдресДанныхПозицииВХранилище") Тогда
			
			ЗапомнитьЗначенияФункциональныхОпцийИНабораПрав();
			
			Если ИспользоватьИсторию Тогда
				
				ДанныеПозиции = ПолучитьИзВременногоХранилища(Параметры.АдресДанныхПозицииВХранилище);
				
				ДанныеПозиции.Свойство("Действие", Действие);
				ДанныеПозиции.Свойство("ТекущееКоличествоСтавок", ТекущееКоличествоСтавок);
				ДанныеПозиции.Свойство("ТекущийФОТ", ТекущийФОТ);
				ЗаполнитьЗначенияСвойств(Объект, ДанныеПозиции, , "Начисления,Показатели,ЕжегодныеОтпуска,Специальности");
				Объект.Владелец = ДанныеПозиции.Организация;
				
				ВнешниеДанные = Истина;
				УстановитьПредставлениеОкладаИФОТ(ЭтаФорма);
				
				РедактируемаяПозиция = ДанныеПозиции.ПозицияШтатногоРасписания;
				СуществующаяПозиция = ЗначениеЗаполнено(РедактируемаяПозиция);
				
				Если Не СуществующаяПозиция Тогда
					
					СсылкаНаОбъект = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Объект.Ссылка).ПолучитьСсылку();
					
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
						Элементы,
						"Наименование",
						"ТолькоПросмотр",
						Истина);
					
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
						Элементы,
						"Наименование",
						"Подсказка",
						НСтр("ru='Позиция еще не сохранена в базе данных, изменить наименование можно будет после проведения документа';uk='Позиція ще не збережено в базі даних, змінювати найменування можна буде після проведення документа'"));
						
					ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
						Элементы,
						"Наименование",
						"ОтображениеПодсказки",
						ОтображениеПодсказки.Кнопка);
				
				КонецЕсли;
				
				ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
					Элементы,
					"Склонения",
					"Доступность",
					Ложь);
				
				Если ДанныеПозиции.Свойство("ВыборПодчиненныхПодразделений")
					И ЗначениеЗаполнено(ДанныеПозиции.ПодразделениеПозиций) Тогда
					
					Запрос = Новый Запрос;
					Запрос.УстановитьПараметр("Подразделение", ДанныеПозиции.ПодразделениеПозиций);
					Запрос.УстановитьПараметр("ДатаУтверждения", ДанныеПозиции.ДатаУтверждения);
					Запрос.УстановитьПараметр("Регистратор", ДанныеПозиции.Регистратор);
					
					Запрос.Текст =
						"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
						|	ПодразделенияОрганизаций.Ссылка
						|ИЗ
						|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
						|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
						|		ПО ПодразделенияОрганизаций.Ссылка = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания.Подразделение
						|			И (ИсторияИспользованияШтатногоРасписания.Дата = &ДатаУтверждения)
						|			И (ИсторияИспользованияШтатногоРасписания.Регистратор <> &Регистратор)
						|ГДЕ
						|	ПодразделенияОрганизаций.Ссылка В ИЕРАРХИИ(&Подразделение)
						|	И (ПодразделенияОрганизаций.ДатаРасформирования = ДАТАВРЕМЯ(1, 1, 1)
						|			ИЛИ ПодразделенияОрганизаций.ДатаРасформирования < &ДатаУтверждения)
						|	И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания ЕСТЬ NULL ";
					
					СписокВыбораПодразделений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
					
					ПолеПодразделение = Элементы.Найти("Подразделение");
					Если ПолеПодразделение <> Неопределено Тогда
						
						ПараметрыВыбораПодразделения = Новый Массив(ПолеПодразделение.ПараметрыВыбора);
						ПараметрыВыбораПодразделения.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(СписокВыбораПодразделений)));
						ПараметрыВыбораПодразделения.Добавить(Новый ПараметрВыбора("Отображение", ОтображениеТаблицы.Список));
						ПолеПодразделение.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораПодразделения);
						
					КонецЕсли;
					
					Если СписокВыбораПодразделений.Количество() = 1 Тогда
						
						ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
							Элементы,
							"Подразделение",
							"ТолькоПросмотр",
							Истина);
						
					КонецЕсли;
					
				КонецЕсли;
				
				ДополнитьФорму();
				
				ТолькоПросмотр = ДанныеПозиции.ТолькоПросмотр;
				
				Объект.ДатаУтверждения = ДанныеПозиции.ДатаУтверждения;
				
				ОрганизацияФормы = Объект.Владелец;
				
				
				Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
					Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
					Модуль.ПриПолученииВнешнихДанныхПозицииШтатногоРасписания(ЭтотОбъект, ДанныеПозиции);
				КонецЕсли;
				
				Если СуществующаяПозиция Тогда
					АвтоЗаголовок = Ложь;
					Заголовок = Строка(ДанныеПозиции.ПозицияШтатногоРасписания);
				Иначе
					
					Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
						Объект.Подразделение = ДанныеПозиции.ПодразделениеПозиций;
					КонецЕсли;
					
				КонецЕсли;
				
				Объект.Начисления.Очистить();
				Объект.Показатели.Очистить();
				Объект.ЕжегодныеОтпуска.Очистить();
				Объект.Специальности.Очистить();
				
				Для каждого ОписаниеНачисления Из ДанныеПозиции.Начисления Цикл
					ЗаполнитьЗначенияСвойств(Объект.Начисления.Добавить(), ОписаниеНачисления);
				КонецЦикла;
					
				Для каждого ОписаниеПоказателя Из ДанныеПозиции.Показатели Цикл
					ЗаполнитьЗначенияСвойств(Объект.Показатели.Добавить(), ОписаниеПоказателя);
				КонецЦикла;
					
				Для каждого ОписаниеЕжегодногоОтпуска Из ДанныеПозиции.ЕжегодныеОтпуска Цикл
					ЗаполнитьЗначенияСвойств(Объект.ЕжегодныеОтпуска.Добавить(), ОписаниеЕжегодногоОтпуска);
				КонецЦикла;
				
				Для каждого ОписаниеСпециальности Из ДанныеПозиции.Специальности Цикл
					ЗаполнитьЗначенияСвойств(Объект.Специальности.Добавить(), ОписаниеСпециальности);
				КонецЦикла;
				
			Иначе
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ВнешниеДанные Тогда
		
		// СтандартныеПодсистемы.Печать
		УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
		// Конец СтандартныеПодсистемы.Печать
		
		// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
		ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
		// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
		
		// СтандартныеПодсистемы.ВерсионированиеОбъектов
		ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
		// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
		
		// СтандартныеПодсистемы.Свойства
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Объект", Объект);
		ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
		УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
		// Конец СтандартныеПодсистемы.Свойства
		
		УправлениеШтатнымРасписаниемФормы.УстановитьПараметрыВыбораПодразделений(ЭтаФорма, "Подразделение");
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОплатаТрудаСтраница",
			"Видимость",
			Ложь);
		
	КонецЕсли; 
	
	ПараметрыСтажевыхЭлементов = ОстаткиОтпусков.ПараметрыДляДополнитьТабличнуюЧастьСтажевымиЭлементами();
	ПараметрыСтажевыхЭлементов.Форма = ЭтаФорма;
	ПараметрыСтажевыхЭлементов.ИмяТаблицы = "ЕжегодныеОтпуска";
	ПараметрыСтажевыхЭлементов.ТабличнаяЧастьВОбъекте = Истина;
	ОстаткиОтпусков.ДополнитьТабличнуюЧастьСтажевымиЭлементами(ПараметрыСтажевыхЭлементов);
	
	УстановитьСвойстваЭлементовУправления();
	УстановитьДоступностьЭлементов(ЭтаФорма);
	
	Элементы.ТарифнаяСетка.Заголовок = РазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитТарифнаяСеткаТариф");
	Элементы.РазрядКатегория.Заголовок = РазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитРазрядКатегорияТариф");	
	Элементы.ТарифнаяСеткаНадбавки.Заголовок = РазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитТарифнаяСеткаНадбавка");
	Элементы.РазрядКатегорияНадбавки.Заголовок = РазрядыКатегорииДолжностей.ИнициализироватьЗаголовокФормыИРеквизитов("РеквизитРазрядКатегорияНадбавка");		
	
	Если СозданиеНовой Тогда 
		ДоступныВсеЛьготы = 1;
	КонецЕсли;
	
	УстановитьДоступностьИндивидуальныхЛьгот();
	УстановитьДоступностьРазмераЛимитаЛьготы(ЭтотОбъект);
	
		
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	СклонениеПредставленийОбъектов.ПриСозданииНаСервере(ЭтотОбъект, Объект.Наименование);	
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ВнешниеДанные Тогда
		
		// СтандартныеПодсистемы.Свойства
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.Свойства
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ВнешниеДанные Тогда
		Модифицированность = Ложь;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьИЗакрытьНаКлиенте", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если НЕ ВнешниеДанные Тогда
		
		Если СозданиеНовой И НЕ Параметры.Ключ.Пустая() Тогда
			ПараметрыОповещения = Новый Структура;
			ПараметрыОповещения.Вставить("Ссылка", Объект.Ссылка);
			ПараметрыОповещения.Вставить("Подразделение", Объект.Подразделение);
			ПараметрыОповещения.Вставить("ДатаУтверждения", Объект.ДатаУтверждения);
			ПараметрыОповещения.Вставить("ДатаЗакрытия", Объект.ДатаЗакрытия);
			Оповестить("СозданаПозицияШтатногоРасписания", ПараметрыОповещения, ВладелецФормы);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Параметры.Свойство("АдресДанныхПозицииВХранилище")
		И Не ПустаяСтрока(Параметры.АдресДанныхПозицииВХранилище) Тогда
		Возврат;
	КонецЕсли; 
	
	ИзмененыНачисления = Ложь;
	
	СсылкаНаОбъект = Объект.Ссылка;
	ОрганизацияФормы = Объект.Владелец;
	
	ЗапомнитьЗначенияФункциональныхОпцийИНабораПрав();
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДополнитьФорму();
	ПоместитьДанныеОбъектаВФорму(ТекущийОбъект);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.ПрочитатьЛьготыПозиции(ЭтотОбъект, Объект.Ссылка, ОписаниеТаблицыЛьгот(), , "ИндивидуальныеЛьготы");
		Модуль.ПрочитатьЛимитыЛьготПозиции(ЭтотОбъект);
	КонецЕсли;
	
	Если Не ВнешниеДанные Тогда
		
		// СтандартныеПодсистемы.Свойства
		УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
		// Конец СтандартныеПодсистемы.Свойства
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Не ВнешниеДанные Тогда
		
		// СтандартныеПодсистемы.Свойства
		Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
			ОбновитьЭлементыДополнительныхРеквизитов();
			УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		КонецЕсли;
		// Конец СтандартныеПодсистемы.Свойства
		
	КонецЕсли; 
	
	
	Если ИмяСобытия = "ЗаписанаПозицияКадровогоРезерва" Тогда
		ОбновитьЭлементыКадровогоРезерва();
	КонецЕсли;
	
	Если ИмяСобытия = "СохраненыПозицииШтатногоРасписания" Тогда
		
		Если Параметр.Получить(Объект.Ссылка) <> Неопределено Тогда
			Прочитать();
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ВнешниеДанные Тогда
		
		// СтандартныеПодсистемы.Свойства
		УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
		// Конец СтандартныеПодсистемы.Свойства
		
	КонецЕсли; 
	
	// Удаление отмененных начислений при проверке внешних данных.
	Если ВнешниеДанные Тогда
		
		НачисленияОбъектаФормы = Объект.Начисления.Выгрузить();
		ОтменяемыеСтроки = Объект.Начисления.НайтиСтроки(Новый Структура("Действие", Перечисления.ДействияСНачислениямиИУдержаниями.Отменить));
		Для каждого ОтменяемаяСтрока Из ОтменяемыеСтроки Цикл
			Объект.Начисления.Удалить(ОтменяемаяСтрока);
		КонецЦикла;
		
		ПроверяемыйОбъект = РеквизитФормыВЗначение("Объект", Тип("СправочникОбъект.ШтатноеРасписание"));
		РеквизитыВДанные(ПроверяемыйОбъект);
		
		Объект.Начисления.Загрузить(НачисленияОбъектаФормы);
		
	Иначе
		
		ПроверяемыйОбъект = РеквизитФормыВЗначение("Объект", Тип("СправочникОбъект.ШтатноеРасписание"));
		РеквизитыВДанные(ПроверяемыйОбъект);
		
	КонецЕсли; 
	
	Отказ = Отказ Или Не ПроверяемыйОбъект.ПроверитьЗаполнение();
	ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "Объект");	
	
	// Подсистема "Охрана труда"
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		МодульОхранаТруда = ОбщегоНазначения.ОбщийМодуль("ОхранаТрудаФормы");
		МодульОхранаТруда.ПроверитьЗаполнениеПозицииШтатногоРасписания(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ОчиститьСообщения();
	
	Если РасчетНеобходимоВыполнить Тогда
		РассчитатьФОТНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Параметры.Ключ.Пустая() Тогда
		ТекущийОбъект.УстановитьСсылкуНового(СсылкаНаОбъект);
	КонецЕсли;
	
	// Подсистема "Охрана труда"
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда") Тогда
		МодульОхранаТруда = ОбщегоНазначения.ОбщийМодуль("ОхранаТрудаФормы");
		МодульОхранаТруда.ЗаписатьДополнительныеЗначенияПозицииШтатногоРасписания(ЭтотОбъект, СсылкаНаОбъект);
	КонецЕсли;
	РеквизитыВДанные(ТекущийОбъект);
	
	Если Не ВнешниеДанные Тогда
		// СтандартныеПодсистемы.Свойства
		УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
		// Конец СтандартныеПодсистемы.Свойства
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Грейды");
		Модуль.ЗаписатьЗначениеГрейда(ЭтотОбъект, ТекущийОбъект.Ссылка);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.СохранитьДанныеПозиции(ЭтотОбъект);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ПриЗаписиФормыПозицииШтатногоРасписания(ЭтаФорма, ТекущийОбъект);
	КонецЕсли;
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	СклонениеПредставленийОбъектов.ПриЗаписиНаСервере(ЭтотОбъект, Объект.Наименование, ТекущийОбъект.Ссылка);	
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	ПоместитьДанныеОбъектаВФорму(ТекущийОбъект);
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	Объект.Владелец = ОрганизацияФормы;
	ВладелецПриИзмененииНаСервере();
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыСотрудников) Тогда
		ВыполнитьРасчетФОТ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ЗаполнитьНаименование();
	ПодразделениеПриИзмененииНаСервере();
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыСотрудников) Тогда
		ВыполнитьРасчетФОТ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_МестоВСтруктуреПредприятияПриИзменении(Элемент)
	ЗаполнитьНаименование();
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	
	ЗаполнитьНаименование();
	ДолжностьПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РазрядКатегорияПриИзменении(Элемент)
	
	ЗаполнитьНаименование();
	УстановитьДоступностьСведенийПозиции();
	
	ИспользоватьТарифныеСетки = ПолучитьФункциональнуюОпциюФормы("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты");
	Если ИспользоватьТарифныеСетки Тогда
		ПриИзмененииТарифнойСетки();
		ВыполнитьРасчетФОТ(Истина);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура РазрядКатегорияНадбавкиПриИзменении(Элемент)
	
	ПриИзмененииТарифнойСеткиНадбавки();
	ВыполнитьРасчетФОТ(Ложь, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифнаяСеткаПриИзменении(Элемент)
		
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты") Тогда
		ПриИзмененииТарифнойСетки();
		ВыполнитьРасчетФОТ(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифнаяСеткаНадбавкиПриИзменении(Элемент)
	
	ПриИзмененииТарифнойСеткиНадбавки();
	ВыполнитьРасчетФОТ(Ложь, Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов(ЭтаФорма);
	
	Если Не Объект.Утверждена Тогда
		Объект.ДатаУтверждения = '00010101';
	ИначеЕсли Объект.ДатаУтверждения = '00010101' Тогда
		Объект.ДатаУтверждения = КонецМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()) + 1;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытаПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов(ЭтаФорма);
	
	Если Не Объект.Закрыта Тогда
		Объект.ДатаЗакрытия = '00010101';
	ИначеЕсли Объект.ДатаЗакрытия = '00010101' Тогда
		Объект.ДатаЗакрытия = КонецМесяца(ОбщегоНазначенияКлиент.ДатаСеанса()) + 1;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСтавокРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	КадровыйУчетРасширенныйКлиент.КоличествоСтавокРегулирование(ЭтаФорма, Объект.КоличествоСтавок, Направление, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ГрафикРаботыСотрудниковПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ГрафикРаботыСотрудников) Тогда
		ВыполнитьРасчетФОТ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура УтвердившийРегистраторНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеШтатнымРасписаниемКлиент.ОткрытьФормуДокументаИзменившегоПозицию(УтвердившийРегистратор, Объект.Ссылка);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрывшийРегистраторНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	УправлениеШтатнымРасписаниемКлиент.ОткрытьФормуДокументаИзменившегоПозицию(ЗакрывшийРегистратор, Объект.Ссылка);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныВсеЛьготыПриИзменении(Элемент)
	
	УстановитьДоступностьИндивидуальныхЛьгот();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьЛимитПриИзменении(Элемент)
	
	УстановитьДоступностьРазмераЛимитаЛьготы(ЭтотОбъект);
	
КонецПроцедуры


&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	// СтандартныеПодсистемы.СклонениеПредставленийОбъектов
	СклонениеПредставленийОбъектовКлиент.ПриИзмененииПредставления(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийТаблицыФормыНачисления

&НаКлиенте
Процедура НачисленияПриАктивизацииСтроки(Элемент)
	
	Если ВнешниеДанные Тогда
		УправлениеШтатнымРасписаниемКлиент.НачисленияПриАктивизацииСтроки(ЭтаФорма);
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
		
	УправлениеШтатнымРасписаниемКлиент.НачисленияПриНачалеРедактирования(ЭтаФорма, НоваяСтрока, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	УправлениеШтатнымРасписаниемКлиент.НачисленияНачислениеПриИзменении(ЭтаФорма, ДатаСобытия(ЭтаФорма, ОбщегоНазначенияКлиент.ДатаСеанса()), Объект.РазрядКатегория, Объект.ТарифнаяСетка, 
		Объект.РазрядКатегорияНадбавки, Объект.ТарифнаяСеткаНадбавки, Истина);

КонецПроцедуры

&НаКлиенте
Процедура НачисленияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если ВнешниеДанные Тогда
		УправлениеШтатнымРасписаниемКлиент.НачисленияПриОкончанииРедактирования(ЭтаФорма, Элемент, НоваяСтрока, ОтменаРедактирования);
	КонецЕсли; 
	
	Если ИспользоватьВилкуСтавок Тогда
		
		Для НомерПоказателя = 1 По ЗарплатаКадрыРасширенныйКлиентСервер.МаксимальноеКоличествоПоказателейПоОписаниюТаблицы(ЭтаФорма, ОписаниеТаблицыНачисленийНаКлиенте(),, 0) Цикл
			
			ПутьКЗначениюМин = "МинимальноеЗначение" + НомерПоказателя;
			ЗначениеМин = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Элемент.ТекущиеДанные, ПутьКЗначениюМин);
			
			ПутьКЗначениюМакс = "МаксимальноеЗначение" + НомерПоказателя;
			ЗначениеМакс = ОбщегоНазначенияКлиентСервер.ПолучитьРеквизитФормыПоПути(Элемент.ТекущиеДанные, ПутьКЗначениюМакс);
			
			Если ЗначениеМин > ЗначениеМакс Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(Элемент.ТекущиеДанные, ПутьКЗначениюМакс, ЗначениеМин);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли; 
	
	ИзмененыНачисления = Модифицированность;

	Если ЗарплатаКадрыРасширенныйКлиент.ВводПлановыхНачисленийРассчитатьФОТПриОкончанииРедактирования(ЭтаФорма, Элемент, 0, ОписаниеТаблицыНачислений(ЭтотОбъект)) Тогда
		ВыполнитьРасчетФОТ();
	КонецЕсли;
	
	УстановитьВидимостьКомандыИзменитьНачисленияСотрудников(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	
	Если ВнешниеДанные Тогда
		УправлениеШтатнымРасписаниемКлиент.НачисленияПослеУдаления(ЭтаФорма);
	КонецЕсли; 

	ВыполнитьРасчетФОТ();
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПередУдалением(Элемент, Отказ)
	
	Если ВнешниеДанные Тогда
		УправлениеШтатнымРасписаниемКлиент.НачисленияПередУдалением(ЭтаФорма, Отказ);
		ВыполнитьРасчетФОТ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НадбавкаЗаВредностьРазмерПриИзменении(Элемент)
	
	РассчитатьИтогиФОТПоПозиции();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыплачиваетсяНадбавкаЗаВредностьПриИзменении(Элемент)
	
	УстановитьОтображениеПолейВводаПроцентаЗаВредность(ЭтаФорма, Объект.ВыплачиваетсяНадбавкаЗаВредность);
	ВыполнитьРасчетФОТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцентНадбавкиЗаВредностьПриИзменении(Элемент)
	
	ВыполнитьРасчетФОТ();
	
КонецПроцедуры

&НаКлиенте
Процедура НадбавкаЗаВредностьРазмерМинПриИзменении(Элемент)
	
	РассчитатьИтогиФОТПоПозиции();
	
КонецПроцедуры

&НаКлиенте
Процедура НадбавкаЗаВредностьРазмерМаксПриИзменении(Элемент)
	
	РассчитатьИтогиФОТПоПозиции();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗначениеПоказателяПриИзмененииНачисления(Элемент)
	
	Если ВнешниеДанные Тогда
		УправлениеШтатнымРасписаниемКлиентСервер.УстановитьКомментарииДействийСНачислением(
			Элементы.Начисления.ТекущиеДанные, 
			ЗарплатаКадрыРасширенныйКлиентСервер.МаксимальноеКоличествоПоказателейПоОписаниюТаблицы(ЭтаФорма, ОписаниеТаблицыНачисленийНаКлиенте(),, 0),
			ИспользоватьВилкуСтавок);
	КонецЕсли;
	
КонецПроцедуры	

#КонецОбласти


#Область ОбработчикиСобытийТаблицыФормыПоказатели

&НаКлиенте
Процедура ПоказателиПриИзменении(Элемент)
	
	ВыполнитьРасчетФОТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если ВнешниеДанные Тогда
		УправлениеШтатнымРасписаниемКлиент.ПоказателиПриНачалеРедактирования(ЭтаФорма, НоваяСтрока, Копирование);
	КонецЕсли; 
	
	ЗарплатаКадрыРасширенныйКлиент.УстановитьОграничениеТипаПоТочностиДополнительногоПоказателя(ЭтотОбъект, "Показатели", 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиПоказательПриИзменении(Элемент)
	
	ЗарплатаКадрыРасширенныйКлиент.УстановитьОграничениеТипаПоТочностиДополнительногоПоказателя(ЭтотОбъект, "Показатели", 0);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийТаблицыФормыЕжегодныеОтпуска

&НаКлиенте
Процедура ЕжегодныеОтпускаПриАктивизацииСтроки(Элемент)
	
	Если ВнешниеДанные Тогда
		ОстаткиОтпусковКлиент.ПриАктивизацииСтрокиЕжегодногоОтпуска(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаПередУдалением(Элемент, Отказ)
	
	Если ВнешниеДанные Тогда
		ОстаткиОтпусковКлиент.ПередУдалениемЕжегодногоОтпуска(ЭтаФорма, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если ВнешниеДанные Тогда
		ОстаткиОтпусковКлиент.ПриОкончанииРедактированияЕжегодногоОтпуска(Элемент.ТекущиеДанные, НоваяСтрока);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаВидЕжегодногоОтпускаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	
	ОстаткиОтпусковКлиент.ПриИзмененииВидаЕжегодногоОтпуска(ЭтаФорма);
	Если ВнешниеДанные Тогда
		ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(ТекущиеДанные);
	КонецЕсли; 
	
	ЗаполнитьЗначенияСтажевыхРеквизитовВСтроке(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаКлиенте
Процедура ЕжегодныеОтпускаКоличествоДнейВГодПриИзменении(Элемент)
	
	Если ВнешниеДанные Тогда
		ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(Элементы.ЕжегодныеОтпуска.ТекущиеДанные);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийТаблицыФормыЛьготы

&НаКлиенте
Процедура ЛьготыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ЗарплатаКадрыРасширенныйКлиент.РедактированиеСоставаНачисленийПриНачалеРедактирования(ЭтотОбъект, "Льготы", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛьготыЛьготаПриИзменении(Элемент)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыЛьгот();
	ЗарплатаКадрыРасширенныйКлиент.РедактированиеСоставаНачисленийНачислениеПриИзменении(ЭтотОбъект, ОписаниеТаблицыВидовРасчета, 1);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда) 
	
	ЗаблокированныеРеквизиты = ЗапретРедактированияРеквизитовОбъектовКлиент.Реквизиты(ЭтаФорма);
	Если ЗаблокированныеРеквизиты.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru='Внимание!
            |При изменении организации, подразделения или должности позиции штатного расписания
            |может быть нарушено соответствие кадровых данных штатному расписанию и правильность кадровых документов.
            |
            |Может потребоваться перезаполнение приказов по этой позиции штатного расписания.'
            |;uk='Увага!
            |При зміні організації, підрозділу або посади позиції штатного розкладу
            |може бути порушена відповідність кадрових даних штатного розкладу та правильність кадрових документів.
            |
            |Може знадобитися перезаповнення наказів з цієї позиції штатного розкладу.'");

		СоставКнопок = Новый СписокЗначений;
		СоставКнопок.Добавить(Истина,  НСтр("ru='Разрешить редактирование';uk='Дозволити редагування'"));
		СоставКнопок.Добавить(Ложь,  НСтр("ru='Отмена';uk='Відмінити'"));
		
		Оповещение = Новый ОписаниеОповещения("Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, СоставКнопок, , Истина);
	
	Иначе
		ЗапретРедактированияРеквизитовОбъектовКлиент.ПоказатьПредупреждениеВсеВидимыеРеквизитыРазблокированы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> Истина Тогда 
		Возврат;
	КонецЕсли;
	
	ЗаблокированныеРеквизиты = ЗапретРедактированияРеквизитовОбъектовКлиент.Реквизиты(ЭтаФорма);
	ЗапретРедактированияРеквизитовОбъектовКлиент.УстановитьДоступностьЭлементовФормы(ЭтаФорма, ЗаблокированныеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьВКадровомУчете(Команда)
	
	ДополнительныеПараметры = Новый Структура("ЗаписатьЭлемент", Истина);
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru='Данные были изменены. Сохранить изменения?';uk='Дані були змінені. Зберегти зміни?'");
		
		Оповещение = Новый ОписаниеОповещения("ОтразитьВКадровомУчетеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе 
		
		ДополнительныеПараметры.ЗаписатьЭлемент = Ложь;
		ОтразитьВКадровомУчетеЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтразитьВКадровомУчетеЗавершение(Ответ, ДополнительныеПараметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли; 
	
	Если ДополнительныеПараметры.ЗаписатьЭлемент И Не Записать() Тогда 
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация", 						Объект.Владелец);
	ЗначенияЗаполнения.Вставить("Подразделение", 					Объект.Подразделение);
	ЗначенияЗаполнения.Вставить("Должность", 						Объект.Должность);
	ЗначенияЗаполнения.Вставить("ДолжностьПоШтатномуРасписанию", 	Объект.Ссылка);
	
	ЗначенияЗаполнения.Вставить("ЭтоОтражениеИзмененияШтатногоРасписания", Истина);
	
	ПараметрыОткрытияФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ИзменениеПлановыхНачислений.Форма.ФормаДокумента", ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьФОТ(Команда)
	
	ПоказыватьФОТ = НЕ Элементы.РедактироватьФОТ.Пометка;
	ОбработатьИзменениеРежимаОтображенияФОТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	Если Модифицированность ИЛИ РасчетНеобходимоВыполнить Тогда
		
		Если РасчетНеобходимоВыполнить Тогда
			РассчитатьФОТНаКлиенте();
		КонецЕсли;
		
		Если ПроверитьЗаполнение() Тогда
			
			Оповестить("ИзмененыДанныеПозицииШтатногоРасписания", АдресДанныхПозицииВХранилище(), ВладелецФормы);
			Модифицированность = Ложь;
			
		Иначе
			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Рассчитать(Команда)
	
	РассчитатьФОТНаКлиенте();
	
КонецПроцедуры


&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	ЗаписатьНаКлиенте(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Склонения(Команда)
	
	СклонениеПредставленийОбъектовКлиент.ОбработатьКомандуСклонения(ЭтотОбъект, Объект.Наименование);
			
КонецПроцедуры

#Область Подключаемый_КадровыйРезерв

&НаКлиенте
Процедура Подключаемый_СоздатьОткрытьПозициюКадровогоРезерваШтатногоРасписания(Команда)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		ПараметрыФормы = МодульКлиент.ПараметрыОткрытияФормыСправочникаКадровыйРезерв(ЭтаФорма);
		МодульКлиент.ОткрытьФормуСправочникаКадровыйРезерв(ЭтаФорма, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиКомандРейтинга

&НаКлиенте
Процедура Подключаемый_Рейтинг1(Команда)
	ПоставитьРейтингНаКлиенте(1);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Рейтинг2(Команда)
	ПоставитьРейтингНаКлиенте(2);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Рейтинг3(Команда)
	ПоставитьРейтингНаКлиенте(3);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Рейтинг4(Команда)
	ПоставитьРейтингНаКлиенте(4);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Рейтинг5(Команда)
	ПоставитьРейтингНаКлиенте(5);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РейтингСбросить(Команда)
	ПоставитьРейтингНаКлиенте(0);
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ЗаявкаНаВключениеВКадровыйРезерв(Команда)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		МодульКлиент.ОткрытьФормуЗаявкаНаВключениеВКадровыйРезерв(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВключениеВКадровыйРезерв(Команда)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		МодульКлиент.ОткрытьФормуВключениеВКадровыйРезерв(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ИсключениеИзКадровогоРезерва(Команда)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		МодульКлиент.ОткрытьФормуИсключениеИзКадровогоРезерва(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти 

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура ОбработатьИзменениеРежимаОтображенияФОТ()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПоказатьФОТ",
		"Пометка",
		ПоказыватьФОТ);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"РедактироватьФОТ",
		"Пометка",
		ПоказыватьФОТ);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыНачислений(Форма)
	
	ОписаниеТаблицыВидовРасчета = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыРасчета();
	ОписаниеТаблицыВидовРасчета.ЭтоПлановыеНачисленияУдержания = Истина;
	Возврат ОписаниеТаблицыВидовРасчета;
	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыЛьгот()
	
	ОписаниеТаблицыВидовРасчета = РасчетЗарплатыРасширенныйКлиентСервер.ОписаниеТаблицыПлановыхНачислений(Ложь, Ложь);
	ОписаниеТаблицыВидовРасчета.ИмяТаблицы = "Льготы";
	ОписаниеТаблицыВидовРасчета.ИмяРеквизитаВидРасчета = "Льгота";
	ОписаниеТаблицыВидовРасчета.ПутьКДанным = "Льготы";
	ОписаниеТаблицыВидовРасчета.ПутьКДаннымПоказателей = "Показатели";
	ОписаниеТаблицыВидовРасчета.НомерТаблицы = 1;
	ОписаниеТаблицыВидовРасчета.ИмяРеквизитаФиксРасчет = "ЛьготыФиксРасчет";
	ОписаниеТаблицыВидовРасчета.ИмяРеквизитаДокументОснование = "";
	
	Возврат ОписаниеТаблицыВидовРасчета;
	
КонецФункции

&НаКлиенте
Функция ОписаниеТаблицыНачисленийНаКлиенте() Экспорт
	
	Возврат ОписаниеТаблицыНачислений(ЭтаФорма);
	
КонецФункции

&НаСервере
Функция ОписаниеТаблицыНачисленийНаСервере() Экспорт
	
	Возврат ОписаниеТаблицыНачислений(ЭтаФорма);
	
КонецФункции

&НаКлиенте
Функция ОписаниеКоманднойПанелиНачислений() Экспорт
	ОписаниеКоманднойПанелиНачислений = ЗарплатаКадрыРасширенныйКлиент.ОписаниеКоманднойПанелиНачислений();
	Возврат ОписаниеКоманднойПанелиНачислений
КонецФункции

&НаСервере
Процедура ВладелецПриИзмененииНаСервере()
	
	ДополнитьФорму();
	УстановитьГрафикРаботы();
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере(УстанавливатьДоступностьСведений = Истина)
	
	ПрочитатьДанныеПодразделенияВФорму();
	
	УстановитьГрафикРаботы();
	УстановитьПодходящуюДатуУтверждения();
	
	ОстаткиОтпусков.ПрименитьНастройкиИспользованияОтпускаЗаВредность(Объект.ЕжегодныеОтпуска, Объект.Должность, Действие <> Перечисления.ДействияСПозициямиШтатногоРасписания.СоздатьНовуюПозицию);
	
	Если УстанавливатьДоступностьСведений Тогда
		УстановитьДоступностьСведенийПозиции();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеПодразделенияВФорму()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеОПодразделении = ДанныеПодразделения(Объект.Подразделение);
	
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ДолжностьПриИзмененииНаСервере(УстанавливатьДоступностьСведений = Истина)
	
	ОстаткиОтпусков.ПрименитьНастройкиИспользованияОтпускаЗаВредность(Объект.ЕжегодныеОтпуска, Объект.Должность, Действие <> Перечисления.ДействияСПозициямиШтатногоРасписания.СоздатьНовуюПозицию);
	
	Если ЗначениеЗаполнено(Объект.Должность) Тогда
		
		УстановитьПодходящуюДатуУтверждения();
		
		ИменаРеквизитовДолжности =
			"ТарифнаяСетка,
			|ТарифнаяСеткаНадбавки,
			|РазрядКатегория,
			|РазрядКатегорияНадбавки,
			|ПроцентНадбавкиЗаВредность,
			|ВыплачиваетсяНадбавкаЗаВредность,
			|КоличествоДнейДополнительногоОтпускаВГод";
		
		ДанныеДолжности = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Должность,ИменаРеквизитовДолжности);
		ЗаполнитьЗначенияСвойств(Объект, ДанныеДолжности);
		
		ЗаполнитьНачислениеНадбавкаЗаВредность();
		
		УстановитьОтображениеПолейВводаПроцентаЗаВредность(ЭтаФорма, Объект.ВыплачиваетсяНадбавкаЗаВредность);
		
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ТарифнаяСетка) Тогда
		ЗаполнитьДанныеТарифнойСеткиНаСервере();
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Объект.ТарифнаяСеткаНадбавки) Тогда
		ЗаполнитьДанныеТарифнойСеткиНадбавкиНаСервере();
	КонецЕсли; 

	Если УстанавливатьДоступностьСведений Тогда
		УстановитьДоступностьСведенийПозиции();
	КонецЕсли; 
	
	УправлениеШтатнымРасписаниемФормы.ПрочитатьДополнительныеДанныеДолжностиДляПозицииШтатногоРасписания(ЭтаФорма);
	
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоОтношениюКДенежномуСодержанию(
		ЭтаФорма,
		ОписаниеТаблицыНачислений(ЭтаФорма),
		УправлениеШтатнымРасписанием.ЗначениеПараметраВыбораНачисленийПоОтношениюКДенежномуСодержанию(ЭтаФорма));
		
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоОтношениюКДенежномуДовольствию(
		ЭтаФорма,
		ОписаниеТаблицыНачислений(ЭтаФорма),
		УправлениеШтатнымРасписанием.ЗначениеПараметраВыбораНачисленийПоОтношениюКДенежномуДовольствию(ЭтаФорма));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьГрафикРаботы()
	
	Если ЗначениеЗаполнено(Объект.Владелец) ИЛИ ЗначениеЗаполнено(Объект.Подразделение) Тогда
		
		ЗначенияДляЗаполнения = Новый Структура;
		
		ЗначенияДляЗаполнения.Вставить("Организация",   "Объект.Владелец");
		ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
		ЗначенияДляЗаполнения.Вставить("ГрафикРаботы",	"Объект.ГрафикРаботыСотрудников");
		
		ФиксированныеЗначения = Новый Массив;
		ФиксированныеЗначения.Добавить("Организация");
		ФиксированныеЗначения.Добавить("Подразделение");
		
		ЗарплатаКадры.ЗаполнитьЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения, ФиксированныеЗначения);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьФорму()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаполнитьНачислениеНадбавкаЗаВредность();
	
	УстановитьПараметрыФункциональныхОпцийФормы(Новый Структура("Организация", Объект.Владелец));
	ПрочитатьДанныеПодразделенияВФорму();
	
	УстановитьОтображениеПолейВводаПроцентаЗаВредность(ЭтаФорма, Объект.ВыплачиваетсяНадбавкаЗаВредность);
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений(ЭтаФорма);
	
	Если ВнешниеДанные Тогда
		
		ЗарплатаКадрыРасширенный.РедактированиеСоставаНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыВидовРасчета, "Начисления", 0, ИспользоватьРасчетЗарплаты);
	
	Иначе
			
		ЗарплатаКадрыРасширенный.ВводНачисленийДополнитьФорму(
			ЭтаФорма, ОписаниеТаблицыВидовРасчета, 0, "Начисления", ИспользоватьРасчетЗарплаты);
			
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	УправлениеШтатнымРасписаниемФормы.ДополнитьФормуЭлементаСправочникаШтатноеРасписание(ЭтаФорма);
	УправлениеШтатнымРасписаниемФормы.ПрочитатьДополнительныеДанныеДолжностиДляПозицииШтатногоРасписания(ЭтаФорма);
	
	// Настройка таблицы ввода дополнительных тарифных ставок, коэффициентов и т.д.	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДополнительныеТарифыКоэффициентыГруппа",
		"Видимость",
		ПолучитьФункциональнуюОпциюФормы("ИспользоватьНесколькоТарифныхСтавок"));
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпециальностиСтраница",
		"Видимость",
		ПолучитьФункциональнуюОпциюФормы("ИспользоватьСпециальностиФизическихЛиц"));
		
	// Показатели с нулевым идентификатором строки - независимые.
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Показатели",
		"ОтборСтрок",
		Новый ФиксированнаяСтруктура("ИдентификаторСтрокиВидаРасчета", 0));
	
	// Настраиваем параметры выбора показателя.
	МассивПараметровВыбора = Новый Массив;
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.СпособПримененияЗначений", Перечисления.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное));
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.НазначениеПоказателя", Перечисления.НазначенияПоказателейРасчетаЗарплаты.ДляСотрудника));
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.ЗначениеРассчитываетсяАвтоматически", Ложь));
	МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.НеИспользуется", Ложь));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПоказателиПоказатель",
		"ПараметрыВыбора",
		Новый ФиксированныйМассив(МассивПараметровВыбора));
	
	// Ограничение типа на значение.
	ТипЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3));
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоказателиЗначение", "ОграничениеТипа", ТипЧисло);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоказателиЗначениеМин", "ОграничениеТипа", ТипЧисло);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоказателиЗначениеМакс", "ОграничениеТипа", ТипЧисло);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.Грейды") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Грейды");
		Модуль.ГруппаГрейдДополнитьФорму(ЭтотОбъект);
		Модуль.ПрочитатьЗначениеГрейда(ЭтотОбъект);
		Если ВнешниеДанные Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Грейд", "Видимость", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.ДополнитьФормуПозиции(ЭтотОбъект, ОписаниеТаблицыЛьгот());
		Если ВнешниеДанные Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЛьготыСтраница", "Видимость", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульФормы = ОбщегоНазначения.ОбщийМодуль("КадровыйРезервФормы");
		МодульФормы.ДополнитьФормуПозиции(ЭтаФорма);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ПриСозданииФормыПозицииШтатногоРасписания(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеОбъектаВФорму(ТекущийОбъект, ЗначениеКопирования = Неопределено)
	
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений(ЭтаФорма);
	Если ИспользоватьИсторию Тогда
		УправлениеШтатнымРасписаниемФормы.ПрочитатьТекущиеДанныеПозицииШтатногоРасписанияВФормуСправочника(Объект, ТекущийОбъект.Ссылка);
	КонецЕсли;
	
	ЗарплатаКадрыРасширенный.ВводНачисленийДанныеВРеквизит(ЭтаФорма, ОписаниеТаблицыВидовРасчета, 0);
	УстановитьВидимостьКомандыИзменитьНачисленияСотрудников(ЭтаФорма);
	УстановитьПредставлениеОкладаИФОТ(ЭтаФорма);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ПриПолученииДанныхФормыПозицииШтатногоРасписания(ЭтаФорма, ТекущийОбъект.Ссылка, ЗначениеКопирования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РеквизитыВДанные(ОбъектПриемник)
	
	ОписаниеТаблицыВидовРасчета = ОписаниеТаблицыНачислений(ЭтаФорма);
	ЗарплатаКадрыРасширенный.ВводНачисленийРеквизитВДанные(ЭтаФорма, ОбъектПриемник, ОписаниеТаблицыВидовРасчета, 0);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ЛьготыСотрудников") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ЛьготыСотрудников");
		Модуль.РеквизитыПозицииВДанные(ЭтотОбъект, ОписаниеТаблицыЛьгот());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура УстановитьСвойстваЭлементовУправления()
	
	Если ИспользоватьИсторию Тогда
		
		Если ВнешниеДанные Тогда
			УстановитьСвойстваЭлементовУправленияДляВнешнихДанных();
		Иначе
			ЗаполнитьУтвердившийИЗакрывшийРегистраторы();
			УстановитьСвойстваЭлементовУправленияПриВеденииИстории();
		КонецЕсли;
		
	Иначе
		
		Если НЕ СозданиеНовой Тогда
			ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
		КонецЕсли;
		
		УстановитьСвойстваЭлементовУправленияПриИспользованииУправленческойСтруктуры();
		
	КонецЕсли; 
	
	Если ТолькоПросмотр ИЛИ ИспользоватьИсторию И НЕ ВнешниеДанные Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"РедактироватьФОТ",
			"Картинка",
			БиблиотекаКартинок.ПоказатьДанные);
			
		КомандаРедактированияФОТ = Команды.Найти("РедактироватьФОТ");
		Если КомандаРедактированияФОТ <> Неопределено Тогда
			КомандаРедактированияФОТ.Заголовок = НСтр("ru='Показать ФОТ';uk='Показати ФОП'");
			КомандаРедактированияФОТ.Подсказка = НСтр("ru='Показать ФОТ';uk='Показати ФОП'");
		КонецЕсли; 
				
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НадписьОклад",
		"Видимость",
		ИспользоватьРасчетЗарплаты);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"НадписьФОТ",
		"Видимость",
		ИспользоватьРасчетЗарплаты);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КоличествоСтавок",
		"ФорматРедактирования",
		УправлениеШтатнымРасписанием.ФорматКоличестваСтавок());
			
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоКатегории(
		ЭтаФорма,
		ОписаниеТаблицыНачислений(ЭтаФорма),
		УправлениеШтатнымРасписанием.ПараметрыВыбораКатегорииНачислений());
	
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоОтношениюКДенежномуСодержанию(
		ЭтаФорма,
		ОписаниеТаблицыНачислений(ЭтаФорма),
		УправлениеШтатнымРасписанием.ЗначениеПараметраВыбораНачисленийПоОтношениюКДенежномуСодержанию(ЭтаФорма));
		
	КадровыйУчетРасширенный.УстановитьПараметрыВыбораНачисленийПоОтношениюКДенежномуДовольствию(
		ЭтаФорма,
		ОписаниеТаблицыНачислений(ЭтаФорма),
		УправлениеШтатнымРасписанием.ЗначениеПараметраВыбораНачисленийПоОтношениюКДенежномуДовольствию(ЭтаФорма));	
		
	УстановитьДоступностьСведенийПозиции();
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовУправленияДляВнешнихДанных()
	
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	УстановитьОтображениеКоманднойПанелиФормыДляВнешнихДанных();
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Владелец",
		"Видимость",
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаДополнительныеРеквизиты",
		"Видимость",
		Ложь);
	
	Если СуществующаяПозиция Тогда
	
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Подразделение",
			"ТолькоПросмотр",
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"Должность",
			"ТолькоПросмотр",
			Истина);
			
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
			Модуль.УстановитьСвойстваЭлементовУправленияШтатногоРасписанияВнешниеДанные(ЭтотОбъект);
		КонецЕсли;
		
	КонецЕсли;
	
	// Даты утверждения, закрытия
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Утверждена",
		"Видимость",
		Ложь);
			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Закрыта",
		"Видимость",
		Ложь);
			
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаУтверждения",
		"Видимость",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаЗакрытия",
		"Видимость",
		Ложь);
	
	// Не редактируемые поля внешних данных.
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КраткоеОписание",
		"Видимость",
		Ложь);
	
	// Показ комментария к ежегодным отпускам.
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЕжегодныеОтпускаКомментарий",
		"Видимость",
		Истина);
		
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеКоманднойПанелиФормыДляВнешнихДанных()

	ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиФормы.Нет;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КоманднаяПанельФормыВнешниеДанные",
		"Видимость",
		Истина);
		
	Если ТолькоПросмотр Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"КомандаОК",
			"Видимость",
			Ложь);
			
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"КомандаЗакрыть",
			"Заголовок",
			НСтр("ru='Закрыть';uk='Закрити'"));
		
		ИмяКнопкиПоУмолчанию = "КомандаЗакрыть";
			
	Иначе
		ИмяКнопкиПоУмолчанию = "КомандаОК";
	КонецЕсли; 
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		ИмяКнопкиПоУмолчанию,
		"КнопкаПоУмолчанию",
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовУправленияПриВеденииИстории()
	
	// Ключевые реквизиты
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Владелец",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Подразделение",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Должность",
		"ТолькоПросмотр",
		Истина);
	
	// Отражение в учете
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СпособОтраженияЗарплатыВБухучете",
		"ТолькоПросмотр",
		Истина);
	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СтатьяФинансирования",
		"ТолькоПросмотр",
		Истина);
	
	// Остальные не редактируемые реквизиты.
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"РазрядКатегория",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТарифнаяСетка",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"РазрядКатегорияНадбавки",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТарифнаяСеткаНадбавки",
		"ТолькоПросмотр",
		Истина);

	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"КоличествоСтавок",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"УсловияПриема",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"УтвердившийРегистраторНадпись",
		"Видимость",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗакрывшийРегистраторНадпись",
		"Видимость",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Утверждена",
		"Видимость",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаУтверждения",
		"Видимость",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Закрыта",
		"Видимость",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаЗакрытия",
		"Видимость",
		Ложь);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГрафикРаботыСотрудников",
		"ТолькоПросмотр",
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЕжегодныеОтпуска",
		"ТолькоПросмотр",
		Истина);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Специальности",
		"ТолькоПросмотр",
		Истина);
		
		
	// Страница "Оплата труда"
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаДанныеГоризонтальная",
		"ТолькоПросмотр",
		Истина);
	
	// Команда пересчета ФОТ
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Рассчитать",
		"Видимость",
		Ложь);
	
		
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.УстановитьСвойстваЭлементовУправленияШтатногоРасписанияПриВеденииИстории(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовУправленияПриИспользованииУправленческойСтруктуры()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.УстановитьСвойстваЭлементовУправленияШтатногоРасписания(ЭтотОбъект, СозданиеНовой);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаименование()
	
	ПриЗаполненииНаименования();		
	
	Если ВнешниеДанные Тогда
		Заголовок = Объект.Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗаполненииНаименования()
	
	СтандартнаяОбработка = Истина;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ОрганизационнаяСтруктураКлиент");
		Модуль.ПриЗаполненииНаименованияПозицииШтатногоРасписания(ЭтаФорма, СтандартнаяОбработка);
	КонецЕсли;
	
	Если Не СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРазрядыКатегорииКлассыДолжностейИПрофессий Тогда
		Объект.Наименование = УправлениеШтатнымРасписаниемКлиентСервер.НаименованиеПозицииШтатногоРасписания(
			Объект.Подразделение,
			Объект.Должность,
			Объект.РазрядКатегория);
	Иначе
		Объект.Наименование = УправлениеШтатнымРасписаниемКлиентСервер.НаименованиеПозицииШтатногоРасписания(
			Объект.Подразделение,
			Объект.Должность);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементов(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДатаЗакрытия",
		"Доступность",
		Форма.Объект.Закрыта);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Закрыта",
		"Доступность",
		Форма.Объект.Утверждена);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ДатаУтверждения",
		"Доступность",
		Форма.Объект.Утверждена);
		
		
	// Подсистема "Охрана труда"
	#Если Клиент Тогда
		ОхранаТрудаСуществует = ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда");
	#Иначе
		ОхранаТрудаСуществует = ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда");
	#КонецЕсли
	Если ОхранаТрудаСуществует Тогда
		#Если Клиент Тогда
			Модуль = ОбщегоНазначенияКлиент.ОбщийМодуль("ОхранаТрудаКлиентСервер");
		#Иначе
			Модуль = ОбщегоНазначения.ОбщийМодуль("ОхранаТрудаКлиентСервер");
		#КонецЕсли
		Модуль.УстановитьДоступностьЭлементовФормыПозицииШтатногоРасписания(Форма);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРасчетФОТ(ЗаполнитьДанныеТарифнойСетки = Ложь, ЗаполнитьДанныеТарифнойСеткиНадбавки = Ложь)
	
	Если ЗаполнитьДанныеТарифнойСетки Тогда
		ПерезаполнитьДанныеТарифнойСетки = Истина;
	КонецЕсли; 
	
	Если ЗаполнитьДанныеТарифнойСеткиНадбавки Тогда
		ПерезаполнитьДанныеТарифнойСеткиНадбавки = Истина;
	КонецЕсли; 

	ЗарплатаКадрыРасширенныйКлиент.ПодключитьОбработчикОжиданияАвтоматическогоРасчета(ЭтаФорма, "РассчитатьФОТНаКлиенте");
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьФОТНаКлиенте()
	
	РассчитатьФОТПозиции();
	ЗарплатаКадрыРасширенныйКлиент.СброситьФлагНеобходимостиВыполненияРасчета(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьФОТПозиции(ПолныйПерерасчет = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ПерезаполнитьДанныеТарифнойСетки Тогда
		ЗаполнитьДанныеТарифнойСеткиНаСервере();
	КонецЕсли; 
	
	Если ПерезаполнитьДанныеТарифнойСеткиНадбавки Тогда
		ЗаполнитьДанныеТарифнойСеткиНадбавкиНаСервере();
	КонецЕсли; 
	
	УправлениеШтатнымРасписанием.РассчитатьФОТВФормеСОднойПозицией(ЭтаФорма, Объект, Объект.Владелец, ДатаСобытия(ЭтаФорма, ТекущаяДатаСеанса()), ОписаниеТаблицыНачисленийНаСервере());
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьПредставлениеОкладаИФОТ(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьИтогиФОТПоПозиции()
	
    УправлениеШтатнымРасписанием.РассчитатьИтогиФОТПоПозиции(ЭтаФорма, Объект, Объект.Начисления);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПодходящуюДатуУтверждения()
	
	Если ВнешниеДанные Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Объект.Подразделение) Тогда
		ДатаСозданияПодразделения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Подразделение, "ДатаСоздания");
	Иначе
		ДатаСозданияПодразделения = '00010101';
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Объект.Должность) Тогда
		ПодходящаяДата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Должность ,"ДатаВвода");
	Иначе
		ПодходящаяДата = '00010101';
	КонецЕсли;
	
	Если ДатаСозданияПодразделения > ПодходящаяДата Тогда
		ПодходящаяДата = ДатаСозданияПодразделения;
	КонецЕсли; 
	
	Если ПодходящаяДата = '00010101' Тогда
		ПодходящаяДата = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	
	Объект.ДатаУтверждения = ПодходящаяДата;
	
КонецПроцедуры

&НаСервере
Функция ДанныеПодразделения(Знач Подразделение)
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		Возврат Новый Структура();
	Иначе
		Возврат Новый Структура();
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьКомандыИзменитьНачисленияСотрудников(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ФормаОтразитьВКадровомУчете",
		"Видимость",
		НЕ Форма.ВнешниеДанные И Форма.ИзмененыНачисления И НЕ Форма.СозданиеНовой);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТарифнойСеткиНаСервере()
	
	ЗарплатаКадрыРасширенныйКлиентСервер.ЗаполнитьЗначенияПоказателейТарифныхСеток(
		ЭтаФорма, Объект.Начисления, ОписаниеТаблицыНачислений(ЭтаФорма), 0, Объект.ТарифнаяСетка, Объект.РазрядКатегория, ДатаСобытия(ЭтаФорма, ТекущаяДатаСеанса()), , , Истина);
		
	ПерезаполнитьДанныеТарифнойСетки = Ложь;
		
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТарифнойСеткиНадбавкиНаСервере()
	
	ЗарплатаКадрыРасширенныйКлиентСервер.ЗаполнитьЗначенияПоказателейТарифныхСеток(
		ЭтаФорма,
		Объект.Начисления,
		ОписаниеТаблицыНачислений(ЭтаФорма),
		0,
		Неопределено,
		Неопределено,
		ДатаСобытия(ЭтаФорма, ТекущаяДатаСеанса()),
		Объект.ТарифнаяСеткаНадбавки,
		Объект.РазрядКатегорияНадбавки,
		Истина);
	
	ПерезаполнитьДанныеТарифнойСеткиНадбавки = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеПолейВводаПроцентаЗаВредность(Форма, ВыплачиваетсяНадбавкаЗаВредность)
	
	ИспользуетсяРасчетЗарплаты = Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьРасчетЗарплатыРасширенная");
	ИспользоватьНадбавкуЗаВредность = ИспользуетсяРасчетЗарплаты И Форма.ПолучитьФункциональнуюОпциюФормы("ИспользоватьНадбавкуЗаВредность");
	
	ЗначениеВидимости = ИспользоватьНадбавкуЗаВредность И ВыплачиваетсяНадбавкаЗаВредность;
	ИспользоватьВилкуСтавок = Форма.ИспользоватьВилкуСтавок;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НачислениеНадбавкаЗаВредностьДекорация",
		"Видимость",
		ИспользоватьНадбавкуЗаВредность);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ПроцентНадбавкиЗаВредность",
		"ТолькоПросмотр",
		Не ВыплачиваетсяНадбавкаЗаВредность);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НадбавкаЗаВредностьРазмерДекорация",
		"Видимость",
		ИспользоватьНадбавкуЗаВредность И Не ВыплачиваетсяНадбавкаЗаВредность И Не ИспользоватьВилкуСтавок);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НадбавкаЗаВредностьРазмер",
		"Видимость",
		ЗначениеВидимости И НЕ ИспользоватьВилкуСтавок);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НадбавкаЗаВредностьРазмерМин",
		"Видимость",
		ЗначениеВидимости И ИспользоватьВилкуСтавок);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НадбавкаЗаВредностьРазмерМинДекорация",
		"Видимость",
		ИспользоватьНадбавкуЗаВредность И Не ВыплачиваетсяНадбавкаЗаВредность И ИспользоватьВилкуСтавок);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НадбавкаЗаВредностьРазмерМакс",
		"Видимость",
		ЗначениеВидимости И ИспользоватьВилкуСтавок);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"НадбавкаЗаВредностьРазмерМаксДекорация",
		"Видимость",
		ИспользоватьНадбавкуЗаВредность И Не ВыплачиваетсяНадбавкаЗаВредность И ИспользоватьВилкуСтавок);
		
КонецПроцедуры

&НаСервере
Процедура ЗапомнитьЗначенияФункциональныхОпцийИНабораПрав()
	
	ИспользоватьВилкуСтавок = ПолучитьФункциональнуюОпциюФормы("ИспользоватьВилкуСтавокВШтатномРасписании");
	
	ИспользоватьИсторию = ПолучитьФункциональнуюОпциюФормы("ИспользоватьИсториюИзмененияШтатногоРасписания");
	
	ИспользоватьРасчетЗарплаты = ПолучитьФункциональнуюОпциюФормы("ИспользоватьРасчетЗарплатыРасширенная")
		И Пользователи.РолиДоступны("ДобавлениеИзменениеНачисленийШтатногоРасписания,ЧтениеНачисленийШтатногоРасписания", , Ложь);
		
	ИспользоватьРазрядыКатегорииКлассыДолжностейИПрофессий = ПолучитьФункциональнуюОпциюФормы("ИспользоватьПриОписанииПозицииШтатногоРасписанияРазрядыКатегорииКлассыДолжностейИПрофессий");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьРазмераЛимитаЛьготы(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, "КоличествоБаллов", "Доступность", Форма.ИспользоватьЛимит);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТарифнойСетки()
	
	Если ПолучитьФункциональнуюОпциюФормы("РаботаВБюджетномУчреждении") Тогда
		
		Если ЗначениеЗаполнено(Объект.ТарифнаяСетка) И ЗначениеЗаполнено(Объект.РазрядКатегория) Тогда
			
			ИдентификаторСтроки = СтрокаНачисленийПоКодуНачисления("ОКЛ"); 
			Если ИдентификаторСтроки <> Неопределено Тогда
				
				Элементы.Начисления.ТекущаяСтрока = ИдентификаторСтроки;
				НачисленияПриНачалеРедактирования(Элементы.Начисления, Истина, Ложь);				
				НачисленияНачислениеПриИзменении(Элементы.НачисленияНачисление);
				
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииТарифнойСеткиНадбавки()
	
	Если ПолучитьФункциональнуюОпциюФормы("РаботаВБюджетномУчреждении") Тогда
		
		Если ЗначениеЗаполнено(Объект.ТарифнаяСеткаНадбавки) И ЗначениеЗаполнено(Объект.РазрядКатегорияНадбавки) Тогда
			
			ИдентификаторСтроки = СтрокаНачисленийКвалификационнойНадбавки(); 
			Если ИдентификаторСтроки <> Неопределено Тогда
				
				Элементы.Начисления.ТекущаяСтрока = ИдентификаторСтроки;
				НачисленияПриНачалеРедактирования(Элементы.Начисления, Истина, Ложь);				
				НачисленияНачислениеПриИзменении(Элементы.НачисленияНачисление);
				
			КонецЕсли; 
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция СтрокаНачисленийКвалификационнойНадбавки()
	
	КодНачисления = "НКК";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Если Модуль.ЭтоДолжностьГосударственнойСлужбы(ЭтаФорма) Тогда
			КодНачисления = "ЕМКВН";
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрокаНачисленийПоКодуНачисления(КодНачисления);
	
КонецФункции

&НаСервере
Функция СтрокаНачисленийПоКодуНачисления(КодНачисления)
	
	ИдентификаторДобавленнойСтроки = Неопределено;
	ИскомыеНачисления = ПланыВидовРасчета.Начисления.НачисленияПоОтбору(Новый Структура("Код", КодНачисления));
	Если ИскомыеНачисления.Количество() > 0 Тогда
		
		НачислениеЕстьВСписке = Ложь;
		Для каждого Начисление Из ИскомыеНачисления Цикл
			СтрокиОкладов = Объект.Начисления.НайтиСтроки(Новый Структура("Начисление", Начисление));
			Если СтрокиОкладов.Количество() > 0 Тогда
				НачислениеЕстьВСписке = Истина;
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если Не НачислениеЕстьВСписке Тогда
			
			СтрокаНачислений = Объект.Начисления.Добавить();
			СтрокаНачислений.Начисление = ИскомыеНачисления[0];
			
			Если Объект.Начисления.Количество() > 1 Тогда
				
				ТаблицаНачислений = Объект.Начисления.Выгрузить();
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Начисления", ТаблицаНачислений);
				
				Запрос.Текст =
					"ВЫБРАТЬ
					|	*
					|ПОМЕСТИТЬ ВТНачисления
					|ИЗ
					|	&Начисления КАК Начисления
					|;
					|
					|////////////////////////////////////////////////////////////////////////////////
					|ВЫБРАТЬ
					|	*,
					|	ВЫРАЗИТЬ(Начисления.Начисление КАК ПланВидовРасчета.Начисления).РеквизитДопУпорядочивания КАК Порядок
					|ИЗ
					|	ВТНачисления КАК Начисления
					|
					|УПОРЯДОЧИТЬ ПО
					|	Порядок";
					
				Объект.Начисления.Загрузить(Запрос.Выполнить().Выгрузить());
				
				СтрокаНачислений = Объект.Начисления.НайтиСтроки(Новый Структура("Начисление", ИскомыеНачисления[0]))[0];
				
			КонецЕсли; 
			
			ИдентификаторДобавленнойСтроки = СтрокаНачислений.ПолучитьИдентификатор();
			
		КонецЕсли; 
			
	КонецЕсли;
	
	Возврат ИдентификаторДобавленнойСтроки;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьИндивидуальныхЛьгот()
	
	Если ДоступныВсеЛьготы = 0 Тогда 
		ИндивидуальныеЛьготы.Очистить();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИндивидуальныеЛьготы", "ТолькоПросмотр", ДоступныВсеЛьготы = 0);
	
КонецПроцедуры




// СтандартныеПодсистемы.СклонениеПредставленийОбъектов

&НаКлиенте 
Процедура Подключаемый_ПросклонятьПредставлениеПоВсемПадежам() 
	
	СклонениеПредставленийОбъектовКлиент.ПросклонятьПредставлениеПоВсемПадежам(ЭтотОбъект, Объект.Наименование);
		
КонецПроцедуры

// Конец СтандартныеПодсистемы.СклонениеПредставленийОбъектов

///////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ ПОДСИСТЕМЫ "СВОЙСТВ"

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма, РеквизитФормыВЗначение("Объект"));

КонецПроцедуры

&НаСервере
Функция АдресДанныхПозицииВХранилище()
	
	ОписаниеПозиции = УправлениеШтатнымРасписанием.ОписаниеПозицииШтатногоРасписания();
	ЗаполнитьЗначенияСвойств(ОписаниеПозиции, Объект);
	ОписаниеПозиции.Вставить("Действие", Действие);
	ОписаниеПозиции.Вставить("ТекущееКоличествоСтавок", ТекущееКоличествоСтавок);
	ОписаниеПозиции.Вставить("ТекущийФОТ", ТекущийФОТ);
	
	ОписаниеПозиции.ПозицияШтатногоРасписания = РедактируемаяПозиция;
	
	// Начисления с показателями
	ОписаниеПозиции.Начисления = Объект.Начисления.Выгрузить();
	
	// Дополнительные показатели и коэффициенты.
	ПоказателиПозиции = Новый Массив;
	ДополнительныеПоказатели =  Объект.Показатели.Выгрузить(
		Новый Структура("ИдентификаторСтрокиВидаРасчета", 0));
			
	Для каждого СтрокаДополнительногоПоказателя Из ДополнительныеПоказатели Цикл
		ОписаниеПоказателя = Новый Структура("Показатель,ИдентификаторСтрокиВидаРасчета,Значение,ЗначениеМин,ЗначениеМакс");
		ЗаполнитьЗначенияСвойств(ОписаниеПоказателя, СтрокаДополнительногоПоказателя);
		ПоказателиПозиции.Добавить(ОписаниеПоказателя);
	КонецЦикла;
	
	ОписаниеПозиции.Вставить("Показатели",	ПоказателиПозиции);
	
	ОписаниеПозиции.ЕжегодныеОтпуска = Объект.ЕжегодныеОтпуска.Выгрузить();
	
	ОписаниеПозиции.Специальности = Объект.Специальности.Выгрузить();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		Модуль.ЗаполнитьМестоВСтруктуреПредприятияПоДаннымФормы(ЭтотОбъект, ОписаниеПозиции);
	КонецЕсли;
	
	Возврат ПоместитьВоВременноеХранилище(ОписаниеПозиции, УникальныйИдентификатор);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДатаСобытия(Форма, ДатаСеанса)
	
	Если Форма.ВнешниеДанные Тогда
		ДатаСобытия = Форма.Объект.ДатаУтверждения;
	Иначе
		
		ДатаСобытия = ДатаСеанса;
		Если Форма.Объект.ДатаУтверждения >= ДатаСобытия Тогда
			ДатаСобытия = Форма.Объект.ДатаУтверждения;
		КонецЕсли; 
	
	КонецЕсли;
	
	Возврат ДатаСобытия;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьСведенийПозиции()
	
	Если ВнешниеДанные Тогда
		
		ДоступностьСведений = ЗначениеЗаполнено(РедактируемаяПозиция);
		Если НЕ ДоступностьСведений Тогда
			ДоступностьСведений = ЗначениеЗаполнено(Объект.Подразделение) И ЗначениеЗаполнено(Объект.Должность);
		КонецЕсли; 
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ОткрытаЗакрытаГруппа",
			"Доступность",
			ДоступностьСведений);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"СтраницыФормы",
			"Доступность",
			ДоступностьСведений);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"КомандаОК",
			"Доступность",
			ДоступностьСведений);
			
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеОкладаИФОТ(Форма)
	
	ФорматнаяСтрока = "ЧДЦ=2";
	
	Если Форма.ИспользоватьВилкуСтавок Тогда
		
		Если Форма.Объект.ОкладТарифМин < Форма.Объект.ОкладТарифМакс Тогда
			Форма.Оклад = Формат(Форма.Объект.ОкладТарифМин, ФорматнаяСтрока) + " - " + Формат(Форма.Объект.ОкладТарифМакс, ФорматнаяСтрока);
		ИначеЕсли Форма.Объект.ОкладТарифМин > 0 И Форма.Объект.ОкладТарифМин = Форма.Объект.ОкладТарифМакс Тогда
			Форма.Оклад = Формат(Форма.Объект.ОкладТарифМин, ФорматнаяСтрока);
		Иначе
			Форма.Оклад = "";
		КонецЕсли;
		
		Если Форма.Объект.ФОТМин < Форма.Объект.ФОТМакс Тогда
			Форма.ФОТ = Формат(Форма.Объект.ФОТМин, ФорматнаяСтрока) + " - " + Формат(Форма.Объект.ФОТМакс, ФорматнаяСтрока);
		ИначеЕсли Форма.Объект.ФОТМин > 0 И Форма.Объект.ФОТМин = Форма.Объект.ФОТМакс Тогда
			Форма.ФОТ = Формат(Форма.Объект.ФОТМин, ФорматнаяСтрока);
		Иначе
			Форма.ФОТ = "";
		КонецЕсли;
		
	Иначе
		
		Форма.Оклад = Формат(Форма.Объект.ОкладТариф, ФорматнаяСтрока);
		Форма.ФОТ = Формат(Форма.Объект.ФОТ, ФорматнаяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУтвердившийИЗакрывшийРегистраторы()
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, УправлениеШтатнымРасписаниемФормы.ИнформацияОбУтвержденииИЗакрытииПозиции(Объект.Ссылка));
	
	Если ЗначениеЗаполнено(Объект.ДатаУтверждения) Тогда
		УтвердившийРегистраторНадпись = Новый ФорматированнаяСтрока(
				НСтр("ru='Утверждена';uk='Затверджена'") + " " + Формат(Объект.ДатаУтверждения, "ДЛФ=DD") + " (",
				Новый ФорматированнаяСтрока(Строка(УтвердившийРегистратор),,,,"УтвердившийРегистратор"),
				")");
	Иначе
		УтвердившийРегистраторНадпись = "";
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Объект.ДатаЗакрытия) Тогда
		ЗакрывшийРегистраторНадпись = Новый ФорматированнаяСтрока(
				НСтр("ru='Закрыта';uk='Закрита'") + " " + Формат(Объект.ДатаЗакрытия, "ДЛФ=DD") + " (",
				Новый ФорматированнаяСтрока(Строка(ЗакрывшийРегистратор),,,,"ЗакрывшийРегистратор"),
				")");
	Иначе
		ЗакрывшийРегистраторНадпись = "";
	КонецЕсли; 
	
КонецПроцедуры

#Область Подключаемый_КадровыйРезерв

&НаСервереБезКонтекста
Функция ПолучитьПозициюРезерваПоСсылке(НавигационнаяСсылка)
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
		Возврат Модуль.ПолучитьПозициюРезерваПоСсылке(НавигационнаяСсылка);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ОбновитьЭлементыКадровогоРезерва()
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульФормы = ОбщегоНазначения.ОбщийМодуль("КадровыйРезервФормы");
		МодульФормы.ОбновитьКадровыйРезервПозицииШР(ЭтаФорма);
		МодульФормы.УстановитьЗначениеСвязаннойПозицииРезерва(ЭтаФорма);
		МодульФормы.СформироватьИнфоСтрокуГруппРезерва(ЭтаФорма);
		МодульФормы.УстановитьСвойстваЭлементовФормыШтатногоРасписания(ЭтаФорма);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПоставитьРейтингНаКлиенте(Рейтинг)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		ТекущиеДанные = МодульКлиент.ТекущиеДанныеТаблицы(ЭтаФорма);
		Если НЕ ТекущиеДанные = Неопределено Тогда
			ПоставитьРейтингНаСервере(ТекущиеДанные, Рейтинг);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоставитьРейтингНаСервере(ТекущиеДанные, Рейтинг)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("КадровыйРезерв");
		Модуль.ПоставитьРейтинг(ТекущиеДанные.ПозицияРезерва, ТекущиеДанные.ФизическоеЛицо, Рейтинг);
		МодульФормы = ОбщегоНазначения.ОбщийМодуль("КадровыйРезервФормы");
		МодульФормы.ОбновитьКадровыйРезервПозицииШР(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Подключаемый_ОбработчикиСобытийТаблицыФормыКадровыйРезерв

&НаКлиенте
Процедура Подключаемый_КадровыйРезервИнфоНадписьГруппРезерваОбработкаНавигационнойСсылки(Элемент, 
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		ПараметрыФормы = Новый Структура("Ключ", ПолучитьПозициюРезерваПоСсылке(НавигационнаяСсылкаФорматированнойСтроки));
		МодульКлиент.ОткрытьФормуСправочникаКадровыйРезерв(ЭтаФорма, ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КадровыйРезервПозицииШРВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйРезерв") Тогда
		МодульКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("КадровыйРезервКлиент");
		МодульКлиент.ОткрытьФормуСправочникаФизическиеЛица(ЭтаФорма, "КадровыйРезервПозицииШР");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаписьЭлемента

&НаКлиенте
Процедура ЗаписатьИЗакрытьНаКлиенте(Результат, ДополнительныеПараметры) Экспорт 
	
	ЗаписатьНаКлиенте(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаКлиенте(ЗакрытьПослеЗаписи, ОповещениеЗавершения = Неопределено) 

	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("ЗаписьЭлементаСправочникаШтатноеРасписание");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи", ЗакрытьПослеЗаписи);
	ДополнительныеПараметры.Вставить("ОповещениеЗавершения", ОповещениеЗавершения);
	
	Оповещение = Новый ОписаниеОповещения("ЗаписатьНаКлиентеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	

   ЗаписатьНаКлиентеЗавершение(Ложь, ДополнительныеПараметры);

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаКлиентеЗавершение(Отказ, ДополнительныеПараметры) Экспорт 

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура("ПроверкаПередЗаписьюВыполнена", Истина);
	
	Если ДополнительныеПараметры.ОповещениеЗавершения <> Неопределено Тогда 
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеЗавершения, ПараметрыЗаписи);
	ИначеЕсли Записать(ПараметрыЗаписи) И ДополнительныеПараметры.ЗакрытьПослеЗаписи Тогда 
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьЗначенияСтажевыхРеквизитовВСтроке(ИдентификаторСтроки)
	ТекущиеДанные = Объект.ЕжегодныеОтпуска.НайтиПоИдентификатору(ИдентификаторСтроки);
	ОстаткиОтпусков.ЗаполнитьЗначенияСтажевыхРеквизитовВСтроке(ТекущиеДанные);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачислениеНадбавкаЗаВредность()
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНадбавкуЗаВредность") Тогда
		
		Если Не ЗначениеЗаполнено(НачислениеНадбавкаЗаВредность) Тогда
			
			НачисленияНадбавкаЗаВредность = РасчетЗарплатыРасширенный.НачислениеНадбавкаЗаВредность();
			ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
				Элементы,
				"ВыплачиваетсяНадбавкаЗаВредность",
				"Заголовок",
				УправлениеШтатнымРасписаниемКлиентСервер.ПредставлениеНадбавкиЗаВредность(НачислениеНадбавкаЗаВредность));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


