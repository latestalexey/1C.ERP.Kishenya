
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Отчет) Тогда
		РодительОтчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отчет, "Родитель");
		
		СвойстваКоллекции = СтандартныеПодсистемыПовтИсп.СвойстваКоллекцийОбъектовМетаданных().Найти("Отчет", "ИмяВЕдЧисле");
		
		Если СвойстваКоллекции <> Неопределено
			И СвойстваКоллекции.Идентификатор <> РодительОтчета.УникальныйИдентификатор() Тогда
			ТекстСообщения = НСтр("ru='Вид регистра учета можно вводить только для отчетов';uk='Вид регістру обліку можна вводити тільки для звітів'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Отчет", "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Отчет", Отчет);
	Запрос.Параметры.Вставить("ВариантОтчета", ВариантОтчета);
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыРегистровУчета.Представление
	|ИЗ
	|	Справочник.ВидыРегистровУчета КАК ВидыРегистровУчета
	|ГДЕ
	|	ВидыРегистровУчета.Отчет = &Отчет
	|	И ВидыРегистровУчета.ВариантОтчета = &ВариантОтчета
	|	И ВидыРегистровУчета.Ссылка <> &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТекстСообщения = НСтр("ru='Вид регистра учета для отчета %1 %2 %3 уже существует %4';uk='Вид регістру обліку для звіту %1 %2 %3 вже існує %4'");
		ОписаниеВариантОтчета = ?(ВариантОтчета = "", "", НСтр("ru='и варианта';uk='і варіанту'"));
		ТекстСообщения =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
							Отчет, ОписаниеВариантОтчета, ВариантОтчета, Выборка.Представление);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "Отчет", "Объект", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

