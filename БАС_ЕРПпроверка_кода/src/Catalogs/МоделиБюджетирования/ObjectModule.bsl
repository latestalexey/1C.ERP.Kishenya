#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//++ НЕ УТКА
	Если Не ЭтоГруппа Тогда
		
		Запрос = Новый Запрос;
		
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЭтапыПодготовкиБюджетов.Ссылка
		|ИЗ
		|	Справочник.ЭтапыПодготовкиБюджетов КАК ЭтапыПодготовкиБюджетов
		|ГДЕ
		|	ЭтапыПодготовкиБюджетов.Владелец = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если Не ВыборкаДетальныеЗаписи.Следующий() Тогда
			
			СправочникОбъект = Справочники.ЭтапыПодготовкиБюджетов.СоздатьГруппу();
			СправочникОбъект.Владелец = Ссылка;
			СправочникОбъект.Периодичность = Периодичность;
			СправочникОбъект.ПорядокВыполненияЭтапов = Перечисления.ПорядокВыполненияЭтаповПодготовкиБюджетов.Параллельно;
			СправочникОбъект.УсловиеЗапуска = Перечисления.ТипыУсловийЗапускаЭтаповПодготовкиБюджетов.ДоНачалаПериода;
			СправочникОбъект.ТипСрока = Перечисления.ТипыСроковЭтаповПодготовкиБюджетов.ВКалендарныхДнях;
			СправочникОбъект.Записать();
			
		КонецЕсли;
	
	КонецЕсли;
	
	Если Справочники.МоделиБюджетирования.МодельБюджетированияДействует(Ссылка) Тогда
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			УстановитьПривилегированныйРежим(Истина);
			Задачи.БюджетнаяЗадача.ФормированиеБюджетныхЗадач(Ссылка);
			УстановитьПривилегированныйРежим(Ложь);
		Иначе
			ФоновыеЗадания.Выполнить("БюджетированиеСервер.ФормированиеБюджетныхЗадач");
		КонецЕсли;
	КонецЕсли;
	//-- НЕ УТКА
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Перем Ошибки;
	
	Если Не ЭтоГруппа Тогда
		Если Статус = Перечисления.СтатусыМоделейБюджетирования.Закрыта Тогда
			ПроверяемыеРеквизиты.Добавить("КонецДействия");
		КонецЕсли;
		Если ЗначениеЗаполнено(КонецДействия) Тогда
			Если КонецДействия <= НачалоДействия Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "КонецДействия", 
						НСтр("ru='Дата окончания действия модели не может быть меньше даты начала действия';uk='Дата закінчення дії моделі не може бути менша дати початку дії'"), "");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли