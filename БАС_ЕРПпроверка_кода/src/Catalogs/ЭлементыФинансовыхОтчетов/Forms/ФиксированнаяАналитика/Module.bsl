
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта = Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтаФорма);
	ИсточникиЗначений.Загрузить(ДанныеОбъекта.ИсточникиЗначений);
	
	ДеревоЭлементов = ДанныеФормыВЗначение(Параметры.ЭлементыОтчета, Тип("ДеревоЗначений"));
	АдресЭлементовОтчета = ПоместитьВоВременноеХранилище(ДеревоЭлементов, УникальныйИдентификатор);
	
	Заголовок = Параметры.ВидЭлемента;
	ЭтоСтроки = Параметры.ЭтоСтроки;

	Справочники.ЭлементыФинансовыхОтчетов.ЗаполнитьПредставлениеДополнительныхПолей(Объект, Элементы.ВыборДополнительныхПолей);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЭтоАналитикаПрочее Тогда
		Префикс = "";
		Если РазрешитьРедактирование Тогда
			Префикс = НСтр("ru='добавляемые и ';uk='додавані і '");
		КонецЕсли;
		Объект.НаименованиеДляПечати = НСтр("ru='<%1прочие %2>';uk='<%1інші %2>'");
		Объект.НаименованиеДляПечати = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Объект.НаименованиеДляПечати, Префикс, НРег(ВидАналитики));
	Иначе
		Объект.НаименованиеДляПечати = Строка(Объект.ЗначениеАналитики);
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект, Отказ);
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(АдресЭлементаВХранилище);
	ДанныеОбъекта.ИсточникиЗначений = ИсточникиЗначений.Выгрузить();
	ПоместитьВоВременноеХранилище(ДанныеОбъекта, АдресЭлементаВХранилище);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Перем Ошибки;
	
	Если Не ЭтоАналитикаПрочее Тогда
		Если Не ЗначениеЗаполнено(Объект.ЗначениеАналитики) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
				"ЗначениеАналитики",
				НСтр("ru='Не заполнено значение аналитики';uk='Не заповнено значення аналітики'"), "");
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоАналитикаПрочееПриИзменении(Элемент)
	
	Если ЭтоАналитикаПрочее Тогда
		Объект.ЗначениеАналитики = Неопределено;
	КонецЕсли;
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникиАналитикПоУмолчаниюПриИзменении(Элемент)
	
	Если ВыбранныеИсточникиЗначений Тогда
		ЗаполнитьИсточникиПоУмолчанию();
	КонецЕсли;
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборДополнительныхПолей(Команда)
	
	ПараметрыФормы = БюджетнаяОтчетностьВызовСервера.ПараметрыНастройкиДополнительныхПолей(Объект, ВидАналитики, ЭтоСтроки);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновлениеДополнительныхПолей", ЭтаФорма);
	ОткрытьФорму("Справочник.ЭлементыФинансовыхОтчетов.Форма.НастройкаДополнительныхПолей", 
					ПараметрыФормы,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИсточники(Команда)
	
	ПараметрыВыбораИсточников = Новый Структура("АдресЭлементаВХранилище, АдресЭлементовОтчета, ИсточникиЗначений, Заполнение", 
														АдресЭлементаВХранилище, АдресЭлементовОтчета, ИсточникиЗначений, Истина);
	
	Оповещение = Новый ОписаниеОповещения("ВыборИсточниковЗначений", ЭтаФорма);
	ОткрытьФорму("Справочник.ЭлементыФинансовыхОтчетов.Форма.ИсточникиЗначений", ПараметрыВыбораИсточников
												,,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ВыбратьИсточники.Доступность = ВыбранныеИсточникиЗначений;
	Если Элементы.ВыбратьИсточники.Доступность 
		И ИсточникиЗначений.Количество() Тогда
		Элементы.ВыбратьИсточники.Заголовок = НСтр("ru='Изменить источники (%1)';uk='Змінити джерела (%1)'");
		Элементы.ВыбратьИсточники.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												Элементы.ВыбратьИсточники.Заголовок, ИсточникиЗначений.Количество());
	Иначе
		Элементы.ВыбратьИсточники.Заголовок = НСтр("ru='Не указаны';uk='Не вказано'");
	КонецЕсли;
	
	Элементы.ЗначениеАналитики.Доступность = Не ЭтоАналитикаПрочее;
	Элементы.ЗначениеАналитики.АвтоОтметкаНезаполненного = Не ЭтоАналитикаПрочее;
	Элементы.ЭтоАналитикаПрочее.Доступность = Не РазрешитьРедактирование;
	Элементы.ГруппаИсточники.Доступность = РазрешитьРедактирование;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеПриИзменении(Элемент)
	
	Если РазрешитьРедактирование Тогда
		ЭтоАналитикаПрочее = Истина;
		Объект.ЗначениеАналитики = Неопределено;
	КонецЕсли;
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьДополнительныеПоля(Результат)
	
	Объект.ДополнительныеПоля.Загрузить(ПолучитьИзВременногоХранилища(Результат));
	Справочники.ЭлементыФинансовыхОтчетов.ЗаполнитьПредставлениеДополнительныхПолей(Объект, Элементы.ВыборДополнительныхПолей);
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеДополнительныхПолей(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьДополнительныеПоля(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборИсточниковЗначений(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИсточникиЗначений.Очистить();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицуИзМассива(ИсточникиЗначений, Результат, "Источник");
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИсточникиПоУмолчанию()
	Перем Кэш;
	
	РассчитанныеИсточникиЗначений = БюджетнаяОтчетностьРасчетКэшаСервер.ИсточникиЗначенийПоУмолчанию(АдресЭлементовОтчета, АдресЭлементаВХранилище, Кэш, Истина);
	ИсточникиЗначений.Загрузить(РассчитанныеИсточникиЗначений);
	
КонецПроцедуры

#КонецОбласти