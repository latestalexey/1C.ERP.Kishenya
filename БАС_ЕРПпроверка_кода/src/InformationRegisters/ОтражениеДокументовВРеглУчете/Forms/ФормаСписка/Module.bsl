
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ОтражениеДокументовВРеглУчете" Тогда
		Элементы.Список.Обновить();	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаСписка = Элементы.Список.ТекущиеДанные;
	Если СтрокаСписка <> Неопределено Тогда
	
		СтруктураПараметры = Новый Структура("Регистратор, Организация",
			СтрокаСписка.Регистратор,
			СтрокаСписка.Организация);
		ОткрытьФорму("РегистрСведений.ОтражениеДокументовВРеглУчете.Форма.ФормаРедактированияЗаписи", СтруктураПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьСтатусКОтражению(Команда)
	
	ВыделенныеСтроки = РаботаСДиалогамиКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		ТекстВопроса = НСтр("ru='У выделенных в списке документов будет установлен статус ""К отражению в регл. учете"". Продолжить?';uk='У виділених в списку документів буде встановлено статус ""До відображення в регл. обліку"". Продовжити?'");
		Ответ = Неопределено;

		ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьСтатусКОтражениюЗавершение", ЭтотОбъект, Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)), ТекстВопроса,РежимДиалогаВопрос.ДаНет);
        Возврат;
	КонецЕсли;
	
	УстановитьСтатусКОтражениюФрагмент(ВыделенныеСтроки);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКОтражениюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
    
    
    Ответ = РезультатВопроса;
    Если Ответ = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
    
    УстановитьСтатусКОтражениюФрагмент(ВыделенныеСтроки);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКОтражениюФрагмент(Знач ВыделенныеСтроки)
    
    Перем КоличествоОбработанных;
    
    КоличествоОбработанных = УстановитьСтатус(ВыделенныеСтроки, "КОтражениюВРеглУчете");
    
    ОбщегоНазначенияУТКлиент.ОповеститьПользователяОбУстановкеСтатуса(Элементы.Список, КоличествоОбработанных, ВыделенныеСтроки.Количество(), НСтр("ru='К отражению в регл. учете';uk='До відображення в регл. обліку'"));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Функция УстановитьСтатус(Знач МассивСтрок, Знач НовыйСтатус)
	
	ЗначениеНовогоСтатуса = Перечисления.СтатусыОтраженияДокументовВРеглУчете[НовыйСтатус];
	
	КоличествоОбработанных = 0;
	Для Каждого СтрокаСписка Из МассивСтрок Цикл
		ЗаписатьДанныеРегистра(СтрокаСписка.Регистратор, ЗначениеНовогоСтатуса, КоличествоОбработанных);
	КонецЦикла;
	
	Возврат КоличествоОбработанных;
	
КонецФункции

&НаСервере
Процедура ЗаписатьДанныеРегистра(Регистратор, НовыйСтатус, КоличествоОбработанных)
	
	НаборЗаписей = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(Регистратор);
	НаборЗаписей.Прочитать();
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Запись.Статус <> НовыйСтатус Тогда
			Запись.Статус = НовыйСтатус;
		КонецЕсли;
	КонецЦикла;
	
	Если НаборЗаписей.Модифицированность() Тогда
		НаборЗаписей.Записывать = Истина;
		НаборЗаписей.Записать();
		КоличествоОбработанных = КоличествоОбработанных + 1;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти
