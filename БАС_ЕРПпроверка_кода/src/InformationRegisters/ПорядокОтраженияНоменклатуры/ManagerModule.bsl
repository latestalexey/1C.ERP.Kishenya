#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.Склад КАК Склад,
	|	ВложенныйЗапрос.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьСобственныеТовары) КАК ЕстьСобственныеТовары,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьРеализацияБезПереходаПраваСобственности) КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСобственныхТоваров) КАК ЕстьПродажаСобственныхТоваров,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаУслуг) КАК ЕстьПродажаУслуг,
	|	МАКСИМУМ(ВложенныйЗапрос.ЕстьПродажаСНДС) КАК ЕстьПродажаСНДС
	|ПОМЕСТИТЬ ДвиженияНоменклатуры
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Товары.Организация КАК Организация,
	|		Склады.Ссылка КАК Склад,
	|		ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета ТОГДА
	|			НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ
	|			ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|		КОНЕЦ КАК ГруппаФинансовогоУчета,
	|		ИСТИНА КАК ЕстьСобственныеТовары,
	|		Товары.ЕстьРеализацияБезПереходаПраваСобственности,
	|		ЛОЖЬ КАК ЕстьПродажаСобственныхТоваров,
	|		ЛОЖЬ КАК ЕстьПродажаУслуг,
	|		ЛОЖЬ КАК ЕстьПродажаСНДС
	|	ИЗ (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			Товары.Организация,
	|			Товары.АналитикаУчетаНоменклатуры,
	|			Товары.ВидЗапасов,
	|			ВЫБОР КОГДА Товары.КорРазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыВПути) ТОГДА
	|				ИСТИНА
	|			ИНАЧЕ
	|				ЛОЖЬ
	|			КОНЕЦ КАК ЕстьРеализацияБезПереходаПраваСобственности
	|		ИЗ
	|			РегистрНакопления.СебестоимостьТоваров КАК Товары
	|		ГДЕ
	|			Товары.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			И Товары.Организация В (&Организации)
	|			И Товары.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыНаСкладах)	
	|		) КАК Товары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО 
	|			Аналитика.Ссылка = Товары.АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|			Справочник.Склады КАК Склады
	|		ПО 
	|			Склады.Ссылка = Аналитика.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ 
	|			Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО 
	|			НоменклатураСпр.Ссылка = Аналитика.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО
	|			ВидыЗапасовСпр.Ссылка = Товары.ВидЗапасов
	//++ НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Отчет.Организация КАК Организация,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|		ЕСТЬNULL(Услуга.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка)) КАК ГруппаФинансовогоУчета,
	|		ЛОЖЬ КАК ЕстьСобственныеТовары,
	|		ЛОЖЬ КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|		ИСТИНА КАК ЕстьПродажаСобственныхТоваров,
	|		ИСТИНА КАК ЕстьПродажаУслуг,
	|		ЛОЖЬ КАК ЕстьПродажаСНДС
	|	ИЗ
	|		Документ.ОтчетДавальцу КАК Отчет
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|			Справочник.Номенклатура КАК Услуга
	|		ПО
	|			Отчет.Номенклатура = Услуга.Ссылка
	|	ГДЕ
	|		Отчет.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		И Отчет.Организация В (&Организации)
	|		И Отчет.Проведен
	//-- НЕ УТКА
	|
	|	ОБЪЕДИНИТЬ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Выручка.Организация,
	|		ВЫБОР КОГДА Выручка.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) ТОГДА
	|			ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ
	|			ЕСТЬNULL(Выручка.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|		КОНЕЦ КАК Склад,
	|
	|		ВЫБОР КОГДА НЕ &ФормироватьВидыЗапасовПоГруппамФинансовогоУчета
	|			ИЛИ Выручка.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		ТОГДА
	|			НоменклатураСпр.ГруппаФинансовогоУчета
	|		ИНАЧЕ
	|			ЕСТЬNULL(ВидыЗапасовСпр.ГруппаФинансовогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыФинансовогоУчетаНоменклатуры.ПустаяСсылка))
	|		КОНЕЦ КАК ГруппаФинансовогоУчета,
	|
	|		ВЫБОР КОГДА Выручка.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ КАК ЕстьСобственныеТовары,
	|	
	|		Выручка.ЕстьРеализацияБезПереходаПраваСобственности,
	|	
	|		ВЫБОР КОГДА Выручка.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|				ИЛИ Выручка.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.АгентскаяУслуга)
	|				ИЛИ НоменклатураСпр.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ КАК ЕстьПродажаСобственныхТоваров,
	|
	|		ВЫБОР КОГДА Выручка.ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга) ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|		КОНЕЦ КАК ЕстьПродажаУслуг,
	|		
	|		Выручка.ЕстьПродажаСНДС
	|		
	|	ИЗ (
	|		ВЫБРАТЬ
	|			Выручка.ТипЗапасов,
	|			Выручка.ВидЗапасов,
	|			КлючиПартнеров.Организация,
	|			ВЫРАЗИТЬ(КлючиНоменклатуры.Склад КАК Справочник.Склады) КАК Склад,
	|			КлючиНоменклатуры.Номенклатура,
	|			Выручка.ЕстьРеализацияБезПереходаПраваСобственности,
	|			Выручка.ЕстьПродажаСНДС,
	|			КОЛИЧЕСТВО(*) как КоличествоЗаписей
	|		ИЗ (
	|			ВЫБРАТЬ
	|				Выручка.ТипЗапасов КАК ТипЗапасов,
	|				Выручка.ВидЗапасов КАК ВидЗапасов,
	|				Выручка.АналитикаУчетаНоменклатуры КАК КлючНоменклатуры,
	|				Выручка.АналитикаУчетаПоПартнерам КАК КлючПартнера,
	|				ЛОЖЬ КАК ЕстьПродажаСНДС, 
	|				ЛОЖЬ КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|				КОЛИЧЕСТВО(*) КАК КоличествоЗаписей
	|			ИЗ
	|				РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК Выручка
	|			ГДЕ
	|				Выручка.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	
	|			СГРУППИРОВАТЬ ПО
	|				Выручка.ТипЗапасов,
	|				Выручка.ВидЗапасов,
	|				Выручка.АналитикаУчетаНоменклатуры,
	|				Выручка.АналитикаУчетаПоПартнерам,
	|				ЛОЖЬ,
	|				ЛОЖЬ
	|			) КАК Выручка
	|		
	|			ЛЕВОЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиПартнеров
	|			ПО
	|				КлючиПартнеров.КлючАналитики = Выручка.КлючПартнера
	|		
	|			ЛЕВОЕ СОЕДИНЕНИЕ
	|				РегистрСведений.АналитикаУчетаНоменклатуры КАК КлючиНоменклатуры
	|			ПО
	|				КлючиНоменклатуры.КлючАналитики = Выручка.КлючНоменклатуры
	|		ГДЕ
	|			КлючиПартнеров.Организация В (&Организации)
	|			И (
	|				ТИПЗНАЧЕНИЯ(КлючиНоменклатуры.Склад) = ТИП(Справочник.Склады)
	|				ИЛИ ТИПЗНАЧЕНИЯ(КлючиНоменклатуры.Склад) = ТИП(Справочник.СтруктураПредприятия)
	|				)
	|		СГРУППИРОВАТЬ ПО
	|			Выручка.ТипЗапасов,
	|			Выручка.ВидЗапасов,
	|			КлючиПартнеров.Организация,
	|			КлючиНоменклатуры.Склад,
	|			КлючиНоменклатуры.Номенклатура,
	|			Выручка.ЕстьРеализацияБезПереходаПраваСобственности,
	|			Выручка.ЕстьПродажаСНДС
	|		) КАК Выручка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Справочник.Номенклатура КАК НоменклатураСпр
	|		ПО
	|			НоменклатураСпр.Ссылка = Выручка.Номенклатура
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Справочник.ВидыЗапасов КАК ВидыЗапасовСпр
	|		ПО
	|			ВидыЗапасовСпр.Ссылка = Выручка.ВидЗапасов) КАК ВложенныйЗапрос
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВложенныйЗапрос.Организация,
	|		ВложенныйЗапрос.Склад,
	|		ВложенныйЗапрос.ГруппаФинансовогоУчета
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДвиженияНоменклатуры.Организация КАК Организация,
	|	ДвиженияНоменклатуры.Склад КАК Склад,
	|	ДвиженияНоменклатуры.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	ДвиженияНоменклатуры.ЕстьСобственныеТовары КАК ЕстьСобственныеТовары,
	|	ДвиженияНоменклатуры.ЕстьРеализацияБезПереходаПраваСобственности КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	ДвиженияНоменклатуры.ЕстьПродажаСобственныхТоваров КАК ЕстьПродажаСобственныхТоваров,
	|	ДвиженияНоменклатуры.ЕстьПродажаУслуг КАК ЕстьПродажаУслуг,
	|	ДвиженияНоменклатуры.ЕстьПродажаСНДС КАК ЕстьПродажаСНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНаСкладе
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНаСкладе
	|	КОНЕЦ КАК СчетУчетаНаСкладе,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНаСкладеПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВычетовИзДоходов, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВычетовИзДоходов
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВычетовИзДоходов
	|	КОНЕЦ КАК СчетУчетаВычетовИзДоходов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВычетовИзДоходов, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВычетовИзДоходов, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВычетовИзДоходовПоУмолчанию,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяЗатратРегл, &ПустаяСтатьяЗатрат) <> &ПустаяСтатьяЗатрат
	|			ТОГДА ПорядокОтражения.СтатьяЗатратРегл
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяЗатратРегл
	|	КОНЕЦ КАК СтатьяЗатратРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяЗатратРегл, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяЗатратРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяЗатратРеглПоУмолчанию,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ПорядокОтражения.СтатьяДоходовРегл
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяДоходовРегл
	|	КОНЕЦ КАК СтатьяДоходовРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяДоходовРеглПоУмолчанию
	|
	|ИЗ
	|	ДвиженияНоменклатуры КАК ДвиженияНоменклатуры
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияНоменклатуры КАК ПорядокОтражения
	|	ПО
	|		ДвиженияНоменклатуры.Организация = ПорядокОтражения.Организация
	|		И ДвиженияНоменклатуры.Склад = ПорядокОтражения.Склад
	|		И ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ПорядокОтражения.ГруппаФинансовогоУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|	ПО
	|		ДвиженияНоменклатуры.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокОтражения.Организация КАК Организация,
	|	ПорядокОтражения.Склад КАК Склад,
	|	ПорядокОтражения.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|
	|	Ложь КАК ЕстьСобственныеТовары,
	|	Ложь КАК ЕстьРеализацияБезПереходаПраваСобственности,
	|	Ложь КАК ЕстьПродажаСобственныхТоваров,
	|	Ложь КАК ЕстьПродажаУслуг,
	|	Ложь КАК ЕстьПродажаСНДС,
	|	
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаНаСкладе
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаНаСкладе
	|	КОНЕЦ КАК СчетУчетаНаСкладе,
	|	ВЫБОР 
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаНаСкладе, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаНаСкладе, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаНаСкладеПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВыручкиОтПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВыручкиОтПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВыручкиОтПродажПоУмолчанию,
	|	
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВычетовИзДоходов, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаВычетовИзДоходов
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаВычетовИзДоходов
	|	КОНЕЦ КАК СчетУчетаВычетовИзДоходов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаВычетовИзДоходов, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаВычетовИзДоходов, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаВычетовИзДоходовПоУмолчанию,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ПорядокОтражения.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродаж,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчетаСебестоимостиПродаж, &ПустойСчет) = &ПустойСчет
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СчетУчетаСебестоимостиПродаж, &ПустойСчет) <> &ПустойСчет
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаСебестоимостиПродажПоУмолчанию,
	|
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяЗатратРегл, &ПустаяСтатьяЗатрат) <> &ПустаяСтатьяЗатрат
	|			ТОГДА ПорядокОтражения.СтатьяЗатратРегл
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяЗатратРегл
	|	КОНЕЦ КАК СтатьяЗатратРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяЗатратРегл, &ПустаяСтатьяЗатрат) = &ПустаяСтатьяЗатрат
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяЗатратРегл, &ПустаяСтатьяЗатрат) <> &ПустаяСтатьяЗатрат
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяЗатратРеглПоУмолчанию,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ПорядокОтражения.СтатьяДоходовРегл
	|		ИНАЧЕ ГруппыФинансовогоУчета.СтатьяДоходовРегл
	|	КОНЕЦ КАК СтатьяДоходовРегл,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СтатьяДоходовРегл, &ПустаяСтатья) = &ПустаяСтатья
	|				И ЕСТЬNULL(ГруппыФинансовогоУчета.СтатьяДоходовРегл, &ПустаяСтатья) <> &ПустаяСтатья
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтатьяДоходовРеглПоУмолчанию
	|
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияНоменклатуры КАК ПорядокОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДвиженияНоменклатуры КАК ДвиженияНоменклатуры
	|	ПО
	|		ПорядокОтражения.Организация = ДвиженияНоменклатуры.Организация
	|		И ПорядокОтражения.Склад = ДвиженияНоменклатуры.Склад
	|		И ПорядокОтражения.ГруппаФинансовогоУчета = ДвиженияНоменклатуры.ГруппаФинансовогоУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ГруппыФинансовогоУчетаНоменклатуры КАК ГруппыФинансовогоУчета
	|	ПО
	|		ПорядокОтражения.ГруппаФинансовогоУчета = ГруппыФинансовогоУчета.Ссылка
	|	
	|ГДЕ
	|	ПорядокОтражения.Организация В (&Организации)
	|	И ДвиженияНоменклатуры.Организация ЕСТЬ NULL
	|	
	|УПОРЯДОЧИТЬ ПО
	|	Организация Возр,
	|	Склад Возр,
	|	ГруппаФинансовогоУчета Возр
	|");
	Запрос.УстановитьПараметр("Организации", МассивОрганизаций);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета",
		ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоГруппамФинансовогоУчета"));
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтатья", ПланыВидовХарактеристик.СтатьиДоходов.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяСтатьяЗатрат", ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка());
	
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Склад КАК Склад,
	|	ИсходнаяТаблица.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ДанныеИзменены
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНастроек.Организация КАК Организация,
	|	Аналитика.КлючАналитики КАК АналитикаУчетаНоменклатуры
	|
	|ПОМЕСТИТЬ ВтАналитика
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		Аналитика.Склад = ТаблицаНастроек.Склад
	|		И Аналитика.Номенклатура.ГруппаФинансовогоУчета = ТаблицаНастроек.ГруппаФинансовогоУчета
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж.Обороты(, &ДатаОкончания, Регистратор,
	|		ТипЗапасов = ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Услуга)
	|		И (АналитикаУчетаПоПартнерам.Организация, АналитикаУчетаНоменклатуры) В (
	|			ВЫБРАТЬ
	|				Аналитика.Организация,
	|				Аналитика.АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВтАналитика КАК Аналитика
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация, Склад, ГруппаФинансовогоУчета, ДанныеИзменены");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли