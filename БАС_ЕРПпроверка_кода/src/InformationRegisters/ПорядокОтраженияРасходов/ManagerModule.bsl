#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив,
	|	СчетаБухгалтерскогоУчетаНМА.СчетУчета,
	|	ПараметрыЦелевогоФинансированияНМА.СчетУчета КАК СчетУчетаЦФ,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансированияНМА.ПрименениеЦелевогоФинансирования, ЛОЖЬ) КАК ПрименениеЦелевогоФинансирования
	|ПОМЕСТИТЬ втСчетаБухгалтерскогоУчетаНМА
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&ДатаНачала, Организация В (&МассивОрганизаций)) КАК СчетаБухгалтерскогоУчетаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(&ДатаНачала, ) КАК ПараметрыЦелевогоФинансированияНМА
	|	ПО СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ПараметрыЦелевогоФинансированияНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ЕСТЬNULL(ДанныеСчетУчета.Подразделение, ЕСТЬNULL(ДанныеСчетУчетаЦФ.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	втСчетаБухгалтерскогоУчетаНМА КАК втСчетаБухгалтерскогоУчетаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаНачала,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.СчетУчета
	|				ИЗ
	|					втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчета
	|	ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчета.Субконто1
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаНачала,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.СчетУчетаЦФ
	|				ИЗ
	|					втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчетаЦФ
	|	ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчетаЦФ.Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпособыОтраженияРасходов.Организация КАК Организация,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеНМА.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиАмортизацииКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК МестонахождениеНМА
	|		ПО СпособыОтраженияРасходов.НематериальныйАктив = МестонахождениеНМА.НематериальныйАктив
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)) КАК Организация,
	|	ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ПринятиеКУчетуОС.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&ДатаОкончания,
	|				Организация В (&МассивОрганизаций)
	|					И Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС) КАК ПервоначальныеСведения
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ПО (ПервоначальныеСведения.Регистратор = ПринятиеКУчетуОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов)
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|;
	|
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпособыОтраженияРасходов.Организация КАК Организация,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиОсновныхСредствКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&ДатаОкончания, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	СтатьиПрочихОпераций.Ссылка
	|ПОМЕСТИТЬ СтатьиПрочихОпераций
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиПрочихОпераций
	|ГДЕ
	|	СтатьиПрочихОпераций.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И СтатьиПрочихОпераций.ВидЦенности = ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ПрочиеРаботыИУслуги)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтатьиПрочихОпераций.Ссылка
	|ПОМЕСТИТЬ СтатьиВнеоборотныхАктивов
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиПрочихОпераций
	|ГДЕ
	|	СтатьиПрочихОпераций.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	И СтатьиПрочихОпераций.ВидЦенности В(
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОС),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.ОбъектыНезавершенногоСтроительства),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыЦенностей.НМА)
	|	)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Организация КАК Организация,
	|	ДД.Подразделение КАК Подразделение,
	|	ДД.СтатьяРасходов КАК СтатьяРасходов
	|
	|ПОМЕСТИТЬ ВТПрочиеРасходы
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ДД
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО СтатьиПрочихОпераций.Ссылка = ДД.СтатьяРасходов
	|ГДЕ
	|	(ДД.Период МЕЖДУ &ДатаНачала И &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Организация КАК Организация,
	|	ДД.Подразделение КАК Подразделение,
	|	ДД.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов КАК ДД
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО СтатьиПрочихОпераций.Ссылка = ДД.СтатьяРасходов
	|ГДЕ
	|	(ДД.Период МЕЖДУ &ДатаНачала И &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|	И ДД.Организация В (&МассивОрганизаций)
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомиссионера.Организация КАК Организация,
	|	ОтчетКомиссионера.Подразделение КАК Подразделение,
	|	ОтчетКомиссионера.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	Документ.ОтчетКомиссионера КАК ОтчетКомиссионера
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО ОтчетКомиссионера.СтатьяРасходов = СтатьиПрочихОпераций.Ссылка
	|ГДЕ
	|	СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|	И (ОтчетКомиссионера.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|	И ОтчетКомиссионера.Проведен
	|	И ОтчетКомиссионера.Организация В (&МассивОрганизаций)
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Организация КАК Организация,
	|	Документ.Подразделение КАК Подразделение,
	|	ТаблицаРаспределениеРасходов.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	Документ.РаспределениеРасходовБудущихПериодов КАК Документ
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеРасходовБудущихПериодов.РаспределениеРасходов КАК ТаблицаРаспределениеРасходов
	|	ПО
	|		Документ.Ссылка = ТаблицаРаспределениеРасходов.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО ТаблицаРаспределениеРасходов.СтатьяРасходов = СтатьиПрочихОпераций.Ссылка
	|	
	|ГДЕ
	|	(Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|	И Документ.Проведен
	|	И Документ.Организация В (&МассивОрганизаций)
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|	
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Документ.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК Подразделение,
	|	ТаблицаНачисления.СтатьяДоходовРасходов КАК СтатьяРасходов
	|ИЗ
	|	Документ.НачисленияКредитовИДепозитов КАК Документ
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.НачисленияКредитовИДепозитов.Начисления КАК ТаблицаНачисления
	|	ПО
	|		Документ.Ссылка = ТаблицаНачисления.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО ТаблицаНачисления.СтатьяДоходовРасходов = СтатьиПрочихОпераций.Ссылка
	|	
	|ГДЕ
	|	(Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|	И Документ.Проведен
	|	И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачисленияПоКредитам)
	|	И Документ.Организация В (&МассивОрганизаций)
	|	И СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяДоходовРасходов КАК СтатьяРасходов
	|ИЗ
	|	РегистрНакопления.ДвиженияНоменклатураДоходыРасходы.Обороты(&ДатаНачала, &ДатаОкончания, Период,
	|		Организация В (&МассивОрганизаций)
	|	) КАК ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК Статьи
	|		ПО Статьи.Ссылка = ДанныеРегистра.СтатьяДоходовРасходов
	|	ЛЕВОЕ СОЕДИНЕНИЕ СтатьиПрочихОпераций КАК СтатьиПрочихОпераций
	|		ПО СтатьиПрочихОпераций.Ссылка = ДанныеРегистра.СтатьяДоходовРасходов
	|ГДЕ
	|	СтатьиПрочихОпераций.Ссылка ЕСТЬ NULL
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции.Обороты(&ДатаНачала, &ДатаОкончания, Период,
	|		Организация В (&МассивОрганизаций)
	|	) КАК ДанныеРегистра
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(&ДатаНачала, &ДатаОкончания, Период,
	|		Организация В (&МассивОрганизаций)
	|	) КАК ДанныеРегистра
	|
	//++ НЕ УТКА
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Организация КАК Организация,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	РегистрНакопления.ТрудозатратыНезавершенногоПроизводства.Обороты(&ДатаНачала, &ДатаОкончания, Период,
	|		Организация В (&МассивОрганизаций)
	|	) КАК ДанныеРегистра
	|	
	//-- НЕ УТКА
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоступлениеОтПереработчика.Организация КАК Организация,
	|	ПоступлениеОтПереработчика.Партнер КАК Подразделение,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов
	|ИЗ
	|	Документ.ПоступлениеОтПереработчика КАК ПоступлениеОтПереработчика
	|ГДЕ
	|	(ПоступлениеОтПереработчика.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|		ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|	И ПоступлениеОтПереработчика.Проведен
	|	И ПоступлениеОтПереработчика.Организация В (&МассивОрганизаций)
	|
	|ОБЪЕДИНИТЬ
	|ВЫБРАТЬ
	|	втСтатьиАмортизацииКОтражению.Организация КАК Организация,
	|	втСтатьиАмортизацииКОтражению.Подразделение КАК Подразделение,
	|	втСтатьиАмортизацииКОтражению.СтатьяРасходов КАК СтатьяРасходов
	|ИЗ
	|	втСтатьиАмортизацииКОтражению КАК втСтатьиАмортизацииКОтражению
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов
	|ПОМЕСТИТЬ ПрочиеРасходы
	|ИЗ
	|	ВТПрочиеРасходы КАК ПрочиеРасходы
	|;
	|
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрочиеРасходы.Организация КАК Организация,
	|	ПрочиеРасходы.Подразделение КАК Подразделение,
	|	ПрочиеРасходы.СтатьяРасходов КАК СтатьяРасходов,
	|	ПрочиеРасходы.СтатьяРасходов.ВариантРаспределенияРасходов КАК ВариантРаспределенияРасходов,
	|	ПрочиеРасходы.СтатьяРасходов.ВидЦенности КАК ВидЦенности,
	|	ПорядокОтражения.СтатьяРасходов.РасходыНаРемонтОС КАК РасходыНаРемонтОС,
	|	ПорядокОтражения.СтатьяРасходов.СебестоимостьРеализации КАК СебестоимостьРеализации,
	|	Истина КАК ЕстьРасходы,
	|
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетУчета
	|		ИНАЧЕ ПрочиеРасходы.СтатьяРасходов.СчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) = &ПустойСчет 
	|				И ПрочиеРасходы.СтатьяРасходов.СчетУчета <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаПоУмолчанию
	|ИЗ
	|	ПрочиеРасходы КАК ПрочиеРасходы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|	ПО
	|		ПрочиеРасходы.Организация = ПорядокОтражения.Организация
	|		И ПрочиеРасходы.Подразделение = ПорядокОтражения.Подразделение
	|		И ПрочиеРасходы.СтатьяРасходов = ПорядокОтражения.СтатьяРасходов
	|		И ПорядокОтражения.Документ = Неопределено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПорядокОтражения.Организация КАК Организация,
	|	ПорядокОтражения.Подразделение КАК Подразделение,
	|	ПорядокОтражения.СтатьяРасходов КАК СтатьяРасходов,
	|	ПорядокОтражения.СтатьяРасходов.ВариантРаспределенияРасходов КАК ВариантРаспределенияРасходов,
	|	ПорядокОтражения.СтатьяРасходов.ВидЦенности КАК ВидЦенности,
	|	ПорядокОтражения.СтатьяРасходов.РасходыНаРемонтОС КАК РасходыНаРемонтОС,
	|	ПорядокОтражения.СтатьяРасходов.СебестоимостьРеализации КАК СебестоимостьРеализации,
	|	Ложь КАК ЕстьРасходы,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) <> &ПустойСчет 
	|			ТОГДА ПорядокОтражения.СчетУчета
	|		ИНАЧЕ ПорядокОтражения.СтатьяРасходов.СчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) = &ПустойСчет 
	|				И ПорядокОтражения.СтатьяРасходов.СчетУчета <> &ПустойСчет 
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетУчетаПоУмолчанию
	|ИЗ
	|	РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПрочиеРасходы КАК ПрочиеРасходы
	|	ПО
	|		ПорядокОтражения.Организация = ПрочиеРасходы.Организация
	|		И ПорядокОтражения.Подразделение = ПрочиеРасходы.Подразделение
	|		И ПорядокОтражения.СтатьяРасходов = ПрочиеРасходы.СтатьяРасходов
	|
	|ГДЕ
	|	ПрочиеРасходы.Организация ЕСТЬ NULL
	|	И ПорядокОтражения.Организация В (&МассивОрганизаций)
	|	И ПорядокОтражения.Документ = Неопределено
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация Возр,
	|	Подразделение Возр,
	|	СтатьяРасходов Возр
	|");
	Запрос.УстановитьПараметр("МассивОрганизаций", МассивОрганизаций);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Функция РезультатЗапросаПоНастройкамОтраженияВУчетеДетально(
	Организация,
	Подразделение,
	СтатьяРасходов,
	ДатаНачала,
	ДатаОкончания,
	ПоказыватьОтраженныеВУчете
	) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ДанныеРегистра.Подразделение КАК Подразделение,
	|	ДанныеРегистра.СтатьяРасходов КАК СтатьяРасходов
	|
	|ПОМЕСТИТЬ ПрочиеРасходы
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Обороты(&ДатаНачала, &ДатаОкончания, Регистратор,
	|		Организация = &Организация
	|		И Подразделение = &Подразделение
	|		И СтатьяРасходов = &СтатьяРасходов
	|	) КАК ДанныеРегистра
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументов
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументов.Регистратор
	|		И (ОтражениеДокументов.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|			ИЛИ &ПоказыватьОтраженныеВУчете)
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПрочиеРасходы.Документ КАК Документ,
	|	ПрочиеРасходы.Документ.Дата КАК Дата,
	|	ПрочиеРасходы.Документ.Номер КАК Номер,
	|	ТИПЗНАЧЕНИЯ(ПрочиеРасходы.Документ) КАК Тип,
	|	Истина КАК ЕстьРасходы,
	|
	|	ЕСТЬNULL(ПорядокОтражения.СчетУчета, Неопределено) КАК СчетУчета
	|ИЗ
	|	ПрочиеРасходы КАК ПрочиеРасходы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтражения
	|	ПО
	|		&Организация = ПорядокОтражения.Организация
	|		И &Подразделение = ПорядокОтражения.Подразделение
	|		И &СтатьяРасходов = ПорядокОтражения.СтатьяРасходов
	|		И ПрочиеРасходы.Документ = ПорядокОтражения.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата Возр,
	|	Документ
	|");
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);
	Запрос.УстановитьПараметр("ПоказыватьОтраженныеВУчете", ПоказыватьОтраженныеВУчете);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции // РезультатЗапросаПоНастройкамОтраженияВУчете()

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.Подразделение КАК Подразделение,
	|	ИсходнаяТаблица.СтатьяРасходов КАК СтатьяРасходов
	|
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ДанныеИзменены
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СтатьяРасходов
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, Подразделение, СтатьяРасходов) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.Подразделение,
	|				ТаблицаНастроек.СтатьяРасходов
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ТаблицаНастроек КАК ТаблицаНастроек
	|	ПО
	|		ДанныеРегистра.Организация = ТаблицаНастроек.Организация
	|		И ДанныеРегистра.Подразделение = ТаблицаНастроек.Подразделение
	|		И ДанныеРегистра.СтатьяРасходовСписания = ТаблицаНастроек.СтатьяРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|ГДЕ
	|	(ДанныеРегистра.Период <= &ДатаОкончания ИЛИ &ДатаОкончания = ДАТАВРЕМЯ(1,1,1))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(, &ДатаОкончания, Регистратор,
	|		(Организация, Подразделение) В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация,
	|				ТаблицаНастроек.Подразделение
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|			ГДЕ
	|				ТаблицаНастроек.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация, Подразделение, СтатьяРасходов, ДанныеИзменены");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает признак наличия ошибок заполнения настроек отражения амортизации НМА в регламентированном учете
//
// Параметры:
// 		МассивОрганизаций - Массив, СправочникСсылка.Организации - Массив или ссылка на элемент справочника организаций, по которым выполняется проверка
// 		Период - Дата - Период отражения амортизации
//
// Возвращаемое значение:
// 		Булево - Истина, если имеются ошибки заполенния настроек отражения расходов
//
Функция ЕстьОшибкиЗаполненияРасходовПоАмортизацииНМА(МассивОрганизаций, Период) Экспорт
	
	ПараметрОрганизации = Неопределено;
	Если ТипЗнч(МассивОрганизаций) = Тип("Массив") Тогда
		ПараметрОрганизации = МассивОрганизаций;
	Иначе
		ПараметрОрганизации = Новый Массив;
		ПараметрОрганизации.Добавить(МассивОрганизаций);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрОрганизации);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив,
	|	СчетаБухгалтерскогоУчетаНМА.СчетУчета,
	|	ПараметрыЦелевогоФинансированияНМА.СчетУчета КАК СчетУчетаЦФ,
	|	ЕСТЬNULL(ПараметрыЦелевогоФинансированияНМА.ПрименениеЦелевогоФинансирования, ЛОЖЬ) КАК ПрименениеЦелевогоФинансирования
	|ПОМЕСТИТЬ втСчетаБухгалтерскогоУчетаНМА
	|ИЗ
	|	РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СчетаБухгалтерскогоУчетаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыЦелевогоФинансированияНМА.СрезПоследних(&НачалоМесяца, ) КАК ПараметрыЦелевогоФинансированияНМА
	|	ПО СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ПараметрыЦелевогоФинансированияНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ЕСТЬNULL(ДанныеСчетУчета.Подразделение, ЕСТЬNULL(ДанныеСчетУчетаЦФ.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Местонахождение
	|ПОМЕСТИТЬ втМестонахожденияНМА
	|ИЗ
	|	втСчетаБухгалтерскогоУчетаНМА КАК втСчетаБухгалтерскогоУчетаНМА
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоМесяца,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.СчетУчета
	|				ИЗ
	|					втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчета
	|	ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчета.Субконто1
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоМесяца,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.СчетУчетаЦФ
	|				ИЗ
	|					втСчетаБухгалтерскогоУчетаНМА КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			Организация В (&МассивОрганизаций)) КАК ДанныеСчетУчетаЦФ
	|	ПО втСчетаБухгалтерскогоУчетаНМА.НематериальныйАктив = ДанныеСчетУчетаЦФ.Субконто1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СпособыОтраженияРасходов.Организация КАК Организация,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеНМА.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))) КАК Подразделение,
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов) КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиНМАКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииНМАБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровНМА.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияНМА КАК МестонахождениеНМА
	|		ПО СпособыОтраженияРасходов.НематериальныйАктив = МестонахождениеНМА.НематериальныйАктив
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	втСтатьиНМАКОтражению.СтатьяРасходов
	|ИЗ
	|	втСтатьиНМАКОтражению КАК втСтатьиНМАКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|		ПО втСтатьиНМАКОтражению.Организация = ПорядокОтраженияРасходов.Организация
	|			И втСтатьиНМАКОтражению.Подразделение = ПорядокОтраженияРасходов.Подразделение
	|			И втСтатьиНМАКОтражению.СтатьяРасходов = ПорядокОтраженияРасходов.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК ПланСчетов
	|		ПО втСтатьиНМАКОтражению.СтатьяРасходов.СчетУчета = ПланСчетов.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, ПланСчетов.Ссылка) ЕСТЬ NULL ";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает признак наличия ошибок заполнения настроек отражения амортизации ОС в регламентированном учете
//
// Параметры:
// 		МассивОрганизаций - Массив, СправочникСсылка.Организации - Массив или ссылка на элемент справочника организаций, по которым выполняется проверка
// 		Период - Дата - Период отражения амортизации
//
// Возвращаемое значение:
// 		Булево - Истина, если имеются ошибки заполенния настроек отражения расходов
//
Функция ЕстьОшибкиЗаполненияРасходовПоАмортизацииОС(МассивОрганизаций, Период) Экспорт
	
	ПараметрОрганизации = Неопределено;
	Если ТипЗнч(МассивОрганизаций) = Тип("Массив") Тогда
		ПараметрОрганизации = МассивОрганизаций;
	Иначе
		ПараметрОрганизации = Новый Массив;
		ПараметрОрганизации.Добавить(МассивОрганизаций);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций", ПараметрОрганизации);
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(Период));
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)) КАК Организация,
	|	ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение,
	|	ПринятиеКУчетуОС.СтатьяРасходов КАК СтатьяРасходов
	|ПОМЕСТИТЬ втСтатьиОсновныхСредствКОтражению
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&НачалоМесяца,
	|				Организация В (&МассивОрганизаций)
	|					И Регистратор ССЫЛКА Документ.ПринятиеКУчетуОС) КАК ПервоначальныеСведения
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПринятиеКУчетуОС КАК ПринятиеКУчетуОС
	|		ПО (ПервоначальныеСведения.Регистратор = ПринятиеКУчетуОС.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(ОрганизацииПоДокументу.Ссылка, ЕСТЬNULL(ОрганизацииПоРегистру.Ссылка, СпособыОтраженияРасходов.Организация)),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.Подразделение, ЕСТЬNULL(МестонахождениеОС.Местонахождение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))),
	|	ЕСТЬNULL(ОтражениеАмортизационныхРасходов.СтатьяРасходов, СпособыОтраженияРасходов.СтатьяРасходов)
	|ИЗ
	|	РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК СпособыОтраженияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИзменениеПараметровОС.ОтражениеАмортизационныхРасходов КАК ОтражениеАмортизационныхРасходов
	|		ПО СпособыОтраженияРасходов.СпособОтраженияРасходов = ОтражениеАмортизационныхРасходов.Ссылка
	|			И (СпособыОтраженияРасходов.СпособОтраженияРасходовЗаданДокументом)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоДокументу
	|		ПО ОтражениеАмортизационныхРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоДокументу.Ссылка
	|			И ОтражениеАмортизационныхРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОрганизацииПоРегистру
	|		ПО СпособыОтраженияРасходов.ОрганизацияПолучательРасходов = ОрганизацииПоРегистру.Ссылка
	|			И СпособыОтраженияРасходов.ПередаватьРасходыВДругуюОрганизацию
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(&НачалоМесяца, Организация В (&МассивОрганизаций)) КАК МестонахождениеОС
	|		ПО СпособыОтраженияРасходов.ОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	втСтатьиОсновныхСредствКОтражению.СтатьяРасходов
	|ИЗ
	|	втСтатьиОсновныхСредствКОтражению КАК втСтатьиОсновныхСредствКОтражению
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияРасходов КАК ПорядокОтраженияРасходов
	|		ПО втСтатьиОсновныхСредствКОтражению.Организация = ПорядокОтраженияРасходов.Организация
	|			И втСтатьиОсновныхСредствКОтражению.Подразделение = ПорядокОтраженияРасходов.Подразделение
	|			И втСтатьиОсновныхСредствКОтражению.СтатьяРасходов = ПорядокОтраженияРасходов.СтатьяРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК ПланСчетов
	|		ПО втСтатьиОсновныхСредствКОтражению.СтатьяРасходов.СчетУчета = ПланСчетов.Ссылка
	|ГДЕ
	|	ЕСТЬNULL(ПорядокОтраженияРасходов.СчетУчета, ПланСчетов.Ссылка) ЕСТЬ NULL ";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецЕсли