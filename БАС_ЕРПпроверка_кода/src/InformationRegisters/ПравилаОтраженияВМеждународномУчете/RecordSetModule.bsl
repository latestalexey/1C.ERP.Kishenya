#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ШаблоныПроводок.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ШаблоныПроводок.ШаблонПроводки КАК ШаблонПроводки
	|ПОМЕСТИТЬ ШаблоныПроводок
	|ИЗ 
	|	&ШаблоныПроводок КАК ШаблоныПроводок
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ШаблонПроводки
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШаблоныПроводок.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ШаблоныПроводок.ШаблонПроводки КАК ШаблонПроводки,
	|	ДанныеСправочника.Операция КАК Операция
	|ПОМЕСТИТЬ ХозяйственныеОперации	
	|ИЗ
	|	ШаблоныПроводок КАК ШаблоныПроводок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ШаблоныПроводокДляМеждународногоУчета КАК ДанныеСправочника
	|	ПО
	|		ШаблоныПроводок.ШаблонПроводки = ДанныеСправочника.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Операция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШаблоныПроводок.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ШаблоныПроводок.ШаблонПроводки КАК ШаблонПроводки,
	|	ДанныеСправочника.СчетРеглУчета КАК СчетРеглУчета
	|ПОМЕСТИТЬ СчетаРеглУчета 
	|ИЗ
	|	ШаблоныПроводок КАК ШаблоныПроводок
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.СоответствияСчетовМеждународногоУчета КАК ДанныеСправочника
	|	ПО
	|		ШаблоныПроводок.ШаблонПроводки = ДанныеСправочника.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетРеглУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозяйственныеОперации.ШаблонПроводки КАК ШаблонПроводки
	|ИЗ
	|	ХозяйственныеОперации КАК ХозяйственныеОперации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ХозяйственныеОперацииНеОтражаемыеВМеждународномУчете КАК НеОтражаемыеХозоперации
	|	ПО
	|		ХозяйственныеОперации.УчетнаяПолитика = НеОтражаемыеХозоперации.УчетнаяПолитика
	|		И ХозяйственныеОперации.Операция = НеОтражаемыеХозоперации.Операция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетаРеглУчета.ШаблонПроводки КАК СоответствиеСчетов
	|ИЗ
	|	СчетаРеглУчета КАК СчетаРеглУчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.СчетаРеглУчетаИгнорируемыеПриОтраженииВМеждународномУчете КАК ИгнорируемыеСчета
	|	ПО
	|		СчетаРеглУчета.УчетнаяПолитика = ИгнорируемыеСчета.УчетнаяПолитика
	|		И СчетаРеглУчета.СчетРеглУчета = ИгнорируемыеСчета.СчетРеглУчета";
	
	Запрос.УстановитьПараметр("ШаблоныПроводок", ЭтотОбъект.Выгрузить());
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	
	ВыборкаПоНеОтражаемымОперациям = РезультатыЗапросов[3].Выбрать(); 
	Пока ВыборкаПоНеОтражаемымОперациям.Следующий() Цикл
		Отказ = Истина;
		ТекстСообщения = НСтр("ru='Хозяйственная операция, используемая в шаблоне проводки ""%ШаблонПроводки%"", исключена из учетной политики.';uk='Господарська операція, яка використовується в шаблоні проводки ""%ШаблонПроводки%"", виключена з облікової політики.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ШаблонПроводки%", ВыборкаПоНеОтражаемымОперациям.ШаблонПроводки); 
	КонецЦикла;
	
	ВыборкаПоИгнорируемымСчетам = РезультатыЗапросов[4].Выбрать(); 
	Пока ВыборкаПоИгнорируемымСчетам.Следующий() Цикл
		Отказ = Истина;
		ТекстСообщения = НСтр("ru='Счет регл. учета, указанный в соответствии ""%СоответствиеСчетов%"", игнорируется в учетной политике.';uk='Рахунок регл. обліку, вказаний у відповідності ""%СоответствиеСчетов%"", ігнорується в обліковій політиці.'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СоответствиеСчетов%", ВыборкаПоИгнорируемымСчетам.СоответствиеСчетов); 
	КонецЦикла;
	
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли