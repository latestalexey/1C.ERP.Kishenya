#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеПериодаПоНомеру(Период, НомерПериода, Периодичность) Экспорт
	
	Результат = "";
	
	Если Периодичность = Перечисления.Периодичность.День Тогда
		Результат = Формат(Период, "ДФ='dd MMMM'");
	ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
		Шаблон = НСтр("ru='%НомерПериода неделя';uk='%НомерПериода тиждень'");
		Результат = СтрЗаменить(Шаблон, "%НомерПериода", НомерПериода);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Декада Тогда
		Шаблон = НСтр("ru='%НомерПериода декада';uk='%НомерПериода декада'");
		Результат = СтрЗаменить(Шаблон, "%НомерПериода", НомерПериода);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		Результат = Формат(Период, "ДФ='MMMM'");
	ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
		Шаблон = НСтр("ru='%НомерПериода квартал';uk='%НомерПериода квартал'");
		Результат = СтрЗаменить(Шаблон, "%НомерПериода", НомерПериода);
	ИначеЕсли Периодичность = Перечисления.Периодичность.Полугодие Тогда
		Шаблон = НСтр("ru='%НомерПериода полугодие';uk='%НомерПериода півріччя'");
		Результат = СтрЗаменить(Шаблон, "%НомерПериода", НомерПериода);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает значения нефинансового показателя в периоде с учетом его вида
//
// Параметры
// 	ТаблицаЗначенийПоказателя - ТаблицаЗначений -  Значения нефинансового показателя для группировки
// 	Валюта - СправочникСсылка.Валюты - Валюта денежного нефинансового показателя
// 	ЕдиницаИзмерения - СправочникСсылка.УпаковкиЕдиницыИзмерения - Единица измерения количественного нефинансового показателя
// 	ВидПоказателя - ПеречислениеСсылка.ВидыНефинансовыхПоказателей - Вид нефинансового показателя
// 	ПоПериодам - Булево - Признак того, что значения вводятся по подпериодам
// 	КоличествоАналитик - Число - Количество аналитик, используемых для финансового показателя
// 	УстанавливатьЗначениеНаКаждыйПериод - Булево - Признак, что требуется установка значения на каждый период
// 	Период - Дата - Текущий период отчета
// 	ПериодичностьОтчета - ПеречислениеСсылка.Периодичность - Периодичность построения отчета
// 	ПериодичностьПоказателя - ПеречислениеСсылка.Периодичность - Периодичность показателя
// 	Группировка - Строка - Группировка, для которой возвращается значение нефинансового показателя
// 	Кэш - Соответствие - Кэш, используемый при работе функции
// 
// Возвращаемое значение
// 	Строка - Представление значений нефинансового показателя для периода отчета
//
Функция Подключаемый_ЗначениеПоказателяДляГруппировки(
		ТаблицаЗначенийПоказателя, 
		Валюта, 
		ЕдиницаИзмерения, 
		ВидПоказателя, 
		ПоПериодам,
		КоличествоАналитик, 
		УстанавливатьЗначениеНаКаждыйПериод,
		Период, 
		ПериодичностьОтчета, 
		ПериодичностьПоказателя, 
		Группировка,
		Кэш) Экспорт
		
	Результат = "";
	
	Группировки = Новый Структура;
	Группировки.Вставить("Подразделение", 0);
	Группировки.Вставить("Аналитика1", 1);
	Группировки.Вставить("Аналитика2", 2);
	Группировки.Вставить("Аналитика3", 3);
	Группировки.Вставить("Аналитика4", 4);
	Группировки.Вставить("Аналитика5", 5);
	Группировки.Вставить("Аналитика6", 6);
	
	Если Группировка <> "Регистратор" И Группировки[Группировка] <> КоличествоАналитик Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТаблицаЗначенийПоказателя.Количество() = 1
		И (ПериодичностьОтчета = ПериодичностьПоказателя 
			Или Не УстанавливатьЗначениеНаКаждыйПериод) Тогда
		Строка = ТаблицаЗначенийПоказателя[0];
		Результат = ПредставлениеЗначенияПоказателяПоВиду(Строка.Значение, ВидПоказателя, Валюта, ЕдиницаИзмерения);
		Возврат Результат;
	КонецЕсли;
	
	ОкончаниеПериода = БюджетированиеКлиентСервер.ДобавитьИнтервал(Период, ПериодичностьОтчета, 1, Кэш) - 1;
	МассивПодстрокРезультата = Новый Массив;
	Для каждого Строка Из ТаблицаЗначенийПоказателя Цикл
		Если ПоПериодам Тогда
			Если Строка.ПериодУстановки < Период Или Строка.ПериодУстановки > ОкончаниеПериода Тогда
				Продолжить;
			КонецЕсли;
			ПредставлениеПериода = Отчеты.ЗначенияНефинансовыхПоказателей.ПредставлениеПериодаПоНомеру(Строка.ПериодУстановки, Строка.НомерПодпериода, ПериодичностьПоказателя);
		ИначеЕсли УстанавливатьЗначениеНаКаждыйПериод Тогда
			ПредставлениеПериода = БюджетированиеКлиентСервер.ПредставлениеПериодаПоДате(Строка.ПериодУстановки, ПериодичностьПоказателя);
		Иначе
			ПредставлениеПериода = БюджетированиеКлиентСервер.ПредставлениеПериодаПоДате(Строка.ПериодУстановки, ПериодичностьОтчета);
		КонецЕсли;
		
		ПредставлениеЗначения = ПредставлениеЗначенияПоказателяПоВиду(
			Строка.Значение, ВидПоказателя, Валюта, ЕдиницаИзмерения, "-");
			
		МассивПодстрокРезультата.Добавить(ПредставлениеПериода + ": " + ПредставлениеЗначения);
		
	КонецЦикла;
	
	Результат = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(
		МассивПодстрокРезультата, 
		"
		|");
	
	Возврат Результат;
	
КонецФункции

Функция ПредставлениеЗначенияПоказателяПоВиду(Значение, ВидПоказателя, Валюта, ЕдиницаИзмерения, ПредставлениеНезаполненного = "")
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат ПредставлениеНезаполненного;
	КонецЕсли;
	
	Если ВидПоказателя = Перечисления.ВидыНефинансовыхПоказателей.Денежный Тогда
		Возврат Формат(Строка(Значение), "ЧЦ=15; ЧДЦ=2") + " " + Строка(Валюта);
	ИначеЕсли ВидПоказателя = Перечисления.ВидыНефинансовыхПоказателей.Количественный Тогда
		Возврат Формат(Строка(Значение), "ЧЦ=15; ЧДЦ=3") + " " + Строка(ЕдиницаИзмерения);
	Иначе
		Возврат Строка(Значение);
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли