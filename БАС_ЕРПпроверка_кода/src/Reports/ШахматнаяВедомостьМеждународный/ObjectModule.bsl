#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВалютаМУ = МеждународнаяОтчетностьВызовСервера.УчетнаяВалюта();
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаФункциональная", ВалютаМУ.Функциональная);
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаПредставления", ВалютаМУ.Представления);
	
	ДокументРезультат.Очистить();
	
	// Сформируем заголовки ресурсов
	СФормироватьЗаголовкиПолей();
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();
	
	// Настроим поля выбора
	ПоказателиОтчета = Новый Структура;
	ПоказателиОтчета.Вставить("ВалютнаяСумма", БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПоказательВалютнаяСумма"));
	ПоказателиОтчета.Вставить("СуммаПредставления", БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПоказательСуммаПредставления"));
	ПоказателиОтчета.Вставить("Сумма", БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПоказательСумма"));
	МеждународнаяОтчетностьСервер.НастроитьПоляВыбора(ПоказателиОтчета,НастройкиКомпоновкиДанных.Выбор.Элементы);
	
	// Выведем отчет
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровки);
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
	МеждународнаяОтчетностьСервер.УстановитьВалюту(ДокументРезультат, ВалютаМУ.Функциональная, НСтр("ru='В функциональной валюте';uk='У функціональній валюті'"));
	МеждународнаяОтчетностьСервер.УстановитьВалюту(ДокументРезультат, ВалютаМУ.Представления, НСтр("ru='В валюте представления';uk='У валюті подання'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

Процедура СФормироватьЗаголовкиПолей()

	ФункциональнаяВалюта = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаФункциональная");
	ВалютаПредставления = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаПредставления");
	
	МеждународнаяОтчетностьСервер.УстановитьЗаголовкиПолей(СхемаКомпоновкиДанных.НаборыДанных.ОсновнойНаборДанных, ВалютаПредставления, ФункциональнаяВалюта);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
