#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаРолей.Ссылка,
	|	ТаблицаРолей.Синоним
	|ПОМЕСТИТЬ ВТТаблицаРолей
	|ИЗ
	|	&ТаблицаРолей КАК ТаблицаРолей
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаРолей.Ссылка
	|ИЗ
	|	ВТТаблицаРолей КАК ТаблицаРолей
	|ГДЕ
	|	ТаблицаРолей.Ссылка В(&ДействующиеРоли)
	|	И ТаблицаРолей.Синоним ПОДОБНО &СтрокаПоиска";
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И ТаблицаРолей.Синоним ПОДОБНО &СтрокаПоиска", "");
	КонецЕсли;
	
	// Составляем таблицу категорий.
	ТаблицаРолей = Новый ТаблицаЗначений;
	ТаблицаРолей.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ПеречислениеСсылка.РолиФизическихЛиц"));
	ТаблицаРолей.Колонки.Добавить("Синоним", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(256)));
	
	Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.РолиФизическихЛиц.ЗначенияПеречисления Цикл
		НоваяСтрока = ТаблицаРолей.Добавить();
		НоваяСтрока.Ссылка = Перечисления.РолиФизическихЛиц[ЗначениеПеречисления.Имя];
		НоваяСтрока.Синоним = ЗначениеПеречисления.Синоним;
	КонецЦикла;
	
	// Отбор только по действующим категориям с учетом введенной строки.
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаРолей", ТаблицаРолей);
	Запрос.УстановитьПараметр("ДействующиеРоли", ДействующиеРолиФизическихЛиц());
	Запрос.УстановитьПараметр("СтрокаПоиска", Строка(Параметры.СтрокаПоиска) + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДействующиеРолиФизическихЛиц()
	
	РолиФизическихЛиц = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("РаботаВХозрасчетнойОрганизации") Тогда
		РолиФизическихЛиц.Добавить(Перечисления.РолиФизическихЛиц.Акционер);
	КонецЕсли;
	РолиФизическихЛиц.Добавить(Перечисления.РолиФизическихЛиц.БывшийСотрудник);
	РолиФизическихЛиц.Добавить(Перечисления.РолиФизическихЛиц.ПрочийПолучательДоходов);
	РолиФизическихЛиц.Добавить(Перечисления.РолиФизическихЛиц.РаздатчикЗарплаты);
	РолиФизическихЛиц.Добавить(Перечисления.РолиФизическихЛиц.Сотрудник);
	
	Возврат РолиФизическихЛиц;
	
КонецФункции

#КонецОбласти

#КонецЕсли