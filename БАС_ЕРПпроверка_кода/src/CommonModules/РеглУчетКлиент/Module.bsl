
Процедура ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассиваСтрокиТЧ(Форма, ИмяТЧ, ИдентификаторСтроки, ПараметрыФормы = Неопределено) Экспорт
	
	СтрокаТЧ = Форма.Объект[ИмяТЧ].НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", НастройкаОтраженияВРеглУчете(СтрокаТЧ));
		ПараметрыФормы.НастройкаОтраженияВРеглУчете.Организация = Форма.Объект.Организация;
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма",               Форма);
	ДопПараметры.Вставить("ИмяТЧ",               ИмяТЧ);
	ДопПараметры.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
	
	Если ПараметрыФормы.Свойство("ПодбираемыеАналитики") Тогда
		ДопПараметры.Вставить("ПодбираемыеАналитики", ПараметрыФормы.ПодбираемыеАналитики);
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеАктиваПассиваСтрокиТЧЗавершение", ЭтотОбъект, ДопПараметры);
	
	ОткрытьФорму("ОбщаяФорма.НастройкаОтраженияВРеглУчетеПрочегоАктиваПассива", 
		ПараметрыФормы, Форма, , , , ОписаниеОповещения); 

КонецПроцедуры

Процедура ОткрытьНастройкуОтраженияВРеглУчетеАктиваПассива(Форма, ОписаниеОповещения = Неопределено, ПараметрыФормы = Неопределено) Экспорт
	
	Если ПараметрыФормы = Неопределено Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("НастройкаОтраженияВРеглУчете", НастройкаОтраженияВРеглУчете(Форма.Объект));
	КонецЕсли;
	
	Если ОписаниеОповещения = Неопределено Тогда 
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("Форма", Форма);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаОтраженияВРеглУчетеАктиваПассиваЗавершение", ЭтотОбъект, ДопПараметры);
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.НастройкаОтраженияВРеглУчетеПрочегоАктиваПассива", 
		ПараметрыФормы, Форма, , , , ОписаниеОповещения); 
	
КонецПроцедуры

Процедура НастройкаОтраженияВРеглУчетеАктиваПассиваСтрокиТЧЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Объект = ДопПараметры.Форма.Объект;
	ИмяТЧ = ДопПараметры.ИмяТЧ;
	ИдентификаторСтроки = ДопПараметры.ИдентификаторСтроки;
	
	Если ДопПараметры.Свойство("ПодбираемыеАналитики") Тогда
		ПодбираемыеАналитики = ДопПараметры.ПодбираемыеАналитики;
	Иначе
		ПодбираемыеАналитики = Неопределено;
	КонецЕсли;

	Строка = Объект[ИмяТЧ].НайтиПоИдентификатору(ИдентификаторСтроки);
	ЗаполнитьЗначенияСвойств(Строка, Результат);
	
	НастройкаОтраженияВРеглУчете = НастройкаОтраженияВРеглУчете(Строка);
	Строка.ПредставлениеОтраженияВРеглУчете = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(НастройкаОтраженияВРеглУчете, ПодбираемыеАналитики);
	
	ДопПараметры.Форма.ОбновитьОтображениеДанных();
	
КонецПроцедуры

Процедура НастройкаОтраженияВРеглУчетеАктиваПассиваЗавершение(Результат, ДопПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма = ДопПараметры.Форма;
	
	ЗаполнитьЗначенияСвойств(Форма.Объект, Результат);
	
	НастройкаОтраженияВРеглУчете = НастройкаОтраженияВРеглУчете(Форма.Объект);
	ПредставлениеОтраженияВРеглУчете = РеглУчетВызовСервера.ПредставлениеОтраженияВРеглУчете(НастройкаОтраженияВРеглУчете);
	
	Форма.Элементы.НастроитьОтражениеВРеглУчете.Заголовок = ПредставлениеОтраженияВРеглУчете;
	
КонецПроцедуры

Функция НастройкаОтраженияВРеглУчете(Объект)
	
	Результат = Новый Структура;
	Результат.Вставить("СчетУчета");
	Результат.Вставить("Субконто1");
	Результат.Вставить("Субконто2");
	Результат.Вставить("Субконто3");
	Результат.Вставить("АналитикаАктивовПассивов");
	Результат.Вставить("Организация");
	
	ЗаполнитьЗначенияСвойств(Результат, Объект);
	
	Возврат Результат;
	
КонецФункции