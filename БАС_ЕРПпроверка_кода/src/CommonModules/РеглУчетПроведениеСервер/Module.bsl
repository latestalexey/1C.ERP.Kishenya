
#Область ПрограммныйИнтерфейс

// Метод для вызова из регламентного задания отражения в регл. учете
Процедура ОтразитьВсеРегламент() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания();
	
	ОтразитьВсе(КонецДня(ТекущаяДата()));
	
КонецПроцедуры

// Формирует движения по регистру Хозрасчетный для всех документов, помеченных к отражению в регламентированном учете.
//
Процедура ОтразитьВсе(ПериодРасчета, Организация = Неопределено, КоличествоОтраженоВУчете = 0, КоличествоНеУказаныСчетаУчета = 0, КоличествоОшибкиПриОтражении = 0) Экспорт
	
	ВременныеТаблицы = ВременныеТаблицы();
	
	ФиксироватьДлительность = ОценкаПроизводительностиВызовСервераПовтИсп.ВыполнятьЗамерыПроизводительности();
	
	// Отразим документы пакетно
	ТипыДокументов = ТипыДокументовКПакетномуОтражению(ПериодРасчета, Организация);
	Для каждого ТипДокумента Из ТипыДокументов Цикл
		МассивНеОтраженоВУчете = Новый Массив;
		Пока СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, Организация, ПериодРасчета, , МассивНеОтраженоВУчете) Цикл
			Результат = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ФиксироватьДлительность, Ложь);
			КоличествоОтраженоВУчете = КоличествоОтраженоВУчете + Результат.ОтраженоВУчете.Количество(); 
			КоличествоНеУказаныСчетаУчета = КоличествоНеУказаныСчетаУчета + Результат.НеУказаныСчетаУчета.Количество(); 
			КоличествоОшибкиПриОтражении = КоличествоОшибкиПриОтражении + Результат.ОшибкиПриОтражении.Количество(); 
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНеОтраженоВУчете, Результат.НеУказаныСчетаУчета);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНеОтраженоВУчете, Результат.ОшибкиПриОтражении);
		КонецЦикла;
	КонецЦикла;
	
	// Отразим документы последовательно
	Выборка = ДокументыКПоследовательномуОтражению(ПериодРасчета, Организация);
	Пока Выборка.Следующий() Цикл
		ТипДокумента = ТипЗнч(Выборка.Ссылка);
		Если СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, Организация, ПериодРасчета, Выборка.Ссылка) Тогда; 
			Результат = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ФиксироватьДлительность, Ложь);
			КоличествоОтраженоВУчете = КоличествоОтраженоВУчете + Результат.ОтраженоВУчете.Количество(); 
			КоличествоНеУказаныСчетаУчета = КоличествоНеУказаныСчетаУчета + Результат.НеУказаныСчетаУчета.Количество(); 
			КоличествоОшибкиПриОтражении = КоличествоОшибкиПриОтражении + Результат.ОшибкиПриОтражении.Количество(); 
		КонецЕсли;
	КонецЦикла;
	
	ВременныеТаблицы.Закрыть();
	
	Документы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ПерепровестиПоРегистрамСведенийОСНМА(ПериодРасчета);
	
КонецПроцедуры

// Формирует движения по регистру Хозрасчетный для указанных документов
//
// Параметры:
//    МассивДокументов - Массив - Документы
//
// Возвращаемое значение:
//    Массив - Документы, которые не удалось отразить в регл. учете
//
Функция ОтразитьДокументыВРеглУчете(МассивДокументов, ВыполнитьПересчеты = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НеотраженныеДокументы = Новый Массив;
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(МассивДокументов);
	КонецЕсли;
	
	ВременныеТаблицы = ВременныеТаблицы();
	
	// Отразим документы пакетно
	ТипыДокументов = ТипыДокументовКПакетномуОтражению(, , МассивДокументов);
	Для каждого ТипДокумента Из ТипыДокументов Цикл
		Пока СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, , , МассивДокументов, НеотраженныеДокументы) Цикл
			Результат = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НеотраженныеДокументы, Результат.НеУказаныСчетаУчета);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НеотраженныеДокументы, Результат.ОшибкиПриОтражении);
		КонецЦикла;
	КонецЦикла;
	
	// Отразим документы последовательно
	Выборка = ДокументыКПоследовательномуОтражению(, , МассивДокументов);
	Пока Выборка.Следующий() Цикл
		ТипДокумента = ТипЗнч(Выборка.Ссылка);
		Если СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, , , Выборка.Ссылка) Тогда
			Результат = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НеотраженныеДокументы, Результат.НеУказаныСчетаУчета);
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НеотраженныеДокументы, Результат.ОшибкиПриОтражении);
		КонецЕсли;
	КонецЦикла;
	
	ВременныеТаблицы.Закрыть();
	
	Возврат НеотраженныеДокументы;
	
КонецФункции

// Формирует движения по регистру Хозрасчетный для указанных документов
//
// Параметры:
//    МассивДокументов - Массив - Документы
//
// Возвращаемое значение:
//    Массив - Документы, которые не удалось отразить в регл. учете
//
Функция ОтразитьДокументыВРеглУчетеПриПечати(МассивДокументов) Экспорт
	
	Возврат ОтразитьДокументыВРеглУчете(МассивДокументов, Истина);
	
КонецФункции

// Формирует движения по регистру Хозрасчетный для указанного документа.
//
// Параметры:
// 		РеквизитыДокумента - структура {Ссылка, Дата, Организация}
// 		ВыполнитьПересчеты - Булево - Признак необходимости выполнить пересчеты
// 		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Поставляемый внешний менеджер временных таблиц
//
// Возвращаемо значение:
//    Статус - ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - Статус отражения документа в учете
//
Функция ОтразитьДокумент(РеквизитыДокумента, ВыполнитьПересчеты = Ложь, МенеджерВременныхТаблиц = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если РеквизитыДокумента.Организация = Справочники.Организации.УправленческаяОрганизация Тогда
		Возврат Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете;
	КонецЕсли;
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(РеквизитыДокумента.Ссылка);
	КонецЕсли;
	
	ВременныеТаблицы = ВременныеТаблицы(МенеджерВременныхТаблиц);
	ТипДокумента = ТипЗнч(РеквизитыДокумента.Ссылка); 
	
	Если НЕ СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, , , РеквизитыДокумента.Ссылка) Тогда
		Возврат Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете;
	КонецЕсли;
	
	РезультатОтражения = ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы);
	ВременныеТаблицы.Закрыть();
	
	Если РезультатОтражения.ОтраженоВУчете.Количество() = 1 Тогда
		Статус = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете;
	Иначе
		Статус = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета;
	КонецЕсли;
	
	Возврат Статус;
	
КонецФункции

// Записывает движения по регистру ОтражениеДокументаВРеглУчете, выполняет очистку неактуальных записей в Хозрасчетный.
//
Процедура ЗарегистрироватьКОтражению(Объект, ДополнительныеСвойства, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
	 	Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("НеРегистрироватьКОтражениюВРеглУчете") 
		 И ДополнительныеСвойства.НеРегистрироватьКОтражениюВРеглУчете Тогда
		Возврат;
	КонецЕсли;
	
	ДвиженияХозрасчетный = Движения.Хозрасчетный;
	ДвиженияХозрасчетный.ДополнительныеСвойства.Вставить("НеВыполнятьДопОбработкуПроводок", Истина);
	ДвиженияХозрасчетный.Записывать = Истина;
	
	ДвиженияОтражениеДокументовВРеглУчете = Движения.ОтражениеДокументовВРеглУчете;
	ДвиженияОтражениеДокументовВРеглУчете.Записывать = Истина;
	
	ТаблицаДанные = Новый ТаблицаЗначений;
	ТаблицаДанные.Колонки.Добавить("Период",         Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("Организация",    Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанные.Колонки.Добавить("ДатаОтражения",  Новый ОписаниеТипов("Дата"));
	ТаблицаДанные.Колонки.Добавить("Статус",         Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете"));
	
	ТаблицаОтражениеДокументовВРеглУчете = Неопределено;
	ПереданаТаблицаОтражениеДокументовВРеглУчете = 
			ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОтражениеДокументовВРеглУчете", ТаблицаОтражениеДокументовВРеглУчете)
			ИЛИ ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ОтражениеДокументовВРеглУчете", ТаблицаОтражениеДокументовВРеглУчете);
	
	Если ПереданаТаблицаОтражениеДокументовВРеглУчете  Тогда 
		// В модуле менеджера была подготовлена таблица данных для регистрации.
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(
			ТаблицаОтражениеДокументовВРеглУчете, 
			ТаблицаДанные);
	Иначе
		// Документ регистрируется общим порядком:
		// - датой документа
		// - по организации документа.
		НоваяСтрока = ТаблицаДанные.Добавить();
		НоваяСтрока.Период         = Объект.Дата;
		НоваяСтрока.Организация    = Объект.Организация;
		НоваяСтрока.ДатаОтражения  = НачалоДня(Объект.Дата);
	КонецЕсли;
	
	СтрокиКУдалению = Новый Массив;
	Для каждого Строка Из ТаблицаДанные Цикл
		Если Строка.Организация = Справочники.Организации.УправленческаяОрганизация Тогда
			СтрокиКУдалению.Добавить(Строка);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Строка.ДатаОтражения) Тогда
			Строка.ДатаОтражения = НачалоДня(Строка.Период);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Строка.Статус) Тогда
			Строка.Статус = Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Строка Из СтрокиКУдалению Цикл
		ТаблицаДанные.Удалить(Строка);
	Конеццикла;
	
	ВыборочнаяРегистрацияКОтражениюВРеглУчете = 
		ДополнительныеСвойства.Свойство("ВыборочнаяРегистрацияКОтражениюВРеглУчете")
		И ДополнительныеСвойства.ВыборочнаяРегистрацияКОтражениюВРеглУчете;
	ИспользуетсяРучнаяКорректировкаПроводок = ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуПроводокПоРеглУчету");
	
	Если НЕ ИспользуетсяРучнаяКорректировкаПроводок И НЕ ВыборочнаяРегистрацияКОтражениюВРеглУчете Тогда
		ДвиженияОтражениеДокументовВРеглУчете.Загрузить(ТаблицаДанные);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДанных.Период         КАК Период,
	|	ТаблицаДанных.Организация    КАК Организация,
	|	ТаблицаДанных.ДатаОтражения  КАК ДатаОтражения
	|ПОМЕСТИТЬ НовыеДанные
	|ИЗ
	|	&ТаблицаДанные КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаВыборочнойРегистрации.Организация    КАК Организация,
	|	ТаблицаВыборочнойРегистрации.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ДанныеКОтражению
	|ИЗ
	|	&ТаблицаВыборочнойРегистрации КАК ТаблицаВыборочнойРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументов.Организация    КАК Организация,
	|	ОтражениеДокументов.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументов.Статус         КАК Статус,
	|	ОтражениеДокументов.Комментарий    КАК Комментарий
	|ПОМЕСТИТЬ ТекущиеСтатусы
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументов
	|ГДЕ
	|	ОтражениеДокументов.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДанные.Период КАК Период,
	|	НовыеДанные.Организация КАК Организация,
	|	НовыеДанные.ДатаОтражения КАК ДатаОтражения,
	|	ВЫБОР
	|		КОГДА ДанныеКОтражению.ДатаОтражения ЕСТЬ NULL И НЕ ТекущиеСтатусы.Статус ЕСТЬ NULL
	|			ТОГДА ТекущиеСтатусы.Статус
	|		КОГДА ЕСТЬNULL(ТекущиеСтатусы.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА &КОтражениюВУчетеВручную
	|		ИНАЧЕ &КОтражениюВРеглУчете
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ДанныеКОтражению.ДатаОтражения ЕСТЬ NULL
	|			ТОГДА ТекущиеСтатусы.Комментарий
	|		КОГДА ЕСТЬNULL(ТекущиеСтатусы.Статус, НЕОПРЕДЕЛЕНО) В (&ОтраженоВУчетеВручную, &КОтражениюВУчетеВручную)
	|			ТОГДА ТекущиеСтатусы.Комментарий
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК Комментарий
	|ПОМЕСТИТЬ НовыеСтатусы
	|ИЗ
	|	НовыеДанные КАК НовыеДанные
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ТекущиеСтатусы КАК ТекущиеСтатусы
	|	ПО 
	|		НовыеДанные.Организация = ТекущиеСтатусы.Организация
	|		И НовыеДанные.ДатаОтражения = ТекущиеСтатусы.ДатаОтражения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ДанныеКОтражению КАК ДанныеКОтражению
	|	ПО 
	|		НовыеДанные.Организация = ДанныеКОтражению.Организация	
	|		И НовыеДанные.ДатаОтражения = ДанныеКОтражению.ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Организация КАК Организация,
	|	Таблица.ДатаОтражения КАК ДатаОтражения
	|ПОМЕСТИТЬ ИзмененияСтатусов
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ДатаОтражения КАК ДатаОтражения,
	|		Таблица.Организация КАК Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВРеглУчете
	|				ТОГДА 1
	|			КОГДА Таблица.Статус = &ОтраженоВРеглУчете
	|				ТОГДА 2
	|		КОНЕЦ КАК Статус
	|	ИЗ
	|		НовыеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВРеглУчете, &ОтраженоВРеглУчете)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ДатаОтражения,
	|		Таблица.Организация,
	|		ВЫБОР
	|			КОГДА Таблица.Статус = &КОтражениюВРеглУчете
	|				ТОГДА -1
	|			КОГДА Таблица.Статус = &ОтраженоВРеглУчете
	|				ТОГДА -2
	|		КОНЕЦ
	|	ИЗ
	|		ТекущиеСтатусы КАК Таблица
	|	ГДЕ
	|		Таблица.Статус В (&КОтражениюВРеглУчете, &ОтраженоВРеглУчете)) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ДатаОтражения,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Статус) <> 0
	|;
	|
	|ВЫБРАТЬ
	|	НовыеСтатусы.Период          КАК Период,
	|	НовыеСтатусы.Организация     КАК Организация,
	|	НовыеСтатусы.ДатаОтражения  КАК ДатаОтражения,
	|	НовыеСтатусы.Статус          КАК Статус,
	|	НовыеСтатусы.Комментарий     КАК Комментарий
	|ИЗ
	|	НовыеСтатусы КАК НовыеСтатусы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Хозрасчетный.Период                                       КАК Период,
	|	Хозрасчетный.Организация                                  КАК Организация,
	|	
	|	Хозрасчетный.ПодразделениеДт                              КАК ПодразделениеДт,
	|	Хозрасчетный.НаправлениеДеятельностиДт                    КАК НаправлениеДеятельностиДт,
	|	Хозрасчетный.НалоговоеНазначениеДт                    	  КАК НалоговоеНазначениеДт,
	|	Хозрасчетный.СчетДт                                       КАК СчетДт,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт1, &ПустойВидСубконто) КАК ВидСубконтоДт1,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоДт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт1,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт2, &ПустойВидСубконто) КАК ВидСубконтоДт2,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоДт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт2,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоДт3, &ПустойВидСубконто) КАК ВидСубконтоДт3,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоДт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоДт3,
	|	Хозрасчетный.ВалютаДт                                     КАК ВалютаДт,
	|	Хозрасчетный.ВалютнаяСуммаДт                              КАК ВалютнаяСуммаДт,
	|	Хозрасчетный.КоличествоДт                                 КАК КоличествоДт,
	|	
	|	Хозрасчетный.ПодразделениеКт                              КАК ПодразделениеКт,
	|	Хозрасчетный.НаправлениеДеятельностиКт                    КАК НаправлениеДеятельностиКт,
	|	Хозрасчетный.НалоговоеНазначениеКт                    	  КАК НалоговоеНазначениеКт,
	|	Хозрасчетный.СчетКт                                       КАК СчетКт,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт1, &ПустойВидСубконто) КАК ВидСубконтоКт1,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоКт1, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт1,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт2, &ПустойВидСубконто) КАК ВидСубконтоКт2,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоКт2, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт2,
	|	ЕСТЬNULL(Хозрасчетный.ВидСубконтоКт3, &ПустойВидСубконто) КАК ВидСубконтоКт3,
	|	ЕСТЬNULL(Хозрасчетный.СубконтоКт3, НЕОПРЕДЕЛЕНО)          КАК СубконтоКт3,
	|	Хозрасчетный.ВалютаКт                                     КАК ВалютаКт,
	|	Хозрасчетный.ВалютнаяСуммаКт                              КАК ВалютнаяСуммаКт,
	|	Хозрасчетный.КоличествоКт                                 КАК КоличествоКт,
	|	
	|	Хозрасчетный.Сумма                                        КАК Сумма,
	|	Хозрасчетный.СуммаНУДт                                    КАК СуммаНУДт,
	|	Хозрасчетный.СуммаНУКт                                    КАК СуммаНУКт,
	|	Хозрасчетный.СуммаПРДт                                    КАК СуммаПРДт,
	|	Хозрасчетный.СуммаПРКт                                    КАК СуммаПРКт,
	|	Хозрасчетный.СуммаВРДт                                    КАК СуммаВРДт,
	|	Хозрасчетный.СуммаВРКт                                    КАК СуммаВРКт,
	|	
	|	Хозрасчетный.Содержание                                   КАК Содержание,
	|	Хозрасчетный.НеКорректироватьСтоимостьАвтоматически       КАК НеКорректироватьСтоимостьАвтоматически
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(,,Регистратор = &Ссылка,,) КАК Хозрасчетный
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ИзмененияСтатусов КАК ИзмененияСтатусов
	|	ПО
	|		Хозрасчетный.Организация = ИзмененияСтатусов.Организация
	|		И НАЧАЛОПЕРИОДА(Хозрасчетный.Период, ДЕНЬ) = ИзмененияСтатусов.ДатаОтражения
	|ГДЕ
	|	ИзмененияСтатусов.Организация ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТаблицаДанные",    ТаблицаДанные);
	Запрос.УстановитьПараметр("Ссылка",             Объект.Ссылка);
	Запрос.УстановитьПараметр("ПустойВидСубконто",  ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ОтраженоВРеглУчете",      Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете);
	Запрос.УстановитьПараметр("КОтражениюВРеглУчете",    Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете);
	Запрос.УстановитьПараметр("ОтраженоВУчетеВручную",   Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную);
	Запрос.УстановитьПараметр("КОтражениюВУчетеВручную", Перечисления.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную);
	
	Если ВыборочнаяРегистрацияКОтражениюВРеглУчете Тогда
		Запрос.УстановитьПараметр("ТаблицаВыборочнойРегистрации",  
			ДополнительныеСвойства.ТаблицаВыборочнойРегистрацииКОтражениюВРеглУчете);
	Иначе
		Запрос.УстановитьПараметр("ТаблицаВыборочнойРегистрации", ТаблицаДанные);
	КонецЕсли;
	
	Результат = Запрос.ВыполнитьПакет();
	Количество = Результат.Количество();
	
	Хозрасчетный  = Результат[Количество - 1].Выгрузить();
	ДвиженияХозрасчетный.Загрузить(Хозрасчетный);
	
	ТаблицаДанные = Результат[Количество - 2].Выгрузить();
	ДвиженияОтражениеДокументовВРеглУчете.Загрузить(ТаблицаДанные);
	
КонецПроцедуры

// Возвращает документы к отражению в регл. учете
//
// Параметры:
// 	ДокументыКОтражению -   ТаблицаЗачений, 
// 							МенеджерВременныхТаблиц - Таблица документов, которые надо вернуть к отражению, 
// 													  или менеджер временых таблиц имеющий таблицу ДокументыКОтражению
// 													  Таблица должна имееть колонки Документ, Организация. 
// 													  Если организация не заполнена, то документ возвращается к отражению во всех организациях, 
// 													  где был зарегистрирован ранее. 
//
Процедура ВернутьДокументыКОтражению(ДокументыКОтражению) Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	Если ТипЗнч(ДокументыКОтражению) = Тип("ТаблицаЗначений") Тогда
		Если ДокументыКОтражению.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументыКОтражению.Документ КАК Документ,
		|	ДокументыКОтражению.Организация КАК Организация,
		|	ДокументыКОтражению.ДатаОтражения КАК ДатаОтражения
		|ПОМЕСТИТЬ ДокументыКОтражению
		|ИЗ
		|	&ДокументыКОтражению КАК ДокументыКОтражению
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Документ,
		|	Организация,
		|	ДатаОтражения
		|;
		|/////////////////////////////////////////////////
		|";
		Запрос.УстановитьПараметр("ДокументыКОтражению", ДокументыКОтражению);
	Иначе
		Запрос.МенеджерВременныхТаблиц = ДокументыКОтражению;
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыКОтражению.Документ КАК Документ
	|ПОМЕСТИТЬ ТолькоДокументыКОтражению
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|    ОтражениеДокументовВРеглУчете.Период КАК Период,
	|    ОтражениеДокументовВРеглУчете.Регистратор КАК Регистратор,
	|    ОтражениеДокументовВРеглУчете.Организация КАК Организация,
	|    ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|    ОтражениеДокументовВРеглУчете.Статус,
	|    ОтражениеДокументовВРеглУчете.Комментарий
	|ПОМЕСТИТЬ ТаблицаРегистра
	|ИЗ
	|    РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|        ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТолькоДокументыКОтражению КАК ДокументыКОтражению
	|        ПО (ДокументыКОтражению.Документ = ОтражениеДокументовВРеглУчете.Регистратор)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|    Регистратор,
	|    Организация,
	|    ДатаОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтражениеДокументовВРеглУчете.Период         КАК Период,
	|	ОтражениеДокументовВРеглУчете.Регистратор    КАК Регистратор,
	|	ОтражениеДокументовВРеглУчете.Организация    КАК Организация,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения  КАК ДатаОтражения,
	|	ВЫБОР 
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL ТОГДА
	|			ВЫБОР КОГДА ОтражениеДокументовВРеглУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
	|				ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете)
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВРеглУчете.Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА НЕ ДокументыКОтражению.Организация ЕСТЬ NULL ТОГДА
	|			ВЫБОР КОГДА ОтражениеДокументовВРеглУчете.Статус В (
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную),
	|					ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную))
	|				ТОГДА ОтражениеДокументовВРеглУчете.Комментарий
	|				ИНАЧЕ """"
	|			КОНЕЦ
	|		ИНАЧЕ ОтражениеДокументовВРеглУчете.Комментарий
	|	КОНЕЦ КАК Комментарий
	|ИЗ
	|	ТаблицаРегистра КАК ОтражениеДокументовВРеглУчете
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению КАК ДокументыКОтражению
	|	ПО
	|		ДокументыКОтражению.Документ = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ДокументыКОтражению.Организация = ОтражениеДокументовВРеглУчете.Организация
	|		И ДокументыКОтражению.ДатаОтражения = ОтражениеДокументовВРеглУчете.ДатаОтражения
	|
	|ИТОГИ ПО
	|	Регистратор";
	
	Запрос.Текст = ТекстЗапроса;
	
	ВыборкаПоДокументам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументам.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаПоДокументам.Регистратор);
		Выборка = ВыборкаПоДокументам.Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
		КонецЦикла;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Записывает движения по регистру ПорядокОтраженияПрочихОпераций
Процедура ОтразитьПорядокОтраженияПрочихОпераций(ДополнительныеСвойства, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Документ = ДополнительныеСвойства.ДляПроведения.Ссылка;
	
	ТаблицаПорядокОтраженияПрочихОпераций = Неопределено;
	Если Не ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПорядокОтраженияПрочихОпераций", ТаблицаПорядокОтраженияПрочихОпераций)
		И Не ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ПорядокОтраженияПрочихОпераций", ТаблицаПорядокОтраженияПрочихОпераций) Тогда
		Возврат;
	КонецЕсли;
	
	// Если параметры отражения заданы в документе (списание безналичных ДС),
	// то ТаблицаПорядокОтраженияПрочихОпераций содержит готовые данные для отражения.
	Если ДополнительныеСвойства.Свойство("ПараметрыОтраженияВРеглУчете") Тогда
		
		ПараметрыОтраженияВРеглУчете = ТаблицаПорядокОтраженияПрочихОпераций;
		
	Иначе
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Операция.Документ,
		|	Операция.ИдентификаторСтроки,
		|	Операция.Организация,
		|	Операция.Дата
		|ПОМЕСТИТЬ
		|	Операция
		|ИЗ &Таблица КАК Операция;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Операция.Документ,
		|	Операция.ИдентификаторСтроки,
		|	Операция.Организация,
		|   Операция.Дата,
		|	Счета.СчетУчета КАК СчетУчета,
		|	Счета.Субконто1 КАК Субконто1,
		|	Счета.Субконто2 КАК Субконто2,
		|	Счета.Субконто3 КАК Субконто3
		|ИЗ
		|	Операция
		|	ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ПорядокОтраженияПрочихОпераций КАК Счета
		|		ПО Счета.Документ = Операция.Документ И Счета.ИдентификаторСтроки = Операция.ИдентификаторСтроки
		|ГДЕ
		|	НЕ Операция.Документ ЕСТЬ NULL
		|");
		Запрос.УстановитьПараметр("Таблица", ТаблицаПорядокОтраженияПрочихОпераций);
		Запрос.УстановитьПараметр("Документ", Документ);
		
		ПараметрыОтраженияВРеглУчете = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.ПорядокОтраженияПрочихОпераций.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Документ);
	НаборЗаписей.Загрузить(ПараметрыОтраженияВРеглУчете);
	НаборЗаписей.Записывать = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Проверяет переданные документы на факт отражения в регл. учете при наличии прав на формирование проводок
//
// Параметры:
//    МассивДокументов - Массив - Документы
//
// Возвращаемое значение:
//    Массив - Документы, которые не отражены в регл. учете
//
Функция ПроверитьПраваДоступаОтражениеДокументовВРеглУчете(МассивДокументов) Экспорт
	
	Если ПравоДоступа("Редактирование", Метаданные.РегистрыБухгалтерии.Хозрасчетный) Тогда
		Возврат НеотраженныеВРеглУчетеДокументы(МассивДокументов);
	Иначе
		Возврат Новый Массив;
	КонецЕСли;
	
КонецФункции

// Возвращает результат формирования движений по регистру Хозрасчетный для указанного документа
//
// Параметры:
//		РеквизитыДокумента - структура {Ссылка, Дата, Организация}
//		ВыполнитьПересчеты - булево, флажок о том, необходимо ли перед формированием проводок выполнять оффлайновые пересчеты по документу
// Возвращаемо значение:
//		Структура - структура {ТаблицаПроводок, КомментарийОшибки}
//			ТаблицаПроводок - таблица значений - сформированные движения по регистру Хозрасчетный
//			КомментарийОшибки - строка - в случае, если документ при формировании проводок не прошел проверки (не настроены счета учета) возвращает информацию со списком проблем
//
Функция ВернутьРезультатОтраженияДокумента(РеквизитыДокумента, ВыполнитьПересчеты = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВыполнитьПересчеты Тогда
		ВыполнитьОффлайновыеРасчеты(РеквизитыДокумента.Ссылка);
	КонецЕсли;
	
	ВременныеТаблицы = ВременныеТаблицы();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.Регистратор                  КАК Ссылка,
	|	Данные.Период                       КАК Период,
	|	Данные.Организация                  КАК Организация,
	|	Данные.ДатаОтражения               КАК ДатаОтражения
	|ПОМЕСТИТЬ РазрезыКОтражению
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|ГДЕ
	|	Данные.Регистратор = &Ссылка
	|;
	|
	|//////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РазрезыКОтражению.Ссылка КАК Ссылка,
	|	РазрезыКОтражению.Период КАК Период
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	РазрезыКОтражению КАК РазрезыКОтражению
	|";
	Запрос.УстановитьПараметр("Ссылка",            РеквизитыДокумента.Ссылка);
	Запрос.Выполнить();
	
	ТипДокумента = ТипЗнч(РеквизитыДокумента.Ссылка);
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
	ИмяДокумента = МетаданныеДокумента.Имя;
	
	Результат = ВыборкиОтраженияДокументов(ИмяДокумента, ВременныеТаблицы);
	
	СтруктураВозврата = Новый Структура("ТаблицаПроводок, КомментарийОшибки, СтатусОтражения");
	
	Если Результат.ВыборкаСтатусов.Следующий() Тогда
		
		Результат.ВыборкаХозрасчетный.Следующий();
		СтруктураВозврата.ТаблицаПроводок = НоваяТаблицаПроводок();
		ДобавитьСтрокиВТаблицуПроводок(СтруктураВозврата.ТаблицаПроводок, Результат.ВыборкаХозрасчетный);
		СтруктураВозврата.КомментарийОшибки = "";
		СтруктураВозврата.СтатусОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете;
		
		Если Результат.ВыборкаПроверки.Следующий() Тогда
			СтруктураВозврата.КомментарийОшибки = КомментарийОшибокЗаполнения(Результат.ВыборкаПроверки);
			СтруктураВозврата.СтатусОтражения = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета;
		КонецЕсли;
		
	КонецЕсли;
	
	ВременныеТаблицы.Закрыть();
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Записывает статус отражения документа в регистре сведений "ОтражениеДокументовВРеглУчете" как ОтраженоВУчетеВручную.
//
//	Параметры:
//		Документ - ДокументСсылка - ссылка на документ, для которого должен быть записан статус ручного отражения. Если ссылка - пустая, записи не происходит.
//		СтатусОтражения - ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - статус, который будет установлен после выполнения процедуры.
//		Комментарий - строка - дополнительные сведения о ручном отражении документа.
//
Процедура ЗарегистрироватьОтражениеДокумента(Документ, СтатусОтражения, Комментарий = Неопределено) Экспорт
	
	Если Документ.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	ПараметрыБлокировки	= Новый Структура("ТипТаблицы, ИмяТаблицы", "РегистрСведений", "ОтражениеДокументовВРеглУчете.НаборЗаписей");
	ЗначенияБлокировки	= Новый Структура("Регистратор", Документ);
	ОбщегоНазначенияБПВызовСервера.УстановитьУправляемуюБлокировку(ПараметрыБлокировки, ЗначенияБлокировки);
	
	ОтражениеДокументовВРегламентированномУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
	ОтражениеДокументовВРегламентированномУчете.Отбор.Регистратор.Установить(Документ);
	ОтражениеДокументовВРегламентированномУчете.Прочитать();
	
	Для каждого Запись из ОтражениеДокументовВРегламентированномУчете Цикл
		
		Запись.Статус = СтатусОтражения;
		
		Если Не Запись.Комментарий = Комментарий Тогда
			Запись.Комментарий = Комментарий;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтражениеДокументовВРегламентированномУчете.Записать();
	
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

// Получает данные об отражении документа в регламентированном учете. Причем получает в определенном порядке. 
//
//	Параметры:
//		Документ - ДокументСсылка - ссылка на документ, для которого необходимо получить данные отражения.
//
//	ВозвращаемоеЗначение:
//		Структура - содержит следующие данные:
//			Статус - ПеречислениеСсылка.СтатусыОтраженияДокументовВРеглУчете - текущий статус отражения документа;
//			Отражен - Булево - признак отражения документа, истина если Статус = ОтраженоВРеглУчете ИЛИ Статус = ОтраженоВУчетеВручную;
//			РучноеОтражение - Булево - признак ручного отражения документа, истина если Статус = ОтраженоВУчетеВручную ИЛИ Статус = КОтражениюВУчетеВручную;
//			Комментарий - строка - дополнительная информация по отражению документа. Заполняется пользователем если отражение ручное или содержит перечень ошибок, если не удалось отразить документ автоматически.
//
Функция ПолучитьДанныеОтраженияДокумента(Документ) Экспорт
	
	СтруктураВозврата = Новый Структура(
		"Статус, Отражен, РучноеОтражение, Комментарий", 
		Перечисления.СтатусыОтраженияДокументовВРеглУчете.ПустаяСсылка(), Ложь, Ложь);
	
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОтражениеДокументовВРеглУчете.Статус,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета)
		|			ТОГДА 1
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
		|			ТОГДА 2
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
		|			ТОГДА 3
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете)
		|			ТОГДА 4
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
		|			ТОГДА 5
		|	КОНЕЦ КАК Приоритет,
		|	ОтражениеДокументовВРеглУчете.Комментарий,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
		|				ИЛИ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Отражен,
		|	ВЫБОР
		|		КОГДА ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
		|				ИЛИ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВУчетеВручную)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РучноеОтражение
		|ИЗ
		|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
		|ГДЕ
		|	ОтражениеДокументовВРеглУчете.Регистратор = &Документ
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		Запрос.УстановитьПараметр("Документ", Документ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, Выборка);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает составной комментарий к неотраженнному в регл. учете документу
//
// Параметры:
// 	Документ - ДокументСсылка - Документ, по которому необходимо получить комментарий
// 	Организация - СправочникСсылка.Организации - Дополнительный отбор, если необходимо получить комментарий отражения по конкретной организации
// Возвращаемое значение:
// 	 Комментарий - Строка - составной комментарий к неотраженному в учете документу
//
Функция СводныйКомментарийПоДокументу(Документ, Организация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Записи.Комментарий КАК Комментарий
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Записи
	|ГДЕ
	|	Записи.Регистратор = &Документ
	|	И (Записи.Организация = &Организация 
	|		ИЛИ &ВсеОрганизации)
	|";
	Запрос.УстановитьПараметр("Статус",         Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета);
	Запрос.УстановитьПараметр("Документ",       Документ);
	Запрос.УстановитьПараметр("Организация",    Организация);
	Запрос.УстановитьПараметр("ВсеОрганизации", НЕ ЗначениеЗаполнено(Организация));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Подстроки = Новый Массив;
	Пока Выборка.Следующий() Цикл
		Если Выборка.Комментарий = "" Тогда
			Продолжить;
		КонецЕсли;
		Если Подстроки.Найти(Выборка.Комментарий) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Подстроки.Добавить(Выборка.Комментарий);
	КонецЦикла;
	
	Результат = СтрСоединить(Подстроки, Символы.ПС);
	
	Возврат Результат;
	
КонецФункции

// Устанавливает параметры выборочной регистрации документа к отражению в регл. учете
// 
// Параметры:
// 	 ДопСвойства - Структура - Дополнительные свойства объекта
// 	 Организация - СправочникСсылка.Организации - Организация для выборочной регистрации к отражению
// 	 Период      - Дата - Дата выборочной регистрации к отражению в регл. учете
//
Процедура ДобавитьПараметрыВыборочнойРегистрацииКОтражениюВРеглУчете(ДопСвойства, Организация, Период) Экспорт
	
	Если Не ДопСвойства.Свойство("ВыборочнаяРегистрацияКОтражениюВРеглУчете") Тогда
		
		ДопСвойства.Вставить("ВыборочнаяРегистрацияКОтражениюВРеглУчете", Истина);
		ТаблицаКРегистрацииВРеглУчете = Новый ТаблицаЗначений;
		ТаблицаКРегистрацииВРеглУчете.Колонки.Добавить("Организация",     Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаКРегистрацииВРеглУчете.Колонки.Добавить("ДатаОтражения",   Новый ОписаниеТипов("Дата"));
		ДопСвойства.Вставить("ТаблицаВыборочнойРегистрацииКОтражениюВРеглУчете", ТаблицаКРегистрацииВРеглУчете);
		
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация",   Организация);
	Отбор.Вставить("ДатаОтражения", НачалоДня(Период));
	
	РезультатПоиска = ДопСвойства.ТаблицаВыборочнойРегистрацииКОтражениюВРеглУчете.НайтиСтроки(Отбор);
	
	Если РезультатПоиска.Количество() = 0 Тогда
		НоваяСтрока = ДопСвойства.ТаблицаВыборочнойРегистрацииКОтражениюВРеглУчете.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Отбор);
	КонецЕсли;
	
КонецПроцедуры

// Отключает регистрацию документа к отражению в регл. учете 
// 
// Параметры:
// 	 ДопСвойства - Структура - Дополнительные свойства объекта
//
Процедура НеРегистрироватьКОтражениюВРеглУчете(ДопСвойства) Экспорт
	
	ДопСвойства.Вставить("НеРегистрироватьКОтражениюВРеглУчете", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Восстановление партий, расчетов по документам, сумм в валюте регл., расчет себестоимости
Процедура ВыполнитьОффлайновыеРасчеты(МассивСсылок)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(Данные.ДатаОтражения) КАК Период
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|ГДЕ
	|	Данные.Регистратор В (&МассивСсылок)
	|;
	|
	|//////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|ГДЕ
	|	Расчеты.Регистратор В (&МассивСсылок)
	|	И Расчеты.Активность
	|;
	|
	|//////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|ГДЕ
	|	Расчеты.Регистратор В (&МассивСсылок)
	|	И Расчеты.Активность
	|";
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Результат = Запрос.ВыполнитьПакет();
	
	ВыборкаПериодРасчета = Результат[0].Выбрать();
	Если ВыборкаПериодРасчета.Следующий() Тогда
		ПериодРасчета = КонецМесяца(ВыборкаПериодРасчета.Период) + 1;
	Иначе
		// Переданные документы не требуют отражения, расчеты не выполняем
		Возврат;
	КонецЕсли;
	
	
	МассивАналитикПоставщиков = Результат[1].Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
	Если МассивАналитикПоставщиков.Количество() > 0 Тогда
		РаспределениеВзаиморасчетов.РаспределитьВсеРасчетыСПоставщиками(ПериодРасчета, МассивАналитикПоставщиков);
	КонецЕсли;
	
	МассивАналитикКлиентов = Результат[2].Выгрузить().ВыгрузитьКолонку("АналитикаУчетаПоПартнерам");
	Если МассивАналитикКлиентов.Количество() > 0 Тогда
		РаспределениеВзаиморасчетов.РаспределитьВсеРасчетыСКлиентами(ПериодРасчета, МассивАналитикКлиентов);
	КонецЕсли;
	
КонецПроцедуры

Функция ВыполнитьОтражение(ТипДокумента, ВременныеТаблицы, ФиксироватьДлительность = Ложь, ИсключениеПриОшибке = Истина)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипДокумента);
	ИмяДокумента = МетаданныеДокумента.Имя;
	
	ТекущаяОперация = "ОтражениеВРег_ФормированиеДвижений";
	ВремяНачалаОбщее = НачатьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация);
	
	Результат = ВыборкиОтраженияДокументов(ИмяДокумента, ВременныеТаблицы, ФиксироватьДлительность);
	
	ВыборкаСтатусов = Результат.ВыборкаСтатусов;
	
	//++ НЕ УТКА
	ВыборкаСтатусовМФУ = Результат.ВыборкаСтатусовМФУ;
	ВыборкаСтатусовМФУ.Следующий();
	//-- НЕ УТКА
	
	ВыборкаПроверки = Результат.ВыборкаПроверки;
	ВыборкаПроверки.Следующий();
	
	ВыборкаХозрасчетный = Результат.ВыборкаХозрасчетный;
	ВыборкаХозрасчетный.Следующий();
	
	ВыборкаХозрасчетныйДополнение = Результат.ВыборкаХозрасчетныйДополнение;
	ВыборкаХозрасчетныйДополнение.Следующий();
	
	ВыборкаСвязанныеОперацииБух = Результат.ВыборкаСвязанныеОперацииБух;
	ВыборкаСвязанныеОперацииБух.Следующий();
	
	ОтраженоВУчете       = Новый Массив();
	НеУказаныСчетаУчета  = Новый Массив();
	ОшибкиПриОтражении   = Новый Массив();
	
	СтатусОтраженоВРеглУчете = Перечисления.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете; 
	СтатусНеУказаныСчетаУчета = Перечисления.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета;
	
	ТекущаяОперация = "ОтражениеВРегл_" + ИмяДокумента + "_" + "ФормированиеДвижений";
	ВремяНачала = НачатьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация);
	КоличествоДокументов = ВыборкаСтатусов.Количество();
	
	Пока ВыборкаСтатусов.Следующий() Цикл
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВРеглУчете.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаСтатусов.Ссылка);
			//++ НЕ УТКА
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВМеждународномУчете.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаСтатусов.Ссылка);
			//-- НЕ УТКА
			
			НачатьТранзакцию();
			Блокировка.Заблокировать();
			
			ОшибкиОтражения = ОшибкиОтражения(ВыборкаСтатусов.Ссылка, ВыборкаПроверки);
			
			СформироватьОтражениеДокументовВРеглУчете(ВыборкаСтатусов, ОшибкиОтражения);
			//++ НЕ УТКА
			СформироватьОтражениеДокументовВМФУ(ВыборкаСтатусов, ВыборкаСтатусовМФУ, ОшибкиОтражения);
			//-- НЕ УТКА
			СформироватьХозрасчетный(ВыборкаСтатусов, ВыборкаХозрасчетный, ВыборкаХозрасчетныйДополнение);
			
			ЗарегистрироватьКОтражениюСвязанныеОперацииБух(ВыборкаСтатусов, ВыборкаСвязанныеОперацииБух);
			
			ЗафиксироватьТранзакцию();
			
			Если ОшибкиОтражения.Количество() > 0 Тогда
				НеУказаныСчетаУчета.Добавить(ВыборкаСтатусов.Ссылка);
			Иначе
				ОтраженоВУчете.Добавить(ВыборкаСтатусов.Ссылка);
			КонецЕсли;
			
		Исключение
			
			ОшибкиПриОтражении.Добавить(ВыборкаСтатусов.Ссылка);
			
			ОтменитьТранзакцию();
			ТекстСообщения = СтрШаблон(
					НСтр("ru='Не удалось отразить в регл. учете документ ""%1"" по причине: %2';uk='Не вдалося відобразити в регл. обліку документ ""%1"" з причини: %2'"),
					ВыборкаСтатусов.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru='Отражение в регламентированном учете.';uk='Відображення в регламентованому обліку.'"), 
				УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
				
			Если ИсключениеПриОшибке Тогда
				ВызватьИсключение ТекстСообщения;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация, ВремяНачала, КоличествоДокументов);
	
	ТекущаяОперация = "ОтражениеВРегл_" + ИмяДокумента + "_" + "ДополнительнаяОбработка";
	ВремяНачала = НачатьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация);
	ДополнительнаяОбработкаПриОтраженииДокумента(ТипДокумента, ВременныеТаблицы);
	ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация, ВремяНачала, КоличествоДокументов);
	
	ТекущаяОперация = "ОтражениеВРегл_" + ИмяДокумента + "_" + "ВыполнениеЗапросаОчистки";
	ЗапросОчистки = РеглУчетВыборкиСерверПовтИсп.ЗапросОчистки(ИмяДокумента);
	ЗапросОчистки.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросОчистки.Выполнить();
	ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация, ВремяНачала, КоличествоДокументов);
	
	Операция = "ОтражениеВРегл_" + ИмяДокумента + "_" + "ОбщееВремя";
	ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, Операция, ВремяНачалаОбщее, КоличествоДокументов);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Структура();
	Результат.Вставить("ОтраженоВУчете",      ОтраженоВУчете);
	Результат.Вставить("НеУказаныСчетаУчета", НеУказаныСчетаУчета);
	Результат.Вставить("ОшибкиПриОтражении",  ОшибкиПриОтражении);
	
	Возврат Результат;
	
КонецФункции

Функция ВыборкиОтраженияДокументов(ИмяДокумента, ВременныеТаблицы, ФиксироватьДлительность = Ложь)
	
	ТекущаяОперация = "ОтражениеВРегл_" + ИмяДокумента + "_" + "ПолучениеДанныхДокумента";
	ВремяНачала = НачатьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация);
	
	// Выборка статусов отражения документов из регистра ОтражениеДокументовВРеглУчете
	ЗапросОтражениеДокументовВРеглУчете = РеглУчетВыборкиСерверПовтИсп.ЗапросОтражениеДокументовВРеглУчете();
	ЗапросОтражениеДокументовВРеглУчете.МенеджерВременныхТаблиц = ВременныеТаблицы;
	
	РезультатОтражениеДокументовВРеглУчете = ЗапросОтражениеДокументовВРеглУчете.Выполнить();
	ВыборкаСтатусов = РезультатОтражениеДокументовВРеглУчете.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоличествоДокументов = ВыборкаСтатусов.Количество();
	
	//++ НЕ УТКА
	// Выборка статусов отражения документов из регистра ОтражениеДокументовВМеждународномУчете
	ЗапросОтражениеДокументовВМФУ = РеглУчетВыборкиСерверПовтИсп.ЗапросОтражениеДокументовВМеждународномУчете();
	ЗапросОтражениеДокументовВМФУ.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросОтражениеДокументовВМФУ.УстановитьПараметр("ПроводкиМеждународногоУчетаПоДаннымРегл", ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымРегламентированного"));
	
	РезультатОтражениеДокументовВМФУ = ЗапросОтражениеДокументовВМФУ.Выполнить();
	ВыборкаСтатусовМФУ = РезультатОтражениеДокументовВМФУ.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	//-- НЕ УТКА
	
	// Выборка контекстных данных (данных из документов)
	ЭтоОбъектРасчетов = РеглУчетВыборкиСерверПовтИсп.ЭтоОбъектРасчетов(ИмяДокумента);
	ЗапросДанных = РеглУчетВыборкиСерверПовтИсп.ЗапросДанных(ИмяДокумента, ЭтоОбъектРасчетов);
	ЗапросДанных.МенеджерВременныхТаблиц = ВременныеТаблицы;
	УстановитьПараметрыЗапросаДанных(ЗапросДанных, Тип("ДокументСсылка." + ИмяДокумента), ИмяДокумента, ВыборкаСтатусов);
	
	ЗапросДанных.Выполнить();
    
	ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация, ВремяНачала, КоличествоДокументов);  
	
	ТекущаяОперация = "ОтражениеВРегл_" + ИмяДокумента + "_" + "ПолучениеИПрименениеНастроек";
	ВремяНачала = НачатьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация);
	
	// Выборка счетов учета прочих операций
	ЗапросПрочихСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросПрочихСчетов();
	ЗапросПрочихСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросПрочихСчетов.Выполнить();
    
	// Выборка создаваемых движений
	ЗапросСопоставлений = РеглУчетВыборкиСерверПовтИсп.ЗапросСопоставлений();
	ЗапросСопоставлений.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросСопоставлений.Выполнить();
    
	// Проверяем полученные и дополненные данные
	ЗапросПроверки = РеглУчетВыборкиСерверПовтИсп.ЗапросПроверки();
	ЗапросПроверки.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатПроверки = ЗапросПроверки.Выполнить();
	ВыборкаПроверки = РезультатПроверки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    
	ЗапросХозрасчетный = РеглУчетВыборкиСерверПовтИсп.ЗапросХозрасчетный();
	ЗапросХозрасчетный.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатХозрасчетный = ЗапросХозрасчетный.Выполнить();
	ВыборкаХозрасчетный = РезультатХозрасчетный.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    
	ЗапросХозрасчетныйДополнение = РеглУчетВыборкиСерверПовтИсп.ЗапросХозрасчетныйДополнение();
	ЗапросХозрасчетныйДополнение.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатХозрасчетныйДополнение = ЗапросХозрасчетныйДополнение.Выполнить();
	ВыборкаЗапросХозрасчетныйДополнение = РезультатХозрасчетныйДополнение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
    
	// Выборка связанных документов ОперацияБух
	ЗапросСвязанныеОперацииБух = РеглУчетВыборкиСерверПовтИсп.ЗапросСвязанныеОперацииБух();
	ЗапросСвязанныеОперацииБух.МенеджерВременныхТаблиц = ВременныеТаблицы;
	РезультатСвязанныеОперацииБух = ЗапросСвязанныеОперацииБух.Выполнить();
	ВыборкаСвязанныеОперацииБух = РезультатСвязанныеОперацииБух.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, ТекущаяОперация, ВремяНачала, КоличествоДокументов); 
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ВыборкаСтатусов",               ВыборкаСтатусов);
	//++ НЕ УТКА
	СтруктураВозврата.Вставить("ВыборкаСтатусовМФУ",            ВыборкаСтатусовМФУ);
	//-- НЕ УТКА
	СтруктураВозврата.Вставить("ВыборкаПроверки",               ВыборкаПроверки);
	СтруктураВозврата.Вставить("ВыборкаХозрасчетный",           ВыборкаХозрасчетный);
	СтруктураВозврата.Вставить("ВыборкаХозрасчетныйДополнение", ВыборкаЗапросХозрасчетныйДополнение);
	СтруктураВозврата.Вставить("ВыборкаСвязанныеОперацииБух",   ВыборкаСвязанныеОперацииБух);
	
	Возврат СтруктураВозврата;
	
КонецФункции

Функция КомментарийОшибокЗаполнения(РезультатПроверки)
	
	СтрокиКомментарий = Новый Массив();
	
	ВсегоОшибок = 0;
	ОШ = РезультатПроверки.Выбрать();
	Пока ОШ.Следующий() Цикл
		КодОшибки = ОШ.КодОшибки;
		Если КодОшибки = NULL Тогда
			// Строка итогов
			Продолжить;
		КонецЕсли;
		ВсегоОшибок = ВсегоОшибок + 1;
		Ошибка = РеглУчетВыборкиСерверПовтИсп.ШаблонКомментарияОшибок(КодОшибки);
		Ошибка = СтрЗаменить(Ошибка, "%ВидСчета%", Строка(ОШ.ВидСчета));
		Ошибка = СтрЗаменить(Ошибка, "%Измерение%", 
			РеглУчетВыборкиСерверПовтИсп.СтрокаИзмерениеНастройки(ОШ.ВидСчета, ОШ.ИдентификаторСтроки, ОШ.Организация));
		Ошибка = СтрЗаменить(Ошибка, "%МестоАналитика%", 
			РеглУчетВыборкиСерверПовтИсп.СтрокаМестоАналитика(ОШ.МестоУчета, ОШ.АналитикаУчета));
		Ошибка = СтрЗаменить(Ошибка, "%Счет%", Строка(ОШ.Счет));
		СтрокиКомментарий.Добавить(Ошибка);
	КонецЦикла;
	
	Комментарий = СтрСоединить(СтрокиКомментарий, Символы.ПС);
	
	Возврат Комментарий;
	
КонецФункции

Функция ВременныеТаблицы(ВнешнийМенеджерВременныхТаблиц = Неопределено)
	
	ВременныеТаблицы = ВнешнийМенеджерВременныхТаблиц;
	
	Если ВременныеТаблицы = Неопределено Тогда
		
		ВременныеТаблицы = Новый МенеджерВременныхТаблиц;
		
		ЗапросПустыхТаблиц = УчетНМА.ЗапросПустаяВременнаяТаблицаАмортизации();
		ЗапросПустыхТаблиц.МенеджерВременныхТаблиц = ВременныеТаблицы;
		ЗапросПустыхТаблиц.Выполнить();
		
	КонецЕсли;
	
	ЗапросПланаСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросПланаСчетов();
	ЗапросПланаСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросПланаСчетов.Выполнить();
	// выборка вариабельных счетов учета
	ЗапросСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросСчетов();
	ЗапросСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросСчетов.Выполнить();
	
	// выборка счетов по умолчанию
	ЗапросСчетов = РеглУчетВыборкиСерверПовтИсп.ЗапросСчетовПоУмолчанию();
	ЗапросСчетов.МенеджерВременныхТаблиц = ВременныеТаблицы;
	ЗапросСчетов.Выполнить();
	
	Возврат ВременныеТаблицы;
КонецФункции

Процедура ДополнительнаяОбработкаПриОтраженииДокумента(ТипДокумента, МенеджерВременныхТаблиц)
	
	
	Если ТипДокумента = Тип("ДокументСсылка.ПринятиеКУчетуОС") Тогда
		Документы.ПринятиеКУчетуОС.ЗаполнитьПервоначальныеСуммыПриОтражении(МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Если ТипДокумента = Тип("ДокументСсылка.МодернизацияОС") Тогда
		Документы.МодернизацияОС.ЗаполнитьПараметрыНачисленияАмортизацииПриОтражении(МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрыЗапросаДанных(ЗапросДанных, ТипДокумента, ИмяДокумента, ВыборкаДокументов)
	
	СтруктураПараметров = Новый Структура;
	
	ТипыДокументов = РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению();
	Если ТипыДокументов.Найти(ТипДокумента) <> Неопределено 
		И ВыборкаДокументов.Количество() = 1 Тогда
		
		ВыборкаДокументов.Следующий();
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Ссылка", ВыборкаДокументов.Ссылка);
		СтруктураПараметров.Вставить("Дата",   ВыборкаДокументов.Период);
		
		ГраницаМесяцНачало = Новый Граница(НачалоМесяца(ВыборкаДокументов.Период), ВидГраницы.Включая);
		ГраницаМесяцОкончание = Новый Граница(КонецМесяца(ВыборкаДокументов.Период), ВидГраницы.Включая);
		СтруктураПараметров.Вставить("ГраницаМесяцНачало",    ГраницаМесяцНачало);
		СтруктураПараметров.Вставить("ГраницаМесяцОкончание", ГраницаМесяцОкончание);
		
		МассивВидовСубконто = Новый Массив;
		МассивВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
		МассивВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
		СтруктураПараметров.Вставить("ВидыСубконтоХозрасчетныеОсновныеСредстваКонтрагенты", МассивВидовСубконто);
		
		// Спозиционируем выборку в начало
		ВыборкаДокументов.Сбросить();
		
	КонецЕсли;
	
	ЭтоОбъектРасчетов = РеглУчетВыборкиСерверПовтИсп.ЭтоОбъектРасчетов(ИмяДокумента); 
	// Устанавливаем параметры по-умолчанию и {&ИмяФункциональнойОпции}
	ПараметрыДанных = РеглУчетВыборкиСерверПовтИсп.ЗапросДанныхПараметры(ИмяДокумента, ЭтоОбъектРасчетов);
	Для Каждого ПараметрДанных Из ПараметрыДанных Цикл
		ЗначениеПараметра = Неопределено;
		Если Не СтруктураПараметров.Свойство(ПараметрДанных.Имя, ЗначениеПараметра) Тогда
			ЗначениеПараметра = ПолучитьФункциональнуюОпцию(ПараметрДанных.Имя);
		КонецЕсли;
		ЗапросДанных.УстановитьПараметр(ПараметрДанных.Имя, ЗначениеПараметра);
	КонецЦикла;
	
КонецПроцедуры

Функция ТипыДокументовКПакетномуОтражению(ПериодОтражения = Неопределено, Организация = Неопределено, МассивСсылок = Неопределено)
	
	ТипыДокументовКОтражению = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	ТИПЗНАЧЕНИЯ(Данные.Регистратор) КАК ТипДокумента
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|ГДЕ
	|	(&ВсеОрганизации 
	|		ИЛИ Данные.Организация = &Организация) 
	|	И (&ВесьПериод 
	|		ИЛИ Данные.ДатаОтражения <= &ПериодОтражения)
	|	И (&ВсеДокументы 
	|		ИЛИ Данные.Регистратор В (&МассивСсылок))
	|	И Данные.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета))
	|";
	
	Запрос.УстановитьПараметр("ПериодОтражения", ПериодОтражения);
	Запрос.УстановитьПараметр("ВесьПериод",      ?(ПериодОтражения = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ВсеОрганизации",  НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("Организация",     Организация);
	Запрос.УстановитьПараметр("МассивСсылок",    МассивСсылок);
	Запрос.УстановитьПараметр("ВсеДокументы",    ?(МассивСсылок = Неопределено, Истина, Ложь));
	
	ТипыДокументов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ТипДокумента");
	
	ИсключаемыеТипы = РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению();
	Результат = ОбщегоНазначенияКлиентСервер.СократитьМассив(ТипыДокументов, ИсключаемыеТипы);
	
	Возврат Результат;
	
КонецФункции

Функция СформироватьТаблицыОтбораДанных(ТипДокумента, ВременныеТаблицы, Организация = Неопределено, ПериодОтражения = Неопределено, МассивСсылок = Неопределено, МассивИсключаемыхСсылок = Неопределено)
	
	Если МассивСсылок = Неопределено Тогда
		МассивСсылок = Новый Массив;
	КонецЕсли;
	
	Если МассивИсключаемыхСсылок = Неопределено Тогда
		МассивИсключаемыхСсылок = Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Данные.Период                       КАК Период,
	|	Данные.Регистратор                  КАК Ссылка,
	|	Данные.Организация                  КАК Организация,
	|	Данные.ДатаОтражения                КАК ДатаОтражения
	|ПОМЕСТИТЬ ДанныеРегистра
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|ГДЕ
	|	(&ВсеОрганизации 
	|		ИЛИ Данные.Организация = &Организация)
	|	И (&ВесьПериод 
	|		ИЛИ Данные.ДатаОтражения <= &ПериодОтражения)
	|	И (&ВсеДокументы 
	|		ИЛИ Данные.Регистратор В (&МассивСсылок))
	|	И НЕ Данные.Регистратор В (&МассивИсключаемыхСсылок)
	|	И ТИПЗНАЧЕНИЯ(Данные.Регистратор) = &ТипДокумента
	|	И Данные.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000 
	|	ДанныеРегистра.Ссылка КАК Ссылка,
	|	ДанныеРегистра.Период КАК Период
	|ПОМЕСТИТЬ ДокументыКОтражению
	|ИЗ
	|	ДанныеРегистра КАК ДанныеРегистра
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистра.Период         КАК Период,
	|	ДанныеРегистра.Ссылка         КАК Ссылка,
	|	ДанныеРегистра.Организация    КАК Организация,
	|	ДанныеРегистра.ДатаОтражения  КАК ДатаОтражения
	|ПОМЕСТИТЬ РазрезыКОтражению
	|ИЗ
	|	ДанныеРегистра КАК  ДанныеРегистра
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ДокументыКОтражению ДокументыКОтражению
	|	ПО
	|		ДанныеРегистра.Ссылка = ДокументыКОтражению.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|///////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДанныеРегистра
	|";
	
	Запрос.УстановитьПараметр("ПериодОтражения",         ПериодОтражения);
	Запрос.УстановитьПараметр("ВесьПериод",              ?(ПериодОтражения = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ВсеОрганизации",          НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("Организация",             Организация);
	Запрос.УстановитьПараметр("ТипДокумента",            ТипДокумента);
	Запрос.УстановитьПараметр("МассивСсылок",            МассивСсылок);
	Запрос.УстановитьПараметр("ВсеДокументы",            ?(НЕ ЗначениеЗаполнено(МассивСсылок), Истина, Ложь));
	Запрос.УстановитьПараметр("МассивИсключаемыхСсылок", МассивИсключаемыхСсылок);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		ЕстьДокументыКОтражению = Выборка.Количество <> 0;
	Иначе
		ЕстьДокументыКОтражению = Ложь;
	КонецЕсли;
	
	ПроверитьБлокировкуВходящихДанных(ТипДокумента, ВременныеТаблицы);
	
	Если НЕ ЕстьДокументыКОтражению Тогда
		// Уничтожим пустую таблицу документов к отражению
		Запрос.Текст = "
			|УНИЧТОЖИТЬ РазрезыКОтражению
			|;
			|/////////////////////////////////////////////////
			|
			|УНИЧТОЖИТЬ ДокументыКОтражению
			|";
		Запрос.Выполнить();
	КонецЕсли;
	
	Возврат ЕстьДокументыКОтражению;
	
КонецФункции

Функция ДокументыКПоследовательномуОтражению(ПериодОтражения, Организация = Неопределено, МассивСсылок = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Данные.Регистратор   КАК Ссылка,
	|	Данные.МоментВремени КАК МоментВремени
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК Данные
	|ГДЕ
	|	(&ВсеОрганизации
	|		ИЛИ Данные.Организация = &Организация)
	|	И (&ВесьПериод 
	|		ИЛИ Данные.Период <= &ПериодОтражения)
	|	И (&ВсеДокументы 
	|		ИЛИ Данные.Регистратор В (&МассивСсылок))
	|	И ТИПЗНАЧЕНИЯ(Данные.Регистратор) В (&ТипыДокументов)
	|	И Данные.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.КОтражениюВРеглУчете),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.НеУказаныСчетаУчета))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Данные.МоментВремени
	|";
	
	Запрос.УстановитьПараметр("ПериодОтражения",   ПериодОтражения);
	Запрос.УстановитьПараметр("ВесьПериод",        ?(ПериодОтражения = Неопределено, Истина, Ложь));
	Запрос.УстановитьПараметр("ВсеОрганизации",    НЕ ЗначениеЗаполнено(Организация));
	Запрос.УстановитьПараметр("Организация",       Организация);
	Запрос.УстановитьПараметр("ТипыДокументов",    РеглУчетВыборкиСерверПовтИсп.ТипыДокументовКПоследовательномуОтражению());
	Запрос.УстановитьПараметр("МассивСсылок",      МассивСсылок);
	Запрос.УстановитьПараметр("ВсеДокументы",     ?(МассивСсылок = Неопределено, Истина, Ложь));
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция НеотраженныеВРеглУчетеДокументы(МассивДокументов)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НеотраженныеДокументы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор КАК Ссылка
	|ИЗ
	|	РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|ГДЕ
	|	ОтражениеДокументовВРеглУчете.Регистратор В (&МассивДокументов)
	|	И НЕ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|	И НЕ ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВУчетеВручную)
	|";
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		НеотраженныеДокументы = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	КонецЕсли;
	
	Возврат НеотраженныеДокументы;
	
КонецФункции

#Область ФормированиеИЗаписьДвижений

Процедура СформироватьОтражениеДокументовВРеглУчете(ВыборкаСтатусов, ОшибкиОтражения)
	
	СтатусыОтраженияВРеглУчете = Перечисления.СтатусыОтраженияДокументовВРеглУчете;
	
	ОтражениеВРеглУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
	ОтражениеВРеглУчете.Отбор.Регистратор.Установить(ВыборкаСтатусов.Ссылка);
	
	ВыборкаСтатусовПоОрганизациям = ВыборкаСтатусов.Выбрать();
	
	Пока ВыборкаСтатусовПоОрганизациям.Следующий() Цикл
		
		ОбновитьСтатус = (ВыборкаСтатусовПоОрганизациям.Статус = Неопределено);
		
		НоваяЗапись = ОтражениеВРеглУчете.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаСтатусовПоОрганизациям);
		
		Если ОбновитьСтатус Тогда
			Отбор = Новый Структура("Организация, ДатаОтражения", НоваяЗапись.Организация, НоваяЗапись.ДатаОтражения);
			Ошибки = ОшибкиОтражения.НайтиСтроки(Отбор);
			Если Ошибки.Количество() > 0 Тогда
				НоваяЗапись.Статус = СтатусыОтраженияВРеглУчете.НеУказаныСчетаУчета;
				НоваяЗапись.Комментарий = Ошибки[0].Комментарий;
			Иначе
				НоваяЗапись.Статус = СтатусыОтраженияВРеглУчете.ОтраженоВРеглУчете;
				НоваяЗапись.Комментарий = "";
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ОтражениеВРеглУчете.Записать();
	
КонецПроцедуры

//++ НЕ УТКА
Процедура СформироватьОтражениеДокументовВМФУ(ВыборкаСтатусов, ВыборкаСтатусовМФУ, ОшибкиОтражения)
	
	Если ВыборкаСтатусов.Ссылка <> ВыборкаСтатусовМФУ.Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	СтатусыОтраженияВМФУ = Перечисления.СтатусыОтраженияВМеждународномУчете;
	
	ОтражениеВМФУ = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
	ОтражениеВМФУ.Отбор.Регистратор.Установить(ВыборкаСтатусовМФУ.Ссылка);
	
	ВыборкаСтатусовМФУПоОрганизациям = ВыборкаСтатусовМФУ.Выбрать();
	Пока ВыборкаСтатусовМФУПоОрганизациям.Следующий() Цикл
		ОбновитьСтатус = ВыборкаСтатусовМФУПоОрганизациям.ОбновитьСтатус;
		НоваяЗапись = ОтражениеВМФУ.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаСтатусовМФУПоОрганизациям);
		Если ОбновитьСтатус Тогда
			Отбор = Новый Структура("Организация, ДатаОтражения", НоваяЗапись.Организация, НачалоДня(НоваяЗапись.Период));
			Ошибки = ОшибкиОтражения.НайтиСтроки(Отбор);
			Если Ошибки.Количество() > 0 Тогда
				НоваяЗапись.Статус = СтатусыОтраженияВМФУ.ОжидаетсяОтражениеВРеглУчете;
			Иначе
				Если ВыборкаСтатусовМФУПоОрганизациям.РучноеОтражение Тогда
					НоваяЗапись.Статус = СтатусыОтраженияВМФУ.КОтражениюВУчетеВручную;
				Иначе
					НоваяЗапись.Статус = СтатусыОтраженияВМФУ.КОтражениюВУчете;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ОтражениеВМФУ.Записать();
	ВыборкаСтатусовМФУ.Следующий();
	
КонецПроцедуры
//-- НЕ УТКА

Процедура СформироватьХозрасчетный(ВыборкаСтатусов, ВыборкаХозрасчетный, ВыборкаХозрасчетныйДополнение)
	
	НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаСтатусов.Ссылка);
	УстановитьДопСвойстваНабораЗаписейХозрасчетный(НаборЗаписей, ВыборкаСтатусов.Ссылка);
	
	ТаблицаПроводок = НоваяТаблицаПроводок(НаборЗаписей);
	Если ВыборкаСтатусов.Ссылка = ВыборкаХозрасчетный.Ссылка Тогда
		ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаХозрасчетный);
		ВыборкаХозрасчетный.Следующий();
	КонецЕсли;
	Если ВыборкаСтатусов.Ссылка = ВыборкаХозрасчетныйДополнение.Ссылка Тогда
		ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаХозрасчетныйДополнение);
		ВыборкаХозрасчетныйДополнение.Следующий();
	КонецЕсли;
	
	ТаблицаПроводок.Сортировать("Период");
	
	НаборЗаписей.Загрузить(ТаблицаПроводок);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ОшибкиОтражения(Документ, ВыборкаПроверки)
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация");
	Результат.Колонки.Добавить("ДатаОтражения");
	Результат.Колонки.Добавить("Комментарий");
	
	Если Документ <> ВыборкаПроверки.Ссылка Тогда
		Возврат Результат;
	КонецЕсли;
	
	ВыборкаПоОрганизации = ВыборкаПроверки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоОрганизации.Следующий() Цикл
		ВыборкаПоМесяцуОтражения = ВыборкаПоОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоМесяцуОтражения.Следующий() Цикл
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Организация    = ВыборкаПоМесяцуОтражения.Организация;
			НоваяСтрока.ДатаОтражения  = ВыборкаПоМесяцуОтражения.ДатаОтражения;
			НоваяСтрока.Комментарий    = КомментарийОшибокЗаполнения(ВыборкаПоМесяцуОтражения);
		КонецЦикла;
	КонецЦикла;
	
	ВыборкаПроверки.Следующий();
	
	Возврат Результат;
	
КонецФункции

Функция НоваяТаблицаПроводок(НаборЗаписей = Неопределено)
	
	Если НаборЗаписей = Неопределено Тогда
		НаборЗаписей = РегистрыБухгалтерии.Хозрасчетный.СоздатьНаборЗаписей();
	КонецЕсли;
	
	ТаблицаПроводок = НаборЗаписей.ВыгрузитьКолонки();
	ТаблицаПроводок.Колонки.Удалить("Активность");
	
	Возврат ТаблицаПроводок;
	
КонецФункции

Процедура ДобавитьСтрокиВТаблицуПроводок(ТаблицаПроводок, ВыборкаПоДокументу)
	
	Выборка = ВыборкаПоДокументу.Выбрать(); 
	
	Пока Выборка.Следующий() Цикл
		НоваяЗапись = ТаблицаПроводок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьДопСвойстваНабораЗаписейХозрасчетный(НаборЗаписей, Ссылка)
	
	ТипСсылки = ТипЗнч(Ссылка);
	
	Если ТипСсылки = Тип("ДокументСсылка.КорректировкаРеализации")
		Или ТипСсылки = Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
		Или ТипСсылки = Тип("ДокументСсылка.АмортизацияНМА")
		Или ТипСсылки = Тип("ДокументСсылка.АмортизацияОС")
		Или ТипСсылки = Тип("ДокументСсылка.ПодготовкаКПередачеНМА")
		Или ТипСсылки = Тип("ДокументСсылка.ПереоценкаНМА")
		Или ТипСсылки = Тип("ДокументСсылка.ПринятиеКУчетуНМА")
		Или ТипСсылки = Тип("ДокументСсылка.СписаниеНМА")
		Или ТипСсылки = Тип("ДокументСсылка.ВозвратОСОтАрендатора")
		Или ТипСсылки = Тип("ДокументСсылка.ВыбытиеАрендованныхОС")
		Или ТипСсылки = Тип("ДокументСсылка.ИзменениеПараметровОС")
		Или ТипСсылки = Тип("ДокументСсылка.МодернизацияОС")
		Или ТипСсылки = Тип("ДокументСсылка.ПеремещениеОС")
		Или ТипСсылки = Тип("ДокументСсылка.ПереоценкаОС")
		Или ТипСсылки = Тип("ДокументСсылка.ПодготовкаКПередачеОС")
		Или ТипСсылки = Тип("ДокументСсылка.ПоступлениеАрендованныхОС")
		Или ТипСсылки = Тип("ДокументСсылка.ПринятиеКУчетуОС")
		Или ТипСсылки = Тип("ДокументСсылка.СписаниеОС")
		Или ТипСсылки = Тип("ДокументСсылка.РемонтОС")
		Или ТипСсылки = Тип("ДокументСсылка.КорректировкаНалоговогоНазначенияОС")		
		 
		Тогда
		
		НаборЗаписей.ДополнительныеСвойства.Вставить("СуммыНалоговогоУчетаЗаполнены", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьКОтражениюСвязанныеОперацииБух(ВыборкаСтатусов, ВыборкаСвязанныеОперацииБух)
	
	Если ВыборкаСтатусов.Ссылка <> ВыборкаСвязанныеОперацииБух.Ссылка Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаДокументы = ВыборкаСвязанныеОперацииБух.Выбрать();
	Пока ВыборкаДокументы.Следующий() Цикл
		
		ОтражениеВРеглУчете = РегистрыСведений.ОтражениеДокументовВРеглУчете.СоздатьНаборЗаписей();
		ОтражениеВРеглУчете.Отбор.Регистратор.Установить(ВыборкаДокументы.Регистратор);
		
		НоваяЗапись = ОтражениеВРеглУчете.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ВыборкаДокументы);
		
		ОтражениеВРеглУчете.Записать();
		
	КонецЦикла;
	
	ВыборкаСвязанныеОперацииБух.Следующий();
	
КонецПроцедуры

#КонецОбласти

#Область ЗамерВремени

Функция НачатьЗамерВремениОперации(ФиксироватьДлительность, Операция = Неопределено) Экспорт
	
	Если НЕ ФиксироватьДлительность Тогда
		Возврат Неопределено
	КонецЕсли;
	
	Возврат ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремениТехнологический(Операция);
	
КонецФункции

Процедура ЗакончитьЗамерВремениОперации(ФиксироватьДлительность, Операция, ВремяНачала, КоличествоДокументов)
	
	Если НЕ ФиксироватьДлительность Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиентСервер.ЗакончитьЗамерВремениТехнологический(Операция, ВремяНачала, Строка(КоличествоДокументов));
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаБлокировкиДанных

Процедура ПроверитьБлокировкуВходящихДанных(ТипДокумента, Отбор = Неопределено, МассивСсылок = Неопределено)
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		Возврат; // обновление ИБ завершено полностью
	КонецЕсли;
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипДокумента);
	ВходящиеДанные = РеглУчетВыборкиСерверПовтИсп.ИспользуемыеТаблицыДляОтраженияТипа(ОбъектМетаданных.Имя);
	
	ИмяВременнойТаблицы = Неопределено;
	Если ТипЗнч(Отбор) = Тип("МенеджерВременныхТаблиц") Тогда
		ИмяВременнойТаблицы = "ДокументыКОтражению";
	КонецЕсли;
	
	ЕстьБлокировкаДанных = ЕстьБлокировкаДанных(ВходящиеДанные, Отбор, ИмяВременнойТаблицы);
	Если ЕстьБлокировкаДанных Тогда
		// Нельзя выполнять отражение документов в регл. учете - не завершено обновление всех входящих данных
		ОписаниеОшибки = СтрШаблон(
			НСтр("ru='Отражение документов ""%1""
                |в регламентированном учете остановлено из-за блокировки входящих данных - не завершено обновление ИБ.'
                |;uk='Відображення документів ""%1""
                |в регламентованому обліку зупинено через блокування вхідних даних - не завершено оновлення ІБ.'"),
			ТипДокумента);
		
		Если ТипЗнч(Отбор) <> Тип("МенеджерВременныхТаблиц") И ТипЗнч(Отбор) <> Тип("Массив") И ЗначениеЗаполнено(Отбор) Тогда
			ОписаниеОшибки = СтрШаблон(
			НСтр("ru='Отражение документа ""%1""
                |в регламентированном учете остановлено из-за блокировки входящих данных - не завершено обновление ИБ.'
                |;uk='Відображення документа ""%1""
                |в регламентованому обліку зупинено через блокування вхідних даних - не завершено оновлення ІБ.'"),
			Отбор);
		КонецЕсли;
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru='Отражение в регламентированном учете';uk='Відображення в регламентованому обліку'"),
			УровеньЖурналаРегистрации.Ошибка,
			ОбъектМетаданных,
			Отбор,
			ОписаниеОшибки);
			
		ВызватьИсключение ОписаниеОшибки;
	КонецЕсли;
		
КонецПроцедуры

// Проверяет, есть ли еще заблокированные обновлением данные.
// В случае если передан массив проверяемых объектов и среди них присутствуют независимые регистры сведений,
// то для таких регистров наличие блокировки проверяется без отбора - если есть ли хоть одна заблокированная запись, 
// то регистр сведений считается заблокированным полностью.
// Проверки независимых регистров сведений с отбором по измерениям возможна только для одного объекта метаданных.
//
// Параметры:
//  ПолныеИменаМетаданныеОбъектов - Строка, ОбъектМетаданных, Массив строк, Массив ОбъектовМетаданых - полное имя обрабатываемого объекта или его метаданные или массив имен или объектов.
//									Например, "Документ.ПриходныйОрдерНаТовары"
//  Отбор - ЛюбаяСсылка, Структура, Неопределено, Массив, МенеджерВременныхТаблиц - отбор данных для проверки.
//									Если передано Неопределено - проверяется по всему типу объекта без отбора,
//									Если объект - регистр, подчиненный регистратору, то в отборе - ссылка на регистратор или массив ссылок
//									Если объект ссылочного типа, то в отборе - или ссылка, или массив ссылок
//									Если объект - независимый регистр сведений, то в отборе - структура со значениями измерений.
//										Ключ структуры - имя измерения, значение - значение отбора (можно передать массив значений)
//	ИмяВременнойТаблицыОтбора - Строка - Имя временной таблицы если отбор задан МенеджеромВременныхТаблиц, если отбор не МенеджерВременныхТаблиц, то игнорируется
//	ИзмеренияРегистраСведений - Строка, Массив - Имена измерений независимого регитра сведений через запятую или в массиве, 
//									по которым необходимо выполнить внутреннее соединение с таблицей отбора переданной в МенеджереВременныхТаблиц
// 
// Возвращаемое значение:
//  Булево
//
Функция ЕстьБлокировкаДанных(ПолныеИменаМетаданныеОбъектов, Отбор = Неопределено, ИмяВременнойТаблицыОтбора = Неопределено, ИзмеренияРегистраСведений = Неопределено)
	
	Если ПолучитьФункциональнуюОпцию("ОтложенноеОбновлениеЗавершеноУспешно") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ТипЗнч(ПолныеИменаМетаданныеОбъектов) = Тип("Массив") Тогда
		МассивПолныхИменМетаданныхОбъектов = ПолныеИменаМетаданныеОбъектов;
	Иначе
		МассивПолныхИменМетаданныхОбъектов = Новый Массив;
		МассивПолныхИменМетаданныхОбъектов.Добавить(ПолныеИменаМетаданныеОбъектов);
	КонецЕсли;
	
	Если ТипЗнч(ИзмеренияРегистраСведений) = Тип("Массив") Тогда
		МассивИзмеренийРегистраСведений = ИзмеренияРегистраСведений;
	ИначеЕсли ТипЗнч(ИзмеренияРегистраСведений) = Тип("Строка") Тогда
		МассивИзмеренийРегистраСведений = СтрРазделить(ИзмеренияРегистраСведений,",",Ложь);
	КонецЕсли;
	
	МассивТекстовЗапросов = Новый Массив;
	ОтборПоСсылкам = ТипЗнч(Отбор) <> Тип("МенеджерВременныхТаблиц") И ЗначениеЗаполнено(Отбор);
	ОтборПоВременнойТаблице = ТипЗнч(Отбор) = Тип("МенеджерВременныхТаблиц");
	Для Каждого ПолноеИмяМетаданныеОбъекта Из МассивПолныхИменМетаданныхОбъектов Цикл
	
		Если ТипЗнч(ПолноеИмяМетаданныеОбъекта) = Тип("Строка") Тогда
			МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданныеОбъекта);
			ПолноеИмяОбъекта = ПолноеИмяМетаданныеОбъекта;
		Иначе
			МетаданныеОбъекта = ПолноеИмяМетаданныеОбъекта;
			ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();
		КонецЕсли;
		
		УсловиеОтбораДанных = "ИСТИНА";
		ТекстВыборкиИзмерений = "ТаблицаИзменений.Ссылка КАК Ссылка";
		Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеОбъекта) Тогда
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТаблицаИзменений.Ссылка КАК Ссылка
			|ИЗ
			|	#ТаблицаИзменения КАК ТаблицаИзменений
			|ГДЕ
			|	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы
			|	И &УсловиеОтбораДанных";
			
			Если ОтборПоСсылкам Тогда
				УсловиеОтбораДанных = "ТаблицаИзменений.Ссылка В (&МассивСсылок)";
			ИначеЕсли ОтборПоВременнойТаблице Тогда
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"	#ТаблицаИзменения КАК ТаблицаИзменений",
								"	#ТаблицаИзменения КАК ТаблицаИзменений
								|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОтбора КАК Отбор
								|	ПО ТаблицаИзменений.Ссылка = Отбор.Ссылка");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОтбора", ИмяВременнойТаблицыОтбора);
				
			КонецЕсли;
				
		ИначеЕсли ОбщегоНазначения.ЭтоРегистрСведений(МетаданныеОбъекта)
			И МетаданныеОбъекта.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.Независимый Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	&ТекстВыборкиИзмерений
			|ИЗ
			|	#ТаблицаИзменения КАК ТаблицаИзменений
			|ГДЕ
			|	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы
			|	И &УсловиеОтбораДанных";
			
			ТекстВыборкиИзмерений = "";
			НовоеУсловиеОтбораДанных = "";
			Для Каждого Измерение из МетаданныеОбъекта.Измерения Цикл
				
				Если МассивПолныхИменМетаданныхОбъектов.Количество() > 1 Тогда
					ТекстВыборкиИзмерений = "ТаблицаИзменений." + Измерение.Имя + " КАК Ссылка";
					Прервать;
					
				Иначе
					ТекстВыборкиИзмерений = ТекстВыборкиИзмерений + "
						|	ТаблицаИзменений." + Измерение.Имя + " КАК " + Измерение.Имя + ",";
					Если ТипЗнч(Отбор) = Тип("Структура") И Отбор.Свойство(Измерение.Имя) Тогда
						НовоеУсловиеОтбораДанных = НовоеУсловиеОтбораДанных + "
							|	(ТаблицаИзменений." + Измерение.Имя + " В (&" + Измерение.Имя + ")
							|		ИЛИ &" + Измерение.Имя + " = НЕОПРЕДЕЛЕНО)
							|	И ";
					КонецЕсли;
				КонецЕсли;
					
			КонецЦикла;// по измерениям регистра
			
			Если МассивПолныхИменМетаданныхОбъектов.Количество() = 1 Тогда
				ТекстВыборкиИзмерений = Лев(ТекстВыборкиИзмерений, СтрДлина(ТекстВыборкиИзмерений) - 1);
				Если НЕ ПустаяСтрока(НовоеУсловиеОтбораДанных) Тогда
					УсловиеОтбораДанных = Лев(НовоеУсловиеОтбораДанных, СтрДлина(НовоеУсловиеОтбораДанных) - 3);
				КонецЕсли;
			КонецЕсли;
			
			Если ОтборПоВременнойТаблице И ИзмеренияРегистраСведений <> Неопределено Тогда
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
						"	#ТаблицаИзменения КАК ТаблицаИзменений",
						"	#ТаблицаИзменения КАК ТаблицаИзменений
						|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОтбора КАК Отбор
						|	ПО ТаблицаИзменений.Ссылка = Отбор.Ссылка");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОтбора", ИмяВременнойТаблицыОтбора);
				
				ТекстСоединения = "";
				Для Каждого Измерение Из МассивИзмеренийРегистраСведений Цикл
					ТекстСоединения = ТекстСоединения +
						СтрШаблон("ТаблицаИзменений.%1 = Отбор.%1
								|	И ", Измерение);
				КонецЦикла;
				ТекстСоединения = Лев(ТекстСоединения, СтрДлина(ТекстСоединения) - 3);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаИзменений.Ссылка = Отбор.Ссылка", ТекстСоединения);
				
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстВыборкиИзмерений", ТекстВыборкиИзмерений);
			
		ИначеЕсли ОбщегоНазначения.ЭтоРегистр(МетаданныеОбъекта) Тогда
			
			ТекстЗапроса =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ТаблицаИзменений.Регистратор КАК Ссылка
			|ИЗ
			|	#ТаблицаИзменения КАК ТаблицаИзменений
			|ГДЕ
			|	ТаблицаИзменений.Узел ССЫЛКА ПланОбмена.ОбновлениеИнформационнойБазы
			|	И &УсловиеОтбораДанных";
			
			Если ОтборПоСсылкам Тогда
				УсловиеОтбораДанных = "ТаблицаИзменений.Регистратор В (&МассивСсылок)";
			ИначеЕсли ОтборПоВременнойТаблице Тогда
				
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
								"	#ТаблицаИзменения КАК ТаблицаИзменений",
								"	#ТаблицаИзменения КАК ТаблицаИзменений
								|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ТаблицаОтбора КАК Отбор
								|	ПО ТаблицаИзменений.Регистратор = Отбор.Ссылка");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаОтбора", ИмяВременнойТаблицыОтбора);
				
			КонецЕсли;
			
		Иначе
			ТекстИсключения = НСтр("ru='Для этого типа метаданных не поддерживается проверка в функции ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки';uk='Для цього типу метаданих не підтримується перевірка в функції ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТаблицаИзменения", ПолноеИмяОбъекта + ".Изменения");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораДанных", УсловиеОтбораДанных);
		МассивТекстовЗапросов.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	Соединитель = "
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|";
	
	ТекстЗапроса = СтрСоединить(МассивТекстовЗапросов, Соединитель);
	Запрос = Новый Запрос;
	Если ОтборПоСсылкам Тогда
		Запрос.УстановитьПараметр("МассивСсылок", Отбор);
	ИначеЕсли ТипЗнч(Отбор) = Тип("Структура") Тогда
		Для Каждого Измерение Из Отбор Цикл
			Запрос.УстановитьПараметр(Измерение.Ключ, ?(ЗначениеЗаполнено(Измерение.Значение), Измерение.Значение, Неопределено));
		КонецЦикла;
	ИначеЕсли ОтборПоВременнойТаблице Тогда
		Запрос.МенеджерВременныхТаблиц = Отбор;
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	
	Возврат НЕ Запрос.Выполнить().Пустой(); 
	
КонецФункции

#КонецОбласти

#КонецОбласти
