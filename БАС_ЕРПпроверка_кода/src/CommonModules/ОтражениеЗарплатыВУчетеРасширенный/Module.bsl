////////////////////////////////////////////////////////////////////////////////
// Отражение зарплаты в учете
// Переопределяемое в пределах библиотеки поведение.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьВТУдержанияПоСотрудникамКонтрагент(МенеджерВременныхТаблиц, ИмяТаблицы = "ВТУдержанияПоСотрудникам") Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УдержанияПоСотрудникам.ФизическоеЛицо КАК ФизическоеЛицо,
	|	УдержанияПоСотрудникам.Удержание,
	|	УдержанияПоСотрудникам.ДокументОснование КАК ДокументОснование,
	|	ПолучателиУдержаний.Контрагент
	|ПОМЕСТИТЬ ВТУдержанияПоСотрудникамКонтрагент
	|ИЗ
	|	#ИмяТаблицы КАК УдержанияПоСотрудникам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПолучателиУдержаний КАК ПолучателиУдержаний
	|		ПО УдержанияПоСотрудникам.ДокументОснование = ПолучателиУдержаний.ДокументОснование
	|			И УдержанияПоСотрудникам.ФизическоеЛицо = ПолучателиУдержаний.ФизическоеЛицо
	|			И УдержанияПоСотрудникам.Удержание = ПолучателиУдержаний.Удержание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОснование,
	|	ФизическоеЛицо";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТаблицы", ИмяТаблицы);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДополнитьТаблицуНДФЛ(ПериодРегистрации, Организация, МенеджерВременныхТаблиц) Экспорт

	ПоВсемОрганизациям = (Организация = Неопределено); 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", ПоВсемОрганизациям);
	
	ВидыОпераций = ОтражениеЗарплатыВУчете.ВидыОсобыхНачисленийИУдержанийНДФЛ(Истина);

	Запрос.УстановитьПараметр("ВидыОпераций", ВидыОпераций);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленииУдержания.Организация,
	|	НачисленииУдержания.ФизическоеЛицо,
	|	НачисленииУдержания.Сотрудник,
	|	НачисленииУдержания.НачислениеУдержание,
	|	НачисленииУдержания.Подразделение,
	|	СУММА(НачисленииУдержания.Сумма) КАК Сумма,
	|	НачисленииУдержания.ТерриторияВыполненияРаботВОрганизации,
	|	0 КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТВременнаяТаблица
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоКонтрагентамАкционерам КАК НачисленииУдержания
	|ГДЕ
	|	(&ПоВсемОрганизациям
	|			ИЛИ НачисленииУдержания.Организация = &Организация)
	|	И НАЧАЛОПЕРИОДА(НачисленииУдержания.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И НачисленииУдержания.НачислениеУдержание В(&ВидыОпераций)
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленииУдержания.Организация,
	|	НачисленииУдержания.ФизическоеЛицо,
	|	НачисленииУдержания.Сотрудник,
	|	НачисленииУдержания.НачислениеУдержание,
	|	НачисленииУдержания.Подразделение,
	|	НачисленииУдержания.ТерриторияВыполненияРаботВОрганизации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаНДФЛ.Организация,
	|	ТаблицаНДФЛ.ФизическоеЛицо,
	|	ТаблицаНДФЛ.Сотрудник,
	|	ТаблицаНДФЛ.НачислениеУдержание,
	|	ТаблицаНДФЛ.Подразделение,
	|	ТаблицаНДФЛ.Сумма,
	|	ТаблицаНДФЛ.ТерриторияВыполненияРаботВОрганизации,
	|	ТаблицаНДФЛ.ИдентификаторСтроки
	|ИЗ
	|	ВТТаблицаНДФЛ КАК ТаблицаНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТТаблицаНДФЛ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.Организация,
	|	ВременнаяТаблица.ФизическоеЛицо,
	|	ВременнаяТаблица.Сотрудник,
	|	ВременнаяТаблица.НачислениеУдержание,
	|	ВременнаяТаблица.Подразделение,
	|	ВременнаяТаблица.Сумма,
	|	ВременнаяТаблица.ТерриторияВыполненияРаботВОрганизации,
	|	ВременнаяТаблица.НачислениеУдержание КАК НачислениеУдержание,
	|	ВременнаяТаблица.ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТТаблицаНДФЛ
	|ИЗ
	|	ВТВременнаяТаблица КАК ВременнаяТаблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачислениеУдержание";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДополнитьТаблицуНачислений(ПериодРегистрации, Организация, МенеджерВременныхТаблиц) Экспорт

	ПоВсемОрганизациям = (Организация = Неопределено);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", ПоВсемОрганизациям);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Организация,
	|	Начисления.Сотрудник,
	|	Начисления.ФизическоеЛицо,
	|	Начисления.Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации,
	|	&ПериодРегистрации КАК ДатаНачала,
	|	&ПериодРегистрации КАК ДатаОкончания,
	|	Начисления.НачислениеУдержание,
	|	Начисления.Сумма КАК Сумма,
	|	Начисления.ДокументОснование,
	|	0 КАК ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТВременнаяТаблица
	|ИЗ
	|	РегистрНакопления.НачисленияУдержанияПоКонтрагентамАкционерам КАК Начисления
	|ГДЕ
	|	(&ПоВсемОрганизациям
	|			ИЛИ Начисления.Организация = &Организация)
	|	И НАЧАЛОПЕРИОДА(Начисления.Период, МЕСЯЦ) = &ПериодРегистрации
	|	И Начисления.ГруппаНачисленияУдержанияВыплаты = ЗНАЧЕНИЕ(Перечисление.ГруппыНачисленияУдержанияВыплаты.Начислено)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Начисления.Организация,
	|	Начисления.Сотрудник,
	|	Начисления.ФизическоеЛицо,
	|	Начисления.Подразделение,
	|	Начисления.ТерриторияВыполненияРаботВОрганизации,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.НачислениеУдержание,
	|	Начисления.Сумма,
	|	Начисления.ДокументОснование,
	|	Начисления.ИдентификаторСтроки
	|ИЗ
	|	ВТНачисления КАК Начисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблица.Организация,
	|	ВременнаяТаблица.Сотрудник,
	|	ВременнаяТаблица.ФизическоеЛицо,
	|	ВременнаяТаблица.Подразделение,
	|	ВременнаяТаблица.ТерриторияВыполненияРаботВОрганизации,
	|	ВременнаяТаблица.ДатаНачала,
	|	ВременнаяТаблица.ДатаОкончания,
	|	ВременнаяТаблица.НачислениеУдержание,
	|	ВременнаяТаблица.Сумма,
	|	ВременнаяТаблица.ДокументОснование,
	|	ВременнаяТаблица.ИдентификаторСтроки
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ВТВременнаяТаблица КАК ВременнаяТаблица";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ДополнитьВТНачислениеУдержаниеВидОперации(МенеджерВременныхТаблиц) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачислениеУдержаниеВидОперации.НачислениеУдержание КАК НачислениеУдержание,
	|	НачислениеУдержаниеВидОперации.ВидОперации КАК ВидОперации
	|ПОМЕСТИТЬ ВТНачислениеУдержаниеВидОперацииВременная
	|ИЗ
	|	ВТНачислениеУдержаниеВидОперации КАК НачислениеУдержаниеВидОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыВыплатБывшимСотрудникам.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ВыплатыБывшимСотрудникам)
	|ИЗ
	|	Справочник.ВидыВыплатБывшимСотрудникам КАК ВидыВыплатБывшимСотрудникам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыПрочихДоходовФизическихЛиц.Ссылка,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоЗарплате.ДоходыКонтрагентов)
	|ИЗ
	|	Справочник.ВидыПрочихДоходовФизическихЛиц КАК ВидыПрочихДоходовФизическихЛиц
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НачислениеУдержание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНачислениеУдержаниеВидОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТНачислениеУдержаниеВидОперацииВременная.НачислениеУдержание,
	|	ВТНачислениеУдержаниеВидОперацииВременная.ВидОперации
	|ПОМЕСТИТЬ ВТНачислениеУдержаниеВидОперации
	|ИЗ
	|	ВТНачислениеУдержаниеВидОперацииВременная КАК ВТНачислениеУдержаниеВидОперацииВременная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНачислениеУдержаниеВидОперацииВременная";
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ДополнитьМассивТиповНачисления(МассивТипов) Экспорт

	МассивТипов.Добавить(Тип("СправочникСсылка.ВидыВыплатБывшимСотрудникам"));
	МассивТипов.Добавить(Тип("СправочникСсылка.ВидыПрочихДоходовФизическихЛиц"));
	
КонецПроцедуры

Функция ТипыПоляДокументОснование() Экспорт
	
	МассивТипов = Метаданные.РегистрыНакопления.НачисленияУдержанияПоСотрудникам.Реквизиты.ДокументОснование.Тип.Типы();
	МассивТиповКА = Метаданные.РегистрыНакопления.НачисленияУдержанияПоКонтрагентамАкционерам.Реквизиты.ДокументОснование.Тип.Типы();
	Для каждого Значение Из МассивТиповКА Цикл
		МассивТипов.Добавить(Значение);
	КонецЦикла;
		
	Возврат МассивТипов;
	
КонецФункции

#КонецОбласти
