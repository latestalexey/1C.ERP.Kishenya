#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция РезультатЗапросаПоНастройкамОтраженияВУчете(МассивОрганизаций, Период) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.КатегорияЭксплуатации КАК КатегорияЭксплуатации
		|ПОМЕСТИТЬ ДвиженияТМЦ
		|ИЗ
		|	(ВЫБРАТЬ
		|		Документ.Организация КАК Организация,
		|		ЕСТЬNULL(Таблица.КатегорияЭксплуатации, ЗНАЧЕНИЕ(Справочник.КатегорииЭксплуатации.ПустаяСсылка)) КАК КатегорияЭксплуатации
		|	ИЗ
		|		Документ.ВнутреннееПотреблениеТоваров КАК Документ
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВнутреннееПотреблениеТоваров.Товары КАК Таблица
		|			ПО Документ.Ссылка = Таблица.Ссылка
		|	ГДЕ
		|		Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И Документ.Организация В(&Организации)
		|		И Документ.Проведен
		|		И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВЭксплуатацию)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Документ.Организация,
		|		ЕСТЬNULL(Таблица.КатегорияЭксплуатации, ЗНАЧЕНИЕ(Справочник.КатегорииЭксплуатации.ПустаяСсылка))
		|	ИЗ
		|		Документ.ВводОстатков КАК Документ
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВводОстатков.ТМЦВЭксплуатации КАК Таблица
		|			ПО Документ.Ссылка = Таблица.Ссылка
		|	ГДЕ
		|		Документ.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|		И Документ.Организация В(&Организации)
		|		И Документ.Проведен
		|		И Документ.ТипОперации В (ЗНАЧЕНИЕ(Перечисление.ТипыОперацийВводаОстатков.ОстаткиТМЦВЭксплуатации))) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияТМЦ.Организация КАК Организация,
		|	ДвиженияТМЦ.КатегорияЭксплуатации КАК КатегорияЭксплуатации,
		|	ИСТИНА КАК ЕстьПередачи,
		|	
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) <> &ПустойСчет
		|			ТОГДА ПорядокОтражения.СчетУчета
		|		ИНАЧЕ КатегорииЭксплуатации.СчетУчета
		|	КОНЕЦ КАК СчетУчета,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) = &ПустойСчет
		|				И КатегорииЭксплуатации.СчетУчета <> &ПустойСчет
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СчетУчетаПоУмолчанию,
		|	
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетАмортизации, &ПустойСчет) <> &ПустойСчет
		|			ТОГДА ПорядокОтражения.СчетАмортизации
		|		ИНАЧЕ КатегорииЭксплуатации.СчетАмортизации
		|	КОНЕЦ КАК СчетАмортизации,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетАмортизации, &ПустойСчет) = &ПустойСчет
		|				И КатегорииЭксплуатации.СчетАмортизации <> &ПустойСчет
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СчетАмортизацииПоУмолчанию
		|	
		|ИЗ
		|	ДвиженияТМЦ КАК ДвиженияТМЦ
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		РегистрСведений.ПорядокОтраженияТМЦВЭксплуатации КАК ПорядокОтражения
		|	ПО 
		|		ДвиженияТМЦ.Организация = ПорядокОтражения.Организация
		|		И ДвиженияТМЦ.КатегорияЭксплуатации = ПорядокОтражения.КатегорияЭксплуатации
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		Справочник.КатегорииЭксплуатации КАК КатегорииЭксплуатации
		|	ПО
		|		ДвиженияТМЦ.КатегорияЭксплуатации = КатегорииЭксплуатации.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПорядокОтражения.Организация,
		|	ПорядокОтражения.КатегорияЭксплуатации,
		|	ЛОЖЬ,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) <> &ПустойСчет
		|			ТОГДА ПорядокОтражения.СчетУчета
		|		ИНАЧЕ КатегорииЭксплуатации.СчетУчета
		|	КОНЕЦ КАК СчетУчета,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетУчета, &ПустойСчет) = &ПустойСчет
		|				И КатегорииЭксплуатации.СчетУчета <> &ПустойСчет
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СчетУчетаПоУмолчанию,
		|	
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетАмортизации, &ПустойСчет) <> &ПустойСчет
		|			ТОГДА ПорядокОтражения.СчетАмортизации
		|		ИНАЧЕ КатегорииЭксплуатации.СчетАмортизации
		|	КОНЕЦ КАК СчетАмортизации,
		|	ВЫБОР 
		|		КОГДА ЕСТЬNULL(ПорядокОтражения.СчетАмортизации, &ПустойСчет) = &ПустойСчет
		|				И КатегорииЭксплуатации.СчетАмортизации <> &ПустойСчет
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СчетАмортизацииПоУмолчанию
		|	
		|ИЗ
		|	РегистрСведений.ПорядокОтраженияТМЦВЭксплуатации КАК ПорядокОтражения
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		ДвиженияТМЦ КАК ДвиженияТМЦ
		|	ПО 
		|		ПорядокОтражения.Организация = ДвиженияТМЦ.Организация
		|		И ПорядокОтражения.КатегорияЭксплуатации = ДвиженияТМЦ.КатегорияЭксплуатации
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ 
		|		Справочник.КатегорииЭксплуатации КАК КатегорииЭксплуатации
		|	ПО
		|		ПорядокОтражения.КатегорияЭксплуатации = КатегорииЭксплуатации.Ссылка
		|	
		|ГДЕ
		|	ПорядокОтражения.Организация В(&Организации)
		|	И ДвиженияТМЦ.Организация ЕСТЬ NULL 
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	КатегорияЭксплуатации");
	Запрос.УстановитьПараметр("Организации", МассивОрганизаций);
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ПустойСчет", ПланыСчетов.Хозрасчетный.ПустаяСсылка());
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса;
	
КонецФункции

Процедура НайтиДокументыСоответствующиеНастройкам(ТаблицаНастроек, Дата, ТаблицаДокументов) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.Организация КАК Организация,
	|	ИсходнаяТаблица.КатегорияЭксплуатации КАК КатегорияЭксплуатации
	|	
	|ПОМЕСТИТЬ ТаблицаНастроек
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|ГДЕ
	|	ИсходнаяТаблица.ДанныеИзменены
	|;
	|/////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеРегистра.Регистратор КАК Документ,
	|	ОтражениеДокументовВРеглУчете.ДатаОтражения КАК ДатаОтражения,
	|	ОтражениеДокументовВРеглУчете.Организация КАК Организация
	|ИЗ
	|	РегистрНакопления.ТМЦВЭксплуатации.Обороты(
	|		,
	|		&ДатаОкончания,
	|		Регистратор,
	|		Организация В (
	|			ВЫБРАТЬ
	|				ТаблицаНастроек.Организация
	|			ИЗ
	|				ТаблицаНастроек КАК ТаблицаНастроек
	|		)
	|	) КАК ДанныеРегистра
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.ОтражениеДокументовВРеглУчете КАК ОтражениеДокументовВРеглУчете
	|	ПО
	|		ДанныеРегистра.Регистратор = ОтражениеДокументовВРеглУчете.Регистратор
	|		И ОтражениеДокументовВРеглУчете.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтраженияДокументовВРеглУчете.ОтраженоВРеглУчете)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ
	|;");
	
	ИсходнаяТаблица = ТаблицаНастроек.Выгрузить(, "Организация, НазначениеИспользования, ДанныеИзменены");
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("ДатаОкончания", Дата);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаДокументов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли