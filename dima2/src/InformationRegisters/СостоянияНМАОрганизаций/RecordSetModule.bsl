#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СостоянияНМАОрганизаций.Период      КАК Период,
	|	СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|	СостоянияНМАОрганизаций.Организация КАК Организация,
	|	СостоянияНМАОрганизаций.Состояние   КАК Состояние
	|ПОМЕСТИТЬ СостоянияНМАОрганизацийПередЗаписью
	|ИЗ
	|	РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|
	|ГДЕ
	|	СостоянияНМАОрганизаций.Регистратор = &Регистратор
	|	И СостоянияНМАОрганизаций.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА
	|";
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	                      
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ДляПроведения") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|	Таблица.Регистратор                  КАК Документ,
	|	Таблица.Организация                  КАК Организация
	|ИЗ
	|	(ВЫБРАТЬ
	|		СостоянияНМАОрганизаций.Период      КАК Период,
	|		СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|		СостоянияНМАОрганизаций.Организация КАК Организация,
	|		ВЫБОР 
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)
	|				ТОГДА 1
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|				ТОГДА 2
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				ТОГДА 3
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|				ТОГДА 4
	|		КОНЕЦ КАК Состояние
	|	ИЗ
	|		СостоянияНМАОрганизацийПередЗаписью КАК СостоянияНМАОрганизаций
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		СостоянияНМАОрганизаций.Период      КАК Период,
	|		СостоянияНМАОрганизаций.Регистратор КАК Регистратор,
	|		СостоянияНМАОрганизаций.Организация КАК Организация,
	|		-(ВЫБОР 
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.НеПринятКУчету)
	|				ТОГДА 1
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Поступил)
	|				ТОГДА 2
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.ПринятКУчету)
	|				ТОГДА 3
	|			КОГДА СостоянияНМАОрганизаций.Состояние = ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан)
	|				ТОГДА 4
	|		КОНЕЦ) КАК Состояние
	|	ИЗ
	|		РегистрСведений.СостоянияНМАОрганизаций КАК СостоянияНМАОрганизаций
	|	ГДЕ
	|		СостоянияНМАОрганизаций.Регистратор = &Регистратор
	|		И СостоянияНМАОрганизаций.Регистратор ССЫЛКА Документ.ПринятиеКУчетуНМА) Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ),
	|	Таблица.Регистратор,
	|	Таблица.Организация
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Состояние) <> 0 
	|;
	|
	|///////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СостоянияНМАОрганизацийПередЗаписью
	|";
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Константа.НомерЗаданияКЗакрытиюМесяца");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(НаборЗаписей, Выборка);
		НаборЗаписей.Операция = Перечисления.ОперацииЗакрытияМесяца.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций;
		НаборЗаписей.НомерЗадания = Константы.НомерЗаданияКЗакрытиюМесяца.Получить();
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
