#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает значения статусов вида бюджета
//
// Параметры
//  ВидБюджета  - СправочникСсылка.ВидыБюджетов - ссылка на вид бюджета
//  Реквизиты  - Строка, Структура - имена статусов, значения которых надо получить
//  Постфикс  - Строка - "Отчет" или "Документ"
//
// Возвращаемое значение:
//   Произвольный, Структура   - значения статусов. Тип определяется в зависимости от типа параметра "Реквизиты"
//
Функция ПолучитьЗначениеСтатуса(ВидБюджета, ИмяРеквизитов, Постфикс = "") Экспорт
	
	ВозвращатьОдноЗначение = ТипЗнч(ИмяРеквизитов) <> Тип("Структура");
	Если ВозвращатьОдноЗначение Тогда
		Реквизиты = Новый Структура(ИмяРеквизитов);
	Иначе
		Реквизиты = ИмяРеквизитов;
	КонецЕсли;
	
	ТекстЗапроса = "";
	
	Для Каждого КлючИЗначение из Реквизиты Цикл
		ТекстЗапроса = ТекстЗапроса + ?(ПустаяСтрока(ТекстЗапроса), "", ",") + КлючИЗначение.Ключ + Постфикс + 
			" КАК " + КлючИЗначение.Ключ;
	КонецЦикла;
	
	ТекстЗапроса = "ВЫБРАТЬ " + ТекстЗапроса + " ИЗ РегистрСведений.СтатусыОбработкиВидаБюджета ГДЕ ВидБюджета = &ВидБюджета";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ВидБюджета", ВидБюджета);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Если ВозвращатьОдноЗначение Тогда
			Возврат Неопределено;
		КонецЕсли;
		Возврат Реквизиты;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	Если ВозвращатьОдноЗначение Тогда
		Возврат Выборка[ИмяРеквизитов];
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Реквизиты, Выборка);
	
	Возврат Реквизиты;
	
КонецФункции

// Процедура устанавливает значение статуса
//
// Параметры
//  ВидБюджета  - СправочникСсылка.ВидыБюджетов - ссылка на вид бюджета
//  Реквизиты  - Структура - значения статусов
//
Процедура УстановитьЗначениеСтатуса(ВидБюджета, Реквизиты, Постфикс = "") Экспорт
	
	Запись = РегистрыСведений.СтатусыОбработкиВидаБюджета.СоздатьМенеджерЗаписи();
	Запись.ВидБюджета = ВидБюджета;
	Запись.Прочитать();
	
	Если Не Запись.Выбран() Тогда
		Запись.ВидБюджета = ВидБюджета;
	КонецЕсли;

	Для Каждого КлючИЗначение из Реквизиты Цикл
		Запись[КлючИЗначение.Ключ + Постфикс] = КлючИЗначение.Значение;
	КонецЦикла;
	
	Запись.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли