#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбновлениеИнформационнойБазы

Процедура ЗарегистироватьДанныеКОбработкеДляПереходаНаВерсию_2_1_2(Параметры) Экспорт
    
    Если НЕ ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного") Тогда
        Возврат;
    КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НалоговаяНакладная.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.НалоговаяНакладная КАК НалоговаяНакладная
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
    |           ПО НалоговаяНакладная.Ссылка = ОтражениеДокументовВМеждународномУчете.Регистратор
	|ГДЕ
	|	НалоговаяНакладная.Проведен И ОтражениеДокументовВМеждународномУчете.Регистратор ЕСТЬ NULL
    |
    |ОБЪЕДИНИТЬ
    |
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетДавальцу.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.ОтчетДавальцу КАК ОтчетДавальцу
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
    |           ПО ОтчетДавальцу.Ссылка = ОтражениеДокументовВМеждународномУчете.Регистратор
	|ГДЕ
	|	ОтчетДавальцу.Проведен И ОтражениеДокументовВМеждународномУчете.Регистратор ЕСТЬ NULL
    |
    |ОБЪЕДИНИТЬ
    |
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Приложение2КНалоговойНакладной.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.Приложение2КНалоговойНакладной КАК Приложение2КНалоговойНакладной
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
    |           ПО Приложение2КНалоговойНакладной.Ссылка = ОтражениеДокументовВМеждународномУчете.Регистратор
	|ГДЕ
	|	Приложение2КНалоговойНакладной.Проведен И ОтражениеДокументовВМеждународномУчете.Регистратор ЕСТЬ NULL
    |
    |ОБЪЕДИНИТЬ
    |
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегистрацияВходящегоНалоговогоДокумента.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.РегистрацияВходящегоНалоговогоДокумента КАК РегистрацияВходящегоНалоговогоДокумента
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
    |           ПО РегистрацияВходящегоНалоговогоДокумента.Ссылка = ОтражениеДокументовВМеждународномУчете.Регистратор
	|ГДЕ
	|	РегистрацияВходящегоНалоговогоДокумента.Проведен И ОтражениеДокументовВМеждународномУчете.Регистратор ЕСТЬ NULL
    |
    |ОБЪЕДИНИТЬ
    |
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РемонтОС.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.РемонтОС КАК РемонтОС
    |       ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтражениеДокументовВМеждународномУчете КАК ОтражениеДокументовВМеждународномУчете
    |           ПО РемонтОС.Ссылка = ОтражениеДокументовВМеждународномУчете.Регистратор
	|ГДЕ
	|	РемонтОС.Проведен И ОтражениеДокументовВМеждународномУчете.Регистратор ЕСТЬ NULL
    |
	|";
	
    ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
    ДополнительныеПараметры.Вставить("ВыбиратьПорциями", Истина);
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, 
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"), 
		ДополнительныеПараметры
    );
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаВерсию_2_1_2(Параметры) Экспорт
    
    Если НЕ ПолучитьФункциональнуюОпцию("ФормироватьПроводкиМеждународногоУчетаПоДаннымОперативного") Тогда
        Параметры.ОбработкаЗавершена = Истина;
        Возврат;
    КонецЕсли; 
    
	ПолноеИмяРегистра = "РегистрСведений.ОтражениеДокументовВМеждународномУчете";
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуРегистраторовРегистраДляОбработки(
	    Параметры.Очередь, 
		Неопределено, // ПолноеИмяДокумента
        ПолноеИмяРегистра, 
        МенеджерВТ
    );
		
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
    КонецЕсли;
    
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
    КонецЕсли;
    
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТДляОбработки.Регистратор КАК Регистратор
    |ИЗ 
    |   ВТДляОбработки КАК ВТДляОбработки
    |";
    
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТДляОбработки", Результат.ИмяВременнойТаблицы);
    
	ВыборкаДокументы = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДокументы.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
            
            Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОтражениеДокументовВМеждународномУчете.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ВыборкаДокументы.Регистратор);
			Блокировка.Заблокировать();
            
        	ТаблицаКОтражениюВМеждународномУчете = МеждународныйУчетПроведениеСервер.ТаблицаКОтражениюВМеждународномУчете(
                ВыборкаДокументы.Регистратор, 
                Истина // ПриПроведении
            );
        	
        	Если ТаблицаКОтражениюВМеждународномУчете.Количество() > 0 Тогда
                НаборЗаписей = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СоздатьНаборЗаписей();
                НаборЗаписей.Отбор.Регистратор.Установить(ВыборкаДокументы.Регистратор);
                НаборЗаписей.Загрузить(ТаблицаКОтражениюВМеждународномУчете);
                ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
            КонецЕсли;
            
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru='Не удалось обработать движения по документу %Ссылка% по причине: %Причина%';uk='Не вдалося обробити руху по документу %Ссылка% з причини: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ВыборкаДокументы.Регистратор);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
                ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
                УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ОтражениеДокументовВМеждународномУчете, 
                ВыборкаДокументы.Регистратор, 
                ТекстСообщения
            );
            
            ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = НЕ ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
																			
КонецПроцедуры

#КонецОбласти

#КонецЕсли