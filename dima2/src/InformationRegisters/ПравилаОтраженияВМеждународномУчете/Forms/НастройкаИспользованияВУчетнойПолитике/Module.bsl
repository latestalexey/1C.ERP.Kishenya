
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ШаблонПроводки") Тогда
	    ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено!';uk='Безпосереднє відкриття цієї форми не передбачено!'");
	    ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	ШаблонПроводки = Параметры.ШаблонПроводки;
	
	ЗаполнитьСписокУчетныхПолитик();
	
	УстановитьДоступностьЭлементовНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Оповестить("ИзмененыНастройкиИспользованияВУчетнойПолитике", , ШаблонПроводки); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ИзменитьПравилаОтраженияВМеждународномУчете(СписокУчетныхПолитик, ШаблонПроводки);
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокУчетныхПолитик()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаОтражения.УчетнаяПолитика КАК УчетнаяПолитика,
	|	ПравилаОтражения.ШаблонПроводки КАК ШаблонПроводки
	|ПОМЕСТИТЬ ПравилаОтражения
	|ИЗ
	|	РегистрСведений.ПравилаОтраженияВМеждународномУчете КАК ПравилаОтражения
	|ГДЕ
	|	ПравилаОтражения.ШаблонПроводки = &ШаблонПроводки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УчетныеПолитики.Ссылка КАК УчетнаяПолитика,
	|	ВЫБОР
	|		КОГДА ПравилаОтражения.ШаблонПроводки ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Использование
	|ИЗ
	|	Справочник.УчетныеПолитики КАК УчетныеПолитики
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		ПравилаОтражения КАК ПравилаОтражения
	|	ПО 
	|		(ПравилаОтражения.УчетнаяПолитика = УчетныеПолитики.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	УчетныеПолитики.Наименование";
	
	Запрос.УстановитьПараметр("ШаблонПроводки", ШаблонПроводки);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СписокУчетныхПолитик.Добавить(Выборка.УчетнаяПолитика,, Выборка.Использование);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьПравилаОтраженияВМеждународномУчете(СписокУчетныхПолитик, ШаблонПроводки)

	НаборЗаписей = РегистрыСведений.ПравилаОтраженияВМеждународномУчете.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ШаблонПроводки.Установить(ШаблонПроводки);
	Для Каждого ЭлементУчетнаяПолитика из СписокУчетныхПолитик Цикл
		Если ЭлементУчетнаяПолитика.Пометка Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.УчетнаяПолитика = ЭлементУчетнаяПолитика.Значение;
			НоваяЗапись.ШаблонПроводки = ШаблонПроводки;
		КонецЕсли;	
	КонецЦикла;	
	НаборЗаписей.Записать();

КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовНаСервере()

	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ФормаЗаписатьИЗакрыть");
	МассивЭлементов.Добавить("СписокУчетныхПолитик");
	
	ДоступноИзменениеНастроекМФУ = МеждународныйУчетОбщегоНазначения.ДоступноИзменениеНастроекМеждународногоУчета();
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", НЕ ДоступноИзменениеНастроекМФУ);

КонецПроцедуры

#КонецОбласти
