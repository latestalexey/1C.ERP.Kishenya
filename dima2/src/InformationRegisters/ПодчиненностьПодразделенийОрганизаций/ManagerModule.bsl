#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПодчиненностьПодразделений(СписокПодразделений) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка,
		|	ПодразделенияОрганизаций.Родитель
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Родитель <> Значение(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)";
	
	РодителиПодразделений = Новый Соответствие;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РодителиПодразделений.Вставить(Выборка.Ссылка, Выборка.Родитель);
	КонецЦикла;
	
	Для каждого Подразделение Из СписокПодразделений Цикл
		
		НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
		
		Запись = НаборЗаписей.Добавить();
		Запись.ВышестоящееПодразделение = Подразделение;
		Запись.Подразделение = Подразделение;
		
		Родитель = РодителиПодразделений.Получить(Запись.ВышестоящееПодразделение);
		Пока Родитель <> Неопределено Цикл
			
			Запись = НаборЗаписей.Добавить();
			Запись.ВышестоящееПодразделение = Родитель;
			Запись.Подразделение = Подразделение;
			
			Родитель = РодителиПодразделений.Получить(Запись.ВышестоящееПодразделение);
			
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбновитьПодчиненностьПодразделенийПриСменеРодителя(СписокПодразделений, ПредыдущийРодитель, ТекущийРодитель) Экспорт
	
	Пока СписокПодразделений.Количество() > 0 Цикл
		
		УстановитьПривилегированныйРежим(Истина);
		
		Для каждого Подразделение Из СписокПодразделений Цикл
			
			Если ЗначениеЗаполнено(ПредыдущийРодитель) Тогда
				
				НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ВышестоящееПодразделение.Установить(ПредыдущийРодитель);
				НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
				НаборЗаписей.Записать();
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекущийРодитель) Тогда
				
				НаборЗаписей = РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ВышестоящееПодразделение.Установить(ТекущийРодитель);
				НаборЗаписей.Отбор.Подразделение.Установить(Подразделение);
				
				Запись = НаборЗаписей.Добавить();
				Запись.ВышестоящееПодразделение = ТекущийРодитель;
				Запись.Подразделение = Подразделение;
				
				НаборЗаписей.Записать();
				
			КонецЕсли;
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
			|ГДЕ
			|	ПодразделенияОрганизаций.Родитель В(&СписокПодразделений)";
		
		СписокПодразделений = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПодчиненностьПодразделений() Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение
		|ИЗ
		|	РегистрСведений.ПодчиненностьПодразделенийОрганизаций КАК ПодчиненностьПодразделенийОрганизаций";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодразделенияОрганизаций.Ссылка
			|ИЗ
			|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций";
		
		ОбновитьПодчиненностьПодразделений(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли