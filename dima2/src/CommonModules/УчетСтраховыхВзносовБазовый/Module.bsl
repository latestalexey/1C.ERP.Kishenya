
#Область СлужебныйПрограммныйИнтерфейс

// Специфические алгоритмы чтения данных.

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
//	ВТКадровыхДанных, которая содержит следующие поля:
//		Сотрудник - СправочникСсылка.Сотрудники
//		ДатаНачала - дата
//		Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//		Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//		Должность - СправочникСсылка.Должности
//		ЯвляетсяФармацевтом - булево.
//		ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ - булево.
//		ЯвляетсяРаботникомСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//		ЯвляетсяПрокурором - булево.
//		ЯвляетсяВоеннослужащим - булево.
//		РаботаетВСтуденческомОтряде - булево.
//		ЯвляетсяЧленомЛетногоЭкипажа - булево.
//		ЯвляетсяШахтером - булево
//		КоэффициентУчетаСтроки - число от 0 до 1.
//      	 
// Параметры:
//		Организация -
//		ПериодРегистрации - дата -
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - содержит вр. таблицу ВТНачисления с полями:
//				Сотрудник - СправочникСсылка.Сотрудники
//				ДатаНачала - дата -
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления -
//				ПодразделениеОрганизации - СправочникСсылка.ПодразделенияОрганизаций
//		УточнятьПериодКадровыхДанных - булево - если выставлен в Истина, период будет скорректирован с учетом дат приема и
//		                                        увольнения, для работающих постоянно - выставлен на конец месяца.
//
Процедура СформироватьВТКадровыхДанных(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, УточнятьПериодКадровыхДанных = Истина) Экспорт

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Скорректируем если требуется, переданные даты для чтения кадровых данных сотрудников.
	Если УточнятьПериодКадровыхДанных Тогда
		
		КадровыеДанные = "ДатаПриема, ДатаУвольнения";
		ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
			МенеджерВременныхТаблиц, "ВТНачисления", "Сотрудник,ДатаНачала");
		КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, КадровыеДанные);
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокНачислений.Сотрудник,
		|	ВЫБОР
		|		КОГДА СписокНачислений.ДатаНачала < ДанныеОСотруднике.ДатаПриема
		|			ТОГДА ДанныеОСотруднике.ДатаПриема
		|		КОГДА ДанныеОСотруднике.ДатаУвольнения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|				И КОНЕЦПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ) > ДанныеОСотруднике.ДатаУвольнения
		|			ТОГДА ДанныеОСотруднике.ДатаУвольнения
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(КОНЕЦПЕРИОДА(СписокНачислений.ДатаНачала, МЕСЯЦ), ДЕНЬ)
		|	КОНЕЦ КАК Период,
		|	СписокНачислений.ДатаНачала КАК ИсходныйПериод
		|ПОМЕСТИТЬ ВТВременнаяТаблица
		|ИЗ
		|	ВТНачисления КАК СписокНачислений
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ДанныеОСотруднике
		|		ПО СписокНачислений.Сотрудник = ДанныеОСотруднике.Сотрудник
		|			И СписокНачислений.ДатаНачала = ДанныеОСотруднике.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СписокНачислений.Сотрудник,
		|	СписокНачислений.ДатаНачала КАК Период,
		|	СписокНачислений.ДатаНачала КАК ИсходныйПериод
		|ПОМЕСТИТЬ ВТВременнаяТаблица
		|ИЗ
		|	ВТНачисления КАК СписокНачислений";
		
	КонецЕсли;
	Запрос.Выполнить();

	// Читаем кадровые данные сотрудников, влияющие на исчисление взносов.
	
	КадровыеДанные = "Должность";
	ОписательВременныхТаблиц = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(
		МенеджерВременныхТаблиц,
		"ВТВременнаяТаблица");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(ОписательВременныхТаблиц, Ложь, КадровыеДанные);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНачислений.Сотрудник,
	|	СписокНачислений.ДатаНачала,
	|	СписокНачислений.Начисление,
	|	СписокНачислений.ПодразделениеОрганизации КАК Подразделение,
	|	ДанныеОСотруднике.Должность,
	|	1 КАК КоэффициентУчетаСтроки
	|ПОМЕСТИТЬ ВТКадровыхДанных
	|ИЗ
	|	ВТНачисления КАК СписокНачислений
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВременнаяТаблица КАК УточненныеПериоды
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеДанныеСотрудников КАК ДанныеОСотруднике
	|			ПО УточненныеПериоды.Сотрудник = ДанныеОСотруднике.Сотрудник
	|				И УточненныеПериоды.Период = ДанныеОСотруднике.Период
	|		ПО СписокНачислений.Сотрудник = УточненныеПериоды.Сотрудник
	|			И СписокНачислений.ДатаНачала = УточненныеПериоды.ИсходныйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТКадровыеДанныеСотрудников
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТВременнаяТаблица";
	Запрос.Выполнить();
	
КонецПроцедуры

// Помещает в переданный МенеджерВременныхТаблиц таблицу 
//	ВТНачисленияСДаннымиУчета, которая содержит следующие поля:
//		Сотрудник - СправочникСсылка.Сотрудники
//		ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//		ДатаНачала - дата
//		ДатаПолученияДохода - дата.
//		Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//		ВидДохода - СправочникСсылка.ВидыДоходовПоСтраховымВзносам -
//		Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//		ЯвляетсяДоходомФармацевта - булево.
//		ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ - булево.
//		ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//		КлассУсловийТруда - ПеречислениеСсылка.КлассыУсловийТрудаПоРезультатамСпециальнойОценки
//		ОблагаетсяВзносамиНаДоплатуЛетчикам - булево.
//		ОблагаетсяВзносамиНаДоплатуШахтерам - булево.
//		Сумма - число 
//		Скидка - число 
//      	 
// Параметры:
//		Организация -
//		ПериодРегистрации - дата -
//		МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - содержит 
//			вр. таблицу ВТНачисления с полями:
//				Сотрудник -
//				ДатаНачала - дата -
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления -
//				Подразделение - 
//				СуммаДохода - 
//				СуммаВычетаВзносы -
//      	        и, возможно, другими
//			вр. таблицу ВТСДаннымиУчета, с полями:
//				Сотрудник - СправочникСсылка.Сотрудники
//				ФизическоеЛицо - СправочникСсылка.ФизическиеЛица
//				ДатаНачала - дата
//				Начисление - ПеречислениеСсылка.ВидыОсобыхНачисленийИУдержаний, ПланВидовРасчетаСсылка.Начисления
//				ВидДохода - СправочникСсылка.ВидыДоходовПоСтраховымВзносам -
//				Подразделение - СправочникСсылка.ПодразделенияОрганизаций
//				ЯвляетсяФармацевтом - булево.
//				ЯвляетсяЧленомЭкипажаСуднаПодФлагомРФ - булево.
//				ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией - ПеречислениеСсылка.ВидыРаботСДосрочнойПенсией
//				КлассУсловийТруда - ПеречислениеСсылка.КлассыУсловийТрудаПоРезультатамСпециальнойОценки
//				ОблагаетсяВзносамиНаДоплатуЛетчикам - булево.
//				ОблагаетсяВзносамиНаДоплатуШахтерам - булево.
//				КоэффициентУчетаСтроки - число от 0 до 1.
//
Процедура СформироватьВТНачисленияСДаннымиУчета(Организация, ПериодРегистрации, МенеджерВременныхТаблиц, ИспользоватьОсобыеУсловияТруда = Ложь) Экспорт 

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ДатаПолученияДохода", УчетСтраховыхВзносовКлиентСервер.ДатаПолученияДохода(ПериодРегистрации));
	Запрос.УстановитьПараметр("ИспользоватьОсобыеУсловияТруда", ИспользоватьОсобыеУсловияТруда);
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	ВременнаяТаблица.Сотрудник,
	|	ВременнаяТаблица.Начисление,
	|	ВременнаяТаблица.ПодразделениеОрганизации КАК Подразделение,
	|	ВременнаяТаблица.ДатаНачала,
	|	ДанныеУчета.ВидДохода,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеУчета.ЯвляетсяДоходомФармацевта,
	|	ДанныеУчета.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеУчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеУчета.КлассУсловийТруда,
	|	ВЫРАЗИТЬ(СУММА(ВременнаяТаблица.СуммаДохода * ЕСТЬNULL(ДанныеУчета.КоэффициентУчетаСтроки, 1)) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫБОР
	|		КОГДА ДанныеУчета.ВидДохода.ОблагаетсяВзносамиЧастично
	|			ТОГДА ВЫРАЗИТЬ(СУММА(ВременнаяТаблица.СуммаВычетаВзносы * ЕСТЬNULL(ДанныеУчета.КоэффициентУчетаСтроки, 1)) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Скидка,
	|	ДанныеУчета.ФизическоеЛицо,
	|	ИСТИНА КАК РаспределятьЕНВД,
	|	НЕОПРЕДЕЛЕНО КАК ОблагаетсяЕНВД
	|ПОМЕСТИТЬ ВТНачисленияСДаннымиУчета
	|ИЗ
	|	ВТНачисления КАК ВременнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСДаннымиУчета КАК ДанныеУчета
	|		ПО ВременнаяТаблица.Сотрудник = ДанныеУчета.Сотрудник
	|			И ВременнаяТаблица.Начисление = ДанныеУчета.Начисление
	|			И ВременнаяТаблица.ПодразделениеОрганизации = ДанныеУчета.Подразделение
	|			И ВременнаяТаблица.ДатаНачала = ДанныеУчета.ДатаНачала
	|			И &ИспользоватьОсобыеУсловияТруда
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблица.Сотрудник,
	|	ВременнаяТаблица.Начисление,
	|	ВременнаяТаблица.ПодразделениеОрганизации,
	|	ВременнаяТаблица.ДатаНачала,
	|	ДанныеУчета.ВидДохода,
	|	ДанныеУчета.ВидДохода.ОблагаетсяВзносамиЧастично,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуЛетчикам,
	|	ДанныеУчета.ОблагаетсяВзносамиНаДоплатуШахтерам,
	|	ДанныеУчета.ЯвляетсяДоходомФармацевта,
	|	ДанныеУчета.ЯвляетсяДоходомЧленаЭкипажаСуднаПодФлагомРФ,
	|	ДанныеУчета.ОблагаетсяВзносамиЗаЗанятыхНаРаботахСДосрочнойПенсией,
	|	ДанныеУчета.КлассУсловийТруда,
	|	ДанныеУчета.ФизическоеЛицо";
	
	Если ИспользоватьОсобыеУсловияТруда Тогда
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ИспользоватьОсобыеУсловияТруда", "ВременнаяТаблица.УсловияТруда = ДанныеУчета.УсловияТруда")
	Иначе
		Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&ИспользоватьОсобыеУсловияТруда", "ИСТИНА")
	КонецЕсли;
	Запрос.Выполнить();
	
КонецПроцедуры


#КонецОбласти
