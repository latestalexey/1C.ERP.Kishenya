
#Область СлужебныйПрограммныйИнтерфейс

// Функция возвращает строку - сообщение пользователю о результатах проверки адреса.
//
Функция СообщениеОРезультатахПроверкиАдреса(Адрес) Экспорт
	
	Если ПустаяСтрока(Адрес) Тогда
		СообщениеПроверки = СообщениеОПустомАдресе();
	Иначе
		РезультатПроверки = РезультатПроверкиАдреса(Адрес);
		Если РезультатПроверкиАдресаСодержитОшибки(РезультатПроверки) Тогда			
			Ошибки = ПредставленияОшибокПроверкиАдреса(РезультатПроверки);
			СообщениеПроверки = СообщениеОбОшибкахПроверкиАдреса(Ошибки);
		Иначе
			СообщениеПроверки = СообщениеОбУспешнойПроверкеАдреса();
		КонецЕсли;
	КонецЕсли;
	
	Возврат СообщениеПроверки;	
	
КонецФункции 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаАдреса

Функция РезультатПроверкиАдреса(Адрес)
	Возврат ЗарплатаКадрыВызовСервера.ПроверитьАдрес(Адрес);
КонецФункции

Функция РезультатПроверкиАдресаСодержитОшибки(РезультатПроверки)
	Возврат НЕ РезультатПроверки.Результат = "Корректный";
КонецФункции

Функция СообщениеОбУспешнойПроверкеАдреса()
	
	Возврат НСтр("ru='Адрес введен правильно - в соответствии с требованиями';uk='Адресу введено правильно - відповідно до вимог'");
	
КонецФункции

Функция СообщениеОПустомАдресе()
	
	Возврат НСтр("ru='Адрес не заполнен';uk='Адреса не заповнена'");
	
КонецФункции

Функция СообщениеОбОшибкахПроверкиАдреса(ОшибкиПроверкиАдреса)
	
	СообщенияПроверки = "";
	
	Для каждого Ошибка Из ОшибкиПроверкиАдреса Цикл
		СообщенияПроверки = СообщенияПроверки + Ошибка + Символы.ПС;
	КонецЦикла;
	
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(СообщенияПроверки, 1);
	
	СообщенияПроверки = НСтр("ru='Адрес не соответствует адресному классификатору:';uk='Адреса не відповідає адресному класифікатору:'") + Символы.ПС + СообщенияПроверки;
	
	Возврат СообщенияПроверки
	
КонецФункции

Функция ПредставленияОшибокПроверкиАдреса(РезультатПроверки)
	
	ОшибкиПроверки = Новый Массив;	
	
	Для каждого СтруктураОшибки Из РезультатПроверки.СписокОшибок Цикл
		ОшибкиПроверки.Добавить(СтруктураОшибки.Представление);
	КонецЦикла;

	Возврат	ОшибкиПроверки;
	
КонецФункции
	
#КонецОбласти

#КонецОбласти
