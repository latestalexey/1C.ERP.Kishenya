
#Область СлужебныеПроцедурыИФункции

Процедура ЗапретИзмененияИспользованияКадровогоУчетаПередЗаписью(Источник, Отказ) Экспорт
	
	// ERP начало: 00-00052344
	Возврат;
	// ERP конец
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.Значение = Ложь Тогда
		ВызватьИсключение НСтр("ru='Нельзя отказаться от ведения кадрового учета в программе. Действие не выполнено';uk='Не можна відмовитися від ведення кадрового обліку в програмі. Дію не виконано'");
	КонецЕсли;
		
КонецПроцедуры

Процедура ЗаполнитьЗначениеРеквизитаОрганизацияСправочникаСотрудникиПриОднофирменномУчетеРасширеннаяОбработкаЗаполнения(Источник, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка) Экспорт
	
	ЗарплатаКадры.ЗаполнитьЗначениеРеквизитаОрганизацияПриОднофирменномУчете(Источник, "ГоловнаяОрганизация"); 
	
КонецПроцедуры

Процедура УстановитьИспользованиеИндивидуальныхПравилПересчетаТарифныхСтавокСотрудниковПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыРасширенный.УстановитьИспользованиеИндивидуальныхПравилПересчетаТарифныхСтавок();
	
КонецПроцедуры

Процедура СоздатьТаблицуОбновляемыхРегистраторовСовокупныхТарифныхСтавокПередЗаписью(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимЗаписи <> РежимЗаписиДокумента.ОтменаПроведения Тогда 
		Возврат;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗарплатаКадрыРасширенный.СоздатьВТРегистраторыСовокупныхТарифныхСтавок(Источник.Ссылка, МенеджерВременныхТаблиц);
	Источник.ДополнительныеСвойства.Вставить("РегистраторыСовокупныхСтавокМенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПерезаполнитьВторичныеЗаписиЗначенийСовокупныхТарифныхСтавокПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Источник.ДополнительныеСвойства.Свойство("РегистраторыСовокупныхСтавокМенеджерВременныхТаблиц") Тогда
		Возврат;
	КонецЕсли;
	
	ЗарплатаКадрыРасширенный.ДополнитьВторичныеЗаписиРегистраторовСовокупныхТарифныхСтавокРегистраторов(Источник.ДополнительныеСвойства.РегистраторыСовокупныхСтавокМенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура УстановитьСдвигПериодаРегистраСПериодичностьюСекунда(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ДополнительныеСвойства.Свойство("НеСдвигатьПериодЗаписей") Тогда
		Возврат;
	КонецЕсли; 
	
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоВторичныйНабор") Тогда
		Возврат;
	КонецЕсли; 
	
	Если Источник.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Регистратор = Источник.Отбор.Регистратор.Значение;
	
	ТаблицаРегистра = Источник.ВыгрузитьКолонки();
	ИспользуютсяВторичныеЗаписи = ТаблицаРегистра.Колонки.Найти("ВторичнаяЗапись") <> Неопределено;
	
	СдвигПериода = ЗарплатаКадрыРасширенный.ЗначениеСдвигаПериодаЗаписиРегистра(Регистратор);
	
	Если СдвигПериода = Неопределено Тогда
		
		КонкурирующиеРегистраторы = ЗарплатаКадрыРасширенный.КонкурирующиеПоПериодуРегистраторыНачислений();
		Если КонкурирующиеРегистраторы.Найти(ТипЗнч(Регистратор)) = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		ВремяРегистрацииДокумента = Неопределено;
		Источник.ДополнительныеСвойства.Свойство("ВремяРегистрацииДокумента", ВремяРегистрацииДокумента);
		
		НеОпределенноеВремяРегистрацииДокумента = ТаблицаРегистра.СкопироватьКолонки("Период, Сотрудник");
		Для Каждого ЗаписьРегистра Из Источник Цикл
			
			Если ИспользуютсяВторичныеЗаписи И ЗаписьРегистра.ВторичнаяЗапись Тогда
				Продолжить;
			КонецЕсли;
			
			Если ВремяРегистрацииДокумента = Неопределено
				Или ВремяРегистрацииДокумента.Получить(ЗаписьРегистра.Период) = Неопределено Тогда
				
				ЗаполнитьЗначенияСвойств(НеОпределенноеВремяРегистрацииДокумента.Добавить(), ЗаписьРегистра, "Период, Сотрудник");
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если НеОпределенноеВремяРегистрацииДокумента.Количество() > 0 Тогда 
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			
			Если ВремяРегистрацииДокумента <> Неопределено Тогда
				ВремяРегистрацииДокумента = ОбщегоНазначенияКлиентСервер.СкопироватьСоответствие(ВремяРегистрацииДокумента);
			КонецЕсли;
			
			Движения = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Источник);
			ЗарплатаКадрыРасширенный.УстановитьВремяРегистрацииДокумента(Движения, НеОпределенноеВремяРегистрацииДокумента, Регистратор, "Период");
			
			Если ВремяРегистрацииДокумента = Неопределено Тогда
				ВремяРегистрацииДокумента = Источник.ДополнительныеСвойства.ВремяРегистрацииДокумента;
			Иначе
				
				Для каждого ОписаниеВремениРегистрации Из Источник.ДополнительныеСвойства.ВремяРегистрацииДокумента Цикл
					ВремяРегистрацииДокумента.Вставить(ОписаниеВремениРегистрации.Ключ, ОписаниеВремениРегистрации.Значение);
				КонецЦикла;
				
			КонецЕсли;
		
		КонецЕсли;
		
		Для Каждого ЗаписьРегистра Из Источник Цикл
			
			ДатаСобытия = НачалоДня(ЗаписьРегистра.Период);
			ВремяРегистрации = ВремяРегистрацииДокумента.Получить(ДатаСобытия);
			
			Если ИспользуютсяВторичныеЗаписи И ЗаписьРегистра.ВторичнаяЗапись Тогда 
				ЗаписьРегистра.Период = ДатаСобытия;
			Иначе 
				ЗаписьРегистра.Период = ВремяРегистрации;
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе 
		
		Для Каждого ЗаписьРегистра Из Источник Цикл
			
			ДатаСобытия = НачалоДня(ЗаписьРегистра.Период);
			ВремяРегистрации = ДатаСобытия + СдвигПериода;
			
			Если ИспользуютсяВторичныеЗаписи И ЗаписьРегистра.ВторичнаяЗапись Тогда 
				ЗаписьРегистра.Период = ДатаСобытия;
			Иначе 
				ЗаписьРегистра.Период = ВремяРегистрации;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВремяРегистрацииПриОтменеПроведенияДокументаОбработкаУдаленияПроведения(Источник, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Документ", Источник.Ссылка);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВремяРегистрацииДокументовПлановыхНачислений.Дата,
	               |	ВремяРегистрацииДокументовПлановыхНачислений.Сотрудник,
	               |	ВремяРегистрацииДокументовПлановыхНачислений.Документ,
	               |	ВремяРегистрацииДокументовПлановыхНачислений.ВремяРегистрации,
	               |	ЛОЖЬ КАК Проведен
	               |ИЗ
	               |	РегистрСведений.ВремяРегистрацииДокументовПлановыхНачислений КАК ВремяРегистрацииДокументовПлановыхНачислений
	               |ГДЕ
	               |	ВремяРегистрацииДокументовПлановыхНачислений.Документ = &Документ";
				   
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.ВремяРегистрацииДокументовПлановыхНачислений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Документ.Установить(Источник.Ссылка);
	
	Пока Выборка.Следующий() Цикл 
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

#Область ОбновитьСтроковыеСведенияФизическогоЛица

Процедура ОбновитьСтроковыеСведенияФизическогоЛицаПриЗаписиРегистраСведений(Источник, Отказ, Замещение) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;

	ПричинаОбновления = ПричинаОбновленияСтроковыхСведенийФизическогоЛица(Источник);
	Если ПричинаОбновления = Неопределено Тогда
	  Возврат;
	КонецЕсли;
	
	ФизическиеЛица = ФизическиеЛицаНабораЗаписей(Источник);
	   
	Для Каждого ФизическоеЛицо Из ФизическиеЛица Цикл 
		РегистрыСведений.СтроковыеСведенияФизическихЛиц.ОбновитьСтроковыеСведенияФизическогоЛица(ФизическоеЛицо, ПричинаОбновления);		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьСтроковыеСведенияФизическогоЛицаПриЗаписиСправочника(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	ПричинаОбновления = ПричинаОбновленияСтроковыхСведенийФизическогоЛица(Источник);
	Если ПричинаОбновления = Неопределено Тогда
	  Возврат;
	КонецЕсли;

	ФизическоеЛицо = Источник["Владелец"];	
	
	Если НЕ ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.СтроковыеСведенияФизическихЛиц.ОбновитьСтроковыеСведенияФизическогоЛица(ФизическоеЛицо, ПричинаОбновления);
	
КонецПроцедуры

Функция ПричинаОбновленияСтроковыхСведенийФизическогоЛица(Источник)
	
	ТипИсточника = ТипЗнч(Источник);
	
	ПричинаОбновления = Неопределено;
	
	Если ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ДокументыФизическихЛиц") Тогда
		ПричинаОбновления = "Документы";
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ЗнаниеЯзыковФизическихЛиц") Тогда	
		ПричинаОбновления = "ЗнаниеЯзыков";
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.НаградыФизическихЛиц") Тогда	
		ПричинаОбновления = "Награды";
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ПрофессииФизическихЛиц") Тогда	
		ПричинаОбновления = "Профессии";
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.РодственникиФизическихЛиц") Тогда	
		ПричинаОбновления = "СоставСемьи";
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.СпециальностиФизическихЛиц") Тогда	
		ПричинаОбновления = "Специальности";
	ИначеЕсли ТипИсточника = Тип("РегистрСведенийНаборЗаписей.ТрудоваяДеятельностьФизическихЛиц") Тогда	
		ПричинаОбновления = "ТрудоваяДеятельность";
	ИначеЕсли ТипИсточника = Тип("СправочникОбъект.ОбразованиеФизическихЛиц") Тогда	
		ПричинаОбновления = "Образование";
	КонецЕсли;
	
	Возврат ПричинаОбновления;	
	
КонецФункции
	
#КонецОбласти


Процедура ИспользоватьНачислениеЗарплатыПриЗаписи(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Константы.НеИспользоватьНачислениеЗарплаты.Установить(Не Источник.Значение);
	
	РасчетЗарплатыРасширенный.ЗаполнитьНастройкиРасчетаЗарплаты(Источник.Значение);
	
	ЗаполнитьНастройкиЗарплатаКадрыРасширенная(Источник.Значение);
	
	ЗаймыСотрудникам.ЗаполнитьНастройкиЗаймовСотрудникам(Источник.Значение);
	
	УправлениеШтатнымРасписанием.ЗаполнитьНастройкиШтатногоРасписания(Источник.Значение);
	
	Константы.ИспользоватьРасчетЗарплатыИСтатьиФинансированияЗарплата.Установить(Константы.ИспользоватьСтатьиФинансированияЗарплата.Получить() И Источник.Значение);
	
	ЗарплатаКадрыРасширенныйПереопределяемый.ПриУстановкеИспользованияЗарплатаКадры(Источник.Значение);
	
КонецПроцедуры

Функция ФизическиеЛицаНабораЗаписей(НаборЗаписей)
	
	ИмяПоляФизическоеЛицо = ИмяПоляФизическоеЛицоРегистраСведений(НаборЗаписей);
	ФизическиеЛица = НаборЗаписей.Выгрузить( , ИмяПоляФизическоеЛицо);
	ФизическиеЛица.Свернуть(ИмяПоляФизическоеЛицо);
	ФизическиеЛица = ФизическиеЛица.ВыгрузитьКолонку(ИмяПоляФизическоеЛицо);
	
	Возврат ФизическиеЛица;
	
КонецФункции 

Функция ИмяПоляФизическоеЛицоРегистраСведений(НаборЗаписей)
	
	ПолеФизическоеЛицо = "";
	
	МетаданныеНабора = НаборЗаписей.Метаданные();
	
	Если МетаданныеНабора.Измерения.Найти("ФизическоеЛицо") <> Неопределено Тогда
		ПолеФизическоеЛицо = "ФизическоеЛицо";
	ИначеЕсли МетаданныеНабора.Измерения.Найти("Физлицо") <> Неопределено Тогда
		ПолеФизическоеЛицо = "Физлицо";
	КонецЕсли;
	
	Возврат ПолеФизическоеЛицо;
	
КонецФункции 

Процедура ЗаполнитьНастройкиЗарплатаКадрыРасширенная(ИспользоватьРасчетЗарплатыРасширенная)
	НастройкиЗарплатаКадрыРасширенная = РегистрыСведений.НастройкиЗарплатаКадрыРасширенная.СоздатьНаборЗаписей();
	НастройкиЗарплатаКадрыРасширенная.Прочитать();
	НастройкиЗарплатаКадрыРасширенная.ДополнительныеСвойства.Вставить("ИспользоватьРасчетЗарплатыРасширенная", ИспользоватьРасчетЗарплатыРасширенная);
	НастройкиЗарплатаКадрыРасширенная.Записать();
КонецПроцедуры

Процедура ПроверитьНеобходимостьОбновленияПодчиненностиПодразделений(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник.ЭтоНовый() Тогда
		Источник.ДополнительныеСвойства.Вставить("НовоеПодразделение", Истина);
	Иначе
		
		ТекущийРодитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Источник.Ссылка, "Родитель");
		Если ТекущийРодитель <> Источник.Родитель Тогда
			Источник.ДополнительныеСвойства.Вставить("ОбновитьПодчиненностьПодразделенийОрганизаций", ТекущийРодитель);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьПодчиненностьПодразделенийОрганизаций(Источник, Отказ) Экспорт
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(Источник) Тогда
		Возврат;
	КонецЕсли; 
	
	СписокПодразделений = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Источник.Ссылка);
	Если Источник.ДополнительныеСвойства.Свойство("НовоеПодразделение")
		Или Источник.ДополнительныеСвойства.Свойство("ОбновитьПодчиненностьПодразделенийОрганизаций") Тогда
		
		РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.ОбновитьПодчиненностьПодразделений(СписокПодразделений);
		
		Если Источник.ДополнительныеСвойства.Свойство("ОбновитьПодчиненностьПодразделенийОрганизаций") Тогда
			
			РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.ОбновитьПодчиненностьПодразделений(СписокПодразделений);
			
			ПредыдущийРодитель = Источник.ДополнительныеСвойства.ОбновитьПодчиненностьПодразделенийОрганизаций;
			ТекущийРодитель = Источник.Родитель;
			
			РегистрыСведений.ПодчиненностьПодразделенийОрганизаций.ОбновитьПодчиненностьПодразделенийПриСменеРодителя(
				СписокПодразделений, ПредыдущийРодитель, ТекущийРодитель);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти