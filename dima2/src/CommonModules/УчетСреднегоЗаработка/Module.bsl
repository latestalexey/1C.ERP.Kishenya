////////////////////////////////////////////////////////////////////////////////
// Подсистема «Учет среднего заработка».
// 
// Серверные процедуры и функции.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Предоставляет значение среднего заработка.
//
// Параметры
//	Сотрудник - тип СправочникСсылка.Сотрудники,
//	ДатаНачалаСобытия - тип Дата
//	ДополнительныеПараметры - тип Структура, см. УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка
//
// Возвращаемое значение - Число, значение среднедневного или среднечасового заработка.
//
Функция СреднийЗаработок(Сотрудник, ДатаНачалаСобытия, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
	КонецЕсли;
	
	ДополнительныеПараметры.ДатаНачалаСобытия = ДатаНачалаСобытия;
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.НачалоПериода) 
		Или Не ЗначениеЗаполнено(ДополнительныеПараметры.ОкончаниеПериода) Тогда
		ПериодРасчета = УчетСреднегоЗаработка.ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(ДатаНачалаСобытия, Сотрудник, ДополнительныеПараметры.Начисление);
		ДополнительныеПараметры.НачалоПериода = ПериодРасчета.ДатаНачала;
		ДополнительныеПараметры.ОкончаниеПериода = ПериодРасчета.ДатаОкончания;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ПорядокРасчета = Неопределено Тогда
		ДополнительныеПараметры.ПорядокРасчета = УчетСреднегоЗаработкаКлиентСервер.ПорядокРасчетаОбщегоСреднегоЗаработка(ДатаНачалаСобытия);
	КонецЕсли;
	
	ДанныеДляРасчета = УчетСреднегоЗаработка.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(
		Сотрудник, 
		ДатаНачалаСобытия, 
		ДополнительныеПараметры.НачалоПериода, 
		ДополнительныеПараметры.ОкончаниеПериода, 
		ДополнительныеПараметры.ПорядокРасчета);
		
	ДополнительныеПараметры.Индексации = ДанныеДляРасчета.ДанныеОбИндексации;
	
	СреднийЗаработок = УчетСреднегоЗаработкаКлиентСервер.СреднийЗаработокОбщий(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДляРасчета.ДанныеОВремени, ДополнительныеПараметры);
	
	Возврат СреднийЗаработок;
	
КонецФункции

// Процедура выполняет регистрацию данных о начисленных суммах и отработанном времени
//  для использования при расчете среднего заработка.
//
// ВНИМАНИЕ. Перед выполнением метода движения по регистру расчетов Начисления и отработанному времени 
//	(в частности по корректировкам отработанного времени) должны быть записаны.
//
//	Параметры:
//		Движения - коллекция движений документа.
//		Отказ
//		МенеджерВременныхТаблиц - менеджер временных таблиц, содержащий таблицу.
//		ВТНачисления - с полями.
//			Сотрудник
//			ДатаДействия
//			Начисление - ПВР Начисления
//			Сумма
//
Процедура ЗарегистрироватьДанныеСреднегоЗаработка(Движения, Отказ, Начисления, ЗаписыватьДвижения = Ложь) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	СоздатьВТНачисленияПоТаблицеНачисления(МенеджерВременныхТаблиц, Начисления);

	УчетПособийСоциальногоСтрахованияРасширенный.ЗарегистрироватьДанныеСреднегоЗаработкаФСС(Движения, Отказ, МенеджерВременныхТаблиц, ЗаписыватьДвижения);

	ЗарегистрироватьДанныеОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ЗаписыватьДвижения);
	
КонецПроцедуры

// Конструирует структуру параметров для регистрации данных документа в учете среднего заработка.
//
Функция ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка(ИменаТаблиц = "Начисления,НачисленияПерерасчет") Экспорт
	
	МассивИмен = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаТаблиц);
	
	ДополнительныеПараметры = Новый Структура(
		"МесяцНачисления,
		|Таблицы");
	ДополнительныеПараметры.МесяцНачисления = "Ссылка.МесяцНачисления";
	
	ДополнительныеПараметры.Таблицы = Новый Структура;
	Для Каждого ИмяТаблицы Из МассивИмен Цикл
		ИменаПолей = Новый Структура(
			"ДатаДействия, 
			|Начисление, 
			|НачалоБазовогоПериода, 
			|ОкончаниеБазовогоПериода,
			|ДатаНачала, 
			|ДатаОкончания");
		ИменаПолей.ДатаДействия = "ДатаНачала";
		ИменаПолей.Начисление = "Начисление";
		ИменаПолей.НачалоБазовогоПериода = "ДатаНачала";
		ИменаПолей.ОкончаниеБазовогоПериода = "ДатаОкончания";
		ИменаПолей.ДатаНачала = "ДатаНачала";
		ИменаПолей.ДатаОкончания = "ДатаОкончания";
		ДополнительныеПараметры.Таблицы.Вставить(ИмяТаблицы, ИменаПолей);
	КонецЦикла;
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

// Функция формирует временные таблицы с данными документа начисления
//  для регистрации данных учета среднего заработка.
//
// Параметры:
//	- ДанныеДляПроведения - структура данных, накапливающая данные для проведения.
//	- ДокументСсылка - ссылка на документ, выполняющий начисления.
//	- ТаблицаНачислений - строка с именем (или именами через запятую) 
//			табличных частей документа с начислениями.
//	- ПолеДатыДействия - имя поля, содержащего период регистрации записей.
//	- ПолеГод - имя поля, содержащего год, за который регистрируются записи.
//
Процедура ЗаполнитьТаблицыДляРегистрацииДанныхСреднегоЗаработка(ДанныеДляПроведения, ДокументСсылка, ДополнительныеПараметры = Неопределено, СписокФизическихЛиц = Неопределено) Экспорт

	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыРегистрацииДанныхСреднегоЗаработка();
	КонецЕсли;

	// Метаданные документа используем для обращения к таблице.
	МетаданныеДокумента = Метаданные.НайтиПоТипу(ТипЗнч(ДокументСсылка));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	// Составляем текст запроса создания временной таблицы.
	ТекстЗапроса = "";
	ПерваяТаблица = Истина;
	Для Каждого КлючИЗначение Из ДополнительныеПараметры.Таблицы Цикл
		ИмяТаблицы = КлючИЗначение.Ключ;
		ИменаПолей = КлючИЗначение.Значение;
		Если Не ПерваяТаблица Тогда
			ТекстЗапроса = ТекстЗапроса + "
				|ОБЪЕДИНИТЬ ВСЕ
				|";
		КонецЕсли;
		ТекстОбъединения = 
			"ВЫБРАТЬ
			|	ТаблицаНачислений.Ссылка КАК ДокументСсылка,
			|	ТаблицаНачислений.ДатаДействия КАК ДатаДействия,
			|	ТаблицаНачислений.ПериодДействия КАК ПериодДействия,
			|	ТаблицаНачислений.МесяцНачисления КАК МесяцНачисления,
			|	ТаблицаНачислений.Начисление КАК Начисление,
			|	ТаблицаНачислений.НачалоБазовогоПериода КАК НачалоБазовогоПериода,
			|	ТаблицаНачислений.ОкончаниеБазовогоПериода КАК ОкончаниеБазовогоПериода,
			|	ТаблицаНачислений.ДатаНачала КАК ДатаНачала,
			|	ТаблицаНачислений.ДатаОкончания КАК ДатаОкончания,
			|	ТаблицаНачислений.НомерСтроки КАК НомерСтроки,
			|	ТаблицаНачислений.Сотрудник КАК Сотрудник,
			|	ТаблицаНачислений.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо,
			|	ТаблицаНачислений.Ссылка.Организация КАК Организация,
			|	ТаблицаНачислений.Результат КАК Сумма,
			|	ТаблицаНачислений.ИдентификаторСтрокиВидаРасчета КАК ИдентификаторСтроки,
			|	ТаблицаНачислений.Сторно КАК Сторно
			|ПОМЕСТИТЬ ВТЗаписиНачислений
			|ИЗ
			|	#ТаблицаНачислений КАК ТаблицаНачислений
			|ГДЕ
			|	ТаблицаНачислений.Ссылка = &ДокументСсылка";
		
		Если СписокФизическихЛиц <> Неопределено Тогда
			
			ТекстОбъединения = ТекстОбъединения + "
				|	И ТаблицаНачислений.Сотрудник.ФизическоеЛицо В (&СписокФизическихЛиц)";
			
		КонецЕсли;
		
		Если Не ПерваяТаблица Тогда
			
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ПОМЕСТИТЬ ВТЗаписиНачислений", "");
			
			Если СписокФизическихЛиц <> Неопределено Тогда
				Запрос.УстановитьПараметр("СписокФизическихЛиц", СписокФизическихЛиц);
			КонецЕсли;
			
		КонецЕсли;
		
		ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "#ТаблицаНачислений", МетаданныеДокумента.ПолноеИмя() + "." + ИмяТаблицы);
		Для Каждого КлючИЗначениеПолей Из ИменаПолей Цикл
			ИмяПоля = КлючИЗначениеПолей.Ключ;
			ТекстОбращения = КлючИЗначениеПолей.Значение;
			Если ТекстОбращения = Неопределено Тогда
				ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений." + ИмяПоля + " КАК " + ИмяПоля, "НЕОПРЕДЕЛЕНО КАК " + ИмяПоля);
			Иначе
				ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений." + ИмяПоля + " КАК " + ИмяПоля, "ТаблицаНачислений." + ТекстОбращения + " КАК " + ИмяПоля);
			КонецЕсли;
		КонецЦикла;
		ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.МесяцНачисления КАК МесяцНачисления", "ТаблицаНачислений." + ДополнительныеПараметры.МесяцНачисления + " КАК МесяцНачисления");
		РеквизитыТабличнойЧасти = МетаданныеДокумента.ТабличныеЧасти[ИмяТаблицы].Реквизиты;
		Если РеквизитыТабличнойЧасти.Найти("Сторно") = Неопределено Тогда
			ТекстОбъединения = СтрЗаменить(ТекстОбъединения, "ТаблицаНачислений.Сторно КАК Сторно", "ЛОЖЬ КАК Сторно");
		КонецЕсли;
		
		// Добавляем объединение в общий запрос.
		ТекстЗапроса = ТекстЗапроса + ТекстОбъединения;
		ПерваяТаблица = Ложь;
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.Выполнить();
	
	// Если используются источники финансирования дополняем результатом распределения начислений.
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный") Тогда
		// Дополнить данными о распределении начислений по источникам финансирования.
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	РаспределениеНачислений.СтатьяФинансирования,
			|	РаспределениеНачислений.СтатьяРасходов,
			|	РаспределениеНачислений.СпособОтраженияЗарплатыВБухучете,
			|	РаспределениеНачислений.ОблагаетсяЕНВД,
			|	ЕСТЬNULL(РаспределениеНачислений.Результат, ЗаписиНачислений.Сумма) КАК Сумма,
			|	ЗаписиНачислений.*
			|ИЗ
			|	ВТЗаписиНачислений КАК ЗаписиНачислений
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #РаспределениеРезультатовНачислений КАК РаспределениеНачислений
			|		ПО ЗаписиНачислений.ИдентификаторСтроки = РаспределениеНачислений.ИдентификаторСтроки
			|			И ЗаписиНачислений.ДокументСсылка = РаспределениеНачислений.Ссылка";
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#РаспределениеРезультатовНачислений", МетаданныеДокумента.ПолноеИмя() + ".РаспределениеРезультатовНачислений");
	Иначе
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ЗНАЧЕНИЕ(Справочник.СтатьиФинансированияЗарплата.ПустаяСсылка) КАК СтатьяФинансирования,
			|	ЗНАЧЕНИЕ(Справочник.СтатьиРасходовЗарплата.ПустаяСсылка) КАК СтатьяРасходов,
			|	ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка) КАК СпособОтраженияЗарплатыВБухучете,
			|	ЛОЖЬ КАК ОблагаетсяЕНВД,
			|	ЗаписиНачислений.Сумма КАК Сумма,
			|	ЗаписиНачислений.*
			|ИЗ
			|	ВТЗаписиНачислений КАК ЗаписиНачислений";
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеДляПроведения.НачисленияДляСреднегоЗаработка = ПустаяТаблицаДляРегистрацииДанныхСреднегоЗаработка();
	
	// Проверяем тип регистратора
	Если Не ДанныеДляПроведения.НачисленияДляСреднегоЗаработка.Колонки.ДокументСсылка.ТипЗначения.СодержитТип(ТипЗнч(ДокументСсылка)) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Тип «%1» не включен в состав типов колонки ДокументСсылка таблицы для регистрации данных среднего заработка.';uk='Тип «%1» не включений до складу типів колонки ДокументСсылка таблиці для реєстрації даних середнього заробітку.'"),
			ТипЗнч(ДокументСсылка));
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеДляПроведения.НачисленияДляСреднегоЗаработка.Добавить(), Выборка);
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет заполнение значений специализированных показателей 
//  учета среднего заработка.
//
// Параметры:
//		ТаблицаПоказателей - таблица показателей, которые не удалось заполнить по данным подсистемы.
//
Процедура ЗаполнитьЗначенияПоказателейРасчетаЗарплаты(ТаблицаПоказателей) Экспорт
	ПоказателиОбщегоСреднегоЗаработка = УчетСреднегоЗаработкаКлиентСервер.ПоказателиОбщегоСреднегоЗаработка();
	
	Для Каждого СтрокаПоказателей Из ТаблицаПоказателей Цикл
		Если ПоказателиОбщегоСреднегоЗаработка.Найти(СтрокаПоказателей.Показатель) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДополнительныеПараметры = УчетСреднегоЗаработкаКлиентСервер.ДополнительныеПараметрыРасчетаСреднегоЗаработка();
		ДополнительныеПараметры.ПоЧасам = СтрокаПоказателей.ВремяВЧасах;
		Если СтрокаПоказателей.Показатель = Справочники.ПоказателиРасчетаЗарплаты.СреднийЗаработокОтпуск Тогда
			ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление100Отпускные
		Иначе
			ДополнительныеПараметры.ПорядокРасчета = Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010
		КонецЕсли;	
		СреднийЗаработок = УчетСреднегоЗаработка.СреднийЗаработок(СтрокаПоказателей.Сотрудник, СтрокаПоказателей.ДатаНачала, ДополнительныеПараметры);
		
		СтрокаПоказателей.Значение = СреднийЗаработок;
		СтрокаПоказателей.КомандаРасшифровки = УчетСреднегоЗаработкаКлиентСервер.ПредставлениеКомандыРасшифровки();
		СтрокаПоказателей.ЗначениеОпределено = Истина;
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет заполнение представления команды расшифровки специализированных показателей 
//  учета среднего заработка.
//
// Параметры:
//		ДокументСсылка
//		ВидРасчетаИнфо - Информация о виде расчета, полученная с помощью метода
//		                 ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета.
//		СтрокаНачислений - строка таблицы "Начисления".
//		ДанныеПоказателей - Таблица показателей.
//		РежимРаботы - режим работы таблицы с видами расчетов
//			0 - режим ввода штатного расписания - вводятся максимальные 
//				и минимальные значения ("вилка") условно-постоянных показателей
//			1 - режим ввода плановых начислений - вводятся значения 
//				условно-постоянных показателей
//			2 - режим ввода начислений в документе-начислятеле - вводятся значения всех 
//				показателей, отображаемых при виде расчета.
//		ОтображатьТекущиеЗначения - признак того, что в форме отображаются действующие на настоящий 
//			момент показатели начислений. Применяется, например, в документах кадровых переводов.
//			По умолчанию - Ложь
//		ДокументСсылка - расчетный документ.
//
Процедура ЗаполнитьДанныеПоказателейРасчетаЗарплаты(ВидРасчетаИнфо, СтрокаНачислений, ДанныеПоказателей, РежимРаботы, ОтображатьТекущиеЗначения = Ложь, ДокументСсылка = Неопределено) Экспорт
	
	Если РежимРаботы = 0 Или РежимРаботы = 1 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипыДокументовРасчетаПоСреднемуЗаработку().Найти(ТипЗнч(ДокументСсылка)) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказателиСреднего = УчетСреднегоЗаработкаКлиентСервер.ПоказателиОбщегоСреднегоЗаработка();
	ПоказателиСреднего.Добавить(ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокФСС"));
	
	СчетчикПоказателей = 1;
	Для Каждого СтрокаПоказателя Из ВидРасчетаИнфо.Показатели Цикл
		Если ПоказателиСреднего.Найти(СтрокаПоказателя.Показатель) <> Неопределено Тогда
			ОтображатьРасшифровку = Истина;
			Если СтрокаНачислений.Свойство("ДокументОснование") И ЗначениеЗаполнено(СтрокаНачислений.ДокументОснование) Тогда
				ОтображатьРасшифровку = Ложь;
			КонецЕсли;
			Если ОтображатьРасшифровку Тогда
				СтрокаНачислений["КомандаРасшифровки" + СчетчикПоказателей] = УчетСреднегоЗаработкаКлиентСервер.ПредставлениеКомандыРасшифровки();
			КонецЕсли;
		КонецЕсли;			
		СчетчикПоказателей = СчетчикПоказателей + 1;
	КонецЦикла;
	
КонецПроцедуры

// Предназначена для получения настроек подсистемы.
//
// Возвращаемое значение - структура с именем настройки в качестве ключа.
//
Функция НастройкиРасчетаСреднегоЗаработка() Экспорт
	
	НастройкиРасчетаСреднегоЗаработка = РегистрыСведений.НастройкиРасчетаСреднегоЗаработка.СоздатьМенеджерЗаписи();
	НастройкиРасчетаСреднегоЗаработка.Прочитать();
	
	СтруктураНастроек = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
							НастройкиРасчетаСреднегоЗаработка, Метаданные.РегистрыСведений.НастройкиРасчетаСреднегоЗаработка);
	
	Возврат СтруктураНастроек;
	
КонецФункции

// Процедура заполняет настройки учета среднего заработка в зависимости 
//  от количества в информационной базе специализированных начислений.
//
Процедура ЗаполнитьНастройкиУчетаСреднегоЗаработка() Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК СреднийЗаработокФССРазличаетсяПоПорядкуРасчета
	|ПОМЕСТИТЬ ВТНастройкаСреднегоФСС
	|ИЗ
	|	ПланВидовРасчета.Начисления.СреднийЗаработокФСС КАК НастройкаСреднегоФСС
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкаСреднегоФСС.Ссылка
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НастройкаСреднегоФСС.ПорядокРасчета) < &КоличествоПорядковРасчетаФСС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ОбщийЗаработок)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОсновныеНачисленияБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияПроцентом)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииПроцентомИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияПроцентом)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииПроцентомБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяПроцентом)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодПроцентомИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяПроцентом)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодПроцентомБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияФиксированнойСуммой)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииСуммойИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияФиксированнойСуммой)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииСуммойБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяФиксированнойСуммой)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодСуммойИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение = ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ПремияГодоваяФиксированнойСуммой)
	|					И НЕ НастройкаСреднегоОбщего.Индексируется
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьПремииЗаГодСуммойБезИндексацииИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ПоказателиНачисления.Показатель = &СреднийЗаработокОбщий
	|					И НЕ Начисления.УчетВремениВЧасах
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноДнейИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ПоказателиНачисления.Показатель = &СреднийЗаработокОбщий
	|					И Начисления.УчетВремениВЧасах
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноЧасовИстина,
	|	СУММА(ВЫБОР
	|			КОГДА НастройкаСреднегоОбщего.Значение В (&Премии)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноПоПятидневкеИстина,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыОтпусков.СпособРасчетаОтпуска В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВРабочихДнях), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОтпуска.ВКалендарныхИлиРабочихДняхВЗависимостиОтТрудовогоДоговора))
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК ИспользоватьОтработаноПоШестидневкеИстина,
	|	ЕСТЬNULL(МАКСИМУМ(НастройкаСреднегоФСС.СреднийЗаработокФССРазличаетсяПоПорядкуРасчета), ЛОЖЬ) КАК СреднийЗаработокФССРазличаетсяПоПорядкуРасчета
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	ПланВидовРасчета.Начисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкаСреднегоОбщего
	|		ПО (НастройкаСреднегоОбщего.Ссылка = Начисления.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.Показатели КАК ПоказателиНачисления
	|		ПО (ПоказателиНачисления.Ссылка = Начисления.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыОтпусков КАК ВидыОтпусков
	|		ПО (ВидыОтпусков.Ссылка = Начисления.ВидОтпуска)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНастройкаСреднегоФСС КАК НастройкаСреднегоФСС
	|		ПО (ИСТИНА)
	|ГДЕ
	|	НЕ Начисления.ПометкаУдаления
	|	И НЕ Начисления.ВАрхиве
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОсновныеНачисленияБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОсновныеНачисленияБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииПроцентомИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииПроцентом,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииПроцентомБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииПроцентомБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодПроцентомИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодПроцентом,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодПроцентомБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодПроцентомБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииСуммойИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииСуммой,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииСуммойБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииСуммойБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодСуммойИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодСуммой,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьПремииЗаГодСуммойБезИндексацииИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПремииЗаГодСуммойБезИндексации,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноДнейИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОтработаноДней,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноЧасовИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ИСТИНА В
	|						(ВЫБРАТЬ ПЕРВЫЕ 1
	|							ИСТИНА
	|						ИЗ
	|							Справочник.ГрафикиРаботыСотрудников
	|						ГДЕ
	|							Справочник.ГрафикиРаботыСотрудников.СуммированныйУчетРабочегоВремени)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ
	|	КОНЕЦ КАК ИспользоватьОтработаноЧасов,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноПоПятидневкеИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОтработаноПоПятидневке,
	|	ВЫБОР
	|		КОГДА Начисления.ИспользоватьОтработаноПоШестидневкеИстина > 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьОтработаноПоШестидневке,
	|	Начисления.СреднийЗаработокФССРазличаетсяПоПорядкуРасчета КАК СреднийЗаработокФССРазличаетсяПоПорядкуРасчета
	|ИЗ
	|	ВТНачисления КАК Начисления");
	
	Запрос.УстановитьПараметр("СреднийЗаработокОбщий", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднийЗаработокОбщий"));
	Запрос.УстановитьПараметр("КоличествоПорядковРасчетаФСС", Метаданные.Перечисления.ПорядокРасчетаСреднегоЗаработкаФСС.ЗначенияПеречисления.Количество());
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	// Запись настроек в регистр сведений.
	Настройки = РегистрыСведений.НастройкиРасчетаСреднегоЗаработка.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	
	ЗаполнитьЗначенияСвойств(Настройки, Выборка);
	
	Настройки.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Функция запрашивает период расчета среднего заработка для указанного начисления (или удержания)
// с учетом периода работы сотрудника.
//
Функция ПериодРасчетаОбщегоСреднегоЗаработкаСотрудника(ДатаНачалаСобытия, Сотрудник, ВидРасчета = Неопределено) Экспорт
	// Период, определяемый по начислению корректируется с учетом даты приема на работу сотрудника.
	ПериодРасчета = Новый СтандартныйПериод;
	
	// Применяются следующие условия.
	// 1. Если сотрудник принят позже, чем начало периода расчета среднего заработка, 
	// то началом периода расчета становится дата его приема на работу.
	// 2. Если сотрудник принят в том же месяце, что и начало события, 
	// то периодом расчета среднего заработка является месяц начала события.
	// Определим дату приема на работу сотрудника и ограничим ею начало периода расчета среднего заработка.
	// Если же дата приема на работу не заполнена, заполняем период без ограничений.
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ДатаПриема, ГрафикРаботы");
	Если КадровыеДанные.Количество() = 0 Тогда
		// Сотрудник не принят на работу.
		Возврат ПериодРасчета;
	Иначе
		Если ЗначениеЗаполнено(КадровыеДанные[0].ДатаПриема) И КадровыеДанные[0].ДатаПриема > ДатаНачалаСобытия Тогда
			// Дата начала события раньше, чем сотрудник принят на работу.
			ВызватьИсключение НСтр("ru='Не удалось определить период расчета среднего заработка, так как сотрудник принят на работу позже даты события.';uk='Не вдалося визначити період розрахунку середнього заробітку, так як працівник прийнятий на роботу пізніше дати події.'");
		КонецЕсли;
	КонецЕсли;
	
	// Устанавливаем привилегированный режим, т.к. возможны обращения к плану видов расчета.
	УстановитьПривилегированныйРежим(Истина);
	
	ПериодПоВидуРасчета = УчетСреднегоЗаработкаКлиентСервер.ПериодРасчетаОбщегоСреднегоЗаработка(ДатаНачалаСобытия, ВидРасчета);
	
	// Корректируем период расчета.
	ПериодРасчета.ДатаНачала = ПериодПоВидуРасчета.ДатаНачала;
	ПериодРасчета.ДатаОкончания = ПериодПоВидуРасчета.ДатаОкончания;
	НеполныйКалендарныйМесяц = Ложь;	
	Если ДобавитьМесяц(НачалоМесяца(ДатаНачалаСобытия),-1) < КадровыеДанные[0].ДатаПриема Тогда
		ПериодРасчета.ДатаНачала = НачалоМесяца(КадровыеДанные[0].ДатаПриема);
		НеполныйКалендарныйМесяц = Истина; 
	КонецЕсли;	

	Если ПериодРасчета.ДатаНачала <= КадровыеДанные[0].ДатаПриема И ПериодРасчета.ДатаОкончания > КадровыеДанные[0].ДатаПриема И НЕ НеполныйКалендарныйМесяц Тогда
		Если Не (ВидРасчета = Неопределено) И Не (ВидРасчета = ПланыВидовРасчета.Начисления.ПустаяСсылка())  И НЕ(ТипЗнч(ВидРасчета) = Тип("ПланВидовРасчетаСсылка.Удержания")) Тогда
			МассивВидыОтпуска = Новый Массив;
			МассивВидыОтпуска = ПланыВидовРасчета.Начисления.КатегорииОплатыОтпуска();
				Если Не МассивВидыОтпуска.Найти(ВидРасчета.КатегорияНачисленияИЛИНеоплаченногоВремени) = Неопределено Тогда
					Если НачалоМесяца(КадровыеДанные[0].ДатаПриема) < КадровыеДанные[0].ДатаПриема Тогда
						Запрос = Новый Запрос;
						МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
						Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
						
						Запрос.УстановитьПараметр("ГрафикРаботы", КадровыеДанные[0].ГрафикРаботы);
						Запрос.УстановитьПараметр("Период", КадровыеДанные[0].ДатаПриема);
						
						Запрос.Текст =
						"ВЫБРАТЬ
						|	&ГрафикРаботы КАК ГрафикРаботы,
						|	НАЧАЛОПЕРИОДА(&Период, МЕСЯЦ) КАК ДатаНачалаПериода,
						|	ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, -1) КАК ДатаОкончанияПериода
						|";
						
						ТаблицаГрафиков = Запрос.Выполнить().Выгрузить();
						
						УчетРабочегоВремени.СоздатьВТВремяПоГрафикам(ТаблицаГрафиков, Запрос.МенеджерВременныхТаблиц);
						
						Запрос.Текст = "ВЫБРАТЬ
						|	ВремяПоГрафикам.ОтработаноДней КАК РабочихДней
						|ИЗ
						|	ВТВремяПоГрафикам КАК ВремяПоГрафикам";
						
						Выборка = Запрос.Выполнить().Выбрать();
						Если Выборка.Следующий() Тогда
							Если Выборка.РабочихДней > 0 Тогда
								// принят не первого числа и не в первый рабочий день по графику
								ПериодРасчета.ДатаНачала = ДобавитьМесяц(НачалоМесяца(КадровыеДанные[0].ДатаПриема),1);	
							Иначе
								// принят не первого числа, но в первый рабочий день по графику
								ПериодРасчета.ДатаНачала = НачалоМесяца(КадровыеДанные[0].ДатаПриема);
							КонецЕсли;
						КонецЕсли;
					Иначе
						// принят первого числа
						ПериодРасчета.ДатаНачала = КадровыеДанные[0].ДатаПриема;	
					КонецЕсли;
				КонецЕсли;
		Иначе		
			ПериодРасчета.ДатаНачала = НачалоМесяца(КадровыеДанные[0].ДатаПриема);
		КонецЕсли;	
	ИначеЕсли ПериодРасчета.ДатаОкончания < КадровыеДанные[0].ДатаПриема Тогда
		ПериодРасчета.ДатаНачала = НачалоМесяца(КадровыеДанные[0].ДатаПриема);
		ПериодРасчета.ДатаОкончания = КонецМесяца(КадровыеДанные[0].ДатаПриема);
	КонецЕсли;
	
	Возврат ПериодРасчета;
	
КонецФункции

// Добавляет в структуру данных для проведения поля, необходимые для заполнения результатов расчета займов.
//
// Параметры:
//	ДанныеДляПроведения - см. РасчетЗарплатыРасширенный.СоздатьДанныеДляПроведенияНачисленияЗарплаты.
//
Процедура ДополнитьОписаниеДанныхДляПроведения(ДанныеДляПроведения) Экспорт
	
	ДанныеДляПроведения.Вставить("НачисленияДляСреднегоЗаработка");
	
КонецПроцедуры

// Функция определяет факт использования в формуле начисления показателя среднечасовой заработок.
//
// Параметры
//	Начисление - ПланВидовРасчетаСсылка.Начисления.
//
// Возвращаемое значение - булево, Истина - если используется, Ложь в противном случае.
//
Функция НачислениеИспользуетСреднечасовойЗаработок(Начисление) Экспорт
	
	СреднечасовойЗаработок = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПоказателиРасчетаЗарплаты.СреднечасовойЗаработок");
	
	Если СреднечасовойЗаработок = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ПланВидовРасчета.Начисления.Показатели КАК Показатели
		|ГДЕ
		|	Показатели.Ссылка = &Начисление
		|	И Показатели.Показатель = &СреднечасовойЗаработок");
		
	Запрос.УстановитьПараметр("Начисление", Начисление);
	Запрос.УстановитьПараметр("СреднечасовойЗаработок", СреднечасовойЗаработок);
	
	Возврат Не Запрос.Выполнить().Пустой();
		
КонецФункции

// Определяет статью финансирования, используемую для сотрудника по умолчанию.
//
Функция СтатьяФинансированияПоУмолчанию(Сотрудник, ДатаАктуальности, Начисление = Неопределено) Экспорт
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	&ДатаАктуальности КАК Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка = &Сотрудник";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.Выполнить();
	
	// Из кадрового учета получаем организацию и подразделение.
	Описатель = КадровыйУчет.ОписательВременныхТаблицДляСоздатьВТКадровыеДанныеСотрудников(МенеджерВременныхТаблиц, "ВТСотрудники");
	КадровыйУчет.СоздатьВТКадровыеДанныеСотрудников(Описатель, Истина, "Организация,Подразделение,ТерриторияВыполненияРаботВОрганизации");
	
	// Формируем временную таблицу с настройками бухучета.
	Если Начисление <> Неопределено Тогда
		ОтражениеЗарплатыВБухучете.СоздатьВТСведенияОБухучетеНачисленийСотрудников(МенеджерВременныхТаблиц, "ВТКадровыеДанныеСотрудников", "Сотрудник,Период", , , Начисление);
		ИмяВТ = "ВТСведенияОБухучетеНачисленийСотрудников";
	Иначе
		ОтражениеЗарплатыВБухучетеРасширенный.СоздатьВТСведенияОБухучетеЗарплатыСотрудников(МенеджерВременныхТаблиц, "ВТКадровыеДанныеСотрудников", "Сотрудник,Период");
		ИмяВТ = "ВТСведенияОБухучетеЗарплатыСотрудников";
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СведенияОБухучете.СтатьяФинансирования
	|ИЗ
	|	#ИмяВТ КАК СведенияОБухучете";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяВТ", ИмяВТ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.СтатьяФинансирования;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#Область ПолучениеДанныхДляРасчетаСреднегоЗаработка

// Возвращает структуру содержащую таблицы значений
// с данными о начислениях, отработанном времени и
// коэффициентах для расчета среднего заработка.
//      	 
// Параметры:
//      Сотрудник 
//		НачалоПериода - начало периода расчета среднего заработка.
//		ОкончаниеПериода - окончание периода расчета среднего заработка.
//		ПорядокРасчета - правила расчета общего среднего заработка.
//		ОтборМесяцев - необязательный, массив дат начала месяцев, 
//			если указан, данные будут получены только за эти месяцы.
//
// Возвращаемое значение:
// 		ДанныеДляРасчетаСреднего - структура, содержащая следующие поля:
//		ДанныеОНачислениях - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			СоставнаяЧасть
//			Индексируется
//			Сумма
//			Год
//			ДатаНачалаБазовогоПериода
//			КоличествоМесяцев.
//
//		ДанныеОВремени - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			ОтработаноДнейПятидневка
//			ОтработаноЧасовПятидневка
//			НормаДнейПроизводственныйКалендарь
//			НормаЧасовПроизводственныйКалендарь
//			ОтработаноДнейКалендарных
//			ОтработаноДней
//			ОтработаноДнейШестидневка.
//
//		ДанныеОбИндексации - таблица значений с колонками.
//			Сотрудник
//			Период
//			КоэффициентИндексации 
//
Функция ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудника(Сотрудник, ДатаНачалаСобытия, НачалоПериода, ОкончаниеПериода, ПорядокРасчета, ОтборМесяцев = Неопределено, ИсключаемыйРегистратор = Неопределено, УчитыватьКорректировки = Истина) Экспорт
	
	ИсходныеДанные = ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка();
	
	СтрокаТаблицы = ИсходныеДанные.Добавить();
	СтрокаТаблицы.Сотрудник = Сотрудник;
	СтрокаТаблицы.ПорядокРасчета = ПорядокРасчета;
	СтрокаТаблицы.ДатаНачалаСобытия = ДатаНачалаСобытия;
	СтрокаТаблицы.НачалоПериодаРасчетаСреднего = НачалоПериода;
	СтрокаТаблицы.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
	
	Возврат ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудников(ИсходныеДанные, ОтборМесяцев, ИсключаемыйРегистратор, УчитыватьКорректировки);
	
КонецФункции

// Возвращает структуру, содержащую таблицы значений с данными о начислениях, 
// отработанном времени и коэффициентах индексации для расчета среднего заработка.
//      	 
// Параметры:
//      ДокументСсылка - ссылка на документ расчета.
//		ТаблицаСотрудники - таблица значений с колонками.
//			Сотрудник: должно быть непустым
//			ДатаНачалаСобытия
//          НачалоПериодаРасчетаСреднего
//			ОкончаниеПериодаРасчетаСреднего.
//
// Возвращаемое значение:
// 		ДанныеДляРасчетаСреднего - структура, содержащая следующие поля:
//		ДанныеОНачислениях - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			СоставнаяЧасть
//			Индексируется
//			Сумма
//			ДатаНачалаБазовогоПериода
//			КоличествоМесяцев.
//
//		ДанныеОВремени - таблица значений с колонками.
//			Сотрудник
//			Период
//			ПорядокРасчета
//			НормаДнейПроизводственныйКалендарь
//			НормаЧасовПроизводственныйКалендарь
//			ОтработаноДнейКалендарных
//			ОтработаноДней
//
//		ДанныеОбИндексации - таблица значений с колонками.
//			Сотрудник
//			Период
//			КоэффициентИндексации 
//
Функция ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудников(ТаблицаСотрудники, ОтборМесяцев = Неопределено, ИсключаемыйРегистратор = Неопределено, УчитыватьКорректировки = Истина) Экспорт
	Перем МинимальнаяДатаНачала;
	Перем МаксимальнаяДатаОкончания;
	
	МесяцыРасчетаСотрудников = Новый ТаблицаЗначений;
	МесяцыРасчетаСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	МесяцыРасчетаСотрудников.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	МесяцыРасчетаСотрудников.Колонки.Добавить("ПорядокРасчетаСреднегоОтбор", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	МесяцыРасчетаСотрудников.Колонки.Добавить("Месяц", Новый ОписаниеТипов("Дата"));
	
	// Определяем границы расчета среднего.
	НайтиГраницыПериодаРасчетаОбщегоСреднегоЗаработка(ТаблицаСотрудники, МинимальнаяДатаНачала, МаксимальнаяДатаОкончания);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(МенеджерВременныхТаблиц, МинимальнаяДатаНачала, МаксимальнаяДатаОкончания);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПериодыРасчетаСреднегоСотрудников", ТаблицаСотрудники);
	Запрос.УстановитьПараметр("ОтборМесяцев", ОтборМесяцев);
	Запрос.УстановитьПараметр("ПоВсемМесяцам", ОтборМесяцев = Неопределено);
	Запрос.УстановитьПараметр("ДатаНачала", МинимальнаяДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", МаксимальнаяДатаОкончания);
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.УстановитьПараметр("УчитыватьКорректировки", УчитыватьКорректировки);
	Запрос.УстановитьПараметр("ГодовыеПремии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	
	// Общий заработок, без учета годовых премий.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПериодыРасчета.Сотрудник,
		|	ПериодыРасчета.ДатаНачалаСобытия,
		|	ПериодыРасчета.НачалоПериодаРасчетаСреднего,
		|	ПериодыРасчета.ОкончаниеПериодаРасчетаСреднего,
		|	ПериодыРасчета.ПорядокРасчета
		|ПОМЕСТИТЬ ВТПериодыРасчетаСреднегоСотрудников
		|ИЗ
		|	&ПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборГодовыхПремий.Сотрудник,
		|	ГОД(ДОБАВИТЬКДАТЕ(ОтборГодовыхПремий.ДатаНачалаСобытия, ГОД, -1)) КАК ГодовыеПремииЗаГод,
		|	ОтборГодовыхПремий.ПорядокРасчета
		|ПОМЕСТИТЬ ВТОтборГодовыхПремий
		|ИЗ
		|	ВТПериодыРасчетаСреднегоСотрудников КАК ОтборГодовыхПремий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистра.Период,
		|	ДанныеРегистра.Сотрудник,
		|	ДанныеРегистра.ПорядокРасчета,
		|	ДанныеРегистра.СоставнаяЧасть,
		|	ДанныеРегистра.СтатьяФинансирования,
		|	ДанныеРегистра.Индексируется,
		|	ДанныеРегистра.Год,
		|	ДанныеРегистра.ДатаНачалаБазовогоПериода,
		|	ДанныеРегистра.КоличествоМесяцев,
		|	ДанныеРегистра.ПереносДанных,
		|	СУММА(ДанныеРегистра.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТДанныеДляРасчетаСреднего
		|ИЗ
		|	(ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ДанныеРегистра.Период, МЕСЯЦ) КАК Период,
		|		ДанныеРегистра.Сотрудник КАК Сотрудник,
		|		ДанныеРегистра.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеРегистра.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ДанныеРегистра.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ДанныеРегистра.Индексируется КАК Индексируется,
		|		ДанныеРегистра.Год КАК Год,
		|		ДанныеРегистра.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|		ДанныеРегистра.КоличествоМесяцев КАК КоличествоМесяцев,
		|		ВЫБОР
		|			КОГДА ДанныеРегистра.Регистратор ССЫЛКА Документ.ПереносДанных
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ПереносДанных,
		|		ДанныеРегистра.Сумма КАК Сумма
		|	ИЗ
		|		РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеРегистра
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборГодовыхПремий КАК Отбор
		|			ПО (Отбор.Сотрудник = ДанныеРегистра.Сотрудник)
		|				И (Отбор.ПорядокРасчета = ДанныеРегистра.ПорядокРасчета)
		|				И (ДанныеРегистра.Регистратор <> &ИсключаемыйРегистратор)
		|				И (ВЫБОР
		|					КОГДА ДанныеРегистра.СоставнаяЧасть В (&ГодовыеПремии)
		|						ТОГДА Отбор.ГодовыеПремииЗаГод = ДанныеРегистра.Год И ГОД(ДанныеРегистра.ДатаНачалаБазовогоПериода) < ГОД(ДанныеРегистра.Период)
		|					ИНАЧЕ ДанныеРегистра.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
		|				КОНЕЦ)) КАК ДанныеРегистра
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеРегистра.Период,
		|	ДанныеРегистра.Сотрудник,
		|	ДанныеРегистра.ПорядокРасчета,
		|	ДанныеРегистра.СоставнаяЧасть,
		|	ДанныеРегистра.СтатьяФинансирования,
		|	ДанныеРегистра.Индексируется,
		|	ДанныеРегистра.Год,
		|	ДанныеРегистра.ДатаНачалаБазовогоПериода,
		|	ДанныеРегистра.КоличествоМесяцев,
		|	ДанныеРегистра.ПереносДанных
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДанныеРегистра.Сумма) <> 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Месяцы.Период КАК Месяц
		|ПОМЕСТИТЬ ВТМесяцы
		|ИЗ
		|	ВТПериоды КАК Месяцы
		|ГДЕ
		|	(Месяцы.Период В (&ОтборМесяцев)
		|			ИЛИ &ПоВсемМесяцам)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыРасчетаСреднегоСотрудников.Сотрудник,
		|	ПериодыРасчетаСреднегоСотрудников.ПорядокРасчета,
		|	Месяцы.Месяц
		|ПОМЕСТИТЬ ВТМесяцыРасчетаСотрудников
		|ИЗ
		|	ВТПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчетаСреднегоСотрудников
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцы КАК Месяцы
		|		ПО ПериодыРасчетаСреднегоСотрудников.НачалоПериодаРасчетаСреднего <= Месяцы.Месяц
		|			И ПериодыРасчетаСреднегоСотрудников.ОкончаниеПериодаРасчетаСреднего >= Месяцы.Месяц";

	Запрос.Выполнить();
	
	// Данные начислений (основной заработок).
	Запрос.Текст =
		"ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления) КАК Источник
		|ПОМЕСТИТЬ ВТНачисленияСреднегоЗаработка
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК МесяцыРасчетаСотрудников
		|		ПО (МесяцыРасчетаСотрудников.Месяц = ДанныеНачислений.Период)
		|			И (МесяцыРасчетаСотрудников.ПорядокРасчета = ДанныеНачислений.ПорядокРасчета)
		|			И (МесяцыРасчетаСотрудников.Сотрудник = ДанныеНачислений.Сотрудник)
		|			И (ДанныеНачислений.ПереносДанных = ЛОЖЬ)
		|			И (НЕ ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	СведенияНачислений.Сотрудник,
		|	СведенияНачислений.Месяц,
		|	СведенияНачислений.СоставнаяЧасть,
		|	СведенияНачислений.ПорядокРасчета,
		|	СведенияНачислений.СтатьяФинансирования,
		|	СведенияНачислений.Год,
		|	СведенияНачислений.ДатаНачалаБазовогоПериода,
		|	СведенияНачислений.КоличествоМесяцев,
		|	СведенияНачислений.Индексируется,
		|	СведенияНачислений.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|ИЗ
		|	РегистрСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК СведенияНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК МесяцыРасчетаСотрудников
		|		ПО (МесяцыРасчетаСотрудников.Месяц = СведенияНачислений.Месяц)
		|			И (МесяцыРасчетаСотрудников.ПорядокРасчета = СведенияНачислений.ПорядокРасчета)
		|			И (МесяцыРасчетаСотрудников.Сотрудник = СведенияНачислений.Сотрудник)
		|			И (НЕ СведенияНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|			И (СведенияНачислений.Сумма <> 0)
		|			И (&УчитыватьКорректировки = ИСТИНА
		|				ИЛИ НЕ ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|						ГДЕ
		|							ДанныеНачислений.Сотрудник = СведенияНачислений.Сотрудник
		|							И ДанныеНачислений.Период = СведенияНачислений.Месяц
		|							И ДанныеНачислений.ПорядокРасчета = СведенияНачислений.ПорядокРасчета
		|							И НЕ ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии)))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления)
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК МесяцыРасчетаСотрудников
		|		ПО (МесяцыРасчетаСотрудников.Месяц = ДанныеНачислений.Период)
		|			И (МесяцыРасчетаСотрудников.ПорядокРасчета = ДанныеНачислений.ПорядокРасчета)
		|			И (МесяцыРасчетаСотрудников.Сотрудник = ДанныеНачислений.Сотрудник)
		|			И (ДанныеНачислений.ПереносДанных = ИСТИНА)
		|			И (НЕ ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	МИНИМУМ(Начисления.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТНачисленияСреднегоЗаработкаМинимальныйПриоритет
		|ИЗ
		|	ВТНачисленияСреднегоЗаработка КАК Начисления
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.Сотрудник,
		|	Начисления.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.Источник,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	СУММА(ДанныеНачислений.Сумма) КАК Сумма
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеНачислений.Сотрудник КАК Сотрудник,
		|		ДанныеНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеНачислений.Период КАК Период,
		|		ДанныеНачислений.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ДанныеНачислений.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ДанныеНачислений.Год КАК Год,
		|		ДанныеНачислений.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|		ДанныеНачислений.КоличествоМесяцев КАК КоличествоМесяцев,
		|		ДанныеНачислений.Индексируется КАК Индексируется,
		|		ДанныеНачислений.Источник КАК Источник,
		|		ДанныеНачислений.Сумма КАК Сумма
		|	ИЗ
		|		ВТНачисленияСреднегоЗаработка КАК ДанныеНачислений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияСреднегоЗаработкаМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ДанныеНачислений.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ДанныеНачислений.Период)
		|				И (МинимальныйПриоритет.Приоритет = ДанныеНачислений.Приоритет)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеНачислений.Сотрудник,
		|		ДанныеНачислений.ПорядокРасчета,
		|		ДанныеНачислений.Период,
		|		ДанныеНачислений.СоставнаяЧасть,
		|		ДанныеНачислений.СтатьяФинансирования,
		|		ДанныеНачислений.Год,
		|		ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|		ДанныеНачислений.КоличествоМесяцев,
		|		ДанныеНачислений.Индексируется,
		|		ДанныеНачислений.Источник,
		|		ДанныеНачислений.Сумма
		|	ИЗ
		|		ВТНачисленияСреднегоЗаработка КАК ДанныеНачислений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисленияСреднегоЗаработкаМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ДанныеНачислений.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ДанныеНачислений.Период)
		|				И (МинимальныйПриоритет.Приоритет = 1)
		|				И (ДанныеНачислений.Приоритет = 3)) КАК ДанныеНачислений
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.Источник,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.КоличествоМесяцев
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.Индексируется УБЫВ";

	ВыборкаНачислений = Запрос.Выполнить().Выбрать();
	
	ДанныеНачислений = ПустаяТаблицаНачисленийСреднийЗаработокОбщий();
	Пока ВыборкаНачислений.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеНачислений.Добавить(), ВыборкаНачислений);
	КонецЦикла;
	
	// Годовые премии.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.ПорядокРасчета,
		|	ГодовыеПремии.СтатьяФинансирования,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.Период,
		|	ГодовыеПремии.Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления) КАК Источник
		|ПОМЕСТИТЬ ВТГодовыеПремии
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ГодовыеПремии
		|ГДЕ
		|	ГодовыеПремии.СоставнаяЧасть В(&ГодовыеПремии)
		|	И ГодовыеПремии.ПереносДанных = ЛОЖЬ
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	СведенияНачислений.Сотрудник,
		|	СведенияНачислений.СоставнаяЧасть,
		|	СведенияНачислений.ПорядокРасчета,
		|	СведенияНачислений.СтатьяФинансирования,
		|	СведенияНачислений.Год,
		|	СведенияНачислений.Месяц,
		|	СведенияНачислений.Индексируется,
		|	СведенияНачислений.ДатаНачалаБазовогоПериода,
		|	СведенияНачислений.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|ИЗ
		|	РегистрСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК СведенияНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборГодовыхПремий КАК ОтборГодовыхПремий
		|		ПО (ОтборГодовыхПремий.Сотрудник = СведенияНачислений.Сотрудник)
		|			И (ОтборГодовыхПремий.ПорядокРасчета = СведенияНачислений.ПорядокРасчета)
		|			И (ОтборГодовыхПремий.ГодовыеПремииЗаГод = СведенияНачислений.Год)
		|			И (СведенияНачислений.СоставнаяЧасть В (&ГодовыеПремии))
		|			И (СведенияНачислений.Сумма <> 0)
		|			И (&УчитыватьКорректировки = ИСТИНА
		|				ИЛИ НЕ ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							ВТДанныеДляРасчетаСреднего КАК ДанныеНачислений
		|						ГДЕ
		|							ДанныеНачислений.Сотрудник = СведенияНачислений.Сотрудник
		|							И ДанныеНачислений.Период = СведенияНачислений.Месяц
		|							И ДанныеНачислений.ПорядокРасчета = СведенияНачислений.ПорядокРасчета
		|							И ДанныеНачислений.СоставнаяЧасть В (&ГодовыеПремии)))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.ПорядокРасчета,
		|	ГодовыеПремии.СтатьяФинансирования,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.Период,
		|	ГодовыеПремии.Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Сумма,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления)
		|ИЗ
		|	ВТДанныеДляРасчетаСреднего КАК ГодовыеПремии
		|ГДЕ
		|	ГодовыеПремии.СоставнаяЧасть В(&ГодовыеПремии)
		|	И ГодовыеПремии.ПереносДанных = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.Год,
		|	МИНИМУМ(ГодовыеПремии.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТГодовыеПремииМинимальныйПриоритет
		|ИЗ
		|	ВТГодовыеПремии КАК ГодовыеПремии
		|
		|СГРУППИРОВАТЬ ПО
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.Год
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.ПорядокРасчета,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.Период,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.СтатьяФинансирования,
		|	ГодовыеПремии.Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Источник,
		|	СУММА(ГодовыеПремии.Сумма) КАК Сумма
		|ИЗ
		|	(ВЫБРАТЬ
		|		ГодовыеПремии.Сотрудник КАК Сотрудник,
		|		ГодовыеПремии.ПорядокРасчета КАК ПорядокРасчета,
		|		ГодовыеПремии.СоставнаяЧасть КАК СоставнаяЧасть,
		|		ГодовыеПремии.СтатьяФинансирования КАК СтатьяФинансирования,
		|		ГодовыеПремии.Год КАК Год,
		|		ГодовыеПремии.Период КАК Период,
		|		ГодовыеПремии.Индексируется КАК Индексируется,
		|		ГодовыеПремии.ДатаНачалаБазовогоПериода КАК ДатаНачалаБазовогоПериода,
		|		ГодовыеПремии.Источник КАК Источник,
		|		ГодовыеПремии.Сумма КАК Сумма
		|	ИЗ
		|		ВТГодовыеПремии КАК ГодовыеПремии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГодовыеПремииМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ГодовыеПремии.Сотрудник)
		|				И (МинимальныйПриоритет.Год = ГодовыеПремии.Год)
		|				И (МинимальныйПриоритет.Приоритет = ГодовыеПремии.Приоритет)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГодовыеПремии.Сотрудник,
		|		ГодовыеПремии.ПорядокРасчета,
		|		ГодовыеПремии.СоставнаяЧасть,
		|		ГодовыеПремии.СтатьяФинансирования,
		|		ГодовыеПремии.Год,
		|		ГодовыеПремии.Период,
		|		ГодовыеПремии.Индексируется,
		|		ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|		ГодовыеПремии.Источник,
		|		ГодовыеПремии.Сумма
		|	ИЗ
		|		ВТГодовыеПремии КАК ГодовыеПремии
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТГодовыеПремииМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ГодовыеПремии.Сотрудник)
		|				И (МинимальныйПриоритет.Год = ГодовыеПремии.Год)
		|				И (МинимальныйПриоритет.Приоритет = 1)
		|				И (ГодовыеПремии.Приоритет = 3)) КАК ГодовыеПремии
		|
		|СГРУППИРОВАТЬ ПО
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.ПорядокРасчета,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.СтатьяФинансирования,
		|	ГодовыеПремии.Индексируется,
		|	ГодовыеПремии.ДатаНачалаБазовогоПериода,
		|	ГодовыеПремии.Источник,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	ГодовыеПремии.Сотрудник,
		|	ГодовыеПремии.Год,
		|	ГодовыеПремии.СоставнаяЧасть,
		|	ГодовыеПремии.Индексируется УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеНачислений.Добавить(), Выборка);
	КонецЦикла;
		
	// Данные отработанного времени.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СотрудникиМесяцы.Сотрудник,
		|	СотрудникиМесяцы.Месяц КАК ДатаНачалаПериода,
		|	КОНЕЦПЕРИОДА(СотрудникиМесяцы.Месяц, МЕСЯЦ) КАК ДатаОкончанияПериода
		|ИЗ
		|	ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы";
	
	ИсходныеДанные = Запрос.Выполнить().Выгрузить();
	
	УчетРабочегоВремени.СоздатьВТВремяПоГрафикамСотрудников(ИсходныеДанные, МенеджерВременныхТаблиц);
	КалендарныеГрафики.СоздатьВТТаблицаПраздничныхДней(МенеджерВременныхТаблиц, МинимальнаяДатаНачала, МаксимальнаяДатаОкончания); 
	Запрос.Текст =
		//
		"ВЫБРАТЬ
		|	ДанныеВремени.Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ПереносДанных,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеВремени.ОтработаноДнейКалендарных - ЕСТЬNULL(ПраздничныеДни.ПраздничныхДней, 0) > 0
		|				ТОГДА ДанныеВремени.ОтработаноДнейКалендарных - ЕСТЬNULL(ПраздничныеДни.ПраздничныхДней, 0)
		|			ИНАЧЕ ДанныеВремени.ОтработаноДнейКалендарных
		|		КОНЕЦ) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТНакопленияВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеВремени.Период КАК Период,
		|		ДанныеВремени.Сотрудник КАК Сотрудник,
		|		ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|		ВЫБОР
		|			КОГДА ДанныеВремени.Регистратор ССЫЛКА Документ.ПереносДанных
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ПереносДанных,
		|		СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|		СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|		СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|	ИЗ
		|		РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК ДанныеВремени
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериодыРасчетаСреднегоСотрудников КАК ПериодыРасчета
		|			ПО (ПериодыРасчета.Сотрудник = ДанныеВремени.Сотрудник)
		|				И (ПериодыРасчета.ПорядокРасчета = ДанныеВремени.ПорядокРасчета)
		|				И (ДанныеВремени.Период МЕЖДУ &ДатаНачала И &ДатаОкончания)
		|				И (ДанныеВремени.Регистратор <> &ИсключаемыйРегистратор)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ДанныеВремени.Период,
		|		ВЫБОР
		|			КОГДА ДанныеВремени.Регистратор ССЫЛКА Документ.ПереносДанных
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ,
		|		ДанныеВремени.Сотрудник,
		|		ДанныеВремени.ПорядокРасчета) КАК ДанныеВремени
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПраздничныеДни КАК ПраздничныеДни
		|		ПО (НАЧАЛОПЕРИОДА(ДанныеВремени.Период, ДЕНЬ) = ПраздничныеДни.Месяц)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ПереносДанных
		|
		|ИМЕЮЩИЕ
		|	(СУММА(ДанныеВремени.ОтработаноДней) <> 0
		|		ИЛИ СУММА(ДанныеВремени.ОтработаноЧасов) <> 0
		|		ИЛИ СУММА(ВЫБОР
		|				КОГДА ДанныеВремени.ОтработаноДнейКалендарных - ЕСТЬNULL(ПраздничныеДни.ПраздничныхДней, 0) > 0
		|					ТОГДА ДанныеВремени.ОтработаноДнейКалендарных - ЕСТЬNULL(ПраздничныеДни.ПраздничныхДней, 0)
		|				ИНАЧЕ ДанныеВремени.ОтработаноДнейКалендарных
		|			КОНЕЦ) <> 0)
		|;
		|	
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	1 КАК Приоритет,
		|	ДанныеВремени.Период КАК Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейКалендарных,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления) КАК Источник
		|ПОМЕСТИТЬ ВТВремяДляРасчетаСреднего
		|ИЗ
		|	ВТНакопленияВремени КАК ДанныеВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = ДанныеВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = ДанныеВремени.Период)
		|			И (СотрудникиМесяцы.ПорядокРасчета = ДанныеВремени.ПорядокРасчета)
		|			И (ДанныеВремени.ПереносДанных = ЛОЖЬ)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	СведенияВремени.Месяц,
		|	СведенияВремени.Сотрудник,
		|	СведенияВремени.ПорядокРасчета,
		|	СведенияВремени.ОтработаноДней,
		|	СведенияВремени.ОтработаноЧасов,
		|	СведенияВремени.ОтработаноДнейКалендарных,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|ИЗ
		|	РегистрСведений.СведенияОВремениДляРасчетаСреднегоОбщий КАК СведенияВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = СведенияВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = СведенияВремени.Месяц)
		|			И (СотрудникиМесяцы.ПорядокРасчета = СведенияВремени.ПорядокРасчета)
		|			И (СведенияВремени.ОтработаноДней <> 0
		|				ИЛИ СведенияВремени.ОтработаноЧасов <> 0
		|				ИЛИ СведенияВремени.ОтработаноЧасов <> 0
		|				ИЛИ СведенияВремени.ОтработаноДнейКалендарных <> 0)
		|			И (&УчитыватьКорректировки = ИСТИНА
		|				ИЛИ НЕ ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							ВТНакопленияВремени КАК НакопленияВремени
		|						ГДЕ
		|							НакопленияВремени.Сотрудник = СведенияВремени.Сотрудник
		|							И НакопленияВремени.Период = СведенияВремени.Месяц
		|							И НакопленияВремени.ПорядокРасчета = СведенияВремени.ПорядокРасчета))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	ДанныеВремени.Период,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейКалендарных,
		|	ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления)
		|ИЗ
		|	ВТНакопленияВремени КАК ДанныеВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = ДанныеВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = ДанныеВремени.Период)
		|			И (СотрудникиМесяцы.ПорядокРасчета = ДанныеВремени.ПорядокРасчета)
		|			И (ДанныеВремени.ПереносДанных = ИСТИНА)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Время.Сотрудник,
		|	Время.Период,
		|	МИНИМУМ(Время.Приоритет) КАК Приоритет
		|ПОМЕСТИТЬ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет
		|ИЗ
		|	ВТВремяДляРасчетаСреднего КАК Время
		|
		|СГРУППИРОВАТЬ ПО
		|	Время.Сотрудник,
		|	Время.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Источник,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТДанныеВремениПоСотрудникам
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеВремени.Период КАК Месяц,
		|		ДанныеВремени.Сотрудник КАК Сотрудник,
		|		ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеВремени.Источник КАК Источник,
		|		ДанныеВремени.ОтработаноДней КАК ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов КАК ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейКалендарных КАК ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТВремяДляРасчетаСреднего КАК ДанныеВремени
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО ДанныеВремени.Сотрудник = МинимальныйПриоритет.Сотрудник
		|				И ДанныеВремени.Период = МинимальныйПриоритет.Период
		|				И ДанныеВремени.Приоритет = МинимальныйПриоритет.Приоритет
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеВремени.Период,
		|		ДанныеВремени.Сотрудник,
		|		ДанныеВремени.ПорядокРасчета,
		|		ДанныеВремени.Источник,
		|		ДанныеВремени.ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТВремяДляРасчетаСреднего КАК ДанныеВремени
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО ДанныеВремени.Сотрудник = МинимальныйПриоритет.Сотрудник
		|				И ДанныеВремени.Период = МинимальныйПриоритет.Период
		|				И (МинимальныйПриоритет.Приоритет = 1)
		|				И (ДанныеВремени.Приоритет = 3)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеВремениКорректировка.Месяц,
		|		ДанныеВремениКорректировка.Сотрудник,
		|		ДанныеВремениКорректировка.ПорядокРасчета,
		|		ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.Начисления),
		|		ДанныеВремениКорректировка.ОтработаноДней,
		|		ДанныеВремениКорректировка.ОтработаноЧасов,
		|		ДанныеВремениКорректировка.ОтработаноДнейКалендарных
		|	ИЗ
		|		РегистрСведений.ДанныеОВремениДляРасчетаСреднегоОбщийКорректировка КАК ДанныеВремениКорректировка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|			ПО (СотрудникиМесяцы.Сотрудник = ДанныеВремениКорректировка.Сотрудник)
		|				И (СотрудникиМесяцы.ПорядокРасчета = ДанныеВремениКорректировка.ПорядокРасчета)
		|				И (СотрудникиМесяцы.Месяц = ДанныеВремениКорректировка.Месяц)
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ДанныеВремениКорректировка.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ДанныеВремениКорректировка.Месяц)
		|				И (МинимальныйПриоритет.Приоритет = 2)
		|	ГДЕ
		|		МинимальныйПриоритет.Период ЕСТЬ NULL
		|) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Источник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Период,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных,
		|	СУММА(ДанныеВремени.НормаДнейПроизводственныйКалендарь) КАК НормаДнейПроизводственныйКалендарь,
		|	СУММА(ДанныеВремени.НормаЧасовПроизводственныйКалендарь) КАК НормаЧасовПроизводственныйКалендарь,
		|	СУММА(ДанныеВремени.НормаДнейПоГрафикуСотрудника) КАК НормаДнейПоГрафикуСотрудника,
		|	СУММА(ДанныеВремени.НормаЧасовПоГрафикуСотрудника) КАК НормаЧасовПоГрафикуСотрудника
		|ПОМЕСТИТЬ ВТДанныеВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		ДанныеВремени.Сотрудник КАК Сотрудник,
		|		ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|		ДанныеВремени.Источник КАК Источник,
		|		ДанныеВремени.Месяц КАК Период,
		|		ДанныеВремени.ОтработаноДней КАК ОтработаноДней,
		|		ДанныеВремени.ОтработаноЧасов КАК ОтработаноЧасов,
		|		ДанныеВремени.ОтработаноДнейКалендарных КАК ОтработаноДнейКалендарных,
		|		0 КАК НормаДнейПроизводственныйКалендарь,
		|		0 КАК НормаЧасовПроизводственныйКалендарь,
		|		0 КАК НормаДнейПоГрафикуСотрудника,
		|		0 КАК НормаЧасовПоГрафикуСотрудника
		|	ИЗ
		|		ВТДанныеВремениПоСотрудникам КАК ДанныеВремени
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СотрудникиМесяцы.Сотрудник,
		|		СотрудникиМесяцы.ПорядокРасчета,
		|		ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.ПустаяСсылка),
		|		СотрудникиМесяцы.Месяц,
		|		0,
		|		0,
		|		0,
		|		КалендарноеВремя.НормаДнейПоПроизводственномуКалендарю,
		|		КалендарноеВремя.НормаЧасовПоПроизводственномуКалендарю,
		|		КалендарноеВремя.НормаДней,
		|		КалендарноеВремя.НормаЧасов
		|	ИЗ
		|		ВТМесяцыРасчетаСотрудников КАК СотрудникиМесяцы
		|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВремяПоГрафикамСотрудников КАК КалендарноеВремя
		|			ПО (КалендарноеВремя.Сотрудник = СотрудникиМесяцы.Сотрудник)
		|				И (НАЧАЛОПЕРИОДА(КалендарноеВремя.ДатаНачалаПериода, МЕСЯЦ) = СотрудникиМесяцы.Месяц)) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.Период,
		|	ЕСТЬNULL(ИсточникиДанных.Источник, ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.ПустаяСсылка)) КАК Источник,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейКалендарных,
		|	ДанныеВремени.НормаДнейПроизводственныйКалендарь,
		|	ДанныеВремени.НормаЧасовПроизводственныйКалендарь,
		|	ДанныеВремени.НормаДнейПоГрафикуСотрудника,
		|	ДанныеВремени.НормаЧасовПоГрафикуСотрудника
		|ИЗ
		|	ВТДанныеВремени КАК ДанныеВремени
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднего КАК ИсточникиДанных
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВремяДляРасчетаСреднегоМинимальныйПриоритет КАК МинимальныйПриоритет
		|			ПО (МинимальныйПриоритет.Сотрудник = ИсточникиДанных.Сотрудник)
		|				И (МинимальныйПриоритет.Период = ИсточникиДанных.Период)
		|		ПО (ИсточникиДанных.Сотрудник = ДанныеВремени.Сотрудник)
		|			И (ИсточникиДанных.Период = ДанныеВремени.Период)
		|			И (ИсточникиДанных.Приоритет = МинимальныйПриоритет.Приоритет)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Период";

	ВыборкаВремени = Запрос.Выполнить().Выбрать();
	ДанныеВремени = ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий();
	Пока ВыборкаВремени.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ДанныеВремени.Добавить(), ВыборкаВремени);
	КонецЦикла;
	
	// Данные индексации заработка
	Для Каждого СтрокаДанных Из ТаблицаСотрудники Цикл
		СтрокаДанных.ОкончаниеПериодаРасчетаСреднего = ДобавитьМесяц(СтрокаДанных.ОкончаниеПериодаРасчетаСреднего, 1);
	КонецЦикла;
	
	ДанныеИндексации = ДанныеИндексации(ТаблицаСотрудники);
	
	ДанныеДляРасчета = Новый Структура(
		"ДанныеОНачислениях, 
		|ДанныеОВремени, 
		|ДанныеОбИндексации");
		
	ДанныеДляРасчета.ДанныеОНачислениях = ДанныеНачислений;
	ДанныеДляРасчета.ДанныеОВремени = ДанныеВремени;
	ДанныеДляРасчета.ДанныеОбИндексации = ДанныеИндексации;
	
	Возврат ДанныеДляРасчета;
	
КонецФункции	

// Получает таблицу коэффициентов по месяцам, 
// которые необходимо применить к среднему заработку из-за проводимой индексации заработка.
//
// Параметры:
//	ИсходныеДанные - таблица значений, 
//		созданная конструктором см. ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка
//	КоэффициентУчитываетПоследующиеИндексации 
//		- если Истина, коэффициент на конкретный месяц учитывает все индексации произошедшие ПОСЛЕ него (для расчета
//		показателя КоэффициентИндексации)
//		- если Ложь, коэффициент на конкретный месяц учитывает все индексации произошедшие ДО него.
//
Функция ДанныеИндексации(ИсходныеДанные, КоэффициентУчитываетПоследующиеИндексации = Ложь) Экспорт
	
	ДанныеИндексации = Новый ТаблицаЗначений;
	ДанныеИндексации.Колонки.Добавить("Сотрудник");
	ДанныеИндексации.Колонки.Добавить("Период");
	ДанныеИндексации.Колонки.Добавить("КоэффициентИндексации");
	
	Если ИсходныеДанные.Количество() = 0 Тогда
		Возврат ДанныеИндексации;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник,
	|	ИсходныеДанные.НачалоПериодаРасчетаСреднего,
	|	ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего,
	|	ИсходныеДанные.ПорядокРасчета
	|ПОМЕСТИТЬ ВТИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ИсходныеДанные.НачалоПериодаРасчетаСреднего) КАК НачалоПервогоИнтервала,
	|	МАКСИМУМ(ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего) КАК ОкончаниеПоследнегоИнтервала
	|ИЗ
	|	ВТИсходныеДанные КАК ИсходныеДанные";
	
	Запрос.УстановитьПараметр("ИсходныеДанные", ИсходныеДанные);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ДанныеИндексации;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	
	// Создаем ВТ с месяцами
	ЗарплатаКадрыОбщиеНаборыДанных.СоздатьВТПериоды(МенеджерВременныхТаблиц, Выборка.НачалоПервогоИнтервала, Выборка.ОкончаниеПоследнегоИнтервала);
	
	// Выбираем коэффициенты индексации заработка: 
	// - для индексации выплат, учитываемых при расчете показателя среднего заработка, 
	// для каждого месяца выбираем коэффициенты, всех индексаций, случившихся ДО него
	// - для расчета показателя КоэффициентИндексации - для каждого месяца 
	// выборка коэффициентов всех индексаций, случившихся ПОСЛЕ него.
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходныеДанные.Сотрудник КАК Сотрудник,
	|	Месяцы.Период КАК Период,
	|	ЕСТЬNULL(КоэффициентИндексацииЗаработка.Коэффициент, 1) КАК КоэффициентИндексации
	|ИЗ
	|	ВТИсходныеДанные КАК ИсходныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПериоды КАК Месяцы
	|		ПО (Месяцы.Период МЕЖДУ ИсходныеДанные.НачалоПериодаРасчетаСреднего И ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КоэффициентИндексацииЗаработка КАК КоэффициентИндексацииЗаработка
	|		ПО ИсходныеДанные.Сотрудник = КоэффициентИндексацииЗаработка.Сотрудник
	|			И (Месяцы.Период >= НАЧАЛОПЕРИОДА(КоэффициентИндексацииЗаработка.Период, МЕСЯЦ))
	|			И (КоэффициентИндексацииЗаработка.Период МЕЖДУ ИсходныеДанные.НачалоПериодаРасчетаСреднего И ИсходныеДанные.ОкончаниеПериодаРасчетаСреднего)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	Период УБЫВ
	|ИТОГИ ПО
	|	Сотрудник,
	|	Период";
	
	Если Не КоэффициентУчитываетПоследующиеИндексации Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, ">=", "<");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Период УБЫВ", "Период ВОЗР");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Обходя результаты запроса агрегируем коэффициенты.
	
	ВыборкаПоСотрудникам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоСотрудникам.Следующий() Цикл
		ВыборкаПоМесяцам = ВыборкаПоСотрудникам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоМесяцам.Следующий() Цикл
			Коэффициент = 1;
			Выборка = ВыборкаПоМесяцам.Выбрать();
			Пока Выборка.Следующий() Цикл
				Коэффициент = Коэффициент * Выборка.КоэффициентИндексации;
			КонецЦикла;
			СтрокаИндексации = ДанныеИндексации.Добавить();
			СтрокаИндексации.Сотрудник = ВыборкаПоСотрудникам.Сотрудник;
			СтрокаИндексации.Период = ВыборкаПоМесяцам.Период;
			СтрокаИндексации.КоэффициентИндексации = Коэффициент;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ДанныеИндексации;
	
КонецФункции

// По переданному массиву ссылок возвращает структуру, содержащую таблицы значений с данными о начислениях, 
// отработанном времени и коэффициентах индексации для расчета среднего заработка.
//
Функция ТаблицыДанныхОСреднемЗаработке(ИмяДокумента, МассивСсылок) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументДанныеОЗаработке.Ссылка,
		|	ДокументДанныеОЗаработке.Сотрудник,
		|	ДокументДанныеОЗаработке.ПорядокРасчета,
		|	ДокументДанныеОЗаработке.СоставнаяЧасть,
		|	ДокументДанныеОЗаработке.СтатьяФинансирования,
		|	ДокументДанныеОЗаработке.Период,
		|	ДокументДанныеОЗаработке.Индексируется,
		|	ДокументДанныеОЗаработке.Сумма,
		|	ДокументДанныеОЗаработке.Источник,
		|	ДокументДанныеОЗаработке.Год,
		|	ДокументДанныеОЗаработке.ДатаНачалаБазовогоПериода,
		|	ДокументДанныеОЗаработке.КоличествоМесяцев
		|ИЗ
		|	Документ.#ИмяДокумента#.СреднийЗаработокОбщий КАК ДокументДанныеОЗаработке
		|ГДЕ
		|	ДокументДанныеОЗаработке.Ссылка В (&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументОтработанноеВремя.Ссылка,
		|	ДокументОтработанноеВремя.Сотрудник,
		|	ДокументОтработанноеВремя.ПорядокРасчета,
		|	ДокументОтработанноеВремя.Период,
		|	ДокументОтработанноеВремя.ОтработаноДней,
		|	ДокументОтработанноеВремя.ОтработаноДнейПятидневка,
		|	ДокументОтработанноеВремя.ОтработаноЧасов,
		|	ДокументОтработанноеВремя.ОтработаноДнейШестидневка,
		|	ДокументОтработанноеВремя.ОтработаноДнейКалендарных,
		|	ДокументОтработанноеВремя.НормаДнейПроизводственныйКалендарь,
		|	ДокументОтработанноеВремя.ОтработаноЧасовПятидневка,
		|	ДокументОтработанноеВремя.НормаЧасовПроизводственныйКалендарь,
		|	ДокументОтработанноеВремя.НормаДнейПоГрафикуСотрудника,
		|	ДокументОтработанноеВремя.НормаЧасовПоГрафикуСотрудника,
		|	ДокументОтработанноеВремя.СреднемесячноеКоличествоДней,
		|	ДокументОтработанноеВремя.Источник
		|ИЗ
		|	Документ.#ИмяДокумента#.ОтработанноеВремяДляСреднегоОбщий КАК ДокументОтработанноеВремя
		|ГДЕ
		|	ДокументОтработанноеВремя.Ссылка В (&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументДанныеОбИндексации.Ссылка,
		|	ДокументДанныеОбИндексации.Сотрудник,
		|	ДокументДанныеОбИндексации.Период,
		|	ДокументДанныеОбИндексации.КоэффициентИндексации
		|ИЗ
		|	Документ.#ИмяДокумента#.ДанныеОбИндексации КАК ДокументДанныеОбИндексации
		|ГДЕ
		|	ДокументДанныеОбИндексации.Ссылка В (&МассивСсылок)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента#", ИмяДокумента);	
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДанныеОНачислениях 	= Результат[0].Выгрузить();
	ДанныеВремени 		= Результат[1].Выгрузить();
	ДанныеИндексации	= Результат[2].Выгрузить();
	ДанныеДляРасчета 	= Новый Структура("ДанныеОНачислениях,ДанныеОВремени,ДанныеОбИндексации", ДанныеОНачислениях, ДанныеВремени, ДанныеИндексации);
	
	Возврат ДанныеДляРасчета;
	
КонецФункции	

// По переданному массиву ссылок возвращает таблицу значений с данными о начислениях по данным ФОТ 
//
Функция ТаблицыДанныхРасшифровкаСоставаФОТ(ИмяДокумента, МассивСсылок) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументРасшифровкаСоставаФОТ.Ссылка,
		|	ДокументРасшифровкаСоставаФОТ.Сотрудник,
		|	ДокументРасшифровкаСоставаФОТ.Начисление,
		|	ДокументРасшифровкаСоставаФОТ.Результат
		|ИЗ
		|	Документ.#ИмяДокумента#.РасшифровкаСоставаФОТ КАК ДокументРасшифровкаСоставаФОТ
		|ГДЕ
		|	ДокументРасшифровкаСоставаФОТ.Ссылка В (&МассивСсылок)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента#", ИмяДокумента);	
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить();
	
	ДанныеФОТ 	= Результат.Выгрузить();
	
	Возврат ДанныеФОТ;
	
КонецФункции	

// По переданному массиву ссылок возвращает структуру, содержащую таблицу значений с данными о начислениях, 
// отработанном времени и коэффициентах индексации для расчета среднего заработка для выходного пособия.
//
Функция ТаблицыДанныхОСреднемЗаработкеДляВыходногоПособия(ИмяДокумента, МассивСсылок) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДокументДанныеОЗаработке.Ссылка,
		|	ДокументДанныеОЗаработке.Сотрудник,
		|	ДокументДанныеОЗаработке.ПорядокРасчета,
		|	ДокументДанныеОЗаработке.СоставнаяЧасть,
		|	ДокументДанныеОЗаработке.СтатьяФинансирования,
		|	ДокументДанныеОЗаработке.Период,
		|	ДокументДанныеОЗаработке.Индексируется,
		|	ДокументДанныеОЗаработке.Сумма,
		|	ДокументДанныеОЗаработке.Источник,
		|	ДокументДанныеОЗаработке.Год,
		|	ДокументДанныеОЗаработке.ДатаНачалаБазовогоПериода,
		|	ДокументДанныеОЗаработке.КоличествоМесяцев
		|ИЗ
		|	Документ.#ИмяДокумента#.СреднийЗаработокДляВыходногоПособия КАК ДокументДанныеОЗаработке
		|ГДЕ
		|	ДокументДанныеОЗаработке.Ссылка В (&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументОтработанноеВремя.Ссылка,
		|	ДокументОтработанноеВремя.Сотрудник,
		|	ДокументОтработанноеВремя.ПорядокРасчета,
		|	ДокументОтработанноеВремя.Период,
		|	ДокументОтработанноеВремя.ОтработаноДней,
		|	ДокументОтработанноеВремя.ОтработаноЧасов,
		|	ДокументОтработанноеВремя.ОтработаноДнейКалендарных,
		|	ДокументОтработанноеВремя.НормаДнейПроизводственныйКалендарь,
		|	ДокументОтработанноеВремя.НормаЧасовПроизводственныйКалендарь,
		|	ДокументОтработанноеВремя.НормаДнейПоГрафикуСотрудника,
		|	ДокументОтработанноеВремя.НормаЧасовПоГрафикуСотрудника,
		|	ДокументОтработанноеВремя.СреднемесячноеКоличествоДней,
		|	ДокументОтработанноеВремя.Источник
		|ИЗ
		|	Документ.#ИмяДокумента#.ОтработанноеВремяДляСреднегоДляВыходногоПособия КАК ДокументОтработанноеВремя
		|ГДЕ
		|	ДокументОтработанноеВремя.Ссылка В (&МассивСсылок)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументДанныеОбИндексации.Ссылка,
		|	ДокументДанныеОбИндексации.Сотрудник,
		|	ДокументДанныеОбИндексации.Период,
		|	ДокументДанныеОбИндексации.КоэффициентИндексации
		|ИЗ
		|	Документ.#ИмяДокумента#.ДанныеОбИндексацииДляВыходногоПособия КАК ДокументДанныеОбИндексации
		|ГДЕ
		|	ДокументДанныеОбИндексации.Ссылка В (&МассивСсылок)";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИмяДокумента#", ИмяДокумента);	
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДанныеОНачислениях 	= Результат[0].Выгрузить();
	ДанныеВремени 		= Результат[1].Выгрузить();
	ДанныеИндексации	= Результат[2].Выгрузить();
	ДанныеДляРасчета 	= Новый Структура("ДанныеОНачислениях,ДанныеОВремени,ДанныеОбИндексации", ДанныеОНачислениях, ДанныеВремени, ДанныеИндексации);
	
	Возврат ДанныеДляРасчета;

КонецФункции

//Создает новую таблицу значений и копирует в нее данные отобранные по значению в указанном поле
//
Функция ТаблицаОтобраннаяПоПолю(ИсходнаяТаблица, Поле, ЗначениеОтбора) Экспорт
	
	ОтобраннаяТаблица = ИсходнаяТаблица.СкопироватьКолонки();
	
	Для каждого Строка Из ИсходнаяТаблица Цикл
		Если Строка[Поле] = ЗначениеОтбора Тогда
			ЗаполнитьЗначенияСвойств(ОтобраннаяТаблица.Добавить(), Строка);
		КонецЕсли;
	КонецЦикла;

	Возврат ОтобраннаяТаблица;

КонецФункции

#КонецОбласти

#Область МетодыОбслуживанияФормДокументовДляРасчетаСреднегоЗаработка

// Процедура заполняет таблицы документа данными учета среднего заработка
// по результатам их редактирования.
//
Процедура ЗаполнитьДанныеУчетаОбщегоСреднегоЗаработка(ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации, РедактируемыеДанные, Модифицированность = Ложь) Экспорт
	
	ОчиститьДанныеОСреднемДокумента(
		РедактируемыеДанные.Сотрудник, 
		РедактируемыеДанные.ПорядокРасчета, 
		РедактируемыеДанные.НачалоПериодаРасчета, 
		РедактируемыеДанные.ОкончаниеПериодаРасчета, 
		ДанныеОНачислениях, 
		ДанныеОВремени, 
		ДанныеОбИндексации);	
	
	РедактируемыеНачисления = ПолучитьИзВременногоХранилища(РедактируемыеДанные.ДанныеОНачислениях);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РедактируемыеНачисления, ДанныеОНачислениях);
	
	Если РедактируемыеДанные.Свойство("ДанныеОВремени") И ЗначениеЗаполнено(РедактируемыеДанные.ДанныеОВремени) Тогда 
		РедактируемоеВремя = ПолучитьИзВременногоХранилища(РедактируемыеДанные.ДанныеОВремени);
		Если РедактируемоеВремя <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РедактируемоеВремя, ДанныеОВремени);
		КонецЕсли;	
	КонецЕсли;	
	
	Если ДанныеОбИндексации <> Неопределено И РедактируемыеДанные.Свойство("ДанныеОбИндексации") Тогда
		РедактируемаяИндексация = ПолучитьИзВременногоХранилища(РедактируемыеДанные.ДанныеОбИндексации);
		Если РедактируемаяИндексация <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(РедактируемаяИндексация, ДанныеОбИндексации);
		КонецЕсли;	
	КонецЕсли;	
	
	Модифицированность = Истина;
	
КонецПроцедуры	

// Функция определяет порядок расчета в зависимости от категории начисления
Функция ПорядокРасчетаОбщегоСреднегоЗаработкаПоКатегорииНачисления(КатегорияНачисления)
	Если НЕ КатегорияНачисления = Неопределено Тогда
		Если НЕ ПланыВидовРасчета.Начисления.КатегорииОплатыОтпуска().Найти(КатегорияНачисления) = Неопределено Тогда
			Возврат Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление100Отпускные
		Иначе
			Возврат Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010
		КонецЕсли;
	Иначе
		Возврат Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010
	КонецЕсли;	
КонецФункции

// Функция определяет порядок расчета в зависимости от категории начисления
Функция ПорядокРасчетаОбщегоСреднегоЗаработкаДляУдержания()
	Возврат Перечисления.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление100Отпускные
КонецФункции	

// Функция выполняет обновление коллекций данных среднего заработка из данных учета.
// Обновление производится для добавленных сотрудников, для удаленных сотрудников строки коллекций удаляются.
//
// Параметры:
//	ДанныеДокумента - структура с полями ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации (необязательный),
//	ДатаНачалаСобытия,
//	НачалоПериода - начало периода расчета среднего заработка,
//	ОкончаниеПериода - окончание периода расчета среднего заработка,
//	СотрудникиДокумента - массив сотрудников, данные для которых должны быть в документе.
//	ПроверятьПериодРасчетаСреднегоЗаработка - если Истина, то будут обновлены данные среднего для всех сотрудников, 
// 			чьи данные не покрывают период расчета среднего заработка.
// 
// Возвращаемое значение - массив сотрудников, данные для расчета среднего которых были обновлены.
//
Функция ОбновитьДанныеОбщегоСреднегоЗаработка(ДанныеДокумента, ДатаНачалаСобытия, НачалоПериода, ОкончаниеПериода, СотрудникиДокумента, ПроверятьПериодРасчетаСреднего = Истина, ИсключаемыйРегистратор = Неопределено) Экспорт

// Различаем сценарии
	// - изменился состав сотрудников
	// - изменился состав периодов среднего заработка
	
	// Если изменился состав периодов расчета среднего заработка
	// - удаляем данные вне периода, если это не ручные корректировки
	// - получаем данные за новый период
	// - переносим имеющиеся ручные корректировки в полученные данные.
	
	// Выявляем состав сотрудников, 
	// - данные которых нужно удалить
	// - данных для которых не хватает.
	
	СотрудникиДанныхСреднего = ОбщегоНазначения.ВыгрузитьКолонку(ДанныеДокумента.ДанныеОНачислениях, "Сотрудник", Истина);
	
	СотрудникиДобавить = ОбщегоНазначенияКлиентСервер.СократитьМассив(СотрудникиДокумента, СотрудникиДанныхСреднего);
	СотрудникиУдалить = ОбщегоНазначенияКлиентСервер.СократитьМассив(СотрудникиДанныхСреднего, СотрудникиДокумента);
	
	ОбновляемыеСотрудники = Новый Массив;
	
	// Удаляем строки сотрудников удаленных сотрудников.
	Если СотрудникиУдалить <> Неопределено Тогда
		ОчиститьДанныеДляРасчетаСреднего(ДанныеДокумента.ДанныеОНачислениях, СотрудникиУдалить);
		ОчиститьДанныеДляРасчетаСреднего(ДанныеДокумента.ДанныеОВремени, СотрудникиУдалить);
	КонецЕсли;
	
	// Таблица для получения данных среднего заработка.
	ТаблицаСотрудники = ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка();
	Если ТипЗнч(ДанныеДокумента.ВидРасчета) = Тип("ПланВидовРасчетаСсылка.Удержания") Тогда
		ПорядокРасчета = ПорядокРасчетаОбщегоСреднегоЗаработкаДляУдержания();
	ИначеЕсли ДанныеДокумента.ВидРасчета = Неопределено Тогда
		ПорядокРасчета = ПорядокРасчетаОбщегоСреднегоЗаработкаПоКатегорииНачисления(Неопределено);                                                               
	Иначе	
		ПорядокРасчета = ПорядокРасчетаОбщегоСреднегоЗаработкаПоКатегорииНачисления(ДанныеДокумента.ВидРасчета.КатегорияНачисленияИлиНеоплаченногоВремени);
	КонецЕсли;
	
	Если СотрудникиДобавить <> Неопределено Тогда
		// Заполняем таблицу добавленными сотрудниками.
		Для Каждого Сотрудник Из СотрудникиДобавить Цикл
			НоваяСтрока = ТаблицаСотрудники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
			НоваяСтрока.ПорядокРасчета = ПорядокРасчета;
			НоваяСтрока.ДатаНачалаСобытия = ДатаНачалаСобытия;
			НоваяСтрока.НачалоПериодаРасчетаСреднего = НачалоПериода;
			НоваяСтрока.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
			ОбновляемыеСотрудники.Добавить(Сотрудник);
		КонецЦикла;
	КонецЕсли;
	
	// Проверяем следствия изменения периода расчета среднего заработка.
	Если ПроверятьПериодРасчетаСреднего Тогда
		// Выявляем сотрудников, для которых нужно дополнить данные в связи с изменением периода.
		// Считаем, что дополнять данные нужно, если минимальный для сотрудника месяц или максимальный 
		// выходят за границы периода расчета среднего заработка.
		ПериодыСотрудников = Новый Соответствие;
		Для Каждого СтрокаДанныхСреднего Из ДанныеДокумента.ДанныеОНачислениях Цикл
			ПериодыСотрудника = ПериодыСотрудников[СтрокаДанныхСреднего.Сотрудник];
			Если ПериодыСотрудников[СтрокаДанныхСреднего.Сотрудник] = Неопределено Тогда
				ПериодыСотрудника = Новый Структура("Минимум, Максимум");
			КонецЕсли;
			Если ПериодыСотрудника.Минимум = Неопределено 
				Или СтрокаДанныхСреднего.Период < ПериодыСотрудника.Минимум Тогда
				ПериодыСотрудника.Минимум = СтрокаДанныхСреднего.Период;
			КонецЕсли;
			Если ПериодыСотрудника.Максимум = Неопределено 
				Или СтрокаДанныхСреднего.Период > ПериодыСотрудника.Максимум Тогда
				ПериодыСотрудника.Максимум = СтрокаДанныхСреднего.Период;
			КонецЕсли;
			ПериодыСотрудников.Вставить(СтрокаДанныхСреднего.Сотрудник, ПериодыСотрудника);
		КонецЦикла;
		
		// Если для сотрудника границы периода данных среднего не покрывают нового периода
		// нужно получить новые данные.
		Для Каждого КлючИЗначение Из ПериодыСотрудников Цикл
			Сотрудник = КлючИЗначение.Ключ;
			ПериодыСотрудника = КлючИЗначение.Значение;
			Если ПериодыСотрудника.Минимум > НачалоПериода 
				Или ПериодыСотрудника.Максимум < НачалоМесяца(ОкончаниеПериода) Тогда 
				НоваяСтрока = ТаблицаСотрудники.Добавить();
				НоваяСтрока.Сотрудник = Сотрудник;
				НоваяСтрока.ПорядокРасчета = ПорядокРасчета;
				НоваяСтрока.ДатаНачалаСобытия = ДатаНачалаСобытия;
				НоваяСтрока.НачалоПериодаРасчетаСреднего = НачалоПериода;
				НоваяСтрока.ОкончаниеПериодаРасчетаСреднего = ОкончаниеПериода;
				ОбновляемыеСотрудники.Добавить(Сотрудник);
			КонецЕсли;
		КонецЦикла;
		
		// Удаляем из коллекций данные, находящиеся вне периода расчета среднего заработка.
		ГодГодовыхПремий = УчетСреднегоЗаработкаКлиентСервер.ГодГодовыхПремий(ДатаНачалаСобытия);
		УдалитьДанныеВнеПериодаРасчетаСреднегоЗаработка(ДанныеДокумента.ДанныеОНачислениях, НачалоПериода, ОкончаниеПериода, , ГодГодовыхПремий);
		УдалитьДанныеВнеПериодаРасчетаСреднегоЗаработка(ДанныеДокумента.ДанныеОВремени, НачалоПериода, ОкончаниеПериода);
		
		Если ДанныеДокумента.Свойство("ДанныеОбИндексации") Тогда
			// Данные индексации перезаполняем полностью.
			ОчиститьДанныеДляРасчетаСреднего(ДанныеДокумента.ДанныеОбИндексации, ОбновляемыеСотрудники);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ТаблицаСотрудники.Количество() = 0 Тогда
		Возврат ОбновляемыеСотрудники;
	КонецЕсли;
	
	// Получаем данные для расчета среднего заработка.
	ДанныеДляРасчета = УчетСреднегоЗаработка.ДанныеДляРасчетаОбщегоСреднегоЗаработкаСотрудников(ТаблицаСотрудники, , ИсключаемыйРегистратор);
	
	// Объединение данных, полученных из учета, с данными документа осуществляется по следующим правилам
	// Данные в документе заменяются данными из учета в одном из следующих случаев
	//	- источник данных в документе не относится к группе источников "Результаты редактирования" (СведенияДоНачалаЭксплуатации и Исправления)
	//	- порядковый номер источника из учета (приоритет) выше, чем в документе 
	
	// Следовательно, необходимо в разрезе сотрудника и месяца для каждой коллекции определить максимальный приоритет источника 
	// и наличие результатов редактирования.
	
	ПриоритетИсточников = Перечисления.ИсточникиДанныхДляРасчетаСреднегоЗаработка.ПриоритетИсточников();
	СохраняемыеЗначения = Перечисления.ИсточникиДанныхДляРасчетаСреднегоЗаработка.РезультатыРедактирования();
	
	СочетанияЗамены = Новый ТаблицаЗначений;
	СочетанияЗамены.Колонки.Добавить("Сотрудник");
	СочетанияЗамены.Колонки.Добавить("Период");
	
	ОтборСтрок = Новый Структура("Сотрудник, Период");
	
	СоответствиеКоллекций = Новый Соответствие;
	СоответствиеКоллекций.Вставить(ДанныеДляРасчета.ДанныеОНачислениях, ДанныеДокумента.ДанныеОНачислениях);
	СоответствиеКоллекций.Вставить(ДанныеДляРасчета.ДанныеОВремени, ДанныеДокумента.ДанныеОВремени);
	
	Для Каждого КлючИЗначение Из СоответствиеКоллекций Цикл
		КоллекцияУчета = КлючИЗначение.Ключ;
		КоллекцияДокумента = КлючИЗначение.Значение;
		СочетанияЗамены.Очистить();
		Для Каждого СтрокаДанных Из КоллекцияУчета Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, СтрокаДанных);
			НайденныеСочетания = СочетанияЗамены.НайтиСтроки(ОтборСтрок);
			Если НайденныеСочетания.Количество() > 0 Тогда
				// Такое сочетание уже есть, пропускаем.
				Продолжить;
			КонецЕсли;
			НайденныеСтроки = КоллекцияДокумента.НайтиСтроки(ОтборСтрок);
			Если НайденныеСтроки.Количество() = 0 Тогда
				// Строк за этот месяц вовсе нет в документе, добавляем.
				ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДанных);
				Продолжить;
			КонецЕсли;
			ЕстьСохраняемыеЗначения = Ложь;
			ПриоритетИзДокумента = 0;
			Для Каждого СтрокаДокумента Из НайденныеСтроки Цикл
				Если СохраняемыеЗначения.Найти(СтрокаДокумента.Источник) <> Неопределено Тогда
					// Источник относится к сохраняемым значениям, пропускаем.
					ЕстьСохраняемыеЗначения = Истина;
				КонецЕсли;
				Приоритет = ПриоритетИсточников[СтрокаДокумента.Источник];
				Если ПриоритетИзДокумента < Приоритет Тогда
					ПриоритетИзДокумента = Приоритет;
				КонецЕсли;
			КонецЦикла;
			Если Не ЕстьСохраняемыеЗначения Тогда
				ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДанных);
				Продолжить;
			КонецЕсли;
			// Сравниваем приоритет.
			ПриоритетИзУчета = ПриоритетИсточников[СтрокаДанных.Источник];
			Если ПриоритетИзУчета > ПриоритетИзДокумента Тогда
				ЗаполнитьЗначенияСвойств(СочетанияЗамены.Добавить(), СтрокаДанных);
			КонецЕсли;
		КонецЦикла;
		
		// Удаляем все строки, соответствующие отобранным сочетаниям Сотрудник + Период.
		Для Каждого Сочетание Из СочетанияЗамены Цикл
			ЗаполнитьЗначенияСвойств(ОтборСтрок, Сочетание);
			// Переносим.
			// Удаляем существующие строки...
			СтрокиДокумента = КоллекцияДокумента.НайтиСтроки(ОтборСтрок);
			Для Каждого СтрокаДокумента Из СтрокиДокумента Цикл
				КоллекцияДокумента.Удалить(СтрокаДокумента);
			КонецЦикла;
			// ..и добавляем из данных расчета.
			СтрокиУчета = КоллекцияУчета.НайтиСтроки(ОтборСтрок);
			Для Каждого СтрокаУчета Из СтрокиУчета Цикл
				ЗаполнитьЗначенияСвойств(КоллекцияДокумента.Добавить(), СтрокаУчета);
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ДанныеДокумента.Свойство("ДанныеОбИндексации") Тогда
		// Заполняем таблицы документа с данными среднего заработка.
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеДляРасчета.ДанныеОбИндексации, ДанныеДокумента.ДанныеОбИндексации);
	КонецЕсли;
	
	Возврат ОбновляемыеСотрудники;
	
КонецФункции

// Выполняет упаковку данных общего среднего заработка формы документа для передачи в форму редактирования.
//
Процедура ЗаполнитьТаблицыДанныхСреднегоЗаработкаПоДокументу(ДанныеФормыОбъект, ПараметрыРедактирования) Экспорт
	
	// Переносим содержимое табличных частей в таблицы значений и помещаем последние во временное хранилище
	// - начисления.
	ДанныеНачислений = УчетСреднегоЗаработка.ПустаяТаблицаНачисленийСреднийЗаработокОбщий();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.СреднийЗаработокОбщий, ДанныеНачислений);
	ПараметрыРедактирования.ДанныеОНачислениях = ПоместитьВоВременноеХранилище(ДанныеНачислений);
	
	// - отработанное время
	ОтработанноеВремя = УчетСреднегоЗаработка.ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.ОтработанноеВремяДляСреднегоОбщий, ОтработанноеВремя);
	ПараметрыРедактирования.ДанныеОВремени = ПоместитьВоВременноеХранилище(ОтработанноеВремя);
	
	// - данные индексации
	ДанныеИндексации = УчетСреднегоЗаработка.ПустаяТаблицаДанныеИндексации();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.ДанныеОбИндексации, ДанныеИндексации);
	ПараметрыРедактирования.ДанныеОбИндексации = ПоместитьВоВременноеХранилище(ДанныеИндексации);
	
КонецПроцедуры

// Выполняет упаковку данных общего среднего заработка формы документа для передачи в форму редактирования.
//
Процедура ЗаполнитьТаблицыДанныхСреднегоЗаработкаПоДокументуДляВыходногоПособия(ДанныеФормыОбъект, ПараметрыРедактирования) Экспорт
	
	// Переносим содержимое табличных частей в таблицы значений и помещаем последние во временное хранилище
	// - начисления.
	ДанныеНачислений = УчетСреднегоЗаработка.ПустаяТаблицаНачисленийСреднийЗаработокОбщий();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.СреднийЗаработокДляВыходногоПособия, ДанныеНачислений);
	ПараметрыРедактирования.ДанныеОНачислениях = ПоместитьВоВременноеХранилище(ДанныеНачислений);
	
	// - отработанное время
	ОтработанноеВремя = УчетСреднегоЗаработка.ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.ОтработанноеВремяДляСреднегоДляВыходногоПособия, ОтработанноеВремя);
	ПараметрыРедактирования.ДанныеОВремени = ПоместитьВоВременноеХранилище(ОтработанноеВремя);
	
	// - данные индексации
	ДанныеИндексации = УчетСреднегоЗаработка.ПустаяТаблицаДанныеИндексации();
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеФормыОбъект.ДанныеОбИндексацииДляВыходногоПособия, ДанныеИндексации);
	ПараметрыРедактирования.ДанныеОбИндексации = ПоместитьВоВременноеХранилище(ДанныеИндексации);
	
КонецПроцедуры

// Добавляет команду печати "Расчет среднего заработка", вызывается из модулей
// менеджеров документов.
//
Процедура ДобавитьКомандуПечатиРасчетаСреднегоЗаработка(КомандыПечати, МенеджерПечати, ФункциональныеОпции = "") Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")
		И Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеРабочегоВремениНачисленнойЗарплаты,ЧтениеРабочегоВремениНачисленнойЗарплаты", , Ложь) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = МенеджерПечати;
		КомандаПечати.Идентификатор = "РасчетСреднегоЗаработка";
		КомандаПечати.Представление = НСтр("ru='Расчет среднего заработка';uk='Розрахунок середнього заробітку'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ТребуетсяЧтениеБезОграничений", Истина);
		КомандаПечати.ФункциональныеОпции = ФункциональныеОпции;
		
	КонецЕсли; 
	
КонецПроцедуры

// Добавляет команду печати "Расчет среднего заработка (для выходного пособия)", вызывается из модулей
// менеджеров документов.
//
Процедура ДобавитьКомандуПечатиРасчетаСреднегоЗаработкаВыходногоПособия(КомандыПечати, МенеджерПечати) Экспорт
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная")
		И Пользователи.РолиДоступны("ПолныеПрава,ДобавлениеИзменениеРабочегоВремениНачисленнойЗарплаты,ЧтениеРабочегоВремениНачисленнойЗарплаты", , Ложь) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = МенеджерПечати;
		КомандаПечати.Идентификатор = "РасчетСреднегоЗаработкаВыходногоПособия";
		КомандаПечати.Представление = НСтр("ru='Расчет среднего заработка (для выходного пособия)';uk='Розрахунок середнього заробітку (для вихідної допомоги)'");
		КомандаПечати.ПроверкаПроведенияПередПечатью = Истина;
		КомандаПечати.ДополнительныеПараметры.Вставить("ТребуетсяЧтениеБезОграничений", Истина);
		
	КонецЕсли; 
	
КонецПроцедуры

#Область ИнформацияОЗаполненностиДанныхСреднегоЗаработка

// Функция формирует структуру информационных сведений о заполненности данных 
//  для расчета общего среднего заработка сотрудника.
//
// Параметры:
//	Ссылка				- ДокументСсылка на документ а котором рассчитывается средний заработок
//	ЗаполнениеВыполнено	- Булево, Истина, если в документе произведено заполнение расчета среднего заработка
// 	Сотрудник
//	НачалоРасчета - дата начала периода расчета среднего заработка.
//	ОкончаниеРасчета - дата окончания периода расчета среднего заработка.
//	ДанныеНачислений - данные формы табличной части начислений общего среднего заработка.
//	ДанныеОВремени - данные формы табличной части времени общего среднего заработка.
//	УчитыватьДанныеОВремени - булево, необходимость проверки заполненности данных о времени.
//
Функция ИнформацияОЗаполненностиДанныхСреднегоЗаработка(Ссылка, ЗаполнениеВыполнено, Сотрудник, ДатаНачалаСобытия, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени = Истина) Экспорт
	
	Если ТребуетсяПерезаполнитьСведенияОСреднемЗаработке(Ссылка, ЗаполнениеВыполнено) Тогда
		
		СтруктураИнфонадписи = ИнфонадписьТребуетсяПерезаполнитьСведенияОСреднемЗаработке();
		
	ИначеЕсли ТребуетсяДозаполнитьСведенияОСреднемЗаработке(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаНачалаСобытия, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени) Тогда
		
		СтруктураИнфонадписи = ИнфонадписьТребуетсяДозаполнитьСведенияОСреднемЗаработке();
		
	Иначе 
		
		СтруктураИнфонадписи = ИнфонадписьИспользованныеДанныеОСреднемЗаработке(НачалоПериодаРасчета, ОкончаниеПериодаРасчета);
		
	КонецЕсли;
	
	Возврат СтруктураИнфонадписи;
	
КонецФункции

// Функция проверяет есть ли в указанном периоде месяцы данные по которым не заполнены.
//
// Параметры:
//	НачалоРасчета - дата начала периода расчета среднего заработка.
//	ОкончаниеРасчета - дата окончания периода расчета среднего заработка.
//	ДанныеНачислений - данные формы табличной части начислений общего среднего заработка.
//	ДанныеОВремени - данные формы табличной части времени общего среднего заработка.
//	УчитыватьДанныеОВремени - булево, необходимость проверки заполненности данных о времени.
//
Функция ЕстьНеЗаполненныеПериодыРасчетаСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени, МесяцыКорректировкиСреднегоЗаработка)
	
	ЕстьНеЗаполненныеДанные = Ложь;
	
	Если ДанныеНачислений.Количество() = 0 
		Или ДанныеОВремени.Количество() = 0 Тогда
		Если МесяцыКорректировкиСреднегоЗаработка.Количество() > 0 Тогда
			ЕстьНеЗаполненныеДанные = Истина;
		КонецЕсли;
	Иначе
		// Необходимо проверить на все ли месяцы интервала 
		// заполнены данные о начислениях и отработанном времени.
		// Проверяем по таблице начислений.
		ПериодыНезаполненныхДанных = ПериодыНезаполненногоСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений);
		Для Каждого Месяц Из ПериодыНезаполненныхДанных Цикл
			Если МесяцыКорректировкиСреднегоЗаработка.Найти(Месяц) <> Неопределено Тогда
				ЕстьНеЗаполненныеДанные = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		// Проверяем по таблице времени.
		Если УчитыватьДанныеОВремени И Не ЕстьНеЗаполненныеДанные Тогда
			ПериодыНезаполненныхДанных = ПериодыНезаполненногоСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеОВремени);
			Для Каждого Месяц Из ПериодыНезаполненныхДанных Цикл
				Если МесяцыКорректировкиСреднегоЗаработка.Найти(Месяц) <> Неопределено Тогда
					ЕстьНеЗаполненныеДанные = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЕстьНеЗаполненныеДанные;
	
КонецФункции

// Функция возвращает структуру для заполнения инфонадписи о заполненности данных для расчета
// среднего заработка сотрудника.
//
Функция ИнфонадписьТребуетсяПерезаполнитьСведенияОСреднемЗаработке() Экспорт
	
	ТекстСообщения = НСтр("ru='Требуется перезаполнить сведения
        |о среднем заработке.'
        |;uk='Потрібно перезаповнити відомості
        |про середній заробіток.'");
	
	ЧастиСообщения = Новый Массив;
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(ТекстСообщения, , ЦветаСтиля.ПоясняющийОшибкуТекст));
	ЧастиСообщения.Добавить(" ");
	ЧастиСообщения.Добавить(Новый ФорматированнаяСтрока(НСтр("ru='Подробнее';uk='Докладніше'") + "...", , , , "Подробнее"));
		
	Картинка = БиблиотекаКартинок.Предупреждение;
	
	Возврат ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(Новый ФорматированнаяСтрока(ЧастиСообщения), Картинка);
	
КонецФункции

// Функция возвращает структуру для заполнения инфонадписи о заполненности данных для расчета
// среднего заработка сотрудника.
//
Функция ИнфонадписьТребуетсяДозаполнитьСведенияОСреднемЗаработке() Экспорт
	
	ТекстСообщения = Новый ФорматированнаяСтрока(НСтр("ru='Данные о заработке неполные.
        |Для ввода недостающих данных используйте команду «Изменить»'
        |;uk='Дані про заробіток неповні.
        |Для введення відсутніх даних використовуйте команду «Змінити»'"));
	
	Картинка = БиблиотекаКартинок.Предупреждение;
	
	Возврат ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(ТекстСообщения, Картинка);
	
КонецФункции

// Функция возвращает структуру для заполнения инфонадписи о заполненности данных для расчета
// среднего заработка сотрудника.
//
// Параметры:
//	НачалоРасчета - дата начала периода расчета среднего заработка.
//	ОкончаниеРасчета - дата окончания периода расчета среднего заработка.
//
Функция ИнфонадписьИспользованныеДанныеОСреднемЗаработке(НачалоПериодаРасчета, ОкончаниеПериодаРасчета) Экспорт 
	
	Если НачалоМесяца(НачалоПериодаРасчета) = НачалоМесяца(ОкончаниеПериодаРасчета) Тогда
		// За один месяц
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Использованы данные о заработке за %1';uk='Використані дані про заробіток за %1'"), 
		Формат(НачалоПериодаРасчета, "ДФ='ММММ гггг'"));
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Использованы данные о заработке за период %1 - %2';uk='Використані дані про заробіток за період %1 - %2'"), 
		Формат(НачалоПериодаРасчета, "ДФ='ММММ гггг'"), 
		Формат(ОкончаниеПериодаРасчета, "ДФ='ММММ гггг'"));
	КонецЕсли;
	
	Картинка = БиблиотекаКартинок.Информация;
	
	Возврат ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(ТекстСообщения, Картинка);
	
КонецФункции

// Инкапсулирует текст и картинку инфонадписи в структуру.
//
// Параметры:
//	Текст - текст сообщения инфонадписи.
//	Картинка - картинка для инфонадписи.
//
Функция ЗаполненнаяСтруктураИнфонадписиОСреднемЗаработке(Текст, Картинка) Экспорт
	
	СтруктураИнфонадписи = Новый Структура;
	СтруктураИнфонадписи.Вставить("Текст", 	Новый ФорматированнаяСтрока(Текст));
	СтруктураИнфонадписи.Вставить("Картинка", Картинка);
	
	Возврат СтруктураИнфонадписи;

КонецФункции

Функция ТребуетсяПерезаполнитьСведенияОСреднемЗаработке(СсылкаНаДокумент, ЗаполнениеВыполнено) Экспорт
	
	ТребуетсяПерезаполнитьСведенияОСреднемЗаработке = Не ЗаполнениеВыполнено И ЗначениеЗаполнено(СсылкаНаДокумент);
	
	Если ТребуетсяПерезаполнитьСведенияОСреднемЗаработке Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);	
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
			|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка
			|ИЗ
			|	РегистрСведений.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
			|ГДЕ
			|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка = &СсылкаНаДокумент";
			
		ТребуетсяПерезаполнитьСведенияОСреднемЗаработке = Не Запрос.Выполнить().Пустой(); 
		
	КонецЕсли;
	
	Возврат ТребуетсяПерезаполнитьСведенияОСреднемЗаработке;
	
КонецФункции

Функция ТребуетсяДозаполнитьСведенияОСреднемЗаработке(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаНачалаСобытия, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени) Экспорт
	
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник), "ДатаПриема");
	Если КадровыеДанные.Количество() > 0 Тогда
		ДатаПриемаНаРаботу = КадровыеДанные[0].ДатаПриема;
	иначе
		ДатаПриемаНаРаботу = '00010101';
	КонецЕсли;	
	
	МесяцыКорректировкиСреднегоЗаработка = МесяцыКорректировкиСреднегоЗаработка(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаПриемаНаРаботу, ДатаНачалаСобытия);
	
	ЕстьНеЗаполненныеДанные = ЕстьНеЗаполненныеПериодыРасчетаСреднегоЗаработка(НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДанныеНачислений, ДанныеОВремени, УчитыватьДанныеОВремени, МесяцыКорректировкиСреднегоЗаработка);
	
	Возврат ЕстьНеЗаполненныеДанные;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РегистрацияРучныхКорректировок

Функция КорректировкиОбщегоСреднегоЗаработкаДокумента(ДокументСсылка) Экспорт
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Начисления.Сотрудник,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Период,
		|	Начисления.Индексируется,
		|	Начисления.Сумма,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Ссылка.Организация КАК Организация,
		|	Начисления.Сотрудник.ФизическоеЛицо КАК ФизическоеЛицо
		|ИЗ
		|	#ТабличнаяЧастьНачисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка = &ДокументСсылка
		|	И Начисления.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Время.Сотрудник,
		|	Время.ПорядокРасчета,
		|	Время.Период,
		|	Время.ОтработаноДней,
		|	Время.ОтработаноДнейПятидневка,
		|	Время.ОтработаноЧасовПятидневка,
		|	Время.ОтработаноЧасов,
		|	Время.ОтработаноДнейШестидневка,
		|	Время.ОтработаноДнейКалендарных,
		|	Время.НормаДнейПроизводственныйКалендарь,
		|	Время.НормаЧасовПроизводственныйКалендарь
		|ИЗ
		|	#ТабличнаяЧастьВремя КАК Время
		|ГДЕ
		|	Время.Ссылка = &ДокументСсылка
		|	И Время.Источник = ЗНАЧЕНИЕ(Перечисление.ИсточникиДанныхДляРасчетаСреднегоЗаработка.СведенияДоНачалаЭксплуатации)";

	ИмяТаблицыДокумента = ДокументСсылка.Метаданные().ПолноеИмя();
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТабличнаяЧастьНачисления", ИмяТаблицыДокумента + ".СреднийЗаработокОбщий");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ТабличнаяЧастьВремя", ИмяТаблицыДокумента + ".ОтработанноеВремяДляСреднегоОбщий");
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Корректировки = Новый Структура("КорректировкиНачислений, КорректировкиВремени");
	Корректировки.КорректировкиНачислений = РезультатыЗапроса[0].Выгрузить();
	Корректировки.КорректировкиВремени = РезультатыЗапроса[1].Выгрузить();
	
	Возврат Корректировки;
	
КонецФункции

Функция ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка() Экспорт
	
	Параметры = Новый Структура(
		"Организация, 
		|ФизическоеЛицо");
	
	Возврат Параметры;
	
КонецФункции

// Процедура выполняет запись сведений об общем среднем заработке 
//  на основе данных, введенных пользователем.
//
// Параметры:
//	КорректировкиНачислений - таблица значений, структуры аналогичной набору записей 
//								регистра сведений СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.
//	КорректировкиВремени	- таблица значений, структуры аналогичной набору записей 
//								регистра сведений СведенияОВремениДляРасчетаСреднегоОбщий.
//
Процедура ЗаписатьКорректировкиОбщегоСреднегоЗаработка(Корректировки, НачалоПериода, ОкончаниеПериода, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		ДополнительныеПараметры = ДополнительныеПараметрыЗаписиКорректировокОбщегоСреднегоЗаработка();
	КонецЕсли;
	
	КорректировкиНачислений = Корректировки.КорректировкиНачислений;
	КорректировкиВремени = Корректировки.КорректировкиВремени;
	
	// Корректировки начислений
	ПереименоватьКолонкуПериодВМесяц(КорректировкиНачислений);
	
	ИзмеренияНачислений = 
		"Сотрудник, 
		|Организация, 
		|ФизическоеЛицо, 
		|ПорядокРасчета, 
		|Месяц, 
		|СоставнаяЧасть, 
		|СтатьяФинансирования, 
		|Индексируется, 
		|Год, 
		|ДатаНачалаБазовогоПериода, 
		|КоличествоМесяцев";
	КорректировкиНачислений.Свернуть(ИзмеренияНачислений, "Сумма");
	
	ЗначенияПоУмолчанию = Новый Структура(
		"Организация, 
		|ФизическоеЛицо");
	ЗаполнитьЗначенияСвойств(ЗначенияПоУмолчанию, ДополнительныеПараметры);
	
	ЗаписатьТаблицуЗначенийВРегистрСведений(
		КорректировкиНачислений, 
		РегистрыСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоздатьНаборЗаписей(), 
		"Сотрудник, ПорядокРасчета, Месяц",
		НачалоПериода, 
		ОкончаниеПериода, 
		Истина,
		ЗначенияПоУмолчанию);
		
	// Корректировки времени
	ПереименоватьКолонкуПериодВМесяц(КорректировкиВремени);
	
	ИзмеренияВремени = "Сотрудник, ПорядокРасчета, Месяц";
	КорректировкиВремени.Свернуть(ИзмеренияВремени, "ОтработаноДней, ОтработаноЧасов, ОтработаноДнейКалендарных");
	
	ЗаписатьТаблицуЗначенийВРегистрСведений(
		КорректировкиВремени, 
		РегистрыСведений.СведенияОВремениДляРасчетаСреднегоОбщий.СоздатьНаборЗаписей(), 
		ИзмеренияВремени,
		НачалоПериода, 
		ОкончаниеПериода,
		Истина);
		
	Если РасчетЗарплатыРасширенный.НастройкиРасчетаЗарплаты().ПереноситьДанныеВДругойУчетСреднегоЗаработка Тогда
		УчетПособийСоциальногоСтрахованияРасширенный.ПеренестиКорректировкиВСреднийЗаработокФСС(КорректировкиНачислений, КорректировкиВремени, НачалоПериода, ОкончаниеПериода);
	КонецЕсли;
		
КонецПроцедуры	

Процедура ПеренестиКорректировкиВСреднийЗаработокОбщий(КорректировкиНачислений, КорректировкиВремени, Организация, НачалоПериода, ОкончаниеПериода) Экспорт
	
	КорректировкиОбщегоЗаработка = СведенияОбщегоСреднегоЗаработкаПоКорректировкамФСС(Организация, КорректировкиНачислений, КорректировкиВремени);
	Если КорректировкиОбщегоЗаработка.Свойство("КорректировкиНачислений") Тогда
		ЗаписатьТаблицуЗначенийВРегистрСведений(
			КорректировкиОбщегоЗаработка["КорректировкиНачислений"],
			РегистрыСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.СоздатьНаборЗаписей(), 
			"Сотрудник, ПорядокРасчета, Месяц",
			НачалоПериода, 
			ОкончаниеПериода, 
			Ложь);
	КонецЕсли;
	
	Если КорректировкиОбщегоЗаработка.Свойство("КорректировкиВремени") Тогда
		ЗаписатьТаблицуЗначенийВРегистрСведений(
			КорректировкиОбщегоЗаработка["КорректировкиВремени"], 
			РегистрыСведений.СведенияОВремениДляРасчетаСреднегоОбщий.СоздатьНаборЗаписей(), 
			"Сотрудник, ПорядокРасчета, Месяц",
			НачалоПериода, 
			ОкончаниеПериода,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КонструкторыТаблицДанныхСреднегоЗаработка

// Создает таблицу значений со структурой аналогичной структуре табличной части СреднийЗаработокОбщий
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаНачисленийСреднийЗаработокОбщий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	ТаблицаДанных.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	ТаблицаДанных.Колонки.Добавить("ДатаНачалаБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("КоличествоМесяцев", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(3, 0));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Создает таблицу значений со структурой аналогичной структуре табличной части СреднийЗаработокОбщий
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаГодовыхПремий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	ТаблицаДанных.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	ТаблицаДанных.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ТаблицаДанных.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Создает таблицу значений со структурой аналогичной структуре табличной части ОтработанноеВремяДляСреднегоОбщий
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаОтработанноеВремяСреднийЗаработокОбщий() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ОтработаноДней", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноЧасов", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("ОтработаноДнейКалендарных", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("НормаДнейПроизводственныйКалендарь", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("НормаЧасовПроизводственныйКалендарь", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("НормаДнейПоГрафикуСотрудника", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("НормаЧасовПоГрафикуСотрудника", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("СреднемесячноеКоличествоДней", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(7, 2));
	ТаблицаДанных.Колонки.Добавить("Источник", Новый ОписаниеТипов("ПеречислениеСсылка.ИсточникиДанныхДляРасчетаСреднегоЗаработка"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

// Создает таблицу значений со структурой аналогичной структуре табличной части ДанныеОбИндексации
// для хранения данных среднего заработка в документе.
//
Функция ПустаяТаблицаДанныеИндексации() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("КоэффициентИндексации", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 2));
	
	Возврат ТаблицаДанных;
	
КонецФункции

Функция ПустаяТаблицаИсходныхДанныхРасчетаОбщегоСреднегоЗаработка() Экспорт
	
	ИсходныеДанные = Новый ТаблицаЗначений;
	
	ИсходныеДанные.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ИсходныеДанные.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	ИсходныеДанные.Колонки.Добавить("ДатаНачалаСобытия", Новый ОписаниеТипов("Дата"));
	ИсходныеДанные.Колонки.Добавить("НачалоПериодаРасчетаСреднего", Новый ОписаниеТипов("Дата"));
	ИсходныеДанные.Колонки.Добавить("ОкончаниеПериодаРасчетаСреднего", Новый ОписаниеТипов("Дата"));
	
	Возврат ИсходныеДанные;
	
КонецФункции

Функция ПустаяТаблицаДляРегистрацииДанныхСреднегоЗаработка() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	
	ТипыРегистраторов = Новый Массив;
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.БольничныйЛист"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ДоходВНатуральнойФорме"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Командировка"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.МатериальнаяПомощь"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.НачислениеЗарплаты"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Отпуск"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОтпускБезСохраненияОплаты"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Отгул"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ОтпускПоУходуЗаРебенком"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПереносДанных"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Премия"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПрогулНеявка"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.ПростойСотрудников"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.РазовоеНачисление"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.СторнированиеНачислений"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.Увольнение"));
	ТипыРегистраторов.Добавить(Тип("ДокументСсылка.УвольнениеСписком"));
	
	ТаблицаДанных.Колонки.Добавить("ДокументСсылка", Новый ОписаниеТипов(ТипыРегистраторов));
	
	ТаблицаДанных.Колонки.Добавить("ДатаДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ПериодДействия", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("МесяцНачисления", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ДатаНачала", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ДатаОкончания", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("НачалоБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("ОкончаниеБазовогоПериода", Новый ОписаниеТипов("Дата"));
	ТаблицаДанных.Колонки.Добавить("Сторно", Новый ОписаниеТипов("Булево"));
	ТаблицаДанных.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаДанных.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ТаблицаДанных.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДанных.Колонки.Добавить("Начисление", Новый ОписаниеТипов("ПланВидовРасчетаСсылка.Начисления"));
	ТаблицаДанных.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	ТаблицаДанных.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаДанных.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьПричиныПерерасчетов(СсылкаНаДокумент, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Получение признака о том, что нужно удалить перерасчеты текущего периода
	УдалитьПерерасчетыСреднегоЗаработка = Неопределено;
	ДополнительныеПараметры.Свойство("УдалитьПерерасчетыСреднегоЗаработка", УдалитьПерерасчетыСреднегоЗаработка);
	Если УдалитьПерерасчетыСреднегоЗаработка <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Сотрудники = Неопределено;
	ДополнительныеПараметры.Свойство("СотрудникиПерерасчетаЗаработка", Сотрудники);
	
	НаборЗаписей = РегистрыСведений.ПерерасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ДокументСреднегоЗаработка.Установить(СсылкаНаДокумент);
	
	Если Сотрудники <> Неопределено Тогда
		
		Если ТипЗнч(Сотрудники) = Тип("СправочникСсылка.Сотрудники") Тогда
			СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудники);
		Иначе
			СписокСотрудников = Сотрудники;
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
		Запрос.УстановитьПараметр("СсылкаНаДокумент", СсылкаНаДокумент);
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПерерасчетСреднегоЗаработка.Организация,
			|	ПерерасчетСреднегоЗаработка.Сотрудник,
			|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка,
			|	ПерерасчетСреднегоЗаработка.ДокументОснование
			|ИЗ
			|	РегистрСведений.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
			|ГДЕ
			|	НЕ ПерерасчетСреднегоЗаработка.Сотрудник В (&СписокСотрудников)
			|	И ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка = &СсылкаНаДокумент";
		
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция РегистрыСреднегоЗаработка() Экспорт
	
	МассивРегистров = Новый Массив;
	
	УчетПособийСоциальногоСтрахованияРасширенный.РегистрыУчетаПособийСоциальногоСтрахованияРасширенная(МассивРегистров);
	
	МассивРегистров.Добавить(Метаданные.РегистрыНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий);
	МассивРегистров.Добавить(Метаданные.РегистрыНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий);
			
	Возврат МассивРегистров;

КонецФункции

#Область РегистрацияДанныхДляРасчетаСреднегоЗаработка

// Процедура выполняет регистрацию данных о начисленных суммах и отработанном времени
//  для использования при расчете общего среднего заработка.
//
//	Параметры: см. комментарий к методу ЗарегистрироватьДанныеСреднегоЗаработка.
//
Процедура ЗарегистрироватьДанныеОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ЗаписыватьДвижения = Ложь)
	

	ИсключатьВПериодКомандировок = Ложь;
	
	ЗарегистрироватьНачисленияДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения);
	
	ЗарегистрироватьДанныеВремениДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения);
	
КонецПроцедуры

Процедура ИсключитьНачисленияВПериодКомандировок(МенеджерВременныхТаблиц, Регистратор, ИсключатьВПериодКомандировок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
	// Определяем суммы начислений, попадающих в период командировки.
	Если Не ИсключатьВПериодКомандировок Тогда
		Возврат;
	КонецЕсли;
	
	// Определяем максимальный период действия для ограничения базовых записей.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(Начисления.ПериодДействия) КАК МаксимальныйПериодДействия
		|ИЗ
		|	ВТНачисления КАК Начисления";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Запрос.УстановитьПараметр("МаксимальныйПериодДействия", Выборка.МаксимальныйПериодДействия);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	&МаксимальныйПериодДействия КАК ПериодРегистрации,
		|	ПериодыКомандировок.Регистратор,
		|	ПериодыКомандировок.НомерСтроки,
		|	ПериодыКомандировок.Сотрудник,
		|	ПериодыКомандировок.ВидРасчета,
		|	ПериодыКомандировок.Начало КАК БазовыйПериодНачало,
		|	ПериодыКомандировок.Окончание КАК БазовыйПериодКонец
		|ПОМЕСТИТЬ ВТОсновныеЗаписи
		|ИЗ
		|	ВТПериодыКомандировок КАК ПериодыКомандировок";
	Запрос.Выполнить();
	
	// Готовим запрос к расчетной базе начислений.
	ИменаИзмерений = РасчетЗарплатыРасширенный.ИменаИзмеренийРасчетнойБазыНачислений();
	ИменаИзмерений.Сотрудник = "Сотрудник";
	
	ОтборБазовыхЗаписей = Новый Массив;
	ОтборБазовыхЗаписей.Добавить(РасчетЗарплатыРасширенный.ЭлементОтбораБазовыхЗаписей("Регистратор", Регистратор));
	
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБаза(МенеджерВременныхТаблиц, ИменаИзмерений, "ВТИсключаемыеВПериодКомандировки", ОтборБазовыхЗаписей);
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИсключаемыеСуммы.ИдентификаторСтрокиРазрез КАК ИдентификаторСтроки,
		|	СУММА(ИсключаемыеСуммы.РезультатБаза) КАК Сумма
		|ПОМЕСТИТЬ ВТИсключаемыеВПериодКомандировкиСуммы
		|ИЗ
		|	ВТРасчетнаяБаза КАК ИсключаемыеСуммы
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсключаемыеСуммы.ИдентификаторСтрокиРазрез
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОсновныеЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасчетнаяБаза";
	Запрос.Выполнить();
	
	// Регистрация сумм начислений для общего среднего заработка.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	*
		|ПОМЕСТИТЬ ВТНачисленияПринимаемые
		|ИЗ
		|	ВТНачисления КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияПринимаемые КАК Начисления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	-ИсключаемыеСуммы.Сумма,
		|	Начисления.*
		|ИЗ
		|	ВТНачисленияПринимаемые КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИсключаемыеВПериодКомандировкиСуммы КАК ИсключаемыеСуммы
		|		ПО (ИсключаемыеСуммы.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗарегистрироватьНачисленияДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения = Ложь)
	
	
	Регистратор = Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Отбор.Регистратор.Значение;
	
	СоздатьВТПорядокРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ДатаДействия,
		|	Начисления.ПериодДействия,
		|	Начисления.ДатаНачала КАК ДатаНачала,
		|	Начисления.ДатаОкончания КАК ДатаОкончания,
		|	Начисления.МесяцНачисления,
		|	Начисления.НомерСтроки,
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Организация КАК Организация,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Начисление,
		|	Начисления.НачалоБазовогоПериода,
		|	Начисления.ОкончаниеБазовогоПериода,
		|	Начисления.ДокументСсылка,
		|	Начисления.Сумма КАК Сумма,
		|	ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|	НастройкиНачисления.Значение КАК СоставнаяЧасть,
		|	НастройкиНачисления.Индексируется КАК Индексируется,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисленияОбщегоСреднегоЗаработка
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокРасчетаОбщегоСреднегоЗаработка КАК ПорядокРасчетаДляУчетаНачислений
		|		ПО (ПорядокРасчетаДляУчетаНачислений.ДатаДействия = Начисления.ДатаДействия)
		|			И (ПорядокРасчетаДляУчетаНачислений.Начисление = Начисления.Начисление)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачисления
		|		ПО (НастройкиНачисления.Ссылка = Начисления.Начисление)
		|			И (НастройкиНачисления.ПорядокРасчета = ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.ДокументСсылка,
		|	Начисления.Начисление,
		|	ВЫБОР
		|		КОГДА ВидыРасчета.ИспользованиеПериода <> ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияПериодаНачисления.ПериодДействия)
		|			ТОГДА Начисления.ПериодДействия
		|		ИНАЧЕ НАЧАЛОПЕРИОДА(Начисления.ДатаДействия, МЕСЯЦ)
		|	КОНЕЦ КАК Период,
		|	Начисления.ДатаДействия,
		|	Начисления.Сотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо КАК ФизическоеЛицо,
		|	Начисления.Организация КАК Организация,
		|	Начисления.ПорядокРасчета КАК ПорядокРасчета,
		|	Начисления.СоставнаяЧасть КАК СоставнаяЧасть,
		|	Начисления.СтатьяФинансирования КАК СтатьяФинансирования,
		|	Начисления.Индексируется КАК Индексируется,
		|	ВЫБОР
		|		КОГДА Начисления.СоставнаяЧасть В (&ГодовыеПремии)
		|			ТОГДА ГОД(Начисления.НачалоБазовогоПериода)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Год,
		|	ВЫБОР
		|		КОГДА Начисления.СоставнаяЧасть В (&Премии)
		|			ТОГДА ВЫБОР
		|					КОГДА Начисления.СоставнаяЧасть В (&ГодовыеПремии)
		|						ТОГДА 12
		|					ИНАЧЕ РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(Начисления.НачалоБазовогоПериода, МЕСЯЦ), НАЧАЛОПЕРИОДА(Начисления.ОкончаниеБазовогоПериода, МЕСЯЦ), МЕСЯЦ) + 1
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК КоличествоМесяцев,
		|	ВЫБОР
		|		КОГДА Начисления.СоставнаяЧасть В (&Премии)
		|			ТОГДА Начисления.НачалоБазовогоПериода
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ДатаНачалаБазовогоПериода,
		|	Начисления.Сумма КАК Сумма,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияОбщегоСреднегоЗаработка КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчета
		|		ПО (ВидыРасчета.Ссылка = Начисления.Начисление)";
	
	Запрос.УстановитьПараметр("Премии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.Премии());
	Запрос.УстановитьПараметр("ГодовыеПремии", Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии());
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.Период КАК Период,
		|	Сотрудники.ГоловнойСотрудник КАК Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Организация,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Индексируется,
		|	Начисления.Год,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	СУММА(Начисления.Сумма) КАК Сумма
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Сотрудники КАК Сотрудники
		|		ПО (Сотрудники.Ссылка = Начисления.Сотрудник)
		|
		|СГРУППИРОВАТЬ ПО
		|	Начисления.СоставнаяЧасть,
		|	Сотрудники.ГоловнойСотрудник,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.ПорядокРасчета,
		|	Начисления.Период,
		|	Начисления.Индексируется,
		|	Начисления.Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Организация,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.Год,
		|	Начисления.КоличествоМесяцев
		|
		|ИМЕЮЩИЕ
		|	СУММА(Начисления.Сумма) <> 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Записывать = Истина;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Добавить(), Выборка);
	КонецЦикла;
	
	Если ЗаписыватьДвижения Тогда
		Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Записать();
		Движения.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеВремениДляРасчетаОбщегоСреднегоЗаработка(Движения, Отказ, МенеджерВременныхТаблиц, ИсключатьВПериодКомандировок, ЗаписыватьДвижения = Ложь)
	
	
	// Вызов общий, и при этом отработанное время регистрируется не всеми документами,
	// поэтому делаем проверку
	Если ТипЗнч(Движения) = Тип("Структура") Тогда
		// для типа Структура
		Если Не Движения.Свойство("ДанныеОВремениДляРасчетаСреднегоОбщий") Тогда
			Возврат;
		КонецЕсли;
	Иначе
		// для типа КоллекцияДвижений
		Если Движения.Найти("ДанныеОВремениДляРасчетаСреднегоОбщий") = Неопределено Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// При этом время необходимо писать только по основному сотруднику, по подработкам не писать, 
	// т.к. считается, что он подработки выполняет в течение времени основной работы.
	
	СоздатьВТВидыНачисленийДляРегистрацииОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Отбор.Регистратор.Значение);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПД.Регистратор,
		|	ФПД.Сотрудник,
		|	ФПД.Сторно,
		|	ФПД.ВидРасчета,
		|	ФПД.ГрафикРаботы,
		|	ФПД.ПериодДействия КАК Месяц,
		|	ФПД.ПериодДействияНачало КАК НачалоПериода,
		|	ФПД.ПериодДействияКонец КАК ОкончаниеПериода
		|ПОМЕСТИТЬ ВТФактическийПериодДействия
		|ИЗ
		|	РегистрРасчета.Начисления.ФактическийПериодДействия(
		|			Регистратор = &Регистратор
		|				И Сотрудник.ГоловнойСотрудник = Сотрудник
		|				И ВидРасчета В
		|					(ВЫБРАТЬ
		|						ВТВидыНачислений.Ссылка
		|					ИЗ
		|						ВТВидыНачислений)) КАК ФПД
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПД.Регистратор,
		|	ФПД.Сотрудник,
		|	ФПД.Сторно,
		|	ФПД.ГрафикРаботы,
		|	ФПД.Месяц,
		|	ФПД.ОкончаниеПериода,
		|	ФПД.ВидРасчета,
		|	ФПД.НачалоПериода,
		|	ВидыНачислений.Прогул,
		|	ВидыНачислений.ЗачетНормыВремени,
		|	ВидыНачислений.ЗачетОтработанногоВремени,
		|	ВидыНачислений.ЕжегодныйОтпуск
		|ПОМЕСТИТЬ ВТФПДСотрудников
		|ИЗ
		|	ВТФактическийПериодДействия КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО (ВидыНачислений.Ссылка = ФПД.ВидРасчета)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыГрафиков.ГрафикРаботы,
		|	ПериодыГрафиков.НачалоПериода,
		|	ПериодыГрафиков.ОкончаниеПериода
		|ПОМЕСТИТЬ ВТПериодыГрафиков
		|ИЗ
		|	ВТФПДСотрудников КАК ПериодыГрафиков
		|ГДЕ
		|	ПериодыГрафиков.ЗачетНормыВремени";
	
	Запрос.УстановитьПараметр("Регистратор", Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
	УчетРабочегоВремени.СоздатьВТДанныеПроизводственногоКалендаряПоГрафикам(МенеджерВременныхТаблиц);
	



Запрос.Текст = 
		"ВЫБРАТЬ
		|		ФПД.Сотрудник КАК Сотрудник,
		|		ФПД.Месяц КАК Месяц,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|		0 КАК ОтработаноДней,
		|		0 КАК ОтработаноЧасов,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Сторно = ИСТИНА
		|					ТОГДА -ДанныеКалендаряПоФПД.ДнейКалендарных
		|				ИНАЧЕ ДанныеКалендаряПоФПД.ДнейКалендарных
		|			КОНЕЦ) КАК ДнейКалендарных
		|	ИЗ
		|		ВТФПДСотрудников КАК ФПД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряПоГрафикам КАК ДанныеКалендаряПоФПД
		|			ПО (ДанныеКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|				И (ДанныеКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|				И (ДанныеКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|				И (ФПД.ЗачетНормыВремени)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокРасчетаОбщегоСреднегоЗаработкаСводно КАК ПорядокРасчетаДляУчетаНачислений
		|		ПО (ПорядокРасчетаДляУчетаНачислений.Начисление = ФПД.ВидРасчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачисления
		|		ПО (НастройкиНачисления.Ссылка = ФПД.ВидРасчета)
		|			И (НастройкиНачисления.ПорядокРасчета = ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ФПД.Сотрудник,
		|		ФПД.Месяц,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета";
		
		


	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц КАК Период,
		|	ДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ИЗ
		|	(ВЫБРАТЬ
		|		ФПД.Сотрудник КАК Сотрудник,
		|		ФПД.Месяц КАК Месяц,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|		0 КАК ОтработаноДней,
		|		0 КАК ОтработаноЧасов,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Сторно = ИСТИНА
		|					ТОГДА -ДанныеКалендаряПоФПД.ДнейКалендарных
		|				ИНАЧЕ ДанныеКалендаряПоФПД.ДнейКалендарных
		|			КОНЕЦ) КАК ДнейКалендарных
		|	ИЗ
		|		ВТФПДСотрудников КАК ФПД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряПоГрафикам КАК ДанныеКалендаряПоФПД
		|			ПО (ДанныеКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|				И (ДанныеКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|				И (ДанныеКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|				И (ФПД.ЗачетНормыВремени)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокРасчетаОбщегоСреднегоЗаработкаСводно КАК ПорядокРасчетаДляУчетаНачислений
		|		ПО (ПорядокРасчетаДляУчетаНачислений.Начисление = ФПД.ВидРасчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачисления
		|		ПО (НастройкиНачисления.Ссылка = ФПД.ВидРасчета)
		|			И (НастройкиНачисления.ПорядокРасчета = ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ФПД.Сотрудник,
		|		ФПД.Месяц,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ФПД.Сотрудник,
		|		ФПД.Месяц,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета,
		|		0,
		|		0,
		|		СУММА(ВЫБОР
		|				КОГДА ФПД.Сторно = ИСТИНА
		|					ТОГДА -ДанныеКалендаряПоФПД.Праздников
		|				ИНАЧЕ ДанныеКалендаряПоФПД.Праздников
		|			КОНЕЦ)
		|	ИЗ
		|		ВТФПДСотрудников КАК ФПД
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеПроизводственногоКалендаряПоГрафикам КАК ДанныеКалендаряПоФПД
		|			ПО (ДанныеКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|				И (ДанныеКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|				И (ДанныеКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|				И (ФПД.ЕжегодныйОтпуск)
		|				И (ФПД.ЗачетНормыВремени)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокРасчетаОбщегоСреднегоЗаработкаСводно КАК ПорядокРасчетаДляУчетаНачислений
		|		ПО (ПорядокРасчетаДляУчетаНачислений.Начисление = ФПД.ВидРасчета)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачисления
		|		ПО (НастройкиНачисления.Ссылка = ФПД.ВидРасчета)
		|			И (НастройкиНачисления.ПорядокРасчета = ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ФПД.Сотрудник,
		|		ФПД.Месяц,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОтработанноеВремя.Сотрудник,
		|		ОтработанноеВремя.ПериодДействия,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета,
		|		СУММА(ОтработанноеВремя.ОтработаноДней),
		|		СУММА(ОтработанноеВремя.ОтработаноЧасов),
		|		0
		|	ИЗ
		|		РегистрНакопления.ОтработанноеВремяПоСотрудникам КАК ОтработанноеВремя
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|			ПО (ВидыНачислений.Ссылка = ОтработанноеВремя.Начисление)
		|				И (ОтработанноеВремя.Регистратор = &Регистратор)
		|				И (ОтработанноеВремя.Сотрудник = ОтработанноеВремя.Сотрудник.ГоловнойСотрудник)
		|				И (ВидыНачислений.ЗачетОтработанногоВремени)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПорядокРасчетаОбщегоСреднегоЗаработкаСводно КАК ПорядокРасчетаДляУчетаНачислений
		|		ПО (ПорядокРасчетаДляУчетаНачислений.Начисление = ОтработанноеВремя.Начисление)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачисления
		|		ПО (НастройкиНачисления.Ссылка = ОтработанноеВремя.Начисление)
		|			И (НастройкиНачисления.ПорядокРасчета = ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ОтработанноеВремя.Регистратор,
		|		ОтработанноеВремя.ПериодДействия,
		|		ОтработанноеВремя.Сотрудник,
		|		ПорядокРасчетаДляУчетаНачислений.ПорядокРасчета
		|	) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.ПорядокРасчета";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Записывать = Истина;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Добавить(), Выборка);
	КонецЦикла;
	
	Если ЗаписыватьДвижения Тогда
		Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Записать();
		Движения.ДанныеОВремениДляРасчетаСреднегоОбщий.Записывать = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет пересчет и регистрацию данных об отработанном времени для учета общего среднего заработка
// для указанных сотрудников в указанных месяцах.
//
// Параметры:
//	- МенеджерВременныхТаблиц - менеджер таблиц, в котором есть таблица ВТСотрудникиМесяцы с полями Сотрудник и Месяц.
//
Процедура ОбновитьДанныеКорректировкиДнейДляРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц, ИсключаемыйРегистратор = Неопределено) Экспорт
	
	СоздатьВТВидыНачисленийДляРегистрацииОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц);
	
	ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СотрудникиМесяцы.Сотрудник,
		|	СотрудникиМесяцы.Месяц
		|ПОМЕСТИТЬ ВТСотрудникиМесяцыБезНачислений
		|ИЗ
		|	ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.Начисления КАК Начисления
		|		ПО (Начисления.Сотрудник = СотрудникиМесяцы.Сотрудник)
		|			И (Начисления.ПериодДействия = СотрудникиМесяцы.Месяц)
		|			И (Начисления.ВидРасчета.ЗачетНормыВремени = ИСТИНА)
		|ГДЕ
		|	Начисления.ПериодДействия ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ФПД.Сотрудник,
		|	ФПД.Сторно,
		|	ФПД.ГрафикРаботы,
		|	ФПД.ПериодДействия КАК Месяц,
		|	ФПД.ПериодРегистрацииВремени КАК ПериодРегистрацииВремени,
		|	ФПД.ПериодДействияНачало КАК НачалоПериода,
		|	ФПД.ПериодДействияКонец КАК ОкончаниеПериода,
		|	НастройкиНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|	ВидыНачислений.Прогул,
		|	ВидыНачислений.ЗачетНормыВремени,
		|	ВидыНачислений.ЗачетОтработанногоВремени,
		|	ВидыНачислений.ЕжегодныйОтпуск
		|ПОМЕСТИТЬ ВТФПДСотрудников
		|ИЗ
		|	РегистрРасчета.Начисления.ФактическийПериодДействия(
		|			Регистратор <> &ИсключаемыйРегистратор
		|				И (Сотрудник, ПериодДействия) В
		|					(ВЫБРАТЬ
		|						СотрудникиМесяцы.Сотрудник,
		|						СотрудникиМесяцы.Месяц
		|					ИЗ
		|						ВТСотрудникиМесяцы КАК СотрудникиМесяцы)
		|				И ВидРасчета В
		|					(ВЫБРАТЬ
		|						ВТВидыНачислений.Ссылка
		|					ИЗ
		|						ВТВидыНачислений)) КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО (ВидыНачислений.Ссылка = ФПД.ВидРасчета)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
		|		ПО (НастройкиНачислений.Ссылка = ФПД.ВидРасчета)
		|			И (НастройкиНачислений.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПериодыГрафиков.ГрафикРаботы,
		|	ПериодыГрафиков.НачалоПериода,
		|	ПериодыГрафиков.ОкончаниеПериода
		|ПОМЕСТИТЬ ВТПериодыГрафиков
		|ИЗ
		|	ВТФПДСотрудников КАК ПериодыГрафиков
		|ГДЕ
		|	ПериодыГрафиков.ЗачетНормыВремени";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ИсключаемыйРегистратор", ИсключаемыйРегистратор);
	Запрос.Выполнить();
	
	УчетРабочегоВремени.СоздатьВТДатыПроизводственногоКалендаряПоГрафикам(МенеджерВременныхТаблиц);
	
	// Данные по календарю (сколько всего должно быть зарегистрировано дней в учете среднего заработка) соотносим с тем, 
	// что уже накоплено в ДанныеОВремениДляРасчетаСреднегоОбщий.
	// Разницу записываем с минусом в ДанныеОВремениДляРасчетаСреднегоОбщийКорректировка.
	// Корректировку отработанных дней получаем "как есть" из учета начисленной зарплаты.
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФПД.Сотрудник,
		|	ФПД.Месяц,
		|	ФПД.Сторно,
		|	ФПД.ПорядокРасчета,
		|	ДатыКалендаряПоФПД.Дата,
		|	ДатыКалендаряПоФПД.ДеньПоПятидневке КАК ДеньПоПятидневке,
		|	ДатыКалендаряПоФПД.ДеньПоШестидневке,
		|	ИСТИНА КАК ДеньКалендарный,
		|	ДатыКалендаряПоФПД.ЧасовПоПятидневке КАК ЧасовПоПятидневке
		|ПОМЕСТИТЬ ВТДатыПроизводственногоКалендаряПоСотрудникам
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|			И (ФПД.ЗачетНормыВремени)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФПД.Сотрудник,
		|	ФПД.Месяц,
		|	ФПД.Сторно,
		|	ФПД.ПорядокРасчета,
		|	ДатыКалендаряПоФПД.Дата,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	0
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|			И (ФПД.Прогул)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ФПД.Сотрудник,
		|	ФПД.Месяц,
		|	ФПД.Сторно,
		|	ФПД.ПорядокРасчета,
		|	ДатыКалендаряПоФПД.Дата,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	0
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|			И (ФПД.ЕжегодныйОтпуск)
		|			И (ДатыКалендаряПоФПД.ДеньПраздничный)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтработанноеВремяКорректировка.Сотрудник,
		|	ОтработанноеВремяКорректировка.ПериодДействия КАК Месяц,
		|	НастройкиНачислений.ПорядокРасчета КАК ПорядокРасчета,
		|	СУММА(ОтработанноеВремяКорректировка.ОтработаноДней) КАК ОтработаноДнейКорректировка
		|ПОМЕСТИТЬ ВТОтработанноеВремяКорректировка
		|ИЗ
		|	РегистрНакопления.ОтработанноеВремяПоСотрудникамКорректировка КАК ОтработанноеВремяКорректировка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = ОтработанноеВремяКорректировка.Сотрудник)
		|			И ОтработанноеВремяКорректировка.ПериодДействия = СотрудникиМесяцы.Месяц
		|			И (ОтработанноеВремяКорректировка.Регистратор <> &ИсключаемыйРегистратор)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТВидыНачислений КАК ВидыНачислений
		|		ПО (ВидыНачислений.Ссылка = ОтработанноеВремяКорректировка.Начисление)
		|			И (ВидыНачислений.ЗачетОтработанногоВремени)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
		|		ПО (НастройкиНачислений.Ссылка = ОтработанноеВремяКорректировка.Начисление)
		|			И (НастройкиНачислений.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|
		|СГРУППИРОВАТЬ ПО
		|	ОтработанноеВремяКорректировка.ПериодДействия,
		|	ОтработанноеВремяКорректировка.Сотрудник,
		|	НастройкиНачислений.ПорядокРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НакопленныеДанныеВремени.Сотрудник КАК Сотрудник,
		|	НАЧАЛОПЕРИОДА(НакопленныеДанныеВремени.Период, МЕСЯЦ) КАК Месяц,
		|	НакопленныеДанныеВремени.ПорядокРасчета КАК ПорядокРасчета,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(НакопленныеДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТНакопленныеДанные
		|ИЗ
		|	РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК НакопленныеДанныеВремени
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ПО (СотрудникиМесяцы.Сотрудник = НакопленныеДанныеВремени.Сотрудник)
		|			И (СотрудникиМесяцы.Месяц = НАЧАЛОПЕРИОДА(НакопленныеДанныеВремени.Период, МЕСЯЦ))
		|			И (НакопленныеДанныеВремени.Регистратор <> &ИсключаемыйРегистратор)
		|
		|СГРУППИРОВАТЬ ПО
		|	НакопленныеДанныеВремени.Сотрудник,
		|	НАЧАЛОПЕРИОДА(НакопленныеДанныеВремени.Период, МЕСЯЦ),
		|	НакопленныеДанныеВремени.ПорядокРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПД.Сотрудник,
		|	ФПД.Месяц,
		|	ФПД.ПорядокРасчета,
		|	ДатыКалендаряПоФПД.Дата,
		|	ДатыКалендаряПоФПД.ЧасовПоПятидневке
		|ПОМЕСТИТЬ ВТЧасовПоДатам
		|ИЗ
		|	ВТФПДСотрудников КАК ФПД
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПроизводственногоКалендаряПоГрафикам КАК ДатыКалендаряПоФПД
		|		ПО (ДатыКалендаряПоФПД.НачалоПериода = ФПД.НачалоПериода)
		|			И (ДатыКалендаряПоФПД.ОкончаниеПериода = ФПД.ОкончаниеПериода)
		|			И (ДатыКалендаряПоФПД.ГрафикРаботы = ФПД.ГрафикРаботы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазличныеДатыПоСотрудникам.Сотрудник КАК Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц КАК Месяц,
		|	РазличныеДатыПоСотрудникам.ПорядокРасчета КАК ПорядокРасчета,
		|	РазличныеДатыПоСотрудникам.Дата КАК Дата,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ДеньПоПятидневке
		|				ТОГДА ВЫБОР
		|						КОГДА РазличныеДатыПоСотрудникам.Сторно
		|							ТОГДА -1
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноНаДатуДнейПятидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ДеньПоШестидневке
		|				ТОГДА ВЫБОР
		|						КОГДА РазличныеДатыПоСотрудникам.Сторно
		|							ТОГДА -1
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноНаДатуДнейШестидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ДеньКалендарный
		|				ТОГДА ВЫБОР
		|						КОГДА РазличныеДатыПоСотрудникам.Сторно
		|							ТОГДА -1
		|						ИНАЧЕ 1
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноНаДатуДнейКалендарных
		|ПОМЕСТИТЬ ВТОтработаноДнейПоДатам
		|ИЗ
		|	ВТДатыПроизводственногоКалендаряПоСотрудникам КАК РазличныеДатыПоСотрудникам
		|
		|СГРУППИРОВАТЬ ПО
		|	РазличныеДатыПоСотрудникам.Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц,
		|	РазличныеДатыПоСотрудникам.ПорядокРасчета,
		|	РазличныеДатыПоСотрудникам.Дата
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РазличныеДатыПоСотрудникам.Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц,
		|	РазличныеДатыПоСотрудникам.ПорядокРасчета,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДнейПятидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейПятидневка < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) * ЧасовПоДатам.ЧасовПоПятидневке КАК ОтработаноЧасовПятидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейШестидневка > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейШестидневка < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДнейШестидневка,
		|	СУММА(ВЫБОР
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейКалендарных > 0
		|				ТОГДА 1
		|			КОГДА РазличныеДатыПоСотрудникам.ОтработаноНаДатуДнейКалендарных < 0
		|				ТОГДА -1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТДанныеПоФПД
		|ИЗ
		|	ВТОтработаноДнейПоДатам КАК РазличныеДатыПоСотрудникам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЧасовПоДатам КАК ЧасовПоДатам
		|		ПО (ЧасовПоДатам.Сотрудник = РазличныеДатыПоСотрудникам.Сотрудник)
		|			И (ЧасовПоДатам.Месяц = РазличныеДатыПоСотрудникам.Месяц)
		|			И (ЧасовПоДатам.Дата = РазличныеДатыПоСотрудникам.Дата)
		|			И (ЧасовПоДатам.ПорядокРасчета = РазличныеДатыПоСотрудникам.ПорядокРасчета)
		|
		|СГРУППИРОВАТЬ ПО
		|	РазличныеДатыПоСотрудникам.Сотрудник,
		|	РазличныеДатыПоСотрудникам.Месяц,
		|	РазличныеДатыПоСотрудникам.ПорядокРасчета,
		|	ЧасовПоДатам.ЧасовПоПятидневке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.ПорядокРасчета,
		|	СУММА(ДанныеВремени.ОтработаноДней) КАК ОтработаноДней,
		|	СУММА(ДанныеВремени.ОтработаноЧасов) КАК ОтработаноЧасов,
		|	СУММА(ДанныеВремени.ОтработаноДнейПятидневка) КАК ОтработаноДнейПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноЧасовПятидневка) КАК ОтработаноЧасовПятидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейШестидневка) КАК ОтработаноДнейШестидневка,
		|	СУММА(ДанныеВремени.ОтработаноДнейКалендарных) КАК ОтработаноДнейКалендарных
		|ПОМЕСТИТЬ ВТДанныеВремени
		|ИЗ
		|	(ВЫБРАТЬ
		|		ОтработанноеВремяКорректировка.Сотрудник КАК Сотрудник,
		|		ОтработанноеВремяКорректировка.Месяц КАК Месяц,
		|		ОтработанноеВремяКорректировка.Месяц КАК ПорядокРасчета,
		|		ОтработанноеВремяКорректировка.ОтработаноДнейКорректировка КАК ОтработаноДней,
		|		0 КАК ОтработаноЧасов,
		|		0 КАК ОтработаноДнейПятидневка,
		|		0 КАК ОтработаноЧасовПятидневка,
		|		0 КАК ОтработаноДнейШестидневка,
		|		0 КАК ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТОтработанноеВремяКорректировка КАК ОтработанноеВремяКорректировка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ДанныеПоФПД.Сотрудник,
		|		ДанныеПоФПД.Месяц,
		|		ДанныеПоФПД.ПорядокРасчета,
		|		0,
		|		0,
		|		ДанныеПоФПД.ОтработаноДнейПятидневка,
		|		ДанныеПоФПД.ОтработаноЧасовПятидневка,
		|		ДанныеПоФПД.ОтработаноДнейШестидневка,
		|		ДанныеПоФПД.ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТДанныеПоФПД КАК ДанныеПоФПД
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НакопленныеДанные.Сотрудник,
		|		НакопленныеДанные.Месяц,
		|		НакопленныеДанные.ПорядокРасчета,
		|		0,
		|		0,
		|		-НакопленныеДанные.ОтработаноДнейПятидневка,
		|		-НакопленныеДанные.ОтработаноЧасовПятидневка,
		|		-НакопленныеДанные.ОтработаноДнейШестидневка,
		|		-НакопленныеДанные.ОтработаноДнейКалендарных
		|	ИЗ
		|		ВТНакопленныеДанные КАК НакопленныеДанные) КАК ДанныеВремени
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеВремени.Сотрудник,
		|	ДанныеВремени.Месяц,
		|	ДанныеВремени.ПорядокРасчета
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СотрудникиМесяцы.Сотрудник КАК Сотрудник,
		|	СотрудникиМесяцы.Месяц КАК Месяц,
		|	ДанныеВремени.ПорядокРасчета,
		|	ДанныеВремени.ОтработаноДней,
		|	ДанныеВремени.ОтработаноЧасов,
		|	ДанныеВремени.ОтработаноДнейКалендарных
		|ИЗ
		|	ВТСотрудникиМесяцы КАК СотрудникиМесяцы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеВремени КАК ДанныеВремени
		|		ПО СотрудникиМесяцы.Сотрудник = ДанныеВремени.Сотрудник
		|			И СотрудникиМесяцы.Месяц = ДанныеВремени.Месяц
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудникиМесяцыБезНачислений КАК СотрудникиМесяцыБезНачислений
		|		ПО (СотрудникиМесяцыБезНачислений.Сотрудник = СотрудникиМесяцы.Сотрудник)
		|			И (СотрудникиМесяцыБезНачислений.Месяц = СотрудникиМесяцы.Месяц)
		|ГДЕ
		|	СотрудникиМесяцыБезНачислений.Месяц ЕСТЬ NULL 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиМесяцыБезНачислений.Сотрудник,
		|	СотрудникиМесяцыБезНачислений.Месяц,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление2010),
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВТСотрудникиМесяцыБезНачислений КАК СотрудникиМесяцыБезНачислений
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиМесяцыБезНачислений.Сотрудник,
		|	СотрудникиМесяцыБезНачислений.Месяц,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий.Постановление100Отпускные),
		|	0,
		|	0,
		|	0
		|ИЗ
		|	ВТСотрудникиМесяцыБезНачислений КАК СотрудникиМесяцыБезНачислений
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	Месяц";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	НаборЗаписей = РегистрыСведений.ДанныеОВремениДляРасчетаСреднегоОбщийКорректировка.СоздатьНаборЗаписей();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
		Пока Выборка.СледующийПоЗначениюПоля("Месяц") Цикл
			НаборЗаписей.Очистить();
			Пока Выборка.Следующий() Цикл
				Если (Выборка.ОтработаноДней = 0 
					И Выборка.ОтработаноЧасов = 0 
					И Выборка.ОтработаноДнейКалендарных = 0)
					ИЛИ НЕ ЗначениеЗаполнено(Выборка.ПорядокРасчета)
					Тогда
					// Не записываем пустые строки.
					Продолжить;
				КонецЕсли;
				НоваяСтрока = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			КонецЦикла;
			НаборЗаписей.Отбор.Сотрудник.Установить(Выборка.Сотрудник);
			НаборЗаписей.Отбор.Месяц.Установить(Выборка.Месяц);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЦикла;

	
КонецПроцедуры

// Процедура добавляет в менеджер таблиц временную таблицу 
//  с порядком расчета общего среднего заработка, 
//  исходя из периода и других данных начисления.
//
Процедура СоздатьВТПорядокРасчетаОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц) 
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ДатаДействия,
		|	Начисления.Начисление,
		|	НастройкиНачислений.ПорядокРасчета
		|ПОМЕСТИТЬ ВТПорядокРасчетаОбщегоСреднегоЗаработка
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиНачислений
		|		ПО (НастройкиНачислений.Ссылка = Начисления.Начисление)
		|			И (НастройкиНачислений.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
        |;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Начисление,
		|	Начисления.ПорядокРасчета
		|ПОМЕСТИТЬ ВТПорядокРасчетаОбщегоСреднегоЗаработкаСводно
		|ИЗ
		|	ВТПорядокРасчетаОбщегоСреднегоЗаработка КАК Начисления
		|
		|";
	
	Запрос.Выполнить();
	
	
	
КонецПроцедуры

Процедура СоздатьВТВидыНачисленийДляРегистрацииОбщегоСреднегоЗаработка(МенеджерВременныхТаблиц)
	
	ТекстЗапроса =
		"ВЫБРАТЬ 
		|	Начисления.Ссылка,
		|	Начисления.ЗачетНормыВремени,
		|	Начисления.ЗачетОтработанногоВремени,
		|	ЛОЖЬ КАК Прогул,
		|	ЛОЖЬ КАК ЕжегодныйОтпуск
		|ПОМЕСТИТЬ ВТВидыНачислений
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	(Начисления.ЗачетОтработанногоВремени ИЛИ Начисления.ЗачетНормыВремени)
		|	И ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкаСреднегоЗаработка
		|			ГДЕ
		|				НастройкаСреднегоЗаработка.Ссылка = Начисления.Ссылка
		|				И НастройкаСреднегоЗаработка.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|			И (Начисления.КатегорияНачисленияИлиНеоплаченногоВремени <> ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ИСТИНА,
		|	ЛОЖЬ
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени В(&КатегорииПрогула)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Ссылка,
		|	ИСТИНА,
		|	ЛОЖЬ,
		|	ЛОЖЬ,
		|	ИСТИНА
		|ИЗ
		|	ПланВидовРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаОтпуска)
		|	И Начисления.ВидОтпуска.ОтпускЯвляетсяЕжегодным
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Начисления.Ссылка";
		
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КатегорииПрогула", ПланыВидовРасчета.Начисления.КатегорииПрогула());
	Запрос.УстановитьПараметр("ВидыВремениРаботаВыходныеПраздничные", Справочники.ВидыИспользованияРабочегоВремени.ВидыВремениРаботаВыходныеПраздничные());
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТНачисленияПоТаблицеНачисления(МенеджерВременныхТаблиц, Начисления)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Начисления", Начисления);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.ДокументСсылка,
		|	Начисления.Сотрудник,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Организация,
		|	Начисления.Начисление,
		|	Начисления.ДатаДействия,
		|	Начисления.МесяцНачисления,
		|	Начисления.ПериодДействия,
		|	Начисления.ДатаНачала,
		|	Начисления.ДатаОкончания,
		|	Начисления.НачалоБазовогоПериода,
		|	Начисления.ОкончаниеБазовогоПериода,
		|	Начисления.Сумма,
		|	Начисления.Сторно,
		|	Начисления.НомерСтроки,
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.СтатьяФинансирования
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	&Начисления КАК Начисления";
		
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура СоздатьВТДляИсключенияКомандировок(МенеджерВременныхТаблиц, ИсключатьНачисленияВПериодКомандировок)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВидыКомандировок.Ссылка,
		|	Начисления.Начисление КАК ВидРасчета
		|ПОМЕСТИТЬ ВТИсключаемыеВПериодКомандировки
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыКомандировок
		|		ПО (ВидыКомандировок.КатегорияНачисленияИлиНеоплаченногоВремени = ЗНАЧЕНИЕ(Перечисление.КатегорииНачисленийИНеоплаченногоВремени.ОплатаКомандировки))
		|			И (ВидыКомандировок.ЗачетНормыВремени)
		|			И (ВидыКомандировок.Ссылка <> Начисления.Начисление)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ИсключаемыеВПериодКомандировки
		|		ПО (ИсключаемыеВПериодКомандировки.Ссылка = Начисления.Начисление)
		|			И (ИсключаемыеВПериодКомандировки.ТребуетсяРасчетВремени)
		|			И (НЕ ИСТИНА В
		|					(ВЫБРАТЬ ПЕРВЫЕ 1
		|						ИСТИНА
		|					ИЗ
		|						ПланВидовРасчета.Начисления.ВытесняющиеВидыРасчета КАК ВытесняющиеВидыРасчета
		|					ГДЕ
		|						ВытесняющиеВидыРасчета.Ссылка = Начисления.Начисление
		|						И ВытесняющиеВидыРасчета.ВидРасчета = ВидыКомандировок.Ссылка))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТИсключаемыеВПериодКомандировки КАК ИсключаемыеВПериодКомандировки";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ИсключатьНачисленияВПериодКомандировок = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ФПДКомандировки.Регистратор,
		|	ФПДКомандировки.НомерСтроки,
		|	ФПДКомандировки.Сотрудник,
		|	ФПДКомандировки.ВидРасчета,
		|	ФПДКомандировки.ПериодДействияНачало КАК Начало,
		|	ФПДКомандировки.ПериодДействияКонец КАК Окончание
		|ПОМЕСТИТЬ ВТПериодыКомандировок
		|ИЗ
		|	РегистрРасчета.Начисления.ФактическийПериодДействия(
		|			(Сотрудник, ПериодДействия) В
		|					(ВЫБРАТЬ
		|						Начисления.Сотрудник,
		|						Начисления.ПериодДействия
		|					ИЗ
		|						ВТНачисления КАК Начисления)
		|				И ВидРасчета В
		|					(ВЫБРАТЬ
		|						ВидыКомандировок.Ссылка
		|					ИЗ
		|						ВТИсключаемыеВПериодКомандировки КАК ВидыКомандировок)) КАК ФПДКомандировки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТПериодыКомандировок КАК ФПДКомандировки";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ИсключатьНачисленияВПериодКомандировок = Ложь;
		Возврат;
	КонецЕсли;
	
	ИсключатьНачисленияВПериодКомандировок = Истина;
	
КонецПроцедуры

Процедура РаспределитьВТНачисленияПоБазе(МенеджерВременныхТаблиц, Регистратор)
	
	// Если начисление отражается в среднем заработке пропорционально базовым начислениям, получаем расчетную базу и выполняем распределение.
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Определяем, вообще есть ли в регистрируемом наборе подобные начисления.
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Начисления.ДокументСсылка,
		|	Начисления.Сумма
		|ПОМЕСТИТЬ ВТНачисленияОтражаемыеПоБазе
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ВидыРасчета
		|		ПО (ВидыРасчета.Ссылка = Начисления.Начисление)
		|			И (ВидыРасчета.СтратегияОтраженияВСреднемЗаработке = ЗНАЧЕНИЕ(Перечисление.СтратегииОтраженияВУчетеНачисленийУдержаний.ПоБазовымРасчетам))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазе КАК НачисленияОтражаемыеПоБазе";
	РезультатЗапроса = Запрос.Выполнить();
	
	// Если нет, то создаем пустышку и ничего далее не делаем.
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	
	// Выполняем расчет базы для получения расшифровки.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.Регистратор,
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.НомерСтроки
		|ПОМЕСТИТЬ ВТРегистрНачисления
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Регистратор,
		|	Начисления.НомерСтроки,
		|	НачисленияОтражаемыеПоБазе.ИдентификаторСтроки,
		|	НачисленияОтражаемыеПоБазе.СтатьяФинансирования,
		|	НачисленияОтражаемыеПоБазе.Сотрудник,
		|	НачисленияОтражаемыеПоБазе.Период,
		|	НачисленияОтражаемыеПоБазе.Сумма
		|ПОМЕСТИТЬ ВТРаспределяемыеСуммы
		|ИЗ
		|	ВТНачисленияОтражаемыеПоБазе КАК НачисленияОтражаемыеПоБазе
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРегистрНачисления КАК Начисления
		|		ПО (Начисления.Регистратор = НачисленияОтражаемыеПоБазе.ДокументСсылка)
		|			И (Начисления.ИдентификаторСтроки = НачисленияОтражаемыеПоБазе.ИдентификаторСтроки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспределяемыеСуммы.Регистратор,
		|	РаспределяемыеСуммы.НомерСтроки,
		|	РаспределяемыеСуммы.ИдентификаторСтроки,
		|	РаспределяемыеСуммы.Сотрудник,
		|	РаспределяемыеСуммы.Период
		|ПОМЕСТИТЬ ВТОтборНачислений
		|ИЗ
		|	ВТРаспределяемыеСуммы КАК РаспределяемыеСуммы";
	Запрос.Выполнить();
	
	РасчетЗарплатыРасширенный.СоздатьВТРасчетнаяБазаНачисленийПоВременнойТаблице(МенеджерВременныхТаблиц);
	
	// Получив расчетную базу, мы можем понять, из каких начислений состоит результат распределяемых сумм,
	// но для аналогичного отражения его в среднем заработке нужно понять, как именно эти базовые начисления были отражены в среднем заработке.
	
	// Выполняем поиск по ключу Регистратор + Сотрудник + СоставнаяЧасть + ПорядокРасчета + Индексируется и распределяем суммы, 
	// входящие в базу по тому, что удалось получить (т.к. могут быть несколько записей, по статьям финансирования и др.)
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеЗаписи.ИдентификаторСтроки,
		|	ОсновныеЗаписи.Сотрудник,
		|	ОсновныеЗаписи.Период,
		|	РасчетнаяБаза.РегистраторРазрез КАК Регистратор,
		|	НастройкиВидаРасчета.ПорядокРасчета,
		|	НастройкиВидаРасчета.Значение КАК СоставнаяЧасть,
		|	НастройкиВидаРасчета.Индексируется,
		|	СУММА(РасчетнаяБаза.РезультатБаза) КАК Сумма
		|ПОМЕСТИТЬ ВТРасшифровкаБазы
		|ИЗ
		|	ВТРасчетнаяБаза КАК РасчетнаяБаза
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборНачислений КАК ОсновныеЗаписи
		|		ПО (ОсновныеЗаписи.Регистратор = РасчетнаяБаза.Регистратор)
		|			И (ОсновныеЗаписи.НомерСтроки = РасчетнаяБаза.НомерСтроки)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.СреднийЗаработокОбщий КАК НастройкиВидаРасчета
		|		ПО (НастройкиВидаРасчета.Ссылка = РасчетнаяБаза.ВидРасчетаРазрез)
		|			И (НастройкиВидаРасчета.Значение <> ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.НеВключать))
		|
		|СГРУППИРОВАТЬ ПО
		|	ОсновныеЗаписи.ИдентификаторСтроки,
		|	ОсновныеЗаписи.Сотрудник,
		|	ОсновныеЗаписи.Период,
		|	РасчетнаяБаза.РегистраторРазрез,
		|	НастройкиВидаРасчета.ПорядокРасчета,
		|	НастройкиВидаРасчета.Значение,
		|	НастройкиВидаРасчета.Индексируется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется
		|ПОМЕСТИТЬ ВТРаспределяемыеСочетания
		|ИЗ
		|	ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО (Начисления.ИдентификаторСтроки = РасшифровкаБазы.ИдентификаторСтроки)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Начисления.ДокументСсылка КАК Регистратор,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.Индексируется,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Организация,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Сумма
		|ПОМЕСТИТЬ ВТОснованияРаспределения
		|ИЗ
		|	ВТРаспределяемыеСочетания КАК Сочетания
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНачисления КАК Начисления
		|		ПО (Начисления.ДокументСсылка = Сочетания.Регистратор)
		|			И (Начисления.Сотрудник = Сочетания.Сотрудник)
		|			И (Начисления.Период = Сочетания.Период)
		|			И (Начисления.ПорядокРасчета = Сочетания.ПорядокРасчета)
		|			И (Начисления.СоставнаяЧасть = Сочетания.СоставнаяЧасть)
		|			И (Начисления.Индексируется = Сочетания.Индексируется)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОтборНачислений КАК ОтборНачислений
		|		ПО (Начисления.ИдентификаторСтроки = ОтборНачислений.ИдентификаторСтроки)
		|ГДЕ
		|	ОтборНачислений.ИдентификаторСтроки ЕСТЬ NULL 
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДанныеНачислений.Сотрудник,
		|	ДанныеНачислений.Период,
		|	ДанныеНачислений.Регистратор,
		|	ДанныеНачислений.ПорядокРасчета,
		|	ДанныеНачислений.СоставнаяЧасть,
		|	ДанныеНачислений.Индексируется,
		|	ДанныеНачислений.СтатьяФинансирования,
		|	ДанныеНачислений.Год,
		|	ДанныеНачислений.ДатаНачалаБазовогоПериода,
		|	ДанныеНачислений.КоличествоМесяцев,
		|	ДанныеНачислений.Организация,
		|	ДанныеНачислений.ФизическоеЛицо,
		|	ДанныеНачислений.Сумма
		|ИЗ
		|	РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеНачислений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределяемыеСочетания КАК Сочетания
		|		ПО ДанныеНачислений.Регистратор = Сочетания.Регистратор
		|			И ДанныеНачислений.Сотрудник = Сочетания.Сотрудник
		|			И ДанныеНачислений.Период = Сочетания.Период
		|			И ДанныеНачислений.ПорядокРасчета = Сочетания.ПорядокРасчета
		|			И ДанныеНачислений.СоставнаяЧасть = Сочетания.СоставнаяЧасть
		|			И ДанныеНачислений.Индексируется = Сочетания.Индексируется
		|			И (ДанныеНачислений.Регистратор <> &Регистратор)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаБазы.ИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник,
		|	РасшифровкаБазы.Период,
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется,
		|	СУММА(1) КАК Количество
		|ПОМЕСТИТЬ ВТКоличествоСтрок
		|ИЗ
		|	ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОснованияРаспределения КАК ОснованияРаспределения
		|		ПО (ОснованияРаспределения.ПорядокРасчета = РасшифровкаБазы.ПорядокРасчета)
		|			И (ОснованияРаспределения.СоставнаяЧасть = РасшифровкаБазы.СоставнаяЧасть)
		|			И (ОснованияРаспределения.Индексируется = РасшифровкаБазы.Индексируется)
		|			И (ОснованияРаспределения.Регистратор = РасшифровкаБазы.Регистратор)
		|			И (ОснованияРаспределения.Сотрудник = РасшифровкаБазы.Сотрудник)
		|			И (ОснованияРаспределения.Период = РасшифровкаБазы.Период)
		|
		|СГРУППИРОВАТЬ ПО
		|	РасшифровкаБазы.ИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник,
		|	РасшифровкаБазы.Период,
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаБазы.ИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник,
		|	РасшифровкаБазы.Период,
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется,
		|	РасшифровкаБазы.Сумма,
		|	ЕСТЬNULL(КоличествоСтрок.Количество, 0) КАК КоличествоСтрок
		|ИЗ
		|	ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоСтрок КАК КоличествоСтрок
		|		ПО (КоличествоСтрок.ИдентификаторСтроки = РасшифровкаБазы.ИдентификаторСтроки)
		|			И (КоличествоСтрок.Регистратор = РасшифровкаБазы.Регистратор)
		|			И (КоличествоСтрок.ПорядокРасчета = РасшифровкаБазы.ПорядокРасчета)
		|			И (КоличествоСтрок.СоставнаяЧасть = РасшифровкаБазы.СоставнаяЧасть)
		|			И (КоличествоСтрок.Индексируется = РасшифровкаБазы.Индексируется)
		|			И (КоличествоСтрок.Сотрудник = РасшифровкаБазы.Сотрудник)
		|			И (КоличествоСтрок.Период = РасшифровкаБазы.Период)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РасшифровкаБазы.Регистратор,
		|	РасшифровкаБазы.ИдентификаторСтроки,
		|	РасшифровкаБазы.Сотрудник,
		|	РасшифровкаБазы.Период,
		|	РасшифровкаБазы.ПорядокРасчета,
		|	РасшифровкаБазы.СоставнаяЧасть,
		|	РасшифровкаБазы.Индексируется
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасшифровкаБазы.ИдентификаторСтроки,
		|	ОснованияРаспределения.Сотрудник,
		|	ОснованияРаспределения.Период,
		|	ОснованияРаспределения.Регистратор,
		|	ОснованияРаспределения.ПорядокРасчета,
		|	ОснованияРаспределения.СоставнаяЧасть,
		|	ОснованияРаспределения.Индексируется,
		|	ОснованияРаспределения.СтатьяФинансирования,
		|	ОснованияРаспределения.Год,
		|	ОснованияРаспределения.ДатаНачалаБазовогоПериода,
		|	ОснованияРаспределения.КоличествоМесяцев,
		|	ОснованияРаспределения.Организация,
		|	ОснованияРаспределения.ФизическоеЛицо,
		|	ОснованияРаспределения.Сумма
		|ИЗ
		|	ВТОснованияРаспределения КАК ОснованияРаспределения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРасшифровкаБазы КАК РасшифровкаБазы
		|		ПО ОснованияРаспределения.ПорядокРасчета = РасшифровкаБазы.ПорядокРасчета
		|			И ОснованияРаспределения.СоставнаяЧасть = РасшифровкаБазы.СоставнаяЧасть
		|			И ОснованияРаспределения.Индексируется = РасшифровкаБазы.Индексируется
		|			И ОснованияРаспределения.Регистратор = РасшифровкаБазы.Регистратор
		|			И ОснованияРаспределения.Сотрудник = РасшифровкаБазы.Сотрудник
		|			И ОснованияРаспределения.Период = РасшифровкаБазы.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОснованияРаспределения.Регистратор,
		|	РасшифровкаБазы.ИдентификаторСтроки,
		|	ОснованияРаспределения.Сотрудник,
		|	ОснованияРаспределения.Период,
		|	ОснованияРаспределения.ПорядокРасчета,
		|	ОснованияРаспределения.СоставнаяЧасть,
		|	ОснованияРаспределения.Индексируется";

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаСумм = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выбрать();
	ВыборкаРасшифровки = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выбрать();
	
	// Выполняем распределение сумм пропорционально базе.
	БазаРаспределения = Новый ТаблицаЗначений;
	БазаРаспределения.Колонки.Добавить("ИдентификаторСтроки", Новый ОписаниеТипов("Число"));
	БазаРаспределения.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	БазаРаспределения.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	БазаРаспределения.Колонки.Добавить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаОбщий"));
	БазаРаспределения.Колонки.Добавить("СоставнаяЧасть", Новый ОписаниеТипов("ПеречислениеСсылка.УчетНачисленийВСреднемЗаработкеОбщий"));
	БазаРаспределения.Колонки.Добавить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	БазаРаспределения.Колонки.Добавить("Индексируется", Новый ОписаниеТипов("Булево"));
	БазаРаспределения.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(15, 2));
	БазаРаспределения.Колонки.Добавить("Год", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(4, 0));
	БазаРаспределения.Колонки.Добавить("ДатаНачалаБазовогоПериода", Новый ОписаниеТипов("Дата"));
	БазаРаспределения.Колонки.Добавить("КоличествоМесяцев", Новый ОписаниеТипов("Число"), Новый КвалификаторыЧисла(3, 0));
	БазаРаспределения.Колонки.Добавить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	БазаРаспределения.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	Пока ВыборкаСумм.Следующий() Цикл
		Если ВыборкаСумм.КоличествоСтрок = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокиРаспределения = Новый Массив;
		КоэффициентыРаспределения = Новый Массив;
		Для Индекс = 1 По ВыборкаСумм.КоличествоСтрок Цикл
			ВыборкаРасшифровки.Следующий();
			НоваяСтрока = БазаРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасшифровки);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСумм);
			СтрокиРаспределения.Добавить(НоваяСтрока);
			КоэффициентыРаспределения.Добавить(ВыборкаРасшифровки.Сумма);
		КонецЦикла;
		Если ВыборкаСумм.КоличествоСтрок < 2 Тогда
			Продолжить;
		КонецЕсли;
		РаспределенныеСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаСумм.Сумма, КоэффициентыРаспределения);
		Если РаспределенныеСуммы <> Неопределено Тогда
			Индекс = 0;
			Пока Индекс < СтрокиРаспределения.Количество() Цикл
				СтрокиРаспределения[Индекс].Сумма = РаспределенныеСуммы[Индекс];
				Индекс = Индекс + 1;	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазаРаспределения.ИдентификаторСтроки,
		|	БазаРаспределения.Сотрудник,
		|	БазаРаспределения.Период,
		|	БазаРаспределения.ПорядокРасчета,
		|	БазаРаспределения.СоставнаяЧасть,
		|	БазаРаспределения.Индексируется,
		|	БазаРаспределения.СтатьяФинансирования,
		|	БазаРаспределения.Год,
		|	БазаРаспределения.ДатаНачалаБазовогоПериода,
		|	БазаРаспределения.КоличествоМесяцев,
		|	БазаРаспределения.Организация,
		|	БазаРаспределения.ФизическоеЛицо,
		|	БазаРаспределения.Сумма
		|ПОМЕСТИТЬ ВТБазаРаспределения
		|ИЗ
		|	&БазаРаспределения КАК БазаРаспределения";
	Запрос.УстановитьПараметр("БазаРаспределения", БазаРаспределения);
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БазаРаспределения.ИдентификаторСтроки,
		|	БазаРаспределения.СтатьяФинансирования,
		|	СУММА(1) КАК КоличествоСтрок
		|ПОМЕСТИТЬ ВТКоличествоСтрокБазыРаспределения
		|ИЗ
		|	ВТБазаРаспределения КАК БазаРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.ИдентификаторСтроки,
		|	БазаРаспределения.СтатьяФинансирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспределяемыеСуммы.ИдентификаторСтроки,
		|	РаспределяемыеСуммы.СтатьяФинансирования,
		|	РаспределяемыеСуммы.Сумма,
		|	ЕСТЬNULL(КоличествоСтрокБазыРаспределения.КоличествоСтрок, 0) КАК КоличествоСтрок
		|ИЗ
		|	ВТРаспределяемыеСуммы КАК РаспределяемыеСуммы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКоличествоСтрокБазыРаспределения КАК КоличествоСтрокБазыРаспределения
		|		ПО (КоличествоСтрокБазыРаспределения.ИдентификаторСтроки = РаспределяемыеСуммы.ИдентификаторСтроки)
		|			И (КоличествоСтрокБазыРаспределения.СтатьяФинансирования = РаспределяемыеСуммы.СтатьяФинансирования)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РаспределяемыеСуммы.ИдентификаторСтроки,
		|	РаспределяемыеСуммы.СтатьяФинансирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	БазаРаспределения.ИдентификаторСтроки,
		|	БазаРаспределения.Сотрудник,
		|	БазаРаспределения.Период,
		|	БазаРаспределения.ПорядокРасчета,
		|	БазаРаспределения.СоставнаяЧасть,
		|	БазаРаспределения.Индексируется,
		|	БазаРаспределения.СтатьяФинансирования,
		|	БазаРаспределения.Год,
		|	БазаРаспределения.ДатаНачалаБазовогоПериода,
		|	БазаРаспределения.КоличествоМесяцев,
		|	БазаРаспределения.Организация,
		|	БазаРаспределения.ФизическоеЛицо,
		|	БазаРаспределения.Сумма
		|ИЗ
		|	ВТБазаРаспределения КАК БазаРаспределения
		|
		|УПОРЯДОЧИТЬ ПО
		|	БазаРаспределения.ИдентификаторСтроки,
		|	БазаРаспределения.СтатьяФинансирования";
		
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаСумм = РезультатыЗапроса[РезультатыЗапроса.Количество() - 2].Выбрать();
	ВыборкаРасшифровки = РезультатыЗапроса[РезультатыЗапроса.Количество() - 1].Выбрать();
	
	// Выполняем распределение сумм пропорционально полученной базе распределения.
	Распределение = БазаРаспределения;
	Распределение.Очистить();
	
	Пока ВыборкаСумм.Следующий() Цикл
		Если ВыборкаСумм.КоличествоСтрок = 0 Тогда
			Продолжить;
		КонецЕсли;
		СтрокиРаспределения = Новый Массив;
		КоэффициентыРаспределения = Новый Массив;
		Для Индекс = 1 По ВыборкаСумм.КоличествоСтрок Цикл
			ВыборкаРасшифровки.НайтиСледующий(ВыборкаСумм.ИдентификаторСтроки, "ИдентификаторСтроки");
			НоваяСтрока = Распределение.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаРасшифровки);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСумм);
			СтрокиРаспределения.Добавить(НоваяСтрока);
			КоэффициентыРаспределения.Добавить(ВыборкаРасшифровки.Сумма);
		КонецЦикла;
		Если ВыборкаСумм.КоличествоСтрок < 2 Тогда
			Продолжить;
		КонецЕсли;
		РаспределенныеСуммы = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(ВыборкаСумм.Сумма, КоэффициентыРаспределения);
		Если РаспределенныеСуммы <> Неопределено Тогда
			Индекс = 0;
			Пока Индекс < СтрокиРаспределения.Количество() Цикл
				СтрокиРаспределения[Индекс].Сумма = РаспределенныеСуммы[Индекс];
				Индекс = Индекс + 1;	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Запрос.Текст = 
		"УНИЧТОЖИТЬ ВТРасшифровкаБазы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРаспределяемыеСуммы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТОтборНачислений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТРасчетнаяБаза";
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Распределение.ИдентификаторСтроки,
		|	Распределение.Сотрудник,
		|	Распределение.Период,
		|	Распределение.ПорядокРасчета,
		|	Распределение.СоставнаяЧасть,
		|	Распределение.Индексируется,
		|	Распределение.СтатьяФинансирования,
		|	Распределение.Год,
		|	Распределение.ДатаНачалаБазовогоПериода,
		|	Распределение.КоличествоМесяцев,
		|	Распределение.Организация,
		|	Распределение.ФизическоеЛицо,
		|	Распределение.Сумма
		|ПОМЕСТИТЬ ВТРаспределениеПоБазовымНачислениям
		|ИЗ
		|	&Распределение КАК Распределение";
		
	Запрос.УстановитьПараметр("Распределение", Распределение);
	Запрос.Выполнить();
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.Индексируется,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Организация,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Сумма,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисленияРаспределяемые
		|ИЗ
		|	ВТНачисления КАК Начисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТНачисления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Начисления.ПорядокРасчета,
		|	Начисления.СоставнаяЧасть,
		|	Начисления.Индексируется,
		|	Начисления.СтатьяФинансирования,
		|	Начисления.Год,
		|	Начисления.ДатаНачалаБазовогоПериода,
		|	Начисления.КоличествоМесяцев,
		|	Начисления.Организация,
		|	Начисления.ФизическоеЛицо,
		|	Начисления.Сумма,
		|	Начисления.*
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	ВТНачисленияРаспределяемые КАК Начисления
		|ГДЕ
		|	НЕ Начисления.ИдентификаторСтроки В
		|				(ВЫБРАТЬ
		|					Распределение.ИдентификаторСтроки
		|				ИЗ
		|					ВТРаспределениеПоБазовымНачислениям КАК Распределение)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.ИдентификаторСтроки,
		|	Начисления.Сотрудник,
		|	Начисления.Период,
		|	Распределение.ПорядокРасчета,
		|	Распределение.СоставнаяЧасть,
		|	Распределение.Индексируется,
		|	Распределение.СтатьяФинансирования,
		|	Распределение.Год,
		|	Распределение.ДатаНачалаБазовогоПериода,
		|	Распределение.КоличествоМесяцев,
		|	Распределение.Организация,
		|	Распределение.ФизическоеЛицо,
		|	Распределение.Сумма,
		|	Начисления.*
		|ИЗ
		|	ВТНачисленияРаспределяемые КАК Начисления
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТРаспределениеПоБазовымНачислениям КАК Распределение
		|		ПО (Распределение.ИдентификаторСтроки = Начисления.ИдентификаторСтроки)
		|			И (Распределение.СтатьяФинансирования = Начисления.СтатьяФинансирования)";
		
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункцииПолученияДанныхСреднегоЗаработка

Процедура НайтиГраницыПериодаРасчетаОбщегоСреднегоЗаработка(ТаблицаДанных, МинимальнаяДата, МаксимальнаяДата)
	
	МинимальнаяДата		= Дата(1, 1, 1);
	МаксимальнаяДата	= Дата(1, 1, 1);
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		Если Не ЗначениеЗаполнено(МинимальнаяДата) Тогда
			МинимальнаяДата = СтрокаТаблицы.НачалоПериодаРасчетаСреднего;
		КонецЕсли;
		МинимальнаяДата		= Мин(МинимальнаяДата, СтрокаТаблицы.НачалоПериодаРасчетаСреднего);
		МаксимальнаяДата	= Макс(МаксимальнаяДата, СтрокаТаблицы.ОкончаниеПериодаРасчетаСреднего);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункцииРегистрацииКорректировок

Процедура ЗаписатьТаблицуЗначенийВРегистрСведений(ИсходнаяТаблицаЗначений, НаборЗаписей, ИменаПолейОтбора, НачалоПериода, ОкончаниеПериода, Замещать = Истина, ЗначенияПоУмолчанию = Неопределено) Экспорт
	
	ПоляОтбора = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИменаПолейОтбора);
	
	// Записываем нулевые наборы записей (набор записей, в котором всего одна заполненная пустыми значениями строка) на месяцы, 
	// для которых в таблице ИсходнаяТаблицаЗначений нет строк за период с НачалоПериода по ОкончаниеПериода.
	Если Замещать Тогда
		Отбор = Новый Структура;
		СочетанияОтбора = Новый ТаблицаЗначений;
		Для Каждого ПолеОтбора Из ПоляОтбора Цикл
			Если СокрЛП(ПолеОтбора) = "Месяц" Тогда
				// Сочетания выбираем без учета месяца.
				Продолжить;
			КонецЕсли;
			Отбор.Вставить(СокрЛП(ПолеОтбора));
			СочетанияОтбора.Колонки.Добавить(СокрЛП(ПолеОтбора));
		КонецЦикла;
		// Заполняем таблицу сочетаний.
		Для Каждого СтрокаТаблицы Из ИсходнаяТаблицаЗначений Цикл
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТаблицы);
			Если СочетанияОтбора.НайтиСтроки(Отбор).Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(СочетанияОтбора.Добавить(), Отбор);
			КонецЕсли;
		КонецЦикла;
		// Записываем или пустые, или нулевые наборы на всем интервале.
		Отбор.Вставить("Месяц");
		МесяцОбхода = НачалоПериода;
		Пока МесяцОбхода < ОкончаниеПериода Цикл
			Для Каждого СтрокаСочетания Из СочетанияОтбора Цикл
				ЗаполнитьЗначенияСвойств(Отбор, СтрокаСочетания);
				Отбор.Месяц = МесяцОбхода;
				Если ИсходнаяТаблицаЗначений.НайтиСтроки(Отбор).Количество() = 0 Тогда
					// Очищаем пространство в регистре сведений.
					Для Каждого КлючИЗначение Из Отбор Цикл
						НаборЗаписей.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
					КонецЦикла;
					// Если запись выполняется в режиме замещения, то не оставляем пустых месяцев, если нет данных записываем нулевую запись.
					// Это позволит в дальнейшем отличать пустые места от отсутствующих данных.
					ПустаяСтрока = НаборЗаписей.Добавить();
					Если ЗначенияПоУмолчанию <> Неопределено Тогда
						ЗаполнитьЗначенияСвойств(ПустаяСтрока, ЗначенияПоУмолчанию);
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(ПустаяСтрока, Отбор);
					НаборЗаписей.Записать();
					НаборЗаписей.Очистить();
				КонецЕсли;
			КонецЦикла;
			МесяцОбхода = ДобавитьМесяц(МесяцОбхода, 1);
		КонецЦикла;
	КонецЕсли;
	
	// Записываем таблицу значений.
	ТаблицаЗначений = ИсходнаяТаблицаЗначений.Скопировать();
	
	СтруктураОтбора = Новый Структура(ИменаПолейОтбора);
	Пока ТаблицаЗначений.Количество() > 0 Цикл
		ЗаполнитьЗначенияСвойств(СтруктураОтбора, ТаблицаЗначений[0]);
		СтрокиПоОтбору = ТаблицаЗначений.НайтиСтроки(СтруктураОтбора);
		Для Каждого СтрокаТаблицы Из СтрокиПоОтбору Цикл
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СтрокаТаблицы);
			ТаблицаЗначений.Удалить(СтрокаТаблицы);
		КонецЦикла;
		Для Каждого КлючИЗначение Из СтруктураОтбора Цикл
			НаборЗаписей.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
		КонецЦикла;
		НаборЗаписей.Записать();
		НаборЗаписей.Очистить();
	КонецЦикла;
	
КонецПроцедуры

Процедура ПереименоватьКолонкуПериодВМесяц(ТаблицаЗначений)
	
	КолонкаПериод = ТаблицаЗначений.Колонки.Найти("Период");
	
	Если КолонкаПериод <> Неопределено Тогда
		КолонкаПериод.Имя = "Месяц";
	КонецЕсли;
	
КонецПроцедуры

// Функция выполняет преобразование сведений о среднем заработке, 
//  введенных по правилам ФСС в сведения по правилам общего среднего заработка.
//
Функция СведенияОбщегоСреднегоЗаработкаПоКорректировкамФСС(Организация, КорректировкиНачисленийФСС, КорректировкиВремениФСС = Неопределено)
	
	КорректировкиОбщегоЗаработка = Новый Структура;
	
	Если КорректировкиНачисленийФСС.Количество() > 0 
		Или (КорректировкиВремениФСС <> Неопределено И КорректировкиВремениФСС.Количество() > 0) Тогда
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

		МассивФизическихЛиц = ОбщегоНазначения.ВыгрузитьКолонку(КорректировкиНачисленийФСС, "ФизическоеЛицо", Истина);
		Если КорректировкиВремениФСС <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивФизическихЛиц, 
				ОбщегоНазначения.ВыгрузитьКолонку(КорректировкиНачисленийФСС, "ФизическоеЛицо", Истина), Истина);
		КонецЕсли;
		
		// Данные ФСС определены для физического лица в целом, 
		// поэтому растиражируем их в учете общего среднего по всем сотрудникам физического лица в этой головной организации.
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудников.ОтбиратьПоГоловнойОрганизации = Истина;
		ПараметрыПолученияСотрудников.Организация = ЗарплатаКадрыПовтИсп.ГоловнаяОрганизация(Организация);
		ПараметрыПолученияСотрудников.СписокФизическихЛиц = МассивФизическихЛиц;
		
		КадровыйУчет.СоздатьВТСотрудникиОрганизации(МенеджерВременныхТаблиц, Ложь, ПараметрыПолученияСотрудников);
		
	КонецЕсли;
	
	Если КорректировкиНачисленийФСС.Количество() > 0 Тогда
		
		// Перенос выполняем только тех сведений, 
		// для которых в учете общего среднего заработка нет «собственных» корректировок.
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	КорректировкиНачисленийФСС.ФизическоеЛицо,
			|	КорректировкиНачисленийФСС.ГоловнаяОрганизация,
			|	КорректировкиНачисленийФСС.Месяц,
			|	КорректировкиНачисленийФСС.ПорядокРасчета,
			|	КорректировкиНачисленийФСС.Сумма
			|ПОМЕСТИТЬ КорректировкиНачисленийФСС
			|ИЗ
			|	&КорректировкиНачисленийФСС КАК КорректировкиНачисленийФСС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.Сотрудник,
			|	КорректировкиНачисленийФСС.ГоловнаяОрганизация КАК Организация,
			|	КорректировкиНачисленийФСС.ФизическоеЛицо,
			|	КорректировкиНачисленийФСС.Месяц,
			|	ПорядокРасчетаОбщегоСреднегоЗаработка.Ссылка КАК ПорядокРасчета,
			|	ЗНАЧЕНИЕ(Перечисление.УчетНачисленийВСреднемЗаработкеОбщий.ОбщийЗаработок) КАК СоставнаяЧасть,
			|	ИСТИНА КАК Индексируется,
			|	КорректировкиНачисленийФСС.Сумма
			|ПОМЕСТИТЬ ВТКорректировкиОбщегоСреднегоЗаработка
			|ИЗ
			|	КорректировкиНачисленийФСС КАК КорректировкиНачисленийФСС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий КАК ПорядокРасчетаОбщегоСреднегоЗаработка
			|		ПО (ИСТИНА)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК Сотрудники
			|		ПО (Сотрудники.ФизическоеЛицо = КорректировкиНачисленийФСС.ФизическоеЛицо)
			|ГДЕ
			|	КорректировкиНачисленийФСС.ПорядокРасчета = &ПорядокРасчетаФСС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КорректировкиОбщегоСреднегоЗаработка.Сотрудник,
			|	КорректировкиОбщегоСреднегоЗаработка.Месяц
			|ПОМЕСТИТЬ ВТСуществующиеЗаписи
			|ИЗ
			|	ВТКорректировкиОбщегоСреднегоЗаработка КАК КорректировкиОбщегоСреднегоЗаработка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК СведенияОНачислениях
			|		ПО (СведенияОНачислениях.Сотрудник = КорректировкиОбщегоСреднегоЗаработка.Сотрудник)
			|			И (СведенияОНачислениях.Месяц = КорректировкиОбщегоСреднегоЗаработка.Месяц)
			|			И (СведенияОНачислениях.ДанныеИзУчетаСреднегоФСС = ЛОЖЬ)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	КорректировкиОбщегоСреднегоЗаработка.Сотрудник,
			|	КорректировкиОбщегоСреднегоЗаработка.Месяц
			|ИЗ
			|	ВТКорректировкиОбщегоСреднегоЗаработка КАК КорректировкиОбщегоСреднегоЗаработка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОНачисленияхДляРасчетаСреднегоЗаработкаОбщий КАК ДанныеНачислений
			|		ПО (ДанныеНачислений.Сотрудник = КорректировкиОбщегоСреднегоЗаработка.Сотрудник)
			|			И (НАЧАЛОПЕРИОДА(ДанныеНачислений.Период, МЕСЯЦ) = КорректировкиОбщегоСреднегоЗаработка.Месяц)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КорректировкиОбщегоСреднегоЗаработка.Сотрудник,
			|	КорректировкиОбщегоСреднегоЗаработка.Организация,
			|	КорректировкиОбщегоСреднегоЗаработка.ФизическоеЛицо,
			|	КорректировкиОбщегоСреднегоЗаработка.Месяц,
			|	КорректировкиОбщегоСреднегоЗаработка.ПорядокРасчета,
			|	КорректировкиОбщегоСреднегоЗаработка.СоставнаяЧасть,
			|	КорректировкиОбщегоСреднегоЗаработка.Индексируется,
			|	КорректировкиОбщегоСреднегоЗаработка.Сумма,
			|	ИСТИНА КАК ДанныеИзУчетаСреднегоФСС
			|ИЗ
			|	ВТКорректировкиОбщегоСреднегоЗаработка КАК КорректировкиОбщегоСреднегоЗаработка
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСуществующиеЗаписи КАК СуществующиеЗаписи
			|		ПО (СуществующиеЗаписи.Сотрудник = КорректировкиОбщегоСреднегоЗаработка.Сотрудник)
			|			И (СуществующиеЗаписи.Месяц = КорректировкиОбщегоСреднегоЗаработка.Месяц)
			|ГДЕ
			|	СуществующиеЗаписи.Месяц ЕСТЬ NULL ";
		
		// Заполняем колонки, имеющие смысл для общего среднего заработка
		// кроме того, данные корректировок представлены для разных порядков расчета ФСС,
		// необходимо данные по одному из порядков (любому, т.к. по всем одинаковые данные) 
		// записать по всем (по которым это имеет смысл) существующим порядкам расчета 
		// общего среднего заработка.
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("КорректировкиНачисленийФСС", КорректировкиНачисленийФСС);
		Запрос.УстановитьПараметр("ПорядокРасчетаФСС", КорректировкиНачисленийФСС[0]["ПорядокРасчета"]);
		
		КорректировкиОбщегоЗаработка.Вставить("КорректировкиНачислений", Запрос.Выполнить().Выгрузить());
		
		Запрос.Текст = 
			"УНИЧТОЖИТЬ ВТСуществующиеЗаписи";
		Запрос.Выполнить();
		
	КонецЕсли;
	
	Если КорректировкиВремениФСС <> Неопределено И КорректировкиВремениФСС.Количество() > 0 Тогда
		
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	КорректировкиВремениФСС.ФизическоеЛицо,
			|	КорректировкиВремениФСС.Месяц,
			|	КорректировкиВремениФСС.ОтработаноДнейКалендарных
			|ПОМЕСТИТЬ КорректировкиВремениФСС
			|ИЗ
			|	&КорректировкиВремениФСС КАК КорректировкиВремениФСС
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Сотрудники.Сотрудник,
			|	КорректировкиВремениФСС.Месяц,
			|	ПорядокРасчетаОбщегоСреднегоЗаработка.Ссылка КАК ПорядокРасчета,
			|	КорректировкиВремениФСС.ОтработаноДнейКалендарных
			|ПОМЕСТИТЬ ВТКорректировкиВремениОбщегоСреднегоЗаработка
			|ИЗ
			|	КорректировкиВремениФСС КАК КорректировкиВремениФСС
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСотрудникиОрганизации КАК Сотрудники
			|		ПО (Сотрудники.ФизическоеЛицо = КорректировкиВремениФСС.ФизическоеЛицо)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Перечисление.ПорядокРасчетаСреднегоЗаработкаОбщий КАК ПорядокРасчетаОбщегоСреднегоЗаработка
			|		ПО (ИСТИНА)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Корректировки.Сотрудник,
			|	Корректировки.Месяц
			|ПОМЕСТИТЬ ВТСуществующиеЗаписи
			|ИЗ
			|	ВТКорректировкиВремениОбщегоСреднегоЗаработка КАК Корректировки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОВремениДляРасчетаСреднегоОбщий КАК СведенияОВремени
			|		ПО (СведенияОВремени.Сотрудник = Корректировки.Сотрудник)
			|			И (СведенияОВремени.Месяц = Корректировки.Месяц)
			|			И (СведенияОВремени.ДанныеИзУчетаСреднегоФСС = ЛОЖЬ)
			|
			|ОБЪЕДИНИТЬ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Корректировки.Сотрудник,
			|	Корректировки.Месяц
			|ИЗ
			|	ВТКорректировкиВремениОбщегоСреднегоЗаработка КАК Корректировки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ДанныеОВремениДляРасчетаСреднегоОбщий КАК ДанныеВремени
			|		ПО (ДанныеВремени.Сотрудник = Корректировки.Сотрудник)
			|			И (НАЧАЛОПЕРИОДА(ДанныеВремени.Период, МЕСЯЦ) = Корректировки.Месяц)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Корректировки.Сотрудник,
			|	Корректировки.Месяц,
			|	Корректировки.ПорядокРасчета,
			|	Корректировки.ОтработаноДнейКалендарных,
			|	ИСТИНА КАК ДанныеИзУчетаСреднегоФСС
			|ИЗ
			|	ВТКорректировкиВремениОбщегоСреднегоЗаработка КАК Корректировки
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСуществующиеЗаписи КАК СуществующиеЗаписи
			|		ПО (СуществующиеЗаписи.Сотрудник = Корректировки.Сотрудник)
			|			И (СуществующиеЗаписи.Месяц = Корректировки.Месяц)
			|ГДЕ
			|	СуществующиеЗаписи.Месяц ЕСТЬ NULL ";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Запрос.УстановитьПараметр("КорректировкиВремениФСС", КорректировкиВремениФСС);
		
		КорректировкиОбщегоЗаработка.Вставить("КорректировкиВремени", Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Возврат КорректировкиОбщегоЗаработка;
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииФормыКалькулятораСреднегоЗаработка

// Процедура заполняет заголовки таблицы представлениями месяцев.
//
Процедура ЗаполнитьМесяцыВЗаголовкахКолонок(ЭлементыФормы, НомераМесяцев, ВыводитьГод) Экспорт
	
	ФорматнаяСтрока = ?(ВыводитьГод, 
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"ДФ='ММММ%1гггг'", 
							Символы.ПС), 
						"ДФ='ММММ'");
						
	Для Каждого КлючИЗначение Из НомераМесяцев Цикл
		НачалоМесяца = КлючИЗначение.Ключ;
		НомерМесяца = КлючИЗначение.Значение;
		ЭлементыФормы["СреднийЗаработокСумма" + НомерМесяца].Заголовок = Формат(НачалоМесяца, ФорматнаяСтрока);
	КонецЦикла;

	// Колонки, которые не входят в период расчета среднего - делаем невидимыми.
	НомерМесяца = 12 - НомераМесяцев.Количество();
	Пока НомерМесяца > 0 Цикл
		ЭлементыФормы["СреднийЗаработокСумма" + НомерМесяца].Видимость = Ложь;
		НомерМесяца = НомерМесяца - 1;
	КонецЦикла;
	
КонецПроцедуры	

Функция СтрокаСреднегоЗаработка(СреднийЗаработок, Идентификатор) Экспорт
	
	НайденныеСтроки = СреднийЗаработок.НайтиСтроки(Новый Структура("Идентификатор", Идентификатор));
	
	Возврат ?(НайденныеСтроки.Количество() = 0, Неопределено, НайденныеСтроки[0]);
	
КонецФункции

// Выбирает из указанного периода месяцы, в которые возможны корректировки, 
// то есть ручной ввод значений в форме калькулятора.
// Корректировки доступны начиная с даты приема на работу и вплоть до первого месяца, 
// в котором есть расчеты.
//
// Параметры:
//	Сотрудник
//	НачалоПериодаРасчета
//	ОкончаниеПериодаРасчета
//
// Возвращаемое значение - фиксированный массив.
//
Функция МесяцыКорректировкиСреднегоЗаработка(Сотрудник, НачалоПериодаРасчета, ОкончаниеПериодаРасчета, ДатаПриемаНаРаботу, ДатаНачалаСобытия) Экспорт
	
	// Метод определяет доступный для редактирования период, 
	// опираясь на наличие данных расчета зарплаты.
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(Начисления.ПериодДействияНачало, МЕСЯЦ)) КАК Месяц
		|ИЗ
		|	РегистрРасчета.Начисления КАК Начисления
		|ГДЕ
		|	Начисления.Сотрудник = &Сотрудник
		|	И НЕ Начисления.Регистратор ССЫЛКА Документ.ПереносДанных";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	
	// Месяцы, в которых доступен ручной ввод, т.е. корректировка.
	МесяцыКорректировкиМассив = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	МесяцОбхода = НачалоПериодаРасчета;
	Пока МесяцОбхода < ОкончаниеПериодаРасчета Цикл
		Если НачалоМесяца(МесяцОбхода) = НачалоМесяца(ДатаНачалаСобытия) Тогда
			// Если период расчета захватывает дату начала события, 
			// значит это месяц приема на работу, зарплата еще не рассчитана, нужно дать возможность корректировок.
			МесяцыКорректировкиМассив.Добавить(МесяцОбхода);
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Месяц) И МесяцОбхода >= Выборка.Месяц Тогда
			// Если начались расчеты, значит месяцы корректировок закончились.
			Прервать;
		КонецЕсли;
		Если КонецМесяца(МесяцОбхода) >= ДатаПриемаНаРаботу Тогда
			МесяцыКорректировкиМассив.Добавить(МесяцОбхода);
		КонецЕсли;
		МесяцОбхода = ДобавитьМесяц(МесяцОбхода, 1);
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(МесяцыКорректировкиМассив);
	
КонецФункции

Процедура ЗаполнитьДатуПриемаНаРаботуСотрудника(ДатаПриема, Сотрудник, ДатаНачалаСобытия, Отказ) Экспорт
	
	// Определим дату приема на работу сотрудника и ограничим ею начало периода расчета среднего заработка.
	КадровыеДанные = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Сотрудник, "ДатаПриема,ОформленПоТрудовомуДоговору");
	Если КадровыеДанные.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Сведения о сотруднике недоступны.';uk='Відомості про працівника недоступні.'"), , , , Отказ);
	Иначе
		Если Не КадровыеДанные[0].ОформленПоТрудовомуДоговору Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Сотрудник не принят на работу.';uk='Співробітник не прийнятий на роботу.'"), , , , Отказ);
		Иначе
			Если Не ЗначениеЗаполнено(ДатаНачалаСобытия) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Дата начала события не заполнена.';uk='Дата початку події не заповнена.'"), , , , Отказ);
			Иначе
				Если КадровыеДанные[0].ДатаПриема > ДатаНачалаСобытия Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Дата начала события раньше, чем сотрудник принят на работу.';uk='Дата початку події раніше, ніж працівник прийнятий на роботу.'"), , , , Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДатаПриема = КадровыеДанные[0].ДатаПриема;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеВспомогательныеПроцедурыИФункции

// Функция составляет массив периодов, входящих в указанный интервал, 
//  для которых отсутствуют строки в коллекции данных среднего заработка.
//
// Параметры:
//  НачалоРасчета - дата начала интервала, в котором определяются не заполненные периоды.
//	ОкончаниеРасчета - дата окончания интервала, в котором определяются не заполненные периоды.
//
// Возвращаемое значение
//	Массив - содержащий периоды, для которых отсутствуют данные среднего заработка.
//
Функция ПериодыНезаполненногоСреднегоЗаработка(НачалоРасчета, ОкончаниеРасчета, ДанныеНачислений)
	
	МассивПериодов = Новый Массив;
	
	ТекущийПериод = НачалоРасчета;
	
	Пока ТекущийПериод < ОкончаниеРасчета Цикл
		Если ДанныеНачислений.НайтиСтроки(Новый Структура("Период", ТекущийПериод)).Количество() = 0 Тогда
			МассивПериодов.Добавить(ТекущийПериод);
		КонецЕсли;
		ТекущийПериод = ДобавитьМесяц(ТекущийПериод, 1);		
	КонецЦикла;	
	
	Возврат МассивПериодов;
	
КонецФункции

Процедура ОчиститьДанныеОСреднемДокумента(Сотрудник, ПорядокРасчета, НачалоПериода, ОкончаниеПериода, ДанныеОНачислениях, ДанныеОВремени, ДанныеОбИндексации = Неопределено)
	
	ТекущийМесяц = НачалоМесяца(НачалоПериода);
	
	Пока ТекущийМесяц <= НачалоМесяца(ОкончаниеПериода) Цикл 
		
		СтруктураПоиска = Новый Структура("Сотрудник, ПорядокРасчета, Период");
		СтруктураПоиска.Сотрудник = Сотрудник;
		СтруктураПоиска.ПорядокРасчета = ПорядокРасчета;
		СтруктураПоиска.Период = ТекущийМесяц;
		
		СтрокиДокументаНачисления = ДанныеОНачислениях.НайтиСтроки(СтруктураПоиска);
		Для Каждого НайденнаяСтрока Из СтрокиДокументаНачисления Цикл
			ДанныеОНачислениях.Удалить(ДанныеОНачислениях.Индекс(НайденнаяСтрока));
		КонецЦикла;
		
		Если ТипЗнч(ПорядокРасчета) = Тип("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаФСС") Тогда
			СтруктураПоиска = Новый Структура("Сотрудник,  Период", Сотрудник,  ТекущийМесяц);	
		КонецЕсли;	
		
		СтрокиДокументаВремя = ДанныеОВремени.НайтиСтроки(СтруктураПоиска);
		Для Каждого НайденнаяСтрока Из СтрокиДокументаВремя Цикл
			ДанныеОВремени.Удалить(ДанныеОВремени.Индекс(НайденнаяСтрока));
		КонецЦикла;
		
		Если ДанныеОбИндексации <> Неопределено Тогда
			УдаляемыеСтрокиИндексации = ДанныеОбИндексации.НайтиСтроки(Новый Структура("Сотрудник, Период", Сотрудник, ТекущийМесяц));
			Для Каждого УдаляемыеСтрока Из УдаляемыеСтрокиИндексации Цикл 
				ДанныеОбИндексации.Удалить(ДанныеОбИндексации.Индекс(УдаляемыеСтрока));
			КонецЦикла;	
		КонецЕсли;	

		ТекущийМесяц = ДобавитьМесяц(ТекущийМесяц, 1);
		
	КонецЦикла;	
	
КонецПроцедуры	

Процедура УдалитьДанныеВнеПериодаРасчетаСреднегоЗаработка(КоллекцияДанныхСреднего, НачалоПериода, ОкончаниеПериода, ПорядокРасчета = Неопределено, ГодГодовыхПремий = Неопределено) Экспорт
	
	СохраняемыеЗначения = Перечисления.ИсточникиДанныхДляРасчетаСреднегоЗаработка.РезультатыРедактирования();
	
 	УдаляемыеСтроки = Новый Массив;
	Для Каждого СтрокаКоллекции Из КоллекцияДанныхСреднего Цикл
		ПодходитПоПериоду = СтрокаКоллекции.Период >= НачалоПериода И СтрокаКоллекции.Период <= ОкончаниеПериода;
		Если ГодГодовыхПремий <> Неопределено Тогда
			Если Перечисления.УчетНачисленийВСреднемЗаработкеОбщий.ГодовыеПремии().Найти(СтрокаКоллекции.СоставнаяЧасть) <> Неопределено Тогда
				ПодходитПоПериоду = СтрокаКоллекции.Год = ГодГодовыхПремий;
			КонецЕсли;
		КонецЕсли;
		
		// Если порядок расчета не указан, то это значит, что его не нужно учитывать, тогда:
		// - корректировки оставляем в любом случае
		// - не корректировки оставляем, если проходит по периоду (если не проходит - удаляем).
		
		// Если порядок расчета указан
		// - корректировки переносим (заменяем порядок расчета)
		// - не корректировки оставляем, если проходит по периоду.
		
		// Если не подходит по периоду, удаляем, но корректировки оставляем.
		Если Не ПодходитПоПериоду И СохраняемыеЗначения.Найти(СтрокаКоллекции.Источник) = Неопределено Тогда
			УдаляемыеСтроки.Добавить(СтрокаКоллекции);
			Продолжить;
		КонецЕсли;	
		
		Если ПорядокРасчета <> Неопределено Тогда
			Если СтрокаКоллекции.ПорядокРасчета <> ПорядокРасчета Тогда
				Если СохраняемыеЗначения.Найти(СтрокаКоллекции.Источник) <> Неопределено Тогда
					СтрокаКоллекции.ПорядокРасчета = ПорядокРасчета;
				Иначе
					// Не корректировка, которая не совпадает по порядку расчета
					// ее удаляем даже несмотря на то, что она проходит по периоду.
					УдаляемыеСтроки.Добавить(СтрокаКоллекции);
				КонецЕсли;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		
		Если Не ПодходитПоПериоду Тогда
			УдаляемыеСтроки.Добавить(СтрокаКоллекции);
		КонецЕсли;
	КонецЦикла;	
	
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		КоллекцияДанныхСреднего.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьДанныеДляРасчетаСреднего(КоллекцияСтрок, Сотрудники)
	
	// Очищает данные для расчета среднего по указанным сотрудникам.
	Для Каждого Сотрудник Из Сотрудники Цикл
		ОтборСтрок = Новый Структура("Сотрудник", Сотрудник);
		УдаляемыеСтроки = КоллекцияСтрок.НайтиСтроки(ОтборСтрок);
		Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
			КоллекцияСтрок.Удалить(УдаляемаяСтрока);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

// Составляет массив типов документов, в которых осуществляется локальный расчет показателей среднего заработка.
//
Функция ТипыДокументовРасчетаПоСреднемуЗаработку() Экспорт 
	
	ТипыДокументов = Новый Массив;
	
	// ФСС
	ТипыДокументов.Добавить(Тип("ДокументСсылка.БольничныйЛист"));
	
	// "Общий" средний заработок
	ТипыДокументов.Добавить(Тип("ДокументСсылка.Командировка"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ОплатаПоСреднемуЗаработку"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.Отпуск"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.Увольнение"));
	
	Возврат ТипыДокументов;
	
КонецФункции

// Устарела. 
// см. УчетСреднегоЗаработкаКлиентСервер.ПараметрыРедактированияОбщегоСреднегоЗаработкаПоДокументу
//
Функция ПараметрыРедактированияОбщегоСреднегоЗаработкаПоДокументу() Экспорт
	Возврат УчетСреднегоЗаработкаКлиентСервер.ПараметрыРедактированияОбщегоСреднегоЗаработкаПоДокументу();
КонецФункции

#КонецОбласти

#Область БлокОбновленияИнформационнойБазы

Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
