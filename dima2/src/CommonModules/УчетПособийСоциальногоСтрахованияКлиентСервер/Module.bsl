
#Область СлужебныеПроцедурыИФункции

// Возвращает дату вступления в силу статьи 1 Федерального закона от 25.02.2011 № 21-ФЗ.
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//   дата
//
Функция ДатаОтдельногоРасчетаПособийПоМатеринству() Экспорт 

	Возврат '20130101'

КонецФункции 

// Функция возвращает длину суток в секундах.
//
Функция ДлинаСуток() Экспорт
	Возврат 24 * 60 * 60;
КонецФункции

// Функция возвращает количество дней за счет работодателя в зависимости от даты начала нетрудоспособности.
//
Функция КоличествоДнейЗаСчетРаботодателя(ДатаНачалаСобытия, ПричинаНетрудоспособности) Экспорт
	Если (ПричинаНетрудоспособности = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.Профзаболевание") ИЛИ
		ПричинаНетрудоспособности = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ТравмаНаПроизводстве") ИЛИ
		ПричинаНетрудоспособности = ПредопределенноеЗначение("Перечисление.ПричиныНетрудоспособности.ОбщееЗаболевание")) Тогда
 		Возврат 5
	Иначе
		Возврат 0
	КонецЕсли;
	
КонецФункции


#Область СреднийЗаработокФСС

Функция СреднийЗаработокФСС(ПараметрыРасчета) Экспорт 
	
	СреднийЗаработокФСС = 0;
	
	Если ПараметрыРасчета.ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010") Тогда
		СреднийЗаработокФСС = СреднийЗаработокФСС2010(ПараметрыРасчета);	
	иначе   
		СреднийЗаработокФСС = СреднийЗаработокФСС2011(ПараметрыРасчета);
	КонецЕсли;
	
	Возврат СреднийЗаработокФСС;
	
КонецФункции
Функция СреднийЗаработокФСС2010(ПараметрыРасчета) Экспорт
	СуммаНачислений = 0; 
	ОтработаноДнейКалендарных = 0;
	
	Для Каждого СтрокаКоллекции Из ПараметрыРасчета.ДанныеНачислений Цикл
		
		Если СтрокаКоллекции.ПорядокРасчета <> ПараметрыРасчета.ПорядокРасчета Тогда 
			Продолжить;
		КонецЕсли;

		Если СтрокаКоллекции.Период >= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало 
			И СтрокаКоллекции.Период <= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание Тогда
			СуммаПредела = 0;  СтрокаКоллекции.СуммаСУчетомОграничений = 0;  
			Если Не ПараметрыРасчета.ПредельныеВеличиныПоМесяцам.Получить(СтрокаКоллекции.Период) = Неопределено Тогда
				СуммаПредела = ПараметрыРасчета.ПредельныеВеличиныПоМесяцам[СтрокаКоллекции.Период];
				СтрокаКоллекции.Предел = СуммаПредела;
			КонецЕсли;	
			Если Не СуммаПредела = 0 Тогда
				СуммаПределаСУчтеннымиСуммами = ?((СуммаПредела - СтрокаКоллекции.УчтеннаяСумма) > 0,(СуммаПредела - СтрокаКоллекции.УчтеннаяСумма),0);
				СтрокаКоллекции.СуммаСУчетомОграничений = ?((СуммаПределаСУчтеннымиСуммами - СтрокаКоллекции.Сумма) > 0, СтрокаКоллекции.Сумма, СуммаПределаСУчтеннымиСуммами);
			Иначе
				СтрокаКоллекции.СуммаСУчетомОграничений = СтрокаКоллекции.Сумма;
			КонецЕсли;	
			СуммаНачислений = СуммаНачислений + СтрокаКоллекции.СуммаСУчетомОграничений;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаКоллекции Из ПараметрыРасчета.ДанныеВремени Цикл
		
		Если СтрокаКоллекции.Период >= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало 
			И СтрокаКоллекции.Период <= ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание Тогда
			ОтработаноДнейКалендарных = ОтработаноДнейКалендарных + СтрокаКоллекции.ОтработаноДнейКалендарных;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ?(ОтработаноДнейКалендарных = 0, 0, Окр(СуммаНачислений / ОтработаноДнейКалендарных, 2));
	
КонецФункции	

Функция СреднийЗаработокФСС2011(ПараметрыРасчета)
	
	СреднедневнойЗаработок = 0;
	
	ПрименятьПредельнуюВеличину = ПараметрыРасчета.ПрименятьПредельнуюВеличину;
	Если ПараметрыРасчета.НеполныйРасчетныйПериод Тогда
		ПараметрыРасчета.ПрименятьПредельнуюВеличину = Ложь
	КонецЕсли;
	
	ДанныеРасчетаСреднегоЗаработка = ДанныеРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета);
	
	Заработок		= УчитываемыйЗаработокФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка);
	КоличествоДней 	= УчитываемыхДнейВКалендарныхГодахФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка);
	
	СреднедневнойЗаработок = СреднедневнойЗаработокФСС(Заработок, КоличествоДней);
	
	Если ПараметрыРасчета.НеполныйРасчетныйПериод И ПрименятьПредельнуюВеличину Тогда
		МаксимальныйСреднедневнойЗаработок = МаксимальныйСреднедневнойЗаработокДляНеполногоРасчетногоПериода(ПараметрыРасчета.ДатаНачалаСобытия);
		СреднедневнойЗаработок = Мин(МаксимальныйСреднедневнойЗаработок, СреднедневнойЗаработок);			
	КонецЕсли;
	
	ПараметрыРасчета.ПрименятьПредельнуюВеличину = ПрименятьПредельнуюВеличину;
	
	Возврат СреднедневнойЗаработок;
	
КонецФункции
Функция ПараметрыРасчетаСреднегоДневногоЗаработкаФСС() Экспорт
	
	Параметры = Новый Структура(
		"ДатаНачалаСобытия, 
		|ПериодРасчетаСреднегоЗаработкаНачало, 
		|ПериодРасчетаСреднегоЗаработкаОкончание, 
		|РасчетныеГоды, 
		|РасчетныеМесяцы,
		|НеполныйРасчетныйПериод, 
		|ДанныеНачислений, 
		|ДанныеВремени, 
		|ДанныеСтрахователей, 
		|УчитыватьЗаработокПредыдущихСтрахователей, 
		|ИспользоватьДниБолезниУходаЗаДетьми, 
		|ПрименятьПредельнуюВеличину, 
		|ПредельныеВеличиныПоГодам, 
		|ПредельныеВеличиныПоМесяцам, 
		|ПорядокРасчета, 
		|ДоляНеполногоВремени, 
		|НомерЛисткаНетрудоспособности, 
		|ПричинаНетрудоспособности, 
		|ОграничениеПособия, 
		|СреднийДневнойЗаработок,
		|МинимальныйРазмерОплатыТруда, 
		|Сотрудник");
	
	Параметры.РасчетныеГоды 							= Новый Массив;
	Параметры.РасчетныеМесяцы 							= Новый Массив;
	Параметры.НеполныйРасчетныйПериод			 		= Ложь;
	Параметры.УчитыватьЗаработокПредыдущихСтрахователей	= Ложь;
	Параметры.ПредельныеВеличиныПоГодам 				= Новый Соответствие;
	Параметры.ПредельныеВеличиныПоМесяцам 				= Новый Соответствие;
	Параметры.ИспользоватьДниБолезниУходаЗаДетьми 		= Ложь;
	Параметры.ПрименятьПредельнуюВеличину 				= Истина;
	Параметры.ДоляНеполногоВремени 						= 1;
	
	Возврат Параметры;
	
КонецФункции

Функция ПорядокРасчетаСреднегоЗаработкаФСС(Период) Экспорт
	ПрименениеПорядкаРасчета = Новый Массив;
	ПрименениеПорядкаРасчета.Добавить(
		СтруктураПримененияПорядкаРасчета(
			ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010"), 
			Дата(2015, 7, 4)));
	
	// значение по умолчанию		
	ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010");
	
	ПодобратьПорядокРасчетаПоДатеПрименения(ПорядокРасчета, Период, ПрименениеПорядкаРасчета);
	
	Возврат ПорядокРасчета;
	
КонецФункции

Функция ПериодРасчетаСреднегоЗаработкаФСС(ДатаНачалаСобытия, ПорядокРасчета = Неопределено) Экспорт
	ПериодРасчета = Новый СтандартныйПериод;
	
	Если ПорядокРасчета = Неопределено Тогда
		ПорядокРасчета = ПорядокРасчетаСреднегоЗаработкаФСС(ДатаНачалаСобытия);
	КонецЕсли;
	
	Если ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010") Тогда
		ПериодРасчета.ДатаНачала	= НачалоМесяца(ДобавитьМесяц(ДатаНачалаСобытия, -12));
		ПериодРасчета.ДатаОкончания	= КонецМесяца(ДобавитьМесяц(ДатаНачалаСобытия, -1));
	КонецЕсли;
	
	Если ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2011") Тогда
		ПериодРасчета.ДатаНачала	= НачалоГода(ДобавитьМесяц(ДатаНачалаСобытия, -24));
		ПериодРасчета.ДатаОкончания	= КонецГода(ДобавитьМесяц(ДатаНачалаСобытия, -12));
	КонецЕсли;
	
	Возврат ПериодРасчета;
	
КонецФункции

Функция МесяцыПредельныхВеличин(ПараметрыРасчета) Экспорт
	МесяцыПредельныхВеличин = Новый Массив;
	
	НачалоМесяцаПредельныхВеличин = ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаНачало;
	Пока КонецМесяца(НачалоМесяцаПредельныхВеличин) <= КонецДня(ПараметрыРасчета.ПериодРасчетаСреднегоЗаработкаОкончание) Цикл
		МесяцыПредельныхВеличин.Добавить(НачалоМесяцаПредельныхВеличин);
		НачалоМесяцаПредельныхВеличин = ДобавитьМесяц(НачалоМесяцаПредельныхВеличин,1);
	КонецЦикла;	
		
	Возврат МесяцыПредельныхВеличин;
	
КонецФункции

#Область ДанныеРасчетаСреднегоЗаработкаФСС
	
Функция ДанныеРасчетаСреднегоЗаработкаФСС(ПараметрыРасчета) Экспорт
	
	ДанныеРасчета = Новый Массив;
		
	ДобавитьСтрокиРасчетныхЛет(ДанныеРасчета, ПараметрыРасчета);
	
	ДобавитьДанныеОЗаработке(ДанныеРасчета, ПараметрыРасчета);
	    
	ДобавитьДанныеОВремени(ДанныеРасчета, ПараметрыРасчета);
	
	Если ПараметрыРасчета.УчитыватьЗаработокПредыдущихСтрахователей Тогда
		ДобавитьДанныеСтороннихСтрахователей(ДанныеРасчета, ПараметрыРасчета);
	КонецЕсли;
		
	Возврат ДанныеРасчета;
	
КонецФункции

Процедура ДобавитьСтрокиРасчетныхЛет(ДанныеРасчета, ПараметрыРасчета)
	
	Отбор = Новый Структура;
	
	Для каждого РасчетныйМесяц Из ПараметрыРасчета.РасчетныеМесяцы Цикл
		Отбор.Очистить();
		Отбор.Вставить("РасчетныйМесяц", РасчетныйМесяц);
		ЭлементДанных = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, Отбор, Истина);
		ПредельнаяВеличина = ПараметрыРасчета.ПредельныеВеличиныПоМесяцам.Получить(РасчетныйМесяц);
		ЭлементДанных.ПредельнаяВеличина = ?(ПредельнаяВеличина = Неопределено, 0, ПредельнаяВеличина); 
	КонецЦикла; 
	
КонецПроцедуры

Процедура ДобавитьДанныеОЗаработке(ДанныеРасчета, ПараметрыРасчета)
	Отбор = Новый Структура;
	
	Для каждого Начисление Из ПараметрыРасчета.ДанныеНачислений Цикл
		
		Если Начисление.ПорядокРасчета <> ПараметрыРасчета.ПорядокРасчета Тогда 
			Продолжить;
		КонецЕсли;
		
		
		Отбор.Очистить();
		Отбор.Вставить("РасчетныйМесяц", Начисление.Период); // было Месяц(Начисление.Период);
		СтрокаПоМесяцу = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, Отбор);
		
		Если СтрокаПоМесяцу <> Неопределено Тогда
			СтрокаПоМесяцу.Заработок = СтрокаПоМесяцу.Заработок + Начисление.Сумма;
			СтрокаПоМесяцу.СуммаСУчетомОграничений = СтрокаПоМесяцу.СуммаСУчетомОграничений + Начисление.Сумма;
			СтрокаПоМесяцу.УчтеннаяСумма = СтрокаПоМесяцу.УчтеннаяСумма + Начисление.УчтеннаяСумма;
			
			Отбор.Очистить();
			Отбор.Вставить("Страхователь", Неопределено);
			СтрокаСтрахователя = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоМесяцу.Страхователи, Отбор);
			СтрокаСтрахователя.Заработок = СтрокаСтрахователя.Заработок + Начисление.Сумма; 
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьДанныеОВремени(ДанныеРасчета, ПараметрыРасчета)
	Отбор = Новый Структура;
	
	Для каждого СтрокаВремени Из ПараметрыРасчета.ДанныеВремени Цикл
		
		
		Отбор.Очистить();
		//Отбор.Вставить("РасчетныйГод", Год(СтрокаВремени.Период));
		Отбор.Вставить("РасчетныйМесяц", СтрокаВремени.Период);
		СтрокаПоМесяцу = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, Отбор);
		
		Если СтрокаПоМесяцу <> Неопределено Тогда
			СтрокаПоМесяцу.ДнейБолезниУходаЗаДетьми = СтрокаПоМесяцу.ДнейБолезниУходаЗаДетьми + СтрокаВремени.ДнейБолезниУходаЗаДетьми;
			
			Отбор.Очистить();
			Отбор.Вставить("Страхователь", Неопределено);
			СтрокаСтрахователя = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоМесяцу.Страхователи, Отбор);
			СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми = СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми + СтрокаВремени.ДнейБолезниУходаЗаДетьми; 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ДобавитьДанныеСтороннихСтрахователей(ДанныеРасчета, ПараметрыРасчета)
	Отбор = Новый Структура;
	
	Для каждого СтороннийСтрахователь Из ПараметрыРасчета.ДанныеСтрахователей Цикл
		
		Отбор.Очистить();
		Отбор.Вставить("РасчетныйМесяц", СтороннийСтрахователь.Месяц);
		СтрокаПоМесяцу = ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(ДанныеРасчета, Отбор);
		
		Если СтрокаПоМесяцу <> Неопределено Тогда			
			СтрокаПоМесяцу.Заработок = СтрокаПоМесяцу.Заработок + СтороннийСтрахователь.Сумма;
			СтрокаПоМесяцу.ДнейБолезниУходаЗаДетьми = СтрокаПоМесяцу.ДнейБолезниУходаЗаДетьми + СтороннийСтрахователь.ДнейБолезниУходаЗаДетьми;
			
			Отбор.Очистить();
			Отбор.Вставить("Страхователь", СтороннийСтрахователь.Страхователь);
			Отбор.Вставить("СтороннийСтрахователь", Истина);
			СтрокаСтрахователя = ЭлементДанныхСтрахователяПоОтбору(СтрокаПоМесяцу.Страхователи, Отбор);
			СтрокаСтрахователя.Заработок = СтрокаСтрахователя.Заработок + СтороннийСтрахователь.Сумма; 
			СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми = СтрокаСтрахователя.ДнейБолезниУходаЗаДетьми + СтороннийСтрахователь.ДнейБолезниУходаЗаДетьми;
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ЭлементДанныхДляРасчетаСреднегоЗаработкаПоОтбору(Коллекция, Отбор, СоздаватьНовый = Ложь)
	
	ПодходящийЭлемент = ЭлементКоллекцииПоОтбору(Коллекция, Отбор);
	
	Если СоздаватьНовый И ПодходящийЭлемент = Неопределено Тогда
		ПодходящийЭлемент = СтруктураДанныхДляРасчетаСреднегоЗаработка();
		ЗаполнитьЭлементКоллекцииПоОтбору(ПодходящийЭлемент, Отбор);
		
		Коллекция.Добавить(ПодходящийЭлемент);
	КонецЕсли;
	
	Возврат ПодходящийЭлемент;
	
КонецФункции

Функция ЭлементДанныхСтрахователяПоОтбору(Коллекция, Отбор)
	
	ПодходящийЭлемент = ЭлементКоллекцииПоОтбору(Коллекция, Отбор);
	
	Если ПодходящийЭлемент = Неопределено Тогда
		ПодходящийЭлемент = СтруктураДанныхСтороннегоСтрахователяДляРасчетаСреднего();
		ЗаполнитьЭлементКоллекцииПоОтбору(ПодходящийЭлемент, Отбор);
		
		Коллекция.Добавить(ПодходящийЭлемент);
	КонецЕсли;
	
	Возврат ПодходящийЭлемент;
КонецФункции

Функция СтруктураДанныхДляРасчетаСреднегоЗаработка()
	
	ТипЧисло = Новый ОписаниеТипов("Число");
	ПустоеЧисло = ТипЧисло.ПривестиЗначение();  
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("РасчетныйМесяц", 				ПустоеЧисло);
	СтруктураДанных.Вставить("Заработок", 					ПустоеЧисло);
	СтруктураДанных.Вставить("ДнейБолезниУходаЗаДетьми", 	ПустоеЧисло);
	СтруктураДанных.Вставить("ПредельнаяВеличина", 			ПустоеЧисло);
	СтруктураДанных.Вставить("УчтеннаяСумма", 				ПустоеЧисло);
	СтруктураДанных.Вставить("СуммаСУчетомОграничений", 	ПустоеЧисло);
	СтруктураДанных.Вставить("Страхователи", 				Новый Массив);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция СтруктураДанныхСтороннегоСтрахователяДляРасчетаСреднего()
	
	ТипЧисло = Новый ОписаниеТипов("Число");
	ПустоеЧисло = ТипЧисло.ПривестиЗначение();
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("Страхователь", 				Неопределено);
	СтруктураДанных.Вставить("СтороннийСтрахователь", 		Ложь);
	СтруктураДанных.Вставить("Заработок", 					ПустоеЧисло);
	СтруктураДанных.Вставить("ДнейБолезниУходаЗаДетьми", 	ПустоеЧисло);
	
	Возврат СтруктураДанных;
	
КонецФункции




#КонецОбласти

#Область АгрегатыДанныхРасчетаСреднегоЗаработка

Функция СреднедневнойЗаработокФСС(Заработок, КоличествоДней) Экспорт
	Возврат ?(КоличествоДней = 0, 0, Окр(Заработок / КоличествоДней,2));
КонецФункции

Функция УчитываемыйЗаработокФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка) Экспорт
	
	ВсегоЗаработка = 0;	
	
	ВсегоЗаработка = ЗаработокСУчетомОграниченияФСС(ДанныеРасчетаСреднегоЗаработка);
	
	Возврат ВсегоЗаработка
	
КонецФункции

Функция УчитываемыйЗаработокФССПоГоду(ПараметрыРасчета, СтрокаДанныхРасчетаСреднегоПоГоду) Экспорт
	
	ВсегоЗаработка = 0;	
	
	Если ПараметрыРасчета.ПрименятьПредельнуюВеличину И Не ПараметрыРасчета.НеполныйРасчетныйПериод Тогда
		ВсегоЗаработка = Мин(СтрокаДанныхРасчетаСреднегоПоГоду.ПредельнаяВеличина, СтрокаДанныхРасчетаСреднегоПоГоду.Заработок);
	Иначе
		ВсегоЗаработка = СтрокаДанныхРасчетаСреднегоПоГоду.Заработок;
	КонецЕсли;
	
	Возврат ВсегоЗаработка
	
КонецФункции

Функция ЗаработокСУчетомОграниченияФСС(ДанныеРасчетаСреднегоЗаработка) Экспорт
	
	ВсегоЗаработка = 0;	
	
	Для каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
		ВсегоЗаработка = ВсегоЗаработка + Элемент.СуммаСУчетомОграничений;
	КонецЦикла;
	
	Возврат ВсегоЗаработка
	
КонецФункции

Функция ЗаработокБезОграниченияФСС(ДанныеРасчетаСреднегоЗаработка) Экспорт
	
	ВсегоЗаработка = 0;	
	
	Для каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
			ВсегоЗаработка = ВсегоЗаработка + Элемент.Заработок;
	КонецЦикла;

	Возврат ВсегоЗаработка

КонецФункции



Функция МаксимальныйСреднедневнойЗаработокДляНеполногоРасчетногоПериода(ДатаНачалаСобытия) Экспорт
	
	МаксимальныйСреднедневнойЗаработок = 0;
	Если ДатаНачалаСобытия < '20160101'  Тогда
		МаксимальныйСреднедневнойЗаработок = 1632.88;
	Иначе
		МаксимальныйСреднедневнойЗаработок = (624000 + 670000) / 730;
	КонецЕсли;
	
	Возврат Окр(МаксимальныйСреднедневнойЗаработок, 2);
	
КонецФункции
Функция МинимальныйСреднедневнойЗаработокФСС(ПараметрыРасчета) Экспорт
	Возврат Окр(ПараметрыРасчета.МинимальныйРазмерОплатыТруда * ДоляНеполногоВремени(ПараметрыРасчета) / СреднееКоличествоДнейВМесяцеФСС(), 2);

КонецФункции

Функция ДоляНеполногоВремени(ПараметрыРасчета)
	
	Возврат ?(ПараметрыРасчета.ДоляНеполногоВремени = 0, 1, Мин(ПараметрыРасчета.ДоляНеполногоВремени, 1));

КонецФункции

Функция УчитываемыхДнейВКалендарныхГодахФСС(ПараметрыРасчета, ДанныеРасчетаСреднегоЗаработка) Экспорт
	
	УчитываемыхДнейВКалендарныхГодах = 365;
	
	Возврат УчитываемыхДнейВКалендарныхГодах;

КонецФункции

Функция КоличествоКалендарныхДнейПособияПоМатеринству(ДанныеРасчетаСреднегоЗаработка) Экспорт
	
	КалендарныхДней = 0;
	
	Для каждого Элемент Из ДанныеРасчетаСреднегоЗаработка Цикл
		КалендарныхДней = КалендарныхДней + ДеньГода(Дата(Элемент.РасчетныйГод, 12, 31)) - Элемент.ДнейБолезниУходаЗаДетьми;
	КонецЦикла;	
	
	Возврат КалендарныхДней
	
КонецФункции

Функция КоличествоКалендарныхДнейВДвухГодахФСС() Экспорт
	
	Возврат 730;
	
КонецФункции
Функция СреднееКоличествоДнейВМесяцеФСС() Экспорт
	Возврат 30.44;
КонецФункции

#КонецОбласти

// Функция выполняет упаковку данных среднего заработка ФСС формы документа для передачи в форму редактирования.
Функция ПараметрыРедактированияСреднегоЗаработкаФССПоДокументу() Экспорт
	ПараметрыРедактирования = Новый Структура(
		"ДокументСсылка,
		|Сотрудник,
		|Организация,
		|ДатаНачалаСобытия,
		|НачалоПериодаРасчета,
		|ОкончаниеПериодаРасчета,
		|ГодыПериодаРасчета,
		
		|МесяцыПериодаРасчета,
		|ПредельныеВеличиныПоМесяцам,
		|СуммаПределаСредней,
		|РучнаяКорректировкаСреднегоЗаработка,
		|СреднийДневнойЗаработок,
		|ПричинаНетрудоспособности,
		|НомерЛисткаНетрудоспособности,
		
		|ФиксПериодРасчета,
		|ПорядокРасчета,
		|ИспользоватьДниБолезниУходаЗаДетьми,
		|УчитыватьЗаработокПредыдущихСтрахователей,
		|ТолькоПросмотр,
		|ДанныеОНачислениях,
		|ДанныеОВремени,
		|ДанныеСтрахователей, 
		|ПериодыОтсутствий, 
		|ПараметрыРасчета");
	
	ПараметрыРедактирования.ФиксПериодРасчета = Ложь;
	ПараметрыРедактирования.ПорядокРасчета = ПредопределенноеЗначение("Перечисление.ПорядокРасчетаСреднегоЗаработкаФСС.Постановление2010");
	ПараметрыРедактирования.УчитыватьЗаработокПредыдущихСтрахователей = Ложь;
	ПараметрыРедактирования.ТолькоПросмотр = Ложь;
	
	Возврат ПараметрыРедактирования;
	
КонецФункции
	   
// Функция конструирует структуру, описывающую данные о начислениях 
// для расчета среднего заработка за один месяц расчетного периода.
// Поля структуры заполнены пустыми значениями соответствующих типов.
//
Функция ОписаниеСтрокиДанныхОНачисленияхСреднегоЗаработкаФСС() Экспорт
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("ФизическоеЛицо", Новый ОписаниеТипов("СправочникСсылка.ФизическиеЛица"));
	ПоляОписания.Вставить("ПорядокРасчета", Новый ОписаниеТипов("ПеречислениеСсылка.ПорядокРасчетаСреднегоЗаработкаФСС"));
	ПоляОписания.Вставить("СтатьяФинансирования", Новый ОписаниеТипов("СправочникСсылка.СтатьиФинансированияЗарплата"));
	ПоляОписания.Вставить("Период", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Сумма", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("Корректировка", Новый ОписаниеТипов("Булево"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции
	   
// Функция конструирует структуру, описывающую данные периодах отсутствия.
// Поля структуры заполнены пустыми значениями соответствующих типов.
//
Функция ОписаниеСтрокиПериодовДнейБолезниУходаЗаДетьми() Экспорт
	
	ПоляОписания = Новый Соответствие;
	ПоляОписания.Вставить("Начало", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Окончание", Новый ОписаниеТипов("Дата"));
	ПоляОписания.Вставить("Дни", Новый ОписаниеТипов("Число"));
	ПоляОписания.Вставить("ВидБолезниУходаЗаДетьми", Новый ОписаниеТипов("Строка"));
	ПоляОписания.Вставить("Корректировка", Новый ОписаниеТипов("Булево"));
	
	Описание = Новый Структура;
	Для Каждого КлючИЗначение Из ПоляОписания Цикл
		Описание.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.ПривестиЗначение());
	КонецЦикла;
	
	Возврат Описание;
	
КонецФункции

Функция СтруктураПримененияПорядкаРасчета(ПорядокРасчета, НачалоПрименения) Экспорт
	
	СтруктураПрименения = Новый Структура;
	СтруктураПрименения.Вставить("ПорядокРасчета", ПорядокРасчета);
	СтруктураПрименения.Вставить("НачалоПрименения", НачалоПрименения);
	
	Возврат СтруктураПрименения;
	
КонецФункции

Функция ЭлементКоллекцииПоОтбору(Коллекция, Отбор) Экспорт
	
	ПодходящийЭлемент = Неопределено;
	
	Для каждого Элемент Из Коллекция Цикл
		ЭтоПодходящийЭлемент = Истина;
		
		Для каждого ЭлементОтбора Из Отбор Цикл
			Если ЭлементОтбора.Значение <> Неопределено
				И Элемент[ЭлементОтбора.Ключ] <> ЭлементОтбора.Значение  Тогда
				ЭтоПодходящийЭлемент = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтоПодходящийЭлемент Тогда
			ПодходящийЭлемент = Элемент;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПодходящийЭлемент;
	
КонецФункции

Процедура ЗаполнитьЭлементКоллекцииПоОтбору(Элемент, Отбор) Экспорт
	Для каждого ЭлементОтбора Из Отбор Цикл
		Если ЭлементОтбора.Значение <> Неопределено Тогда
			Элемент[ЭлементОтбора.Ключ] = ЭлементОтбора.Значение; 
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПодобратьПорядокРасчетаПоДатеПрименения(ПорядокРасчета, Период, ПравилаПримененияПорядкаРасчета) Экспорт
	
	// Подбирается порядок расчета, 
	// дата начала применения которого "ближе" всего к заданному периоду.
	
	Для Каждого ПрименениеПорядкаРасчета Из ПравилаПримененияПорядкаРасчета Цикл
		Если Период >= ПрименениеПорядкаРасчета.НачалоПрименения Тогда
			ПорядокРасчета = ПрименениеПорядкаРасчета.ПорядокРасчета;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
