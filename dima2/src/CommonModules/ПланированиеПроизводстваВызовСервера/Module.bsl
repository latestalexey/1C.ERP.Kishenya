////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции планирования производства и расчета графика производства
//
////////////////////////////////////////////////////////////////////////////////

//++ НЕ УТКА

#Область ПрограммныйИнтерфейс

Функция ОчередьПланирования(Заказ) Экспорт
	
	Возврат Документы.ЗаказНаПроизводство.ОчередьПланирования(Заказ);
	
КонецФункции

Функция РассчитатьГрафикВыпуска(Заказ) Экспорт
	
	Если Документы.ЗаказНаПроизводство.ТребуетсяПланировать(Заказ).Количество() = 0 Тогда
		
		Возврат Новый Структура("Запланирован", Истина);
		
	КонецЕсли;
	
	ЗаказОбъект = Заказ.ПолучитьОбъект();
	ЗаказОбъект.Заблокировать();
	
	РезультатРасчета = ЗаказОбъект.РассчитатьГрафикВыпуска();
	
	Если РезультатРасчета.Запланирован Тогда
		
		ЗаказОбъект.ИнициализироватьПараметрыАктуализацииМаршрутныхЛистов();
		
		ЗаказОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Возврат Новый Структура("Запланирован", Истина);
		
	Иначе
		
		Возврат Новый Структура("Запланирован, Ошибки", Ложь, РезультатРасчета.Ошибки);
		
	КонецЕсли;
	
КонецФункции

// Возвращает фиксированные настройки для отчета ОтклонениеЗаказаФактаОтКалькуляции
//
// Параметры:
//   Параметры - Структура - структура устанавливаемых параметров
//
// Возвращаемое значение:
//	НастройкиКомпоновкиДанных - фиксированные настройки.
//
Функция ПараметрыРасшифровкиОтклонениеЗаказаФактаОтКалькуляции(Параметры) Экспорт
	
	ОтчетОбъект = Отчеты.ОтклонениеЗаказаФактаОтКалькуляции.Создать();
	
	ФиксированныеНастройки = ОтчетОбъект.КомпоновщикНастроек.ФиксированныеНастройки;
	УстанавливаемыеПараметры = Новый Соответствие;
	УстанавливаемыеПараметры.Вставить("ЗаказРасшифровка",            Параметры.ЗаказНаПроизводство);
	УстанавливаемыеПараметры.Вставить("КодСтрокиРасшифровка",        Параметры.КодСтрокиЗаказаНаПроизводство);
	КомпоновкаДанныхКлиентСервер.УстановитьКоллекциюПараметров(ФиксированныеНастройки, УстанавливаемыеПараметры);
	
	Возврат ФиксированныеНастройки;
	
КонецФункции

#Область РедактированиеСпецификацииСтрокиЗаказа

// Функция возвращает пустую структуру данных по продукции
//
// Параметры
//  ДанныеЗаполнения - Структура - структура данных строки продукции заказа на производство
//    * КлючСвязи - УникальныйИдентификатор - Идентификатор строки продукции в заказе на производство
//    * Номенклатура - СправочникСсылка.Номенклатура - номенклатура продукции
//    * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика продукции
//    * Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение-получатель для работ
//    * Склад - СправочникСсылка.Склады - склад для продукции
//    * Назначение - СправочникСсылка.Назначения - назначение продукции заказа на производство
//    * НазначениеЗаказа - СправочникСсылка.Назначения - назначение заказа на производство
//
// Возвращаемое значение
//  Структура - данные по продукции 
//
Функция СтруктураДанныхПродукции(Знач ДанныеЗаполнения = Неопределено) Экспорт
	
	Возврат ПланированиеПроизводства.СтруктураДанныхПродукции(ДанныеЗаполнения);
	
КонецФункции

// Функция возвращает способ получения материала для этапа производства по умолчанию
//
// Параметры:
//   * ДанныеЗаказа - Структура - структура данных заказа
//       * ПодразделениеДиспетчер - СправочникСсылка.СтруктураПредприятия - Подразделение, ответственное за выполнение заказа
//       * Назначение             - СправочникСсылка.Назначения - назначение обособленной потребности
//       * НачатьНеРанее          - Дата - дата, не ранее которой необходимо начать производить.
//   * ДанныеМатериала - Структура - структура данных материала
//       * Подразделение          - СправочникСсылка.СтруктураПредприятия - подразделение-получатель 
//       * ПроизводствоНаСтороне  - Булево - признак, что производство выполняется на стороне
//       * Номенклатура           - СправочникСсылка.Номенклатура - материала, для которого определяется настройка
//       * Характеристика         - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика материала, для которого определяется настройка
// 
// Возвращаемое значение:
//   * Структура
//       * Номенклатура - СправочникСсылка.Номенклатура - материала, для которого определяется настройка
//       * Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика материала, для которого определяется настройка
//       * ПолучениеМатериалов - ПеречислениеСсылка.СпособыПолучениеМатериаловЭтапаПроизводства - способы получение материалов этапа производства
//       * Подразделение - СправочникСсылка.СтруктураПредприятия - подразделение-получатель 
//       * Склад - СправочникСсылка.Склады - Склад, с которого передаются материалы в подразделение. 
//       * ЗаказатьНаСклад - Булево - признак, материал необходимо заказать на склад
//       * ВариантОбеспечения - ПеречислениеСсылка.ВариантыОбеспечения - вариант обеспечения
//       * Запланировать - Булево - признак, материал возможно 
//
Функция СпособПолученияМатериалаПоУмолчанию(Знач ДанныеЗаказа, Знач ДанныеМатериала) Экспорт
	
	Возврат ПланированиеПроизводства.СпособПолученияМатериалаПоУмолчанию(ДанныеЗаказа, ДанныеМатериала);
	
КонецФункции

#КонецОбласти

#КонецОбласти

//-- НЕ УТКА