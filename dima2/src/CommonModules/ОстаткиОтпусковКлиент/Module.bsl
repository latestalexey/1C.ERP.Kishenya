////////////////////////////////////////////////////////////////////////////////
// ПОДСИСТЕМА ОСТАТКИ ОТПУСКОВ
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Процедура ПриАктивизацииСтрокиЕжегодногоОтпуска(Форма) Экспорт
	
	ОписаниеКоманднойПанели = ОписаниеКоманднойПанелиЕжегодныхОтпусков();
	
	ДанныеТекущейСтроки = Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		Если ДанныеТекущейСтроки <> Неопределено Тогда
			Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить") Или (Не ДанныеТекущейСтроки.ДействующийОтпуск) Тогда
				Форма.Элементы.ЕжегодныеОтпускаВидЕжегодногоОтпуска.ТолькоПросмотр = Ложь;
				
			Иначе
				Форма.Элементы.ЕжегодныеОтпускаВидЕжегодногоОтпуска.ТолькоПросмотр = Истина;
				
			КонецЕсли;
		КонецЕсли;
		
		Если ТипЗнч(ОписаниеКоманднойПанели) = Тип("Структура") Тогда
			УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередНачаломДобавленияЕжегодногоОтпуска(Форма, Отказ) Экспорт
	
	Если Форма.Элементы.Позиции.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриНачалеРедактированияЕжегодногоОтпуска(Форма, НоваяСтрока) Экспорт
	
	Если НоваяСтрока Тогда
		Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные.ИдентификаторСтрокиПозиции = Форма.Элементы.Позиции.ТекущиеДанные.ИдентификаторСтрокиПозиции;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОкончанииРедактированияЕжегодногоОтпуска(НовыйОтпуск, НоваяСтрока) Экспорт
	
	Если НовыйОтпуск = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока Тогда
		НовыйОтпуск.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить");
	КонецЕсли;
	
	ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(НовыйОтпуск);
	
КонецПроцедуры

Процедура ПередУдалениемЕжегодногоОтпуска(Форма, Отказ) Экспорт
	
	ОписаниеКоманднойПанели = ОписаниеКоманднойПанелиЕжегодныхОтпусков();
	
	Отказ = Истина;
	
	СтруктураОпределения = Новый Структура("Объект",Неопределено);
	ЗаполнитьЗначенияСвойств(СтруктураОпределения,Форма);
	Если СтруктураОпределения.Объект = Неопределено Тогда
		ЕжегодныеОтпуска	= Форма.ЕжегодныеОтпуска;
	Иначе	
		ЕжегодныеОтпуска	= Форма.Объект.ЕжегодныеОтпуска;
	КонецЕсли;
	
	ДанныеТекущейСтроки	= Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	
	Если ДанныеТекущейСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Форма.Модифицированность = Истина;
	
	Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить")
		ИЛИ (Не ДанныеТекущейСтроки.ДействующийОтпуск) Тогда
		
		ЕжегодныеОтпуска.Удалить(ЕжегодныеОтпуска.Индекс(ДанныеТекущейСтроки));
		Возврат;
		
	КонецЕсли;
	
	Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить") Тогда
		Если ДанныеТекущейСтроки.ДействующийОтпуск Тогда
			ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.ПустаяСсылка");
		Иначе
			ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Утвердить");
		КонецЕсли;
	Иначе
		ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить");
	КонецЕсли;
	
	Если ТипЗнч(ОписаниеКоманднойПанели) = Тип("Структура") Тогда
		УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки);
	КонецЕсли;
	
	ОстаткиОтпусковКлиентСервер.УстановитьКомментарииДействийСЕжегоднымОтпуском(ДанныеТекущейСтроки);
	
КонецПроцедуры

Процедура ПриИзмененииВидаЕжегодногоОтпуска(Форма) Экспорт
	
	ДанныеСтроки = Форма.Элементы.ЕжегодныеОтпуска.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Или ДанныеСтроки.ВидЕжегодногоОтпуска.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоДнейВГод = ОстаткиОтпусковВызовСервера.КоличествоДнейОтпускаВГодПоУмолчанию(ДанныеСтроки.ВидЕжегодногоОтпуска);
	Если КоличествоДнейВГод <> Неопределено Тогда
		ДанныеСтроки.КоличествоДнейВГод = КоличествоДнейВГод;
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеКоманднойПанелиЕжегодныхОтпусков()
	
	ОписаниеКоманднойПанели = Новый Структура("СтраницыКоманднойПанели,СтраницаДобавитьОтменить,СтраницаДобавитьПродолжить,СтраницаДобавитьУдалить");
	ОписаниеКоманднойПанели.СтраницыКоманднойПанели		= "СтраницыКоманднойПанелиЕО";
	ОписаниеКоманднойПанели.СтраницаДобавитьОтменить	= "СтраницаДобавитьОтменитьЕО";
	ОписаниеКоманднойПанели.СтраницаДобавитьПродолжить	= "СтраницаДобавитьПродолжитьЕО";
	ОписаниеКоманднойПанели.СтраницаДобавитьУдалить		= "СтраницаДобавитьУдалитьЕО";
	
	Возврат ОписаниеКоманднойПанели;
	
КонецФункции

Процедура УстановитьТекущуюСтраницуКоманднойПанели(Форма, ДанныеТекущейСтроки)
	
	ОписаниеКоманднойПанели = ОписаниеКоманднойПанелиЕжегодныхОтпусков();
	
	Если ДанныеТекущейСтроки.Действие = ПредопределенноеЗначение("Перечисление.ДействияСЕжегоднымиОтпусками.Отменить") Тогда
		Форма.Элементы[ОписаниеКоманднойПанели.СтраницыКоманднойПанели].ТекущаяСтраница = Форма.Элементы[ОписаниеКоманднойПанели.СтраницаДобавитьПродолжить];
		
	Иначе
		Если ДанныеТекущейСтроки.ДействующийОтпуск Тогда
			Форма.Элементы[ОписаниеКоманднойПанели.СтраницыКоманднойПанели].ТекущаяСтраница = Форма.Элементы[ОписаниеКоманднойПанели.СтраницаДобавитьОтменить];
			
		Иначе
			Форма.Элементы[ОписаниеКоманднойПанели.СтраницыКоманднойПанели].ТекущаяСтраница = Форма.Элементы[ОписаниеКоманднойПанели.СтраницаДобавитьУдалить];
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСФормамиВводаОстатков

Функция СуществуетГодовойОстаток(ОстаткиОтпусковПоРабочимГодам, ВидЕжегодногоОтпуска) Экспорт

	Существует = Ложь;
	
	Для каждого Остаток Из ОстаткиОтпусковПоРабочимГодам Цикл
		Если Остаток.ВидЕжегодногоОтпуска = ВидЕжегодногоОтпуска Тогда
			Существует = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла; 

	Возврат Существует;
	
КонецФункции

Процедура ОчиститьОстаткиОтпусковПоВидуОтпуска(ОстаткиОтпусковПоРабочимГодам, ВидОтпуска) Экспорт

	МассивУдаляемыхСтрок = ОстаткиОтпусковПоРабочимГодам.НайтиСтроки(Новый Структура("ВидЕжегодногоОтпуска", ВидОтпуска));
	
	Для каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
		ОстаткиОтпусковПоРабочимГодам.Удалить(УдаляемаяСтрока);
	КонецЦикла; 

КонецПроцедуры

#КонецОбласти

#КонецОбласти
