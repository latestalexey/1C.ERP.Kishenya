#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		
		Возврат;

	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	БлокироватьДляИзменения = Истина;

	// Текущее состояние набора помещается во временную таблицу "ДвиженияЗаказыНаПроизводствоСпецификацииПередЗаписью",
	// чтобы при записи получить изменение нового набора относительно текущего.

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Таблица.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|	Таблица.КодСтрокиПродукция    КАК КодСтрокиПродукция,
	|	Таблица.КодСтрокиЭтапыГрафик  КАК КодСтрокиЭтапыГрафик,
	|	Таблица.Этап                  КАК Этап,
	|	Таблица.Подразделение         КАК Подразделение,
	|	Таблица.Номенклатура          КАК Номенклатура,
	|	Таблица.Характеристика        КАК Характеристика,
	|	Таблица.Назначение            КАК Назначение,
	|	Таблица.ТипДвиженияЗапасов    КАК ТипДвиженияЗапасов,
	|	Таблица.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|	Таблица.ВариантОбеспечения    КАК ВариантОбеспечения,
	|	Таблица.ДатаПотребности       КАК ДатаПотребности,
	|	Таблица.Серия                 КАК Серия,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|		Таблица.Заказано
	|	ИНАЧЕ
	|		-Таблица.Заказано
	|	КОНЕЦ                         КАК ЗаказаноПередЗаписью
	|
	|ПОМЕСТИТЬ ДвиженияЗаказыНаПроизводствоСпецификацииПередЗаписью
	|
	|ИЗ
	|	РегистрНакопления.ЗаказыНаПроизводствоСпецификации КАК Таблица
	|
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.ЭтоНовый);
	
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ОбменДанными.Загрузка ИЛИ НЕ ПроведениеСервер.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		
		Возврат;
		
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаИзменений.ЗаказНаПроизводство      КАК ЗаказНаПроизводство,
	|	ТаблицаИзменений.КодСтрокиПродукция       КАК КодСтрокиПродукция,
	|	ТаблицаИзменений.КодСтрокиЭтапыГрафик     КАК КодСтрокиЭтапыГрафик,
	|	ТаблицаИзменений.Этап                     КАК Этап,
	|	ТаблицаИзменений.Подразделение            КАК Подразделение,
	|	ТаблицаИзменений.Номенклатура             КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика           КАК Характеристика,
	|	ТаблицаИзменений.Назначение               КАК Назначение,
	|	ТаблицаИзменений.ТипДвиженияЗапасов       КАК ТипДвиженияЗапасов,
	|	ТаблицаИзменений.ПроизводитсяВПроцессе    КАК ПроизводитсяВПроцессе,
	|	ТаблицаИзменений.ВариантОбеспечения       КАК ВариантОбеспечения,
	|	ТаблицаИзменений.ДатаПотребности          КАК ДатаПотребности,
	|	ТаблицаИзменений.Серия                    КАК Серия,
	|	СУММА(ТаблицаИзменений.ЗаказаноИзменение) КАК ЗаказаноИзменение
	|
	|ПОМЕСТИТЬ ДвиженияЗаказыНаПроизводствоСпецификацииИзменение
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|		Таблица.КодСтрокиПродукция    КАК КодСтрокиПродукция,
	|		Таблица.КодСтрокиЭтапыГрафик  КАК КодСтрокиЭтапыГрафик,
	|		Таблица.Этап                  КАК Этап,
	|		Таблица.Подразделение         КАК Подразделение,
	|		Таблица.Номенклатура          КАК Номенклатура,
	|		Таблица.Характеристика        КАК Характеристика,
	|		Таблица.Назначение            КАК Назначение,
	|		Таблица.ТипДвиженияЗапасов    КАК ТипДвиженияЗапасов,
	|		Таблица.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|		Таблица.ВариантОбеспечения    КАК ВариантОбеспечения,
	|		Таблица.ДатаПотребности       КАК ДатаПотребности,
	|		Таблица.Серия                 КАК Серия,
	|		Таблица.ЗаказаноПередЗаписью  КАК ЗаказаноИзменение
	|	ИЗ
	|		ДвиженияЗаказыНаПроизводствоСпецификацииПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|		Таблица.КодСтрокиПродукция    КАК КодСтрокиПродукция,
	|		Таблица.КодСтрокиЭтапыГрафик  КАК КодСтрокиЭтапыГрафик,
	|		Таблица.Этап                  КАК Этап,
	|		Таблица.Подразделение         КАК Подразделение,
	|		Таблица.Номенклатура          КАК Номенклатура,
	|		Таблица.Характеристика        КАК Характеристика,
	|		Таблица.Назначение            КАК Назначение,
	|		Таблица.ТипДвиженияЗапасов    КАК ТипДвиженияЗапасов,
	|		Таблица.ПроизводитсяВПроцессе КАК ПроизводитсяВПроцессе,
	|		Таблица.ВариантОбеспечения    КАК ВариантОбеспечения,
	|		Таблица.ДатаПотребности       КАК ДатаПотребности,
	|		Таблица.Серия                 КАК Серия,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|			-Таблица.Заказано
	|		ИНАЧЕ
	|			Таблица.Заказано
	|		КОНЕЦ                         КАК ЗаказаноИзменение
	|	ИЗ
	|		РегистрНакопления.ЗаказыНаПроизводствоСпецификации КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ЗаказНаПроизводство,
	|	ТаблицаИзменений.КодСтрокиПродукция,
	|	ТаблицаИзменений.КодСтрокиЭтапыГрафик,
	|	ТаблицаИзменений.Этап,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.ТипДвиженияЗапасов,
	|	ТаблицаИзменений.ПроизводитсяВПроцессе,
	|	ТаблицаИзменений.ВариантОбеспечения,
	|	ТаблицаИзменений.ДатаПОтребности,
	|	ТаблицаИзменений.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.ЗаказаноИзменение) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Подразделение,
	|	КодСтрокиПродукция,
	|	Характеристика,
	|	КодСтрокиЭтапыГрафик,
	|	ТипДвиженияЗапасов,
	|	Номенклатура,
	|	ПроизводитсяВПроцессе,
	|	ВариантОбеспечения,
	|	Серия,
	|	Этап,
	|	ЗаказНаПроизводство
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияЗаказыНаПроизводствоСпецификацииПередЗаписью");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;

	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	Выборка.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияЗаказыНаПроизводствоСпецификацииИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЗаказыНаПроизводствоСпецификацииИзменение", Выборка.Количество > 0);

КонецПроцедуры

#КонецОбласти

#КонецЕсли