#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Функция ПолноеИмяРегистра()
	Возврат "РегистрНакопления.ДенежныеСредстваВПути";
КонецФункции 

Функция ЗаполнитьДеньгиВПутиБезналичныйРасчет_ДанныеДляОбновления(Параметры) Экспорт
    
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таб.Регистратор КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|		ДокументыДвижения.Ссылка КАК Регистратор
	|	ИЗ
	|		Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДокументыДвижения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваВПути КАК ДанныеРегистра
	|		ПО ДокументыДвижения.Ссылка = ДанныеРегистра.Регистратор
	|	ГДЕ
	|		ДокументыДвижения.Проведен
	|		И ДокументыДвижения.ПроведеноБанком
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|		И ДокументыДвижения.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|			)
	|				
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|		ДокументыДвижения.Ссылка
	|	ИЗ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК ДокументыДвижения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваВПути КАК ДанныеРегистра
	|		ПО ДокументыДвижения.Ссылка = ДанныеРегистра.Регистратор
	|	ГДЕ
	|		ДокументыДвижения.Проведен
	|		И ДокументыДвижения.ПроведеноБанком
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|		И ДокументыДвижения.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты),
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет)
	|			)
	|
	|	) КАК Таб
	|
    |";
    
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
    
    ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
        Параметры, 
        Регистраторы, 
        ПолноеИмяРегистра()
    );
	
КонецФункции

// Обработчик обновления BAS УТ 3.2.1
// Формирует движения по ДенежныеСредстваВПути для документов ПоступлениеБезналичныхДенежныхСредств, СписаниеБезналичныхДенежныхСредств
// с операциями КонвертацияВалюты, ПеречислениеДенежныхСредствНаДругойСчет
Процедура ЗаполнитьДеньгиВПутиБезналичныйРасчет(Параметры) Экспорт
    
	ТипыРегистраторов = Новый Массив;
	ТипыРегистраторов.Добавить("Документ.ПоступлениеБезналичныхДенежныхСредств");
	ТипыРегистраторов.Добавить("Документ.СписаниеБезналичныхДенежныхСредств");
    
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		ТипыРегистраторов,
	    ПолноеИмяРегистра(),
		Параметры.Очередь
    );
    
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
    
КонецПроцедуры

Функция ЗаполнитьДеньгиВПутиНаличныйРасчет_ДанныеДляОбновления(Параметры) Экспорт
    
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таб.Регистратор КАК Ссылка
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|		ДокументыДвижения.Ссылка КАК Регистратор
	|	ИЗ
	|		Документ.ПриходныйКассовыйОрдер КАК ДокументыДвижения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваВПути КАК ДанныеРегистра
	|		ПО ДокументыДвижения.Ссылка = ДанныеРегистра.Регистратор
	|	ГДЕ
	|		ДокументыДвижения.Проведен
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|		И ДокументыДвижения.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы)
	|			)
	|				
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ 
	|		ДокументыДвижения.Ссылка
	|	ИЗ
	|		Документ.РасходныйКассовыйОрдер КАК ДокументыДвижения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваВПути КАК ДанныеРегистра
	|		ПО ДокументыДвижения.Ссылка = ДанныеРегистра.Регистратор
	|	ГДЕ
	|		ДокументыДвижения.Проведен
	|		И ДанныеРегистра.Регистратор ЕСТЬ NULL
	|		И ДокументыДвижения.ХозяйственнаяОперация В (
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствВДругуюКассу), 
	|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы)
	|			)
	|
	|	) КАК Таб
	|
    |";
    
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
    
    ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
        Параметры, 
        Регистраторы, 
        ПолноеИмяРегистра()
    );
	
КонецФункции

// Обработчик обновления BAS УТ 3.2.1
// Формирует движения по ДенежныеСредстваВПути для документов ПриходныйКассовыйОрдер, РасходныйКассовыйОрдер
// с операциями КонвертацияВалюты, ВыдачаДенежныхСредствВДругуюКассу, ПоступлениеДенежныхСредствИзДругойКассы
Процедура ЗаполнитьДеньгиВПутиНаличныйРасчет(Параметры) Экспорт
    
	ТипыРегистраторов = Новый Массив;
	ТипыРегистраторов.Добавить("Документ.ПриходныйКассовыйОрдер");
	ТипыРегистраторов.Добавить("Документ.РасходныйКассовыйОрдер");
    
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
		ТипыРегистраторов,
	    ПолноеИмяРегистра(),
		Параметры.Очередь
    );
    
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
    
КонецПроцедуры

Процедура КорректироватьДвижения_ДанныеДляОбновления(Параметры) Экспорт
	
	ДополнительныеПараметрыОтметки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметрыОтметки.ЭтоДвижения = Истина;
	ДополнительныеПараметрыОтметки.ПолноеИмяРегистра = "РегистрНакопления.ДенежныеСредстваВПути";
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДенежныеСредстваВПути.Регистратор КАК Регистратор,
	|	""ПриходныйКассовыйОрдер"" КАК Имя,
	|	ДенежныеСредстваВПути.МоментВремени КАК МоментВремени
	|ИЗ
	|	РегистрНакопления.ДенежныеСредстваВПути КАК ДенежныеСредстваВПути
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(ДенежныеСредстваВПути.Регистратор) = ТИП(Документ.ПриходныйКассовыйОрдер)
	|	И ДенежныеСредстваВПути.ВидПереводаДенежныхСредств В (ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПриобретениеВалюты),
	|														ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.РеализацияВалюты))
	|
	|УПОРЯДОЧИТЬ ПО
	|	МоментВремени УБЫВ
    |";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	ДанныеКОбработке = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
        Параметры, 
        ДанныеКОбработке, 
        ДополнительныеПараметрыОтметки
    );

КонецПроцедуры

// Обработчик обновления BAS УТ 3.2.3
// Формирует движения в регистре "ДеньгиВпути" по хоз. операциям "КонвертацияВалюты"
Процедура КорректироватьДвижения(Параметры) Экспорт
	
	Регистраторы = Новый Массив;
	Регистраторы.Добавить("Документ.ПриходныйКассовыйОрдер");
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
        Регистраторы,
		"РегистрНакопления.ДенежныеСредстваВПути",
		Параметры.Очередь
    );
	
КонецПроцедуры


#КонецОбласти

#КонецОбласти

#КонецЕсли