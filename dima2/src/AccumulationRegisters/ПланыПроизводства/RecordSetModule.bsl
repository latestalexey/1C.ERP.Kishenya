#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если ТипЗнч(Отбор.Регистратор.Значение) <> Тип("ДокументСсылка.ПланПроизводства") Тогда
		Возврат;
	КонецЕсли; 
	
	ПланПроизводства = Отбор.Регистратор.Значение;
	
	Если ДополнительныеСвойства.Свойство("СценарийПланирования") Тогда
		ЗаполнятьСпецификацию = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДополнительныеСвойства.СценарийПланирования,"ИспользоватьДляПланированияМатериалов");
	Иначе
		ЗаполнятьСпецификацию = Ложь
	КонецЕсли; 
	
	Если Не ЗаполнятьСпецификацию ИЛИ ЭтотОбъект.Количество() = 0 Тогда
		РегистрыНакопления.ПланыПроизводства.ОчиститьДвиженияПоМатериалу(ПланПроизводства);
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = Новый МенеджерВременныхТаблиц();
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы;
	Запрос.УстановитьПараметр("Ссылка", ПланПроизводства);
	Запрос.УстановитьПараметр("НовыеДвиженияПланыПроизводства", ЭтотОбъект);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПланыПроизводства.ПланПроизводства,
	|	ПланыПроизводства.Номенклатура,
	|	ПланыПроизводства.Характеристика,
	|	ПланыПроизводства.Спецификация,
	|	ПланыПроизводства.Количество КАК Количество,
	|	ПланыПроизводства.Период,
	|	ПланыПроизводства.Сценарий,
	|	ПланыПроизводства.Подразделение,
	|	ПланыПроизводства.Активность
	|ПОМЕСТИТЬ ДвиженияПланыПроизводства
	|ИЗ
	|	РегистрНакопления.ПланыПроизводства КАК ПланыПроизводства
	|ГДЕ
	|	ПланыПроизводства.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НовыеДвиженияПланыПроизводства.ПланПроизводства,
	|	НовыеДвиженияПланыПроизводства.Номенклатура,
	|	НовыеДвиженияПланыПроизводства.Характеристика,
	|	НовыеДвиженияПланыПроизводства.Спецификация,
	|	НовыеДвиженияПланыПроизводства.Количество,
	|	НовыеДвиженияПланыПроизводства.Период,
	|	НовыеДвиженияПланыПроизводства.Сценарий,
	|	НовыеДвиженияПланыПроизводства.Подразделение,
	|	НовыеДвиженияПланыПроизводства.Активность
	|ПОМЕСТИТЬ НовыеДвиженияПланыПроизводства
	|ИЗ
	|	&НовыеДвиженияПланыПроизводства КАК НовыеДвиженияПланыПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НовыеДвиженияПланыПроизводства.ПланПроизводства,
	|	НовыеДвиженияПланыПроизводства.Номенклатура,
	|	НовыеДвиженияПланыПроизводства.Характеристика,
	|	НовыеДвиженияПланыПроизводства.Спецификация,
	|	НовыеДвиженияПланыПроизводства.Количество,
	|	НовыеДвиженияПланыПроизводства.Период,
	|	НовыеДвиженияПланыПроизводства.Сценарий,
	|	НовыеДвиженияПланыПроизводства.Подразделение,
	|	НовыеДвиженияПланыПроизводства.Активность
	|ИЗ
	|	НовыеДвиженияПланыПроизводства КАК НовыеДвиженияПланыПроизводства
	|ГДЕ
	|	НовыеДвиженияПланыПроизводства.Активность";
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		РегистрыНакопления.ПланыПроизводства.ОчиститьДвиженияПоМатериалу(ПланПроизводства);
	Иначе
		ДополнительныеСвойства.Вставить("СтруктураВременныеТаблицы", СтруктураВременныеТаблицы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка или Не ДополнительныеСвойства.Свойство("СтруктураВременныеТаблицы") или НЕ ДополнительныеСвойства.Свойство("СценарийПланирования") Тогда
		Возврат;
	КонецЕсли;
	
	ПланПроизводства = Отбор.Регистратор.Значение;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.СтруктураВременныеТаблицы;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НовыеДвиженияПланыПроизводства.ПланПроизводства,
	|	НовыеДвиженияПланыПроизводства.Номенклатура,
	|	НовыеДвиженияПланыПроизводства.Характеристика,
	|	НовыеДвиженияПланыПроизводства.Спецификация,
	|	НовыеДвиженияПланыПроизводства.Количество,
	|	НовыеДвиженияПланыПроизводства.Количество КАК КоличествоРазность,
	|	НовыеДвиженияПланыПроизводства.Период,
	|	НовыеДвиженияПланыПроизводства.Сценарий,
	|	НовыеДвиженияПланыПроизводства.Подразделение,
	|	НовыеДвиженияПланыПроизводства.Активность
	|ПОМЕСТИТЬ ОчередьРасчетаПлановПроизводства
	|ИЗ
	|	НовыеДвиженияПланыПроизводства КАК НовыеДвиженияПланыПроизводства
	|ГДЕ
	|	НовыеДвиженияПланыПроизводства.Активность
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияПланыПроизводства.ПланПроизводства,
	|	ДвиженияПланыПроизводства.Номенклатура,
	|	ДвиженияПланыПроизводства.Характеристика,
	|	ДвиженияПланыПроизводства.Спецификация,
	|	0,
	|	-ДвиженияПланыПроизводства.Количество,
	|	ДвиженияПланыПроизводства.Период,
	|	ДвиженияПланыПроизводства.Сценарий,
	|	ДвиженияПланыПроизводства.Подразделение,
	|	ДвиженияПланыПроизводства.Активность
	|ИЗ
	|	ДвиженияПланыПроизводства КАК ДвиженияПланыПроизводства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОчередьРасчетаПлановПроизводства.ПланПроизводства,
	|	ОчередьРасчетаПлановПроизводства.Номенклатура,
	|	ОчередьРасчетаПлановПроизводства.Характеристика,
	|	ОчередьРасчетаПлановПроизводства.Спецификация,
	|	ОчередьРасчетаПлановПроизводства.Период КАК ДатаВыпуска,
	|	ОчередьРасчетаПлановПроизводства.Сценарий,
	|	ОчередьРасчетаПлановПроизводства.Подразделение,
	|	СУММА(ОчередьРасчетаПлановПроизводства.Количество) КАК Количество,
	|	СУММА(ОчередьРасчетаПлановПроизводства.КоличествоРазность) КАК КоличествоРазность,
	|	ОчередьРасчетаПлановПроизводства.Активность КАК Активность
	|ПОМЕСТИТЬ АктивныеИНеАктивные
	|ИЗ
	|	ОчередьРасчетаПлановПроизводства КАК ОчередьРасчетаПлановПроизводства
	|
	|СГРУППИРОВАТЬ ПО
	|	ОчередьРасчетаПлановПроизводства.Спецификация,
	|	ОчередьРасчетаПлановПроизводства.ПланПроизводства,
	|	ОчередьРасчетаПлановПроизводства.Номенклатура,
	|	ОчередьРасчетаПлановПроизводства.Период,
	|	ОчередьРасчетаПлановПроизводства.Сценарий,
	|	ОчередьРасчетаПлановПроизводства.Подразделение,
	|	ОчередьРасчетаПлановПроизводства.Характеристика,
	|	ОчередьРасчетаПлановПроизводства.Активность
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОчередьРасчетаПлановПроизводства.КоличествоРазность) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктивныеИНеАктивные.ПланПроизводства,
	|	АктивныеИНеАктивные.Номенклатура,
	|	АктивныеИНеАктивные.Характеристика,
	|	АктивныеИНеАктивные.Спецификация,
	|	АктивныеИНеАктивные.ДатаВыпуска,
	|	АктивныеИНеАктивные.Сценарий,
	|	АктивныеИНеАктивные.Подразделение,
	|	АктивныеИНеАктивные.Количество,
	|	АктивныеИНеАктивные.КоличествоРазность,
	|	АктивныеИНеАктивные.Активность
	|ИЗ
	|	АктивныеИНеАктивные КАК АктивныеИНеАктивные
	|ГДЕ
	|	АктивныеИНеАктивные.Активность";
	
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	
	ДополнительныеСвойства.СтруктураВременныеТаблицы.Закрыть();
	
	Разделитель = 1;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ПланПроизводства);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЕСТЬNULL(МАКСИМУМ(ОчередьРасчетаПлановПроизводства.Разделитель), 0) КАК Разделитель
	|ИЗ
	|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПлановПроизводства
	|ГДЕ
	|	ОчередьРасчетаПлановПроизводства.ПланПроизводства = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Разделитель = ВыборкаДетальныеЗаписи.Разделитель + 1;
	КонецЕсли;
	
	ТаблицаДвижений.Колонки.Добавить("Разделитель");
	ТаблицаДвижений.ЗаполнитьЗначения(Разделитель, "Разделитель");
	
	НаборЗаписей = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ПланПроизводства.Установить(ПланПроизводства);
	НаборЗаписей.Отбор.Разделитель.Установить(Разделитель);
	НаборЗаписей.Загрузить(ТаблицаДвижений);
	
	Попытка
		НаборЗаписей.Записать();
	Исключение
		
		ТекстСообщения = НСтр("ru='Не удалось выполнить проведение документа: %Ссылка% по причине: %Причина%';uk='Не вдалося виконати проведення документа: %Ссылка% по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ПланПроизводства);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПланПроизводства, ПланПроизводства, ТекстСообщения);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	Если НаборЗаписей.Количество() > 0 Тогда
		Планирование.ЗапускВыполненияФоновогоПроведения(ПланПроизводства);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли