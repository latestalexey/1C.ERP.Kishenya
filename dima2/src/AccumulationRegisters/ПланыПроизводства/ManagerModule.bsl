#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ВыполнитьПроведение(Параметры) Экспорт
	
	Если Параметры.Свойство("КоличествоОшибок") Тогда
		
		МаксимальноеКоличествоОшибок = 100;
		Если Параметры.Свойство("МаксимальноеКоличествоОшибок") Тогда
			МаксимальноеКоличествоОшибок = Параметры.МаксимальноеКоличествоОшибок;
		КонецЕсли;
		
		Если Параметры.КоличествоОшибок >= МаксимальноеКоличествоОшибок Тогда
			
			ТекстСообщения = НСтр("ru='Проведение документа: %Ссылка% остановлено. Количество неудачных попыток: %КоличествоОшибок%';uk='Проведення документа: %Ссылка% зупинено. Кількість невдалих спроб: %КоличествоОшибок%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Параметры.ДокументСсылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоОшибок%", Параметры.КоличествоОшибок);
			
			ЗаписьЖурналаРегистрации(НСтр("ru='Фоновое проведение.План производства';uk='Фонове проведення.План виробництва'"), УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.ПланПроизводства, Параметры.ДокументСсылка, ТекстСообщения);
			
			Возврат;
		КонецЕсли;
	Иначе
		Параметры.Вставить("КоличествоОшибок", 0);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПараметрыПлана = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ДокументСсылка, "Периодичность, Сценарий");
	Параметры.Вставить("Периодичность", ПараметрыПлана.Периодичность);
	Параметры.Вставить("Сценарий",      ПараметрыПлана.Сценарий);
	
	Попытка
		
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		
			СтруктураВременныеТаблицы = Новый МенеджерВременныхТаблиц();
			Параметры.Вставить("СтруктураВременныеТаблицы",СтруктураВременныеТаблицы);
			
			ПолучитьОчередтьРасчета(Параметры);
		
		КонецЕсли; 
		
		НачатьТранзакцию();
		
		Блокировка = Новый БлокировкаДанных();
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПланыПотребленияМатериалов.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Параметры.ДокументСсылка);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПланыЗанятостиТрудовыхРесурсов.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Параметры.ДокументСсылка);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		//++ НЕ УТКА
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПланыЗанятостиВидовРабочихЦентров.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Параметры.ДокументСсылка);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		//-- НЕ УТКА
		
		Если НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	РегистрСведений.ПланПроизводства КАК ПланПроизводства,
			|	РегистрСведений.Разделитель КАК Разделитель
			|ИЗ
			|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК РегистрСведений
			|ГДЕ
			|	РегистрСведений.ПланПроизводства = &ПланПроизводства";
			
			Запрос.УстановитьПараметр("ПланПроизводства", Параметры.ДокументСсылка);
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьРасчетаПланаПроизводства");
			
			ЭлементБлокировки.ИсточникДанных = Запрос.Выполнить();
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ПланПроизводства", "ПланПроизводства");
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Разделитель", "Разделитель");
			
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			СтруктураВременныеТаблицы = Новый МенеджерВременныхТаблиц();
			Параметры.Вставить("СтруктураВременныеТаблицы",СтруктураВременныеТаблицы);
			
			ПолучитьОчередтьРасчета(Параметры);
			
		Иначе
			
			Блокировка.Заблокировать();
			
		КонецЕсли;
		
		ПолучитьПродукциюТребующуюРазузлованиеПересчетУдаление(Параметры);
		
		ВыполнитьРазузлование(Параметры);
		
		ТаблицыДвижений = СформироватьТаблицыДвижений(Параметры);
		
		Параметры.СтруктураВременныеТаблицы.Закрыть();
		
		НаборЗаписей = РегистрыНакопления.ПланыПотребленияМатериалов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Параметры.ДокументСсылка);
		НаборЗаписей.Загрузить(ТаблицыДвижений.Материалы);
		
		НаборЗаписей.Записать();
		
		НаборЗаписей = РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Параметры.ДокументСсылка);
		НаборЗаписей.Загрузить(ТаблицыДвижений.Трудозатраты);
		
		НаборЗаписей.Записать();
		
		//++ НЕ УТКА
		НаборЗаписей = РегистрыНакопления.ПланыЗанятостиВидовРабочихЦентров.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Регистратор.Установить(Параметры.ДокументСсылка);
		НаборЗаписей.Загрузить(ТаблицыДвижений.ЗанятостьВидовРЦ);
		
		НаборЗаписей.Записать();
		//-- НЕ УТКА
		
		УдалитьДвиженияИзОчереди(ТаблицыДвижений.ОтработаннаяОчередь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Параметры.КоличествоОшибок = Параметры.КоличествоОшибок + 1;
		
		ТекстСообщения = НСтр("ru='Не удалось выполнить проведение документа: %Ссылка% количество попыток: %КоличествоОшибок% по причине: %Причина%';uk='Не вдалося виконати проведення документа: %Ссылка% кількість спроб: %КоличествоОшибок% по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Параметры.ДокументСсылка);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КоличествоОшибок%", Параметры.КоличествоОшибок);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Фоновое проведение.План производства';uk='Фонове проведення.План виробництва'"), УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПланПроизводства, Параметры.ДокументСсылка, ТекстСообщения);
		
	КонецПопытки;
	
	ПроверитьНаличиеЗаписейВОчереди(Параметры);
	
КонецПроцедуры

Процедура ОчиститьДвиженияПоМатериалу(ДокументСсылка) Экспорт

	НаборЗаписейОчередь = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
	НаборЗаписейОчередь.Отбор.ПланПроизводства.Установить(ДокументСсылка);
	
	НаборЗаписейМатериалы = РегистрыНакопления.ПланыПотребленияМатериалов.СоздатьНаборЗаписей();
	НаборЗаписейМатериалы.Отбор.Регистратор.Установить(ДокументСсылка);
	
	НаборЗаписейТрудозатраты = РегистрыНакопления.ПланыЗанятостиТрудовыхРесурсов.СоздатьНаборЗаписей();
	НаборЗаписейТрудозатраты.Отбор.Регистратор.Установить(ДокументСсылка);
	
	//++ НЕ УТКА
	НаборЗаписейЗанятостьВидовРЦ = РегистрыНакопления.ПланыЗанятостиВидовРабочихЦентров.СоздатьНаборЗаписей();
	НаборЗаписейЗанятостьВидовРЦ.Отбор.Регистратор.Установить(ДокументСсылка);
	//-- НЕ УТКА
	
	Попытка
		НачатьТранзакцию();
		
		НаборЗаписейОчередь.Записать();
		
		НаборЗаписейМатериалы.Записать();
		НаборЗаписейТрудозатраты.Записать();
		//++ НЕ УТКА
		НаборЗаписейЗанятостьВидовРЦ.Записать();
		//-- НЕ УТКА
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = НСтр("ru='Не удалось выполнить очистку регистра очереди по документу: %Ссылка% по причине: %Причина%';uk='Не вдалося виконати очищення регістра черзі по документу: %Ссылка% по причині: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", ДокументСсылка);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Фоновое проведение.План производства';uk='Фонове проведення.План виробництва'"), УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.ПланПроизводства, ДокументСсылка, ТекстСообщения);
		
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область ФоновоеПроведение

Процедура ПолучитьОчередтьРасчета(Параметры)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Параметры.СтруктураВременныеТаблицы;
	Запрос.УстановитьПараметр("ПланПроизводства", Параметры.ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОчередьРасчетаПлановПроизводства.ПланПроизводства,
	|	ОчередьРасчетаПлановПроизводства.Номенклатура КАК Номенклатура,
	|	ОчередьРасчетаПлановПроизводства.Характеристика КАК Характеристика,
	|	ОчередьРасчетаПлановПроизводства.Спецификация КАК Спецификация,
	|	ОчередьРасчетаПлановПроизводства.Разделитель КАК Разделитель,
	|	ОчередьРасчетаПлановПроизводства.ДатаВыпуска КАК ДатаВыпуска,
	|	ОчередьРасчетаПлановПроизводства.Подразделение КАК ПодразделениеДиспетчер,
	|	ОчередьРасчетаПлановПроизводства.Сценарий,
	|	ОчередьРасчетаПлановПроизводства.Количество
	|ПОМЕСТИТЬ Очередь
	|ИЗ
	|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПлановПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ОчередьРасчетаПлановПроизводства.ПланПроизводства КАК ПланПроизводства,
	|			МАКСИМУМ(ОчередьРасчетаПлановПроизводства.Разделитель) КАК Разделитель,
	|			ОчередьРасчетаПлановПроизводства.Номенклатура КАК Номенклатура,
	|			ОчередьРасчетаПлановПроизводства.Характеристика КАК Характеристика,
	|			ОчередьРасчетаПлановПроизводства.Спецификация КАК Спецификация,
	|			ОчередьРасчетаПлановПроизводства.ДатаВыпуска КАК ДатаВыпуска,
	|			ОчередьРасчетаПлановПроизводства.Подразделение КАК Подразделение
	|		ИЗ
	|			РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПлановПроизводства
	|		ГДЕ
	|			ОчередьРасчетаПлановПроизводства.ПланПроизводства = &ПланПроизводства
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ОчередьРасчетаПлановПроизводства.ПланПроизводства,
	|			ОчередьРасчетаПлановПроизводства.Номенклатура,
	|			ОчередьРасчетаПлановПроизводства.Характеристика,
	|			ОчередьРасчетаПлановПроизводства.Спецификация,
	|			ОчередьРасчетаПлановПроизводства.Подразделение,
	|			ОчередьРасчетаПлановПроизводства.ДатаВыпуска) КАК ЗапросРазделителя
	|		ПО ОчередьРасчетаПлановПроизводства.ПланПроизводства = ЗапросРазделителя.ПланПроизводства
	|			И ОчередьРасчетаПлановПроизводства.Разделитель = ЗапросРазделителя.Разделитель
	|			И ОчередьРасчетаПлановПроизводства.Номенклатура = ЗапросРазделителя.Номенклатура
	|			И ОчередьРасчетаПлановПроизводства.Характеристика = ЗапросРазделителя.Характеристика
	|			И ОчередьРасчетаПлановПроизводства.Спецификация = ЗапросРазделителя.Спецификация
	|			И ОчередьРасчетаПлановПроизводства.ДатаВыпуска = ЗапросРазделителя.ДатаВыпуска
	|			И ОчередьРасчетаПлановПроизводства.Подразделение = ЗапросРазделителя.Подразделение
	|ГДЕ
	|	ОчередьРасчетаПлановПроизводства.ПланПроизводства = &ПланПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Спецификация,
	|	ДатаВыпуска,
	|	ПодразделениеДиспетчер";
	
	Запрос.Выполнить();

КонецПроцедуры
 
Процедура ПолучитьПродукциюТребующуюРазузлованиеПересчетУдаление(Параметры)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Параметры.СтруктураВременныеТаблицы;
	Запрос.УстановитьПараметр("ПланПроизводства", Параметры.ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПланыПотребленияМатериаловПродукция.НоменклатураПродукции КАК Номенклатура,
	|	ПланыПотребленияМатериаловПродукция.ХарактеристикаПродукции КАК Характеристика,
	|	ПланыПотребленияМатериаловПродукция.Спецификация КАК Спецификация,
	|	ПланыПотребленияМатериаловПродукция.ДатаВыпуска КАК ДатаВыпуска,
	|	ПланыПотребленияМатериаловПродукция.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер
	|ПОМЕСТИТЬ РазузлованнаяПродукция
	|ИЗ
	|	РегистрНакопления.ПланыПотребленияМатериалов КАК ПланыПотребленияМатериаловПродукция
	|ГДЕ
	|	ПланыПотребленияМатериаловПродукция.Регистратор = &ПланПроизводства
	|
	|СГРУППИРОВАТЬ ПО
	|	ПланыПотребленияМатериаловПродукция.НоменклатураПродукции,
	|	ПланыПотребленияМатериаловПродукция.ХарактеристикаПродукции,
	|	ПланыПотребленияМатериаловПродукция.Спецификация,
	|	ПланыПотребленияМатериаловПродукция.ДатаВыпуска,
	|	ПланыПотребленияМатериаловПродукция.ПодразделениеДиспетчер
	|
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ПланыПотребленияМатериаловПродукция.СодержитРасчетноеКоличество) = ЛОЖЬ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Спецификация,
	|	ДатаВыпуска,
	|	ПодразделениеДиспетчер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Очередь.ПланПроизводства,
	|	Очередь.Номенклатура,
	|	Очередь.Характеристика,
	|	Очередь.Спецификация,
	|	Очередь.Сценарий,
	|	Очередь.Разделитель,
	|	Очередь.Количество,
	|	Очередь.ПодразделениеДиспетчер,
	|	Очередь.ДатаВыпуска
	|ПОМЕСТИТЬ ПродукцияРазузловка
	|ИЗ
	|	Очередь КАК Очередь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазузлованнаяПродукция КАК РазузлованнаяПродукция
	|		ПО Очередь.Номенклатура = РазузлованнаяПродукция.Номенклатура
	|			И Очередь.Характеристика = РазузлованнаяПродукция.Характеристика
	|			И Очередь.Спецификация = РазузлованнаяПродукция.Спецификация
	|			И Очередь.ДатаВыпуска = РазузлованнаяПродукция.ДатаВыпуска
	|			И Очередь.ПодразделениеДиспетчер = РазузлованнаяПродукция.ПодразделениеДиспетчер
	|ГДЕ
	|	РазузлованнаяПродукция.Номенклатура ЕСТЬ NULL 
	|	И Очередь.Количество <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Очередь.ПланПроизводства,
	|	Очередь.Номенклатура,
	|	Очередь.Характеристика,
	|	Очередь.Спецификация,
	|	Очередь.Сценарий,
	|	Очередь.Разделитель,
	|	Очередь.Количество,
	|	Очередь.ПодразделениеДиспетчер,
	|	Очередь.ДатаВыпуска
	|ПОМЕСТИТЬ ПродукцияПересчет
	|ИЗ
	|	Очередь КАК Очередь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РазузлованнаяПродукция КАК РазузлованнаяПродукция
	|		ПО Очередь.Номенклатура = РазузлованнаяПродукция.Номенклатура
	|			И Очередь.Характеристика = РазузлованнаяПродукция.Характеристика
	|			И Очередь.Спецификация = РазузлованнаяПродукция.Спецификация
	|			И Очередь.ДатаВыпуска = РазузлованнаяПродукция.ДатаВыпуска
	|			И Очередь.ПодразделениеДиспетчер = РазузлованнаяПродукция.ПодразделениеДиспетчер
	|ГДЕ
	|	Очередь.Количество <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РазузлованнаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Очередь.ПланПроизводства,
	|	Очередь.Номенклатура,
	|	Очередь.Характеристика,
	|	Очередь.Спецификация,
	|	Очередь.Сценарий,
	|	Очередь.Разделитель,
	|	Очередь.ПодразделениеДиспетчер,
	|	Очередь.ДатаВыпуска
	|ПОМЕСТИТЬ ПродукцияУдалить
	|ИЗ
	|	Очередь КАК Очередь
	|ГДЕ
	|	Очередь.Количество = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Очередь";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
КонецПроцедуры

Процедура ВыполнитьРазузлование(Параметры)

	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Параметры.СтруктураВременныеТаблицы;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПродукцияРазузловка.ПланПроизводства,
	|	ПродукцияРазузловка.Номенклатура,
	|	ПродукцияРазузловка.Характеристика,
	|	ПродукцияРазузловка.Спецификация,
	|	ПродукцияРазузловка.Сценарий,
	|	ВЫРАЗИТЬ(ПродукцияРазузловка.Количество КАК ЧИСЛО(15, 6)) КАК Количество,
	|	ПродукцияРазузловка.Количество КАК КоличествоИзделий,
	|	ПродукцияРазузловка.ПодразделениеДиспетчер,
	|	ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.ВидыРаботСотрудников.ПустаяСсылка) КАК ВидРабот,
	//++ НЕ УТКА
	|	ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка) КАК ВидРабочегоЦентра,
	//-- НЕ УТКА
	|	ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка) КАК ПодразделениеЭтапа,
	|	ПродукцияРазузловка.ДатаВыпуска,
	|	ПродукцияРазузловка.Номенклатура КАК НоменклатураПродукции,
	|	ПродукцияРазузловка.Характеристика КАК ХарактеристикаПродукции,
	|	0 КАК КоличествоИзделие,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(15, 6)) КАК КоличествоНаЕдиницуПродукции,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(10, 0)) КАК ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий,
	|	ЛОЖЬ КАК СодержитРасчетноеКоличество,
	|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(10, 0)) КАК ДнейДоОкончания
	|ИЗ
	|	ПродукцияРазузловка КАК ПродукцияРазузловка";
	
	ТаблицаПродукцииНаРазузлование = Запрос.Выполнить().Выгрузить();
	
	ОбщиеКолонки = "ПланПроизводства, Спецификация, Сценарий, Количество, КоличествоИзделий, ПодразделениеДиспетчер, 
		|ПодразделениеЭтапа, ДатаВыпуска, НоменклатураПродукции, ХарактеристикаПродукции, КоличествоИзделие, 
		|КоличествоНаЕдиницуПродукции, ДнейДоОкончания";
	
	ТаблицаМатериалов = ТаблицаПродукцииНаРазузлование.СкопироватьКолонки(ОбщиеКолонки + ", Номенклатура, Характеристика,
		|Склад, СодержитРасчетноеКоличество");
	ТаблицаТрудозатрат = ТаблицаПродукцииНаРазузлование.СкопироватьКолонки(ОбщиеКолонки + ", ВидРабот");
	//++ НЕ УТКА
	ТаблицаЗанятостиВидовРЦ = ТаблицаПродукцииНаРазузлование.СкопироватьКолонки(ОбщиеКолонки + ", ВидРабочегоЦентра,
		|ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий");
	//-- НЕ УТКА
	
	ПереченьДанных = Новый Массив;
	ПереченьДанных.Добавить("ВыходныеИзделия");
	ПереченьДанных.Добавить("МатериалыИУслуги");
	ПереченьДанных.Добавить("Трудозатраты");
	//++ НЕ УТКА
	ПереченьДанных.Добавить("ВидыРабочихЦентров");
	//-- НЕ УТКА
	
	КэшированныеСпецификации = Неопределено;
	
	Для каждого ЭлементПродукция Из ТаблицаПродукцииНаРазузлование Цикл
		
		ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(
			ЭлементПродукция.Спецификация,
			ЭлементПродукция.Номенклатура,
			ЭлементПродукция.Характеристика);
		ДанныеСпецификации = Справочники.РесурсныеСпецификации.ДанныеСпецификацииСПолуфабрикатами(
			ДанныеПоНоменклатуре, 
			Истина,
			,
			ПереченьДанных, 
			КэшированныеСпецификации);
		
		ДанныеОПродукции = Неопределено;
		ПродукцияСтроки = ДанныеСпецификации.ВыходныеИзделия.НайтиСтроки(
			Новый Структура("Номенклатура, Характеристика", ЭлементПродукция.Номенклатура, ЭлементПродукция.Характеристика));
		Если ПродукцияСтроки.Количество() > 0 Тогда
			ДанныеОПродукции = ПродукцияСтроки[0];
		Иначе
			ПродукцияСтроки = ДанныеСпецификации.ВыходныеИзделия.НайтиСтроки(
				Новый Структура("Номенклатура, Характеристика", ЭлементПродукция.Номенклатура, Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка()));
			Если ПродукцияСтроки.Количество() > 0 Тогда
				ДанныеОПродукции = ПродукцияСтроки[0];
			КонецЕсли;
		КонецЕсли;
		
		// Данные по материалам
		Для каждого ЭлементМатериал Из ДанныеСпецификации.МатериалыИУслуги Цикл
		
			НоваяСтрока = ТаблицаМатериалов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементМатериал,	"Номенклатура,Характеристика,ПодразделениеЭтапа,Количество,
				|ДнейДоОкончания");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементПродукция, "НоменклатураПродукции,ХарактеристикаПродукции,ПодразделениеДиспетчер,
				|Сценарий,Спецификация,ПланПроизводства,ДатаВыпуска,КоличествоИзделий");
			
			Если ДанныеОПродукции <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеОПродукции,	"КоличествоИзделие");
			КонецЕсли; 
			
			РассчитататьКоличество(НоваяСтрока);
			НоваяСтрока.СодержитРасчетноеКоличество = ЗначениеЗаполнено(ЭлементМатериал.АлгоритмРасчетаКоличества);
		КонецЦикла;
		
		// Данные по трудозатратам
		Для каждого ЭлементТрудозатрата Из ДанныеСпецификации.Трудозатраты Цикл
			
			Количество        = ЭлементТрудозатрата.Количество;
			ДнейДоОкончания   = ЭлементТрудозатрата.ДнейДоОкончания;
			ДлительностьЭтапа = ЭлементТрудозатрата.ДлительностьЭтапа;
			
			Пока Количество > 0 Цикл
				
				НоваяСтрока = ТаблицаТрудозатрат.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементТрудозатрата, "ВидРабот, ПодразделениеЭтапа");
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементПродукция, "НоменклатураПродукции,ХарактеристикаПродукции,
					|ПодразделениеДиспетчер, Сценарий,Спецификация,ПланПроизводства,ДатаВыпуска,КоличествоИзделий");
				
				Если ДанныеОПродукции <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеОПродукции,	"КоличествоИзделие");
				КонецЕсли; 
				
				Если ДлительностьЭтапа > 0 И ЭлементТрудозатрата.ДлительностьЭтапа < ЭлементТрудозатрата.ДнейДоОкончания Тогда
					
					Коэффициент = 1;
					НоваяСтрока.ДнейДоОкончания = ДнейДоОкончания;
					
					ДатаПотребности = НачалоДня(НоваяСтрока.ДатаВыпуска) - 86400 * ДнейДоОкончания;
					ДатаОкончанияЭтапа = ДатаПотребности + 86400 * ДлительностьЭтапа-1;
					ДатаОкончанияПериода = ПланированиеКлиентСерверПовтИсп.РассчитатьДатуОкончанияПериода(
						ДатаПотребности, 
						Параметры.Периодичность);
					Если ДатаОкончанияЭтапа > ДатаОкончанияПериода Тогда
					
						НоваяСтрока.ДнейДоОкончания = ДнейДоОкончания;
						ДнейЭтапаВПериоде = Цел((ДатаОкончанияПериода+1 - ДатаПотребности)/86400);
						Коэффициент = ДнейЭтапаВПериоде / ДлительностьЭтапа;
						ДлительностьЭтапа = ДлительностьЭтапа - ДнейЭтапаВПериоде;
						ДнейДоОкончания = ДнейДоОкончания - ДнейЭтапаВПериоде;
					КонецЕсли; 
					
					 // Целое количество секунд занятости вида РЦ.
					НоваяСтрока.Количество = Окр(Коэффициент * Количество, 3, РежимОкругления.Окр15как10);
				Иначе
				
					НоваяСтрока.ДнейДоОкончания = ДнейДоОкончания;
					НоваяСтрока.Количество = Количество;
				КонецЕсли; 
				
				Количество = Количество - НоваяСтрока.Количество;
				
				РассчитататьКоличество(НоваяСтрока);
			
			КонецЦикла;
			
		КонецЦикла;
		
		//++ НЕ УТКА
		// Данные по занятости видов РЦ
		Для каждого ЭлементЗанятостьВидаРЦ Из ДанныеСпецификации.ВидыРабочихЦентров Цикл
			
			Занятость         = ЭлементЗанятостьВидаРЦ.Занятость;
			ДнейДоОкончания   = ЭлементЗанятостьВидаРЦ.ДнейДоОкончания;
			ДлительностьЭтапа = ЭлементЗанятостьВидаРЦ.ДлительностьЭтапа;
			
			Пока Занятость > 0 Цикл
				
				НоваяСтрока = ТаблицаЗанятостиВидовРЦ.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементЗанятостьВидаРЦ, "ВидРабочегоЦентра, ПодразделениеЭтапа, 
					|ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий");
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементПродукция, "НоменклатураПродукции,ХарактеристикаПродукции,
					|ПодразделениеДиспетчер, Сценарий,Спецификация,ПланПроизводства,ДатаВыпуска,КоличествоИзделий");
				
				Если ДанныеОПродукции <> Неопределено Тогда
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеОПродукции,	"КоличествоИзделие");
				КонецЕсли; 
				
				Если ДлительностьЭтапа > 0 И ЭлементЗанятостьВидаРЦ.ДлительностьЭтапа < ЭлементЗанятостьВидаРЦ.ДнейДоОкончания Тогда
					
					Коэффициент = 1;
					НоваяСтрока.ДнейДоОкончания = ДнейДоОкончания;
					
					ДатаПотребности = НачалоДня(НоваяСтрока.ДатаВыпуска) - 86400 * ДнейДоОкончания;
					ДатаОкончанияЭтапа = ДатаПотребности + 86400 * ДлительностьЭтапа-1;
					ДатаОкончанияПериода = ПланированиеКлиентСерверПовтИсп.РассчитатьДатуОкончанияПериода(
						ДатаПотребности, 
						Параметры.Периодичность);
					Если ДатаОкончанияЭтапа > ДатаОкончанияПериода Тогда
					
						НоваяСтрока.ДнейДоОкончания = ДнейДоОкончания;
						ДнейЭтапаВПериоде = Цел((ДатаОкончанияПериода+1 - ДатаПотребности)/86400);
						Коэффициент = ДнейЭтапаВПериоде / ДлительностьЭтапа;
						ДлительностьЭтапа = ДлительностьЭтапа - ДнейЭтапаВПериоде;
						ДнейДоОкончания = ДнейДоОкончания - ДнейЭтапаВПериоде;
					КонецЕсли; 
					
					НоваяСтрока.Количество = Цел(Коэффициент * Занятость); // Целое количество секунд занятости вида РЦ.
				Иначе
				
					НоваяСтрока.ДнейДоОкончания = ДнейДоОкончания;
					НоваяСтрока.Количество = Занятость;
				КонецЕсли; 
				
				Занятость = Занятость - НоваяСтрока.Количество;
				
				РассчитататьКоличество(НоваяСтрока);
				
			КонецЦикла;
		КонецЦикла;
		//-- НЕ УТКА
		
	КонецЦикла;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Материалы", ТаблицаМатериалов);
	Запрос.УстановитьПараметр("Трудозатраты", ТаблицаТрудозатрат);
	//++ НЕ УТКА
	Запрос.УстановитьПараметр("ЗанятостьВидовРЦ", ТаблицаЗанятостиВидовРЦ);
	//-- НЕ УТКА
	Запрос.УстановитьПараметр("Календарь", Константы.ОсновнойКалендарьПредприятия.Получить());
	Запрос.МенеджерВременныхТаблиц = Параметры.СтруктураВременныеТаблицы;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Материалы.Номенклатура,
	|	Материалы.Характеристика,
	|	Материалы.НоменклатураПродукции,
	|	Материалы.ХарактеристикаПродукции,
	|	Материалы.ПодразделениеЭтапа,
	|	Материалы.ПодразделениеДиспетчер,
	|	Материалы.Сценарий,
	|	Материалы.Спецификация,
	|	Материалы.СодержитРасчетноеКоличество,
	|	Материалы.ДатаВыпуска,
	|	Материалы.Количество КАК КоличествоНаЕдиницуПродукции,
	|	Материалы.КоличествоИзделий КАК Количество,
	|	Материалы.ДнейДоОкончания,
	|	Материалы.ПланПроизводства
	|ПОМЕСТИТЬ Материалы
	|ИЗ
	|	&Материалы КАК Материалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КалендарныеГрафики.Календарь,
	|	КалендарныеГрафики.Год,
	|	МАКСИМУМ(КалендарныеГрафики.КоличествоДнейВГрафикеСНачалаГода) КАК КоличествоДней
	|ПОМЕСТИТЬ КоличествоРабочихДнейВГоду
	|ИЗ
	|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
	|ГДЕ
	|	КалендарныеГрафики.Календарь = &Календарь
	|
	|СГРУППИРОВАТЬ ПО
	|	КалендарныеГрафики.Календарь,
	|	КалендарныеГрафики.Год
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Материалы.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ИЛИ СпрНоменклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа), ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		КОГДА ЕСТЬNULL(НастройкаПередачиХарактеристика.ОснованиеДляПолучения, ЕСТЬNULL(НастройкаПередачиНоменклатура.ОснованиеДляПолучения, ЕСТЬNULL(НастройкаПередачиСклад.ОснованиеДляПолучения, ЗНАЧЕНИЕ(Перечисление.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаПроизводство)))) = ЗНАЧЕНИЕ(Перечисление.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаПроизводство)
	|			ТОГДА ЕСТЬNULL(НастройкаПередачиХарактеристика.Склад, ЕСТЬNULL(НастройкаПередачиНоменклатура.Склад, ЕСТЬNULL(НастройкаПередачиСклад.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))))
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	Материалы.Номенклатура,
	|	Материалы.Характеристика,
	|	Материалы.НоменклатураПродукции,
	|	Материалы.ХарактеристикаПродукции,
	|	Материалы.ПодразделениеЭтапа,
	|	Материалы.ПодразделениеДиспетчер,
	|	Материалы.Сценарий,
	|	Материалы.Спецификация,
	|	Материалы.СодержитРасчетноеКоличество,
	|	Материалы.ДатаВыпуска КАК ДатаВыпуска,
	|	Материалы.ПланПроизводства,
	|	ВЫРАЗИТЬ(Материалы.КоличествоНаЕдиницуПродукции * Материалы.Количество КАК ЧИСЛО(15, 3)) КАК Количество,
	|	Материалы.КоличествоНаЕдиницуПродукции,
	|	ВЫБОР
	|		КОГДА КалендарьПотребности.Календарь ЕСТЬ NULL 
	|			ТОГДА ДОБАВИТЬКДАТЕ(Материалы.ДатаВыпуска, ДЕНЬ, -Материалы.ДнейДоОкончания)
	|		ИНАЧЕ КалендарьПотребности.ДатаГрафика
	|	КОНЕЦ КАК Период
	|ПОМЕСТИТЬ МатериалыРазузлованные
	|ИЗ
	|	Материалы КАК Материалы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиХарактеристика
	|		ПО Материалы.ПодразделениеЭтапа = НастройкаПередачиХарактеристика.Подразделение
	|			И Материалы.Номенклатура = НастройкаПередачиХарактеристика.Номенклатура
	|			И Материалы.Характеристика = НастройкаПередачиХарактеристика.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиНоменклатура
	|		ПО Материалы.ПодразделениеЭтапа = НастройкаПередачиНоменклатура.Подразделение
	|			И Материалы.Номенклатура = НастройкаПередачиНоменклатура.Номенклатура
	|			И (НастройкаПередачиНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиСклад
	|		ПО Материалы.ПодразделениеЭтапа = НастройкаПередачиСклад.Подразделение
	|			И (НастройкаПередачиСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (НастройкаПередачиСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (НастройкаПередачиХарактеристика.Подразделение ЕСТЬ NULL )
	|			И (НастройкаПередачиНоменклатура.Подразделение ЕСТЬ NULL )
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО Материалы.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ПО Материалы.Спецификация = ЭтапыПроизводства.Владелец
	|			И (ЭтапыПроизводства.НомерЭтапа = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК КалендарьДатаВыпуска
	|		ПО КалендарьДатаВыпуска.Календарь = &Календарь
	|			И Материалы.ДатаВыпуска = КалендарьДатаВыпуска.ДатаГрафика
	|			И (ГОД(Материалы.ДатаВыпуска) = КалендарьДатаВыпуска.Год)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоРабочихДнейВГоду КАК ПрошлыйГод
	|		ПО ПрошлыйГод.Календарь = &Календарь
	|			И (ГОД(Материалы.ДатаВыпуска) - 1 = ПрошлыйГод.Год)
	|			И (ВЫБОР
	|				КОГДА Материалы.ДнейДоОкончания > ЕСТЬNULL(КалендарьДатаВыпуска.КоличествоДнейВГрафикеСНачалаГода, 0)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КалендарныеГрафики КАК КалендарьПотребности
	|		ПО КалендарьПотребности.Календарь = &Календарь
	|			И (КалендарьПотребности.ДеньВключенВГрафик)
	|			И (ВЫБОР
	|				КОГДА Материалы.ДнейДоОкончания > ЕСТЬNULL(КалендарьДатаВыпуска.КоличествоДнейВГрафикеСНачалаГода, 0)
	|					ТОГДА ЕСТЬNULL(ПрошлыйГод.КоличествоДней, 0) + ЕСТЬNULL(КалендарьДатаВыпуска.КоличествоДнейВГрафикеСНачалаГода, 0) - Материалы.ДнейДоОкончания = КалендарьПотребности.КоличествоДнейВГрафикеСНачалаГода
	|				ИНАЧЕ ЕСТЬNULL(КалендарьДатаВыпуска.КоличествоДнейВГрафикеСНачалаГода, 0) - Материалы.ДнейДоОкончания = КалендарьПотребности.КоличествоДнейВГрафикеСНачалаГода
	|			КОНЕЦ)
	|			И (ВЫБОР
	|				КОГДА Материалы.ДнейДоОкончания > ЕСТЬNULL(КалендарьДатаВыпуска.КоличествоДнейВГрафикеСНачалаГода, 0)
	|					ТОГДА ГОД(Материалы.ДатаВыпуска) - 1 = КалендарьПотребности.Год
	|				ИНАЧЕ ГОД(Материалы.ДатаВыпуска) = КалендарьПотребности.Год
	|			КОНЕЦ)
	|ГДЕ
	|	НЕ ЭтапыПроизводства.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Материалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Трудозатраты.ВидРабот,
	|	Трудозатраты.НоменклатураПродукции,
	|	Трудозатраты.ХарактеристикаПродукции,
	|	Трудозатраты.ПодразделениеЭтапа,
	|	Трудозатраты.ПодразделениеДиспетчер,
	|	Трудозатраты.Сценарий,
	|	Трудозатраты.Спецификация,
	|	Трудозатраты.ДатаВыпуска,
	|	Трудозатраты.Количество КАК КоличествоНаЕдиницуПродукции,
	|	Трудозатраты.КоличествоИзделий КАК Количество,
	|	Трудозатраты.ДнейДоОкончания,
	|	Трудозатраты.ПланПроизводства
	|ПОМЕСТИТЬ Трудозатраты
	|ИЗ
	|	&Трудозатраты КАК Трудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Трудозатраты.ВидРабот,
	|	Трудозатраты.НоменклатураПродукции,
	|	Трудозатраты.ХарактеристикаПродукции,
	|	Трудозатраты.ПодразделениеЭтапа,
	|	Трудозатраты.ПодразделениеДиспетчер,
	|	Трудозатраты.Сценарий,
	|	Трудозатраты.Спецификация,
	|	Трудозатраты.ДатаВыпуска КАК ДатаВыпуска,
	|	Трудозатраты.ПланПроизводства,
	|	ВЫРАЗИТЬ(Трудозатраты.КоличествоНаЕдиницуПродукции * Трудозатраты.Количество КАК ЧИСЛО(15, 3)) КАК Количество,
	|	Трудозатраты.КоличествоНаЕдиницуПродукции,
	|	ДОБАВИТЬКДАТЕ(Трудозатраты.ДатаВыпуска, ДЕНЬ, -Трудозатраты.ДнейДоОкончания) КАК Период
	|ПОМЕСТИТЬ ТрудозатратыРазузлованные
	|ИЗ
	|	Трудозатраты КАК Трудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ Трудозатраты
	|;
	|
	//++ НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗанятостьВидовРЦ.ВидРабочегоЦентра,
	|	ЗанятостьВидовРЦ.НоменклатураПродукции,
	|	ЗанятостьВидовРЦ.ХарактеристикаПродукции,
	|	ЗанятостьВидовРЦ.ПодразделениеЭтапа,
	|	ЗанятостьВидовРЦ.ПодразделениеДиспетчер,
	|	ЗанятостьВидовРЦ.Сценарий,
	|	ЗанятостьВидовРЦ.Спецификация,
	|	ЗанятостьВидовРЦ.ДатаВыпуска,
	|	ЗанятостьВидовРЦ.Количество КАК КоличествоНаЕдиницуПродукции,
	|	ЗанятостьВидовРЦ.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий,
	|	ЗанятостьВидовРЦ.КоличествоИзделий КАК Количество,
	|	ЗанятостьВидовРЦ.ДнейДоОкончания,
	|	ЗанятостьВидовРЦ.ПланПроизводства
	|ПОМЕСТИТЬ ЗанятостьВидовРЦ
	|ИЗ
	|	&ЗанятостьВидовРЦ КАК ЗанятостьВидовРЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗанятостьВидовРЦ.ВидРабочегоЦентра,
	|	ЗанятостьВидовРЦ.НоменклатураПродукции,
	|	ЗанятостьВидовРЦ.ХарактеристикаПродукции,
	|	ЗанятостьВидовРЦ.ПодразделениеЭтапа,
	|	ЗанятостьВидовРЦ.ПодразделениеДиспетчер,
	|	ЗанятостьВидовРЦ.Сценарий,
	|	ЗанятостьВидовРЦ.Спецификация,
	|	ЗанятостьВидовРЦ.ДатаВыпуска КАК ДатаВыпуска,
	|	ЗанятостьВидовРЦ.ПланПроизводства,
	|	ВЫБОР
	|		КОГДА (ВЫРАЗИТЬ(ЗанятостьВидовРЦ.Количество / ЗанятостьВидовРЦ.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ЧИСЛО(15, 0))) < ЗанятостьВидовРЦ.Количество / ЗанятостьВидовРЦ.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
	|			ТОГДА ВЫРАЗИТЬ(ЗанятостьВидовРЦ.КоличествоНаЕдиницуПродукции * ((ВЫРАЗИТЬ(ЗанятостьВидовРЦ.Количество / ЗанятостьВидовРЦ.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ЧИСЛО(15, 0))) + 1) КАК ЧИСЛО(15, 0))
	|		ИНАЧЕ ВЫРАЗИТЬ(ЗанятостьВидовРЦ.КоличествоНаЕдиницуПродукции * (ВЫРАЗИТЬ(ЗанятостьВидовРЦ.Количество / ЗанятостьВидовРЦ.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ЧИСЛО(15, 0))) КАК ЧИСЛО(15, 0))
	|	КОНЕЦ КАК Занятость,
	|	ЗанятостьВидовРЦ.КоличествоНаЕдиницуПродукции,
	|	ЗанятостьВидовРЦ.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий,
	|	ДОБАВИТЬКДАТЕ(ЗанятостьВидовРЦ.ДатаВыпуска, ДЕНЬ, -ЗанятостьВидовРЦ.ДнейДоОкончания) КАК Период
	|ПОМЕСТИТЬ ЗанятостьВидовРЦРазузлованные
	|ИЗ
	|	ЗанятостьВидовРЦ КАК ЗанятостьВидовРЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЗанятостьВидовРЦ
	//-- НЕ УТКА
	|";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Функция СформироватьТаблицыДвижений(Параметры)
	
	ТаблицыДвижений = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Параметры.СтруктураВременныеТаблицы;
	Запрос.УстановитьПараметр("Ссылка", Параметры.ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Движения.Период,
	|	Движения.НомерСтроки,
	|	Движения.Активность,
	|	Движения.Номенклатура,
	|	Движения.Характеристика,
	|	Движения.ПодразделениеДиспетчер,
	|	Движения.ПодразделениеИсполнитель,
	|	Движения.Склад,
	|	Движения.Сценарий,
	|	Движения.ПланПроизводства,
	|	Движения.ДатаВыпуска,
	|	ВЫБОР
	|		КОГДА ПродукцияПересчет.Количество ЕСТЬ NULL 
	|			ТОГДА Движения.Количество
	|		ИНАЧЕ Движения.КоличествоНаЕдиницуПродукции * ПродукцияПересчет.Количество
	|	КОНЕЦ КАК Количество,
	|	Движения.Спецификация,
	|	Движения.НоменклатураПродукции,
	|	Движения.ХарактеристикаПродукции,
	|	Движения.КоличествоНаЕдиницуПродукции,
	|	Движения.СодержитРасчетноеКоличество
	|ПОМЕСТИТЬ МатериалыПересчитанные
	|ИЗ
	|	РегистрНакопления.ПланыПотребленияМатериалов КАК Движения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПродукцияПересчет КАК ПродукцияПересчет
	|		ПО Движения.НоменклатураПродукции = ПродукцияПересчет.Номенклатура
	|			И Движения.ХарактеристикаПродукции = ПродукцияПересчет.Характеристика
	|			И Движения.Спецификация = ПродукцияПересчет.Спецификация
	|			И Движения.ДатаВыпуска = ПродукцияПересчет.ДатаВыпуска
	|			И Движения.ПодразделениеДиспетчер = ПродукцияПересчет.ПодразделениеДиспетчер
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПродукцияРазузловка.Номенклатура КАК Номенклатура,
	|			ПродукцияРазузловка.Характеристика КАК Характеристика,
	|			ПродукцияРазузловка.Спецификация КАК Спецификация,
	|			ПродукцияРазузловка.ДатаВыпуска КАК ДатаВыпуска,
	|			ПродукцияРазузловка.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер
	|		ИЗ
	|			ПродукцияРазузловка КАК ПродукцияРазузловка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ПродукцияУдалить.Номенклатура,
	|			ПродукцияУдалить.Характеристика,
	|			ПродукцияУдалить.Спецификация,
	|			ПродукцияУдалить.ДатаВыпуска,
	|			ПродукцияУдалить.ПодразделениеДиспетчер
	|		ИЗ
	|			ПродукцияУдалить КАК ПродукцияУдалить) КАК ПродукцияРазузловкаУдалить
	|		ПО Движения.НоменклатураПродукции = ПродукцияРазузловкаУдалить.Номенклатура
	|			И Движения.ХарактеристикаПродукции = ПродукцияРазузловкаУдалить.Характеристика
	|			И Движения.Спецификация = ПродукцияРазузловкаУдалить.Спецификация
	|			И Движения.ДатаВыпуска = ПродукцияРазузловкаУдалить.ДатаВыпуска
	|			И Движения.ПодразделениеДиспетчер = ПродукцияРазузловкаУдалить.ПодразделениеДиспетчер
	|ГДЕ
	|	Движения.Регистратор = &Ссылка
	|	И ПродукцияРазузловкаУдалить.Номенклатура ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МатериалыПересчитанные.Период КАК Период,
	|	МатериалыПересчитанные.Номенклатура КАК Номенклатура,
	|	МатериалыПересчитанные.Характеристика КАК Характеристика,
	|	МатериалыПересчитанные.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
	|	МатериалыПересчитанные.ПодразделениеИсполнитель КАК ПодразделениеИсполнитель,
	|	МатериалыПересчитанные.Склад КАК Склад,
	|	МатериалыПересчитанные.Сценарий КАК Сценарий,
	|	МатериалыПересчитанные.ПланПроизводства КАК ПланПроизводства,
	|	МатериалыПересчитанные.ДатаВыпуска КАК ДатаВыпуска,
	|	МатериалыПересчитанные.Количество КАК Количество,
	|	МатериалыПересчитанные.Спецификация КАК Спецификация,
	|	МатериалыПересчитанные.НоменклатураПродукции КАК НоменклатураПродукции,
	|	МатериалыПересчитанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	МатериалыПересчитанные.КоличествоНаЕдиницуПродукции КАК КоличествоНаЕдиницуПродукции,
	|	МатериалыПересчитанные.СодержитРасчетноеКоличество
	|ИЗ
	|	МатериалыПересчитанные КАК МатериалыПересчитанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МатериалыРазузлованные.Период,
	|	МатериалыРазузлованные.Номенклатура,
	|	МатериалыРазузлованные.Характеристика,
	|	МатериалыРазузлованные.ПодразделениеДиспетчер,
	|	МатериалыРазузлованные.ПодразделениеЭтапа,
	|	МатериалыРазузлованные.Склад,
	|	МатериалыРазузлованные.Сценарий,
	|	МатериалыРазузлованные.ПланПроизводства,
	|	МатериалыРазузлованные.ДатаВыпуска,
	|	МатериалыРазузлованные.Количество,
	|	МатериалыРазузлованные.Спецификация,
	|	МатериалыРазузлованные.НоменклатураПродукции,
	|	МатериалыРазузлованные.ХарактеристикаПродукции,
	|	МатериалыРазузлованные.КоличествоНаЕдиницуПродукции,
	|	МатериалыРазузлованные.СодержитРасчетноеКоличество
	|ИЗ
	|	МатериалыРазузлованные КАК МатериалыРазузлованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МатериалыПересчитанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МатериалыРазузлованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Период,
	|	Движения.НомерСтроки,
	|	Движения.Активность,
	|	Движения.ВидРабот,
	|	Движения.ПодразделениеДиспетчер,
	|	Движения.ПодразделениеИсполнитель,
	|	Движения.Сценарий,
	|	Движения.ПланПроизводства,
	|	Движения.ДатаВыпуска,
	|	ВЫБОР
	|		КОГДА ПродукцияПересчет.Количество ЕСТЬ NULL 
	|			ТОГДА Движения.Количество
	|		ИНАЧЕ Движения.КоличествоНаЕдиницуПродукции * ПродукцияПересчет.Количество
	|	КОНЕЦ КАК Количество,
	|	Движения.Спецификация,
	|	Движения.НоменклатураПродукции,
	|	Движения.ХарактеристикаПродукции,
	|	Движения.КоличествоНаЕдиницуПродукции
	|ПОМЕСТИТЬ ТрудозатратыПересчитанные
	|ИЗ
	|	РегистрНакопления.ПланыЗанятостиТрудовыхРесурсов КАК Движения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПродукцияПересчет КАК ПродукцияПересчет
	|		ПО Движения.НоменклатураПродукции = ПродукцияПересчет.Номенклатура
	|			И Движения.ХарактеристикаПродукции = ПродукцияПересчет.Характеристика
	|			И Движения.Спецификация = ПродукцияПересчет.Спецификация
	|			И Движения.ДатаВыпуска = ПродукцияПересчет.ДатаВыпуска
	|			И Движения.ПодразделениеДиспетчер = ПродукцияПересчет.ПодразделениеДиспетчер
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПродукцияРазузловка.Номенклатура КАК Номенклатура,
	|			ПродукцияРазузловка.Характеристика КАК Характеристика,
	|			ПродукцияРазузловка.Спецификация КАК Спецификация,
	|			ПродукцияРазузловка.ДатаВыпуска КАК ДатаВыпуска,
	|			ПродукцияРазузловка.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер
	|		ИЗ
	|			ПродукцияРазузловка КАК ПродукцияРазузловка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ПродукцияУдалить.Номенклатура,
	|			ПродукцияУдалить.Характеристика,
	|			ПродукцияУдалить.Спецификация,
	|			ПродукцияУдалить.ДатаВыпуска,
	|			ПродукцияУдалить.ПодразделениеДиспетчер
	|		ИЗ
	|			ПродукцияУдалить КАК ПродукцияУдалить) КАК ПродукцияРазузловкаУдалить
	|		ПО Движения.НоменклатураПродукции = ПродукцияРазузловкаУдалить.Номенклатура
	|			И Движения.ХарактеристикаПродукции = ПродукцияРазузловкаУдалить.Характеристика
	|			И Движения.Спецификация = ПродукцияРазузловкаУдалить.Спецификация
	|			И Движения.ДатаВыпуска = ПродукцияРазузловкаУдалить.ДатаВыпуска
	|			И Движения.ПодразделениеДиспетчер = ПродукцияРазузловкаУдалить.ПодразделениеДиспетчер
	|ГДЕ
	|	Движения.Регистратор = &Ссылка
	|	И ПродукцияРазузловкаУдалить.Номенклатура ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТрудозатратыПересчитанные.Период КАК Период,
	|	ТрудозатратыПересчитанные.ВидРабот КАК ВидРабот,
	|	ТрудозатратыПересчитанные.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
	|	ТрудозатратыПересчитанные.ПодразделениеИсполнитель КАК ПодразделениеИсполнитель,
	|	ТрудозатратыПересчитанные.Сценарий КАК Сценарий,
	|	ТрудозатратыПересчитанные.ПланПроизводства КАК ПланПроизводства,
	|	ТрудозатратыПересчитанные.ДатаВыпуска КАК ДатаВыпуска,
	|	ТрудозатратыПересчитанные.Количество КАК Количество,
	|	ТрудозатратыПересчитанные.Спецификация КАК Спецификация,
	|	ТрудозатратыПересчитанные.НоменклатураПродукции КАК НоменклатураПродукции,
	|	ТрудозатратыПересчитанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ТрудозатратыПересчитанные.КоличествоНаЕдиницуПродукции КАК КоличествоНаЕдиницуПродукции
	|ИЗ
	|	ТрудозатратыПересчитанные КАК ТрудозатратыПересчитанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТрудозатратыРазузлованные.Период,
	|	ТрудозатратыРазузлованные.ВидРабот,
	|	ТрудозатратыРазузлованные.ПодразделениеДиспетчер,
	|	ТрудозатратыРазузлованные.ПодразделениеЭтапа,
	|	ТрудозатратыРазузлованные.Сценарий,
	|	ТрудозатратыРазузлованные.ПланПроизводства,
	|	ТрудозатратыРазузлованные.ДатаВыпуска,
	|	ТрудозатратыРазузлованные.Количество,
	|	ТрудозатратыРазузлованные.Спецификация,
	|	ТрудозатратыРазузлованные.НоменклатураПродукции,
	|	ТрудозатратыРазузлованные.ХарактеристикаПродукции,
	|	ТрудозатратыРазузлованные.КоличествоНаЕдиницуПродукции
	|ИЗ
	|	ТрудозатратыРазузлованные КАК ТрудозатратыРазузлованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТрудозатратыПересчитанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТрудозатратыРазузлованные
	|;
	|
	//++ НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Движения.Период,
	|	Движения.НомерСтроки,
	|	Движения.Активность,
	|	Движения.ВидРабочегоЦентра,
	|	Движения.ПодразделениеДиспетчер,
	|	Движения.ПодразделениеИсполнитель,
	|	Движения.Сценарий,
	|	Движения.ПланПроизводства,
	|	Движения.ДатаВыпуска,
	|	ВЫБОР
	|		КОГДА ПродукцияПересчет.Количество ЕСТЬ NULL 
	|			ТОГДА Движения.Занятость
	|		ИНАЧЕ ВЫБОР
	|				КОГДА (ВЫРАЗИТЬ(ПродукцияПересчет.Количество / Движения.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ЧИСЛО(15, 0))) < ПродукцияПересчет.Количество / Движения.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
	|					ТОГДА ВЫРАЗИТЬ(Движения.КоличествоНаЕдиницуПродукции * ((ВЫРАЗИТЬ(ПродукцияПересчет.Количество / Движения.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ЧИСЛО(15, 0))) + 1) КАК ЧИСЛО(15, 0))
	|				ИНАЧЕ ВЫРАЗИТЬ(Движения.КоличествоНаЕдиницуПродукции * (ВЫРАЗИТЬ(ПродукцияПересчет.Количество / Движения.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий КАК ЧИСЛО(15, 0))) КАК ЧИСЛО(15, 0))
	|			КОНЕЦ
	|	КОНЕЦ КАК Занятость,
	|	Движения.Спецификация,
	|	Движения.НоменклатураПродукции,
	|	Движения.ХарактеристикаПродукции,
	|	Движения.КоличествоНаЕдиницуПродукции,
	|	Движения.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
	|ПОМЕСТИТЬ ЗанятостьВидовРЦПересчитанные
	|ИЗ
	|	РегистрНакопления.ПланыЗанятостиВидовРабочихЦентров КАК Движения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПродукцияПересчет КАК ПродукцияПересчет
	|		ПО Движения.НоменклатураПродукции = ПродукцияПересчет.Номенклатура
	|			И Движения.ХарактеристикаПродукции = ПродукцияПересчет.Характеристика
	|			И Движения.Спецификация = ПродукцияПересчет.Спецификация
	|			И Движения.ДатаВыпуска = ПродукцияПересчет.ДатаВыпуска
	|			И Движения.ПодразделениеДиспетчер = ПродукцияПересчет.ПодразделениеДиспетчер
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПродукцияРазузловка.Номенклатура КАК Номенклатура,
	|			ПродукцияРазузловка.Характеристика КАК Характеристика,
	|			ПродукцияРазузловка.Спецификация КАК Спецификация,
	|			ПродукцияРазузловка.ДатаВыпуска КАК ДатаВыпуска,
	|			ПродукцияРазузловка.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер
	|		ИЗ
	|			ПродукцияРазузловка КАК ПродукцияРазузловка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ПродукцияУдалить.Номенклатура,
	|			ПродукцияУдалить.Характеристика,
	|			ПродукцияУдалить.Спецификация,
	|			ПродукцияУдалить.ДатаВыпуска,
	|			ПродукцияУдалить.ПодразделениеДиспетчер
	|		ИЗ
	|			ПродукцияУдалить КАК ПродукцияУдалить) КАК ПродукцияРазузловкаУдалить
	|		ПО Движения.НоменклатураПродукции = ПродукцияРазузловкаУдалить.Номенклатура
	|			И Движения.ХарактеристикаПродукции = ПродукцияРазузловкаУдалить.Характеристика
	|			И Движения.Спецификация = ПродукцияРазузловкаУдалить.Спецификация
	|			И Движения.ДатаВыпуска = ПродукцияРазузловкаУдалить.ДатаВыпуска
	|			И Движения.ПодразделениеДиспетчер = ПродукцияРазузловкаУдалить.ПодразделениеДиспетчер
	|ГДЕ
	|	Движения.Регистратор = &Ссылка
	|	И ПродукцияРазузловкаУдалить.Номенклатура ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗанятостьВидовРЦПересчитанные.Период КАК Период,
	|	ЗанятостьВидовРЦПересчитанные.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ЗанятостьВидовРЦПересчитанные.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
	|	ЗанятостьВидовРЦПересчитанные.ПодразделениеИсполнитель КАК ПодразделениеИсполнитель,
	|	ЗанятостьВидовРЦПересчитанные.Сценарий КАК Сценарий,
	|	ЗанятостьВидовРЦПересчитанные.ПланПроизводства КАК ПланПроизводства,
	|	ЗанятостьВидовРЦПересчитанные.ДатаВыпуска КАК ДатаВыпуска,
	|	ЗанятостьВидовРЦПересчитанные.Занятость КАК Занятость,
	|	ЗанятостьВидовРЦПересчитанные.Спецификация КАК Спецификация,
	|	ЗанятостьВидовРЦПересчитанные.НоменклатураПродукции КАК НоменклатураПродукции,
	|	ЗанятостьВидовРЦПересчитанные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ЗанятостьВидовРЦПересчитанные.КоличествоНаЕдиницуПродукции КАК КоличествоНаЕдиницуПродукции,
	|	ЗанятостьВидовРЦПересчитанные.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
	|ИЗ
	|	ЗанятостьВидовРЦПересчитанные КАК ЗанятостьВидовРЦПересчитанные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗанятостьВидовРЦРазузлованные.Период,
	|	ЗанятостьВидовРЦРазузлованные.ВидРабочегоЦентра,
	|	ЗанятостьВидовРЦРазузлованные.ПодразделениеДиспетчер,
	|	ЗанятостьВидовРЦРазузлованные.ПодразделениеЭтапа,
	|	ЗанятостьВидовРЦРазузлованные.Сценарий,
	|	ЗанятостьВидовРЦРазузлованные.ПланПроизводства,
	|	ЗанятостьВидовРЦРазузлованные.ДатаВыпуска,
	|	ЗанятостьВидовРЦРазузлованные.Занятость,
	|	ЗанятостьВидовРЦРазузлованные.Спецификация,
	|	ЗанятостьВидовРЦРазузлованные.НоменклатураПродукции,
	|	ЗанятостьВидовРЦРазузлованные.ХарактеристикаПродукции,
	|	ЗанятостьВидовРЦРазузлованные.КоличествоНаЕдиницуПродукции,
	|	ЗанятостьВидовРЦРазузлованные.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
	|ИЗ
	|	ЗанятостьВидовРЦРазузлованные КАК ЗанятостьВидовРЦРазузлованные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЗанятостьВидовРЦПересчитанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ЗанятостьВидовРЦРазузлованные
	|;
	|
	//-- НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПродукцияРазузловка.Разделитель,
	|	ПродукцияРазузловка.ПланПроизводства,
	|	ПродукцияРазузловка.Номенклатура,
	|	ПродукцияРазузловка.Характеристика,
	|	ПродукцияРазузловка.Спецификация,
	|	ПродукцияРазузловка.ДатаВыпуска,
	|	ПродукцияРазузловка.ПодразделениеДиспетчер
	|ПОМЕСТИТЬ ОтработаннаяОчередь
	|ИЗ
	|	ПродукцияРазузловка КАК ПродукцияРазузловка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПродукцияПересчет.Разделитель,
	|	ПродукцияПересчет.ПланПроизводства,
	|	ПродукцияПересчет.Номенклатура,
	|	ПродукцияПересчет.Характеристика,
	|	ПродукцияПересчет.Спецификация,
	|	ПродукцияПересчет.ДатаВыпуска,
	|	ПродукцияПересчет.ПодразделениеДиспетчер
	|ИЗ
	|	ПродукцияПересчет КАК ПродукцияПересчет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПродукцияУдалить.Разделитель,
	|	ПродукцияУдалить.ПланПроизводства,
	|	ПродукцияУдалить.Номенклатура,
	|	ПродукцияУдалить.Характеристика,
	|	ПродукцияУдалить.Спецификация,
	|	ПродукцияУдалить.ДатаВыпуска,
	|	ПродукцияУдалить.ПодразделениеДиспетчер
	|ИЗ
	|	ПродукцияУдалить КАК ПродукцияУдалить
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтработаннаяОчередь.ПланПроизводства,
	|	ОчередьРасчетаПлановПроизводства.Разделитель,
	|	ОтработаннаяОчередь.Номенклатура,
	|	ОтработаннаяОчередь.Характеристика,
	|	ОтработаннаяОчередь.Спецификация,
	|	ОтработаннаяОчередь.ДатаВыпуска,
	|	ОтработаннаяОчередь.ПодразделениеДиспетчер КАК Подразделение
	|ИЗ
	|	ОтработаннаяОчередь КАК ОтработаннаяОчередь
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПлановПроизводства
	|		ПО ОтработаннаяОчередь.ПланПроизводства = ОчередьРасчетаПлановПроизводства.ПланПроизводства
	|			И ОтработаннаяОчередь.Разделитель >= ОчередьРасчетаПлановПроизводства.Разделитель
	|			И ОтработаннаяОчередь.Номенклатура = ОчередьРасчетаПлановПроизводства.Номенклатура
	|			И ОтработаннаяОчередь.Характеристика = ОчередьРасчетаПлановПроизводства.Характеристика
	|			И ОтработаннаяОчередь.Спецификация = ОчередьРасчетаПлановПроизводства.Спецификация
	|			И ОтработаннаяОчередь.ДатаВыпуска = ОчередьРасчетаПлановПроизводства.ДатаВыпуска
	|			И ОтработаннаяОчередь.ПодразделениеДиспетчер = ОчередьРасчетаПлановПроизводства.Подразделение";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	// 0 - ВТ - МатериалыПересчитанные
	ТаблицыДвижений.Вставить("Материалы", РезультатыЗапроса[1].Выгрузить());
	// 2 - Удаление ВТ МатериалыПересчитанные
	// 3 - Удаление ВТ МатериалыРазузлованные
	// 4 - ВТ - ТрудозатратыПересчитанные
	ТаблицыДвижений.Вставить("Трудозатраты", РезультатыЗапроса[5].Выгрузить());
	// 6 - Удаление ВТ ТрудозатратыПересчитанные
	// 7 - Удаление ВТ ТрудозатратыРазузлованные
	ТаблицыДвижений.Вставить("ОтработаннаяОчередь", РезультатыЗапроса[9].Выгрузить());
	//++ НЕ УТКА
	// 8 - ВТ - ЗанятостьВидовРЦПересчитанные
	ТаблицыДвижений.Вставить("ЗанятостьВидовРЦ", РезультатыЗапроса[9].Выгрузить());
	// 10 - Удаление ВТ ЗанятостьВидовРЦПересчитанные
	// 11 - Удаление ВТ ЗанятостьВидовРЦРазузлованные
	// 12 - ВТ ОтработаннаяОчередь
	ТаблицыДвижений.Вставить("ОтработаннаяОчередь", РезультатыЗапроса[13].Выгрузить());
	//-- НЕ УТКА
	
	Возврат ТаблицыДвижений;
	
КонецФункции

Процедура УдалитьДвиженияИзОчереди(Таблица)

	Для каждого Строка Из Таблица Цикл
		
		НаборЗаписей = РегистрыСведений.ОчередьРасчетаПланаПроизводства.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ПланПроизводства.Установить(Строка.ПланПроизводства);
		НаборЗаписей.Отбор.Разделитель.Установить(Строка.Разделитель);
		НаборЗаписей.Отбор.Номенклатура.Установить(Строка.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(Строка.Характеристика);
		НаборЗаписей.Отбор.Спецификация.Установить(Строка.Спецификация);
		НаборЗаписей.Отбор.ДатаВыпуска.Установить(Строка.ДатаВыпуска);
		НаборЗаписей.Отбор.Подразделение.Установить(Строка.Подразделение);
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			ТекстСообщения = НСтр("ru='Не удалось выполнить очистку регистра очереди по документу: %Ссылка% по причине: %Причина%';uk='Не вдалося виконати очищення регістра черзі по документу: %Ссылка% по причині: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Строка.ПланПроизводства);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(НСтр("ru='Фоновое проведение.План производства';uk='Фонове проведення.План виробництва'"), УровеньЖурналаРегистрации.Ошибка,
				Метаданные.Документы.ПланПроизводства, Строка.ПланПроизводства, ТекстСообщения);
		КонецПопытки;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура РассчитататьКоличество(СтрокаТаблицы)
		
	Если СтрокаТаблицы.КоличествоИзделие = 0 Тогда
		СтрокаТаблицы.Количество = 0;
	Иначе
		СтрокаТаблицы.Количество = СтрокаТаблицы.Количество / СтрокаТаблицы.КоличествоИзделие;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНаличиеЗаписейВОчереди(Параметры)

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ПланПроизводства", Параметры.ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОчередьРасчетаПлановПроизводства.Разделитель
	|ИЗ
	|	РегистрСведений.ОчередьРасчетаПланаПроизводства КАК ОчередьРасчетаПлановПроизводства
	|ГДЕ
	|	ОчередьРасчетаПлановПроизводства.ПланПроизводства = &ПланПроизводства";
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда
		ВыполнитьПроведение(Параметры);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли