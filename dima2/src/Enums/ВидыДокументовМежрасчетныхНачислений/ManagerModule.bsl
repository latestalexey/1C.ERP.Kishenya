#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКИИ

Функция ДоступныеПоФункциональнымОпциям() Экспорт
	
	ДоступныеЗначения = Новый Массив;
	
	Для Каждого Значение Из Метаданные.Перечисления.ВидыДокументовМежрасчетныхНачислений.ЗначенияПеречисления Цикл
		Попытка
			ОбъектМетаданных = Метаданные.Документы[Значение.Имя];
			Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда
				ДоступныеЗначения.Добавить(Перечисления.ВидыДокументовМежрасчетныхНачислений[Значение.Имя])
			КонецЕсли	
		Исключение
		КонецПопытки;	
			
	КонецЦикла;
	
	Возврат ДоступныеЗначения
	
КонецФункции

// Возвращает значение перечисления, соответствующее переданному документу.
// Параметр:
//	Документ - документ-объект или документ-ссылка.
//
// Возвращаемое значение:
//	значение перечисления ВидыДокументовМежрасчетныхНачислений
//
Функция ПоДокументу(Документ) Экспорт
	
	ВидДокумента = Неопределено;
	
	ИмяДокумента = Документ.Метаданные().Имя;
	ИмяДокумента = СтрЗаменить(ИмяДокумента, "Списком", ""); // если "списком", то это тоже самое
		
	ЗначенияПеречисления = Метаданные.Перечисления.ВидыДокументовМежрасчетныхНачислений.ЗначенияПеречисления;
	ОбъектМетаданных = ЗначенияПеречисления.Найти(ИмяДокумента);
	Если ОбъектМетаданных <> Неопределено Тогда
		ИндексЗначения = ЗначенияПеречисления.Индекс(ОбъектМетаданных);
			Если ИндексЗначения <> -1 Тогда
				ВидДокумента = Перечисления.ВидыДокументовМежрасчетныхНачислений[ИндексЗначения];
			КонецЕсли	
	КонецЕсли;	
	
	Возврат ВидДокумента;
	
КонецФункции

#КонецЕсли