#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имя формы, соответствующее значению перечисления для настройки.
//
// Параметры:
//  ТипДействия  - ПеречислениеСсылка.ТипыДействийШаговБюджетныхПроцессов.
//                 Действие, для которого определяется форма настройки.
//
// Возвращаемое значение:
//   Строка   - Имя формы.
//
Функция ИмяФормы(ТипДействия) Экспорт
	
	Возврат "Перечисление.ТипыДействийЭтаповПодготовкиБюджетов.Форма." + 
						ОбщегоНазначения.ИмяЗначенияПеречисления(ТипДействия);
		
КонецФункции

// Возвращает таблицу значений, заполненную настройками действия.
//
// Параметры:
//  Форма  - УправляемаяФорма - форма, где пользователь указывал настройки.
//  РеквизитыНастроек - Массив - Имена реквизитов, которые необходимо сохранить как настройки.
//
// Возвращаемое значение:
//   ТаблицаЗначний - сформированные настройки.
//
Функция НастройкиДействия(Форма) Экспорт
	
	РеквизитыНастроек = РеквизитыПоТипуДействия(Форма, ТипДействия(Форма.ИмяФормы), Форма.МодельБюджетирования);
	
	Настройки = Новый ТаблицаЗначений;
	Настройки.Колонки.Добавить("Имя");
	Настройки.Колонки.Добавить("Значение");
	Настройки.Колонки.Добавить("Представление");
	
	РеквизитыФормы = Форма.ПолучитьРеквизиты();
	Для каждого РеквизитНастройки Из РеквизитыНастроек Цикл
		Для каждого РеквизитФормы Из РеквизитыФормы Цикл
			Если РеквизитФормы.Имя <> РеквизитНастройки Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = Настройки.Добавить();
			НоваяСтрока.Имя = РеквизитФормы.Имя;
			НоваяСтрока.Значение = Форма[РеквизитФормы.Имя];
			НоваяСтрока.Представление = РеквизитФормы.Заголовок;
			Прервать;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Настройки;
	
КонецФункции

// Процедура заполняет реквизиты формы на основании переданной таблицы значений с настройками.
//
// Параметры:
//  Параметры  - ТаблицаЗначений - таблица, сохраненная в шаге процесса или в задаче.
//  Форма - УправляемаяФорма - форма, в реквизиты которой требуется восстановить настройки.
//
Процедура ВосстановитьНастройкиДействия(Настройки, Форма) Экспорт
	
	Реквизиты = Форма.ПолучитьРеквизиты();
	Для Каждого Реквизит из Реквизиты Цикл
		Если Реквизит.Имя = "Адрес" Или Реквизит.Имя = "ЭтапПодготовкиБюджетов" Или Реквизит.Имя = "МодельБюджетирования" Тогда 
			Продолжить;
		КонецЕсли;
		Элемент = Настройки.Найти(Реквизит.Имя, "Имя");
		Если Элемент <> Неопределено И Реквизит.ТипЗначения.СодержитТип(ТипЗнч(Элемент.Значение)) Тогда
			Форма[Реквизит.Имя] = Элемент.Значение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Функция возвращает представление настроек для отображения на форме бюджетной задачи или шага процесса.
//
// Параметры:
//  Настройка - настройки действия, для которых требуется определить представление.
//
// Возвращаемое значение:
//   Строка - имя настроек.
//
Функция ПолучитьПредставлениеДействия(Знач НастройкиДействия) Экспорт
	
	Результат = "";
	
	Если НастройкиДействия.Количество() = 0 Тогда
		Результат = НСтр("ru='<Настройки действия не заданы>';uk='<Настройки дії не задані>'");
		Возврат Результат;
	КонецЕсли;
	
	Шаблон = "%Представление: %Значение";
	Для каждого Настройка Из НастройкиДействия Цикл
		Если Не ЗначениеЗаполнено(Настройка.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Текст = СтрЗаменить(Шаблон, "%Представление", Настройка.Представление);
		Текст = СтрЗаменить(Текст, "%Значение", Настройка.Значение);
		
		Результат = Результат + ?(ЗначениеЗаполнено(Результат), ", ", "") + Текст;
		
	КонецЦикла;
	
	Возврат Результат; 
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипДействия(ИмяФормы)
	
	ИмяФормыСокращенное = Метаданные.НайтиПоПолномуИмени(ИмяФормы).Имя;
	ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов[ИмяФормыСокращенное];
	Возврат ТипДействия;
	
КонецФункции

Функция ПолучитьПоляШапки(Ссылка) Экспорт
	
	Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.НефинансовыеПоказателиБюджетов") И НЕ Ссылка.Пустая() Тогда
		Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "ПоОрганизациям, ПоПодразделениям, ПоСценариям");
		Возврат Реквизиты;
		
	ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ШаблоныВводаНефинансовыхПоказателей")  И НЕ Ссылка.Пустая() Тогда
		ПараметрыПериода = Новый Структура("НачалоПериода, ОкончаниеПериода",
											НачалоГода(ТекущаяДатаСеанса()), КонецГода(ТекущаяДатаСеанса()));
		
		СтруктураОписанияВвода = Документы.УстановкаЗначенийНефинансовыхПоказателей.СтруктураОписанияПолейДокументаВвода(
				Перечисления.ВидыОперацийУстановкиЗначенийНефинансовыхПоказателей.ВводЗначенийПоШаблону, , Ссылка,
					Ссылка.ЗначенияСложнойТаблицыПоУмолчанию, ПараметрыПериода);
		
		Результат = Новый Структура("ПоОрганизациям, ПоПодразделениям, ПоСценариям", Ложь, Ложь, Ложь);
		Если СтруктураОписанияВвода <> Неопределено Тогда
			Для Каждого ЭлементМассива ИЗ СтруктураОписанияВвода.ЛеваяКолонка Цикл
				Если ЭлементМассива.Имя = "Сценарий" Тогда
					Результат.ПоСценариям = Истина;
				ИначеЕсли ЭлементМассива.Имя = "Организация" Тогда
					Результат.ПоОрганизациям = Истина;
				ИначеЕсли ЭлементМассива.Имя = "Подразделение" Тогда
					Результат.ПоПодразделениям = Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Возврат Результат;
		
	Иначе
		Возврат Новый Структура("ПоОрганизациям, ПоПодразделениям, ПоСценариям", Ложь, Ложь, Ложь);
		
	КонецЕсли;
		
КонецФункции

Функция РеквизитыПоТипуДействия(Форма, ТипДействия, МодельБюджетирования) Экспорт
	
	ПараметрыФО = Новый Структура("МодельБюджетирования", МодельБюджетирования);
	Если ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ВводЭкземпляраБюджета Тогда
		Реквизиты = Новый Массив;
		Реквизиты.Добавить("ВидБюджета");
		Реквизиты.Добавить("Сценарий");
		Если ПолучитьФункциональнуюОпцию("ФормироватьБюджетыПоОрганизациям", ПараметрыФО) Тогда
			Реквизиты.Добавить("Организация");
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ФормироватьБюджетыПоПодразделениям", ПараметрыФО) Тогда
			Реквизиты.Добавить("Подразделение");
		КонецЕсли;
	ИначеЕсли ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ВводПлана Тогда
		Реквизиты = Новый Массив;
		Реквизиты.Добавить("ТипПлана");
		Реквизиты.Добавить("Сценарий");
		Реквизиты.Добавить("ВидПлана");
		Реквизиты.Добавить("СтатьяБюджетов");
	ИначеЕсли ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.УстановкаЗначенийНефинансовыхПоказателей Тогда
		Реквизиты = Новый Массив;
		Реквизиты.Добавить("ВидОперации");
		Если Форма.ВидОперации = Перечисления.ВидыОперацийУстановкиЗначенийНефинансовыхПоказателей.ВводЗначенийПоШаблону Тогда
			РеквизитыНастроек = ПолучитьПоляШапки(Форма.ШаблонВвода);
			Реквизиты.Добавить("ШаблонВвода");
		Иначе
			РеквизитыНастроек = ПолучитьПоляШапки(Форма.НефинансовыйПоказатель);
			Реквизиты.Добавить("НефинансовыйПоказатель");
		КонецЕсли;
		Если РеквизитыНастроек.ПоОрганизациям Тогда
			Реквизиты.Добавить("Организация");
		КонецЕсли;
		Если РеквизитыНастроек.ПоПодразделениям Тогда
			Реквизиты.Добавить("Подразделение");
		КонецЕсли;
		Если РеквизитыНастроек.ПоСценариям Тогда
			Реквизиты.Добавить("Сценарий");
		КонецЕсли;
	ИначеЕсли ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.УтверждениеБюджетов Тогда
		Реквизиты = Новый Массив;
		Реквизиты.Добавить("УтверждаемыеЭтапыПодготовкиБюджетов");
	ИначеЕсли ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ПовторГруппыЭтапов Тогда
		Реквизиты = Новый Массив;
		Реквизиты.Добавить("ГруппаШагов");
		Реквизиты.Добавить("Сценарий");
		Если ПолучитьФункциональнуюОпцию("ФормироватьБюджетыПоОрганизациям", ПараметрыФО) Тогда
			Реквизиты.Добавить("Организация");
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ФормироватьБюджетыПоПодразделениям", ПараметрыФО) Тогда
			Реквизиты.Добавить("Подразделение");
		КонецЕсли;
	ИначеЕсли ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.Прочее Тогда
		Реквизиты = Новый Массив;
	ИначеЕсли ТипДействия = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.УстановкаЛимитовРасходаДС Тогда
		Реквизиты = Новый Массив;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЛимитыРасходаДенежныхСредствПоОрганизациям") Тогда
			Реквизиты.Добавить("Организация");
		КонецЕсли;
		Если ПолучитьФункциональнуюОпцию("ИспользоватьЛимитыРасходаДенежныхСредствПоПодразделениям") Тогда
			Реквизиты.Добавить("Подразделение");
		КонецЕсли;
	Иначе
		ВызватьИсключение НСтр("ru='Неизвестное действие этапа подготовки бюджетов';uk='Невідома дія етапу підготовки бюджетів'")
	КонецЕсли;
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#КонецЕсли

