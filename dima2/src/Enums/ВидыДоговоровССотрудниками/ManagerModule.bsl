#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТаблицаВидовДоговоров.Ссылка,
	|	ТаблицаВидовДоговоров.Синоним
	|ПОМЕСТИТЬ ВТТаблицаВидовДоговоров
	|ИЗ
	|	&ТаблицаВидовДоговоров КАК ТаблицаВидовДоговоров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидовДоговоров.Ссылка
	|ИЗ
	|	ВТТаблицаВидовДоговоров КАК ТаблицаВидовДоговоров
	|ГДЕ
	|	ТаблицаВидовДоговоров.Синоним ПОДОБНО &СтрокаПоиска";
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаВидовДоговоров.Синоним ПОДОБНО &СтрокаПоиска", "ИСТИНА");
	КонецЕсли;
	
	// Составляем таблицу категорий.
	ТаблицаВидовДоговоров = Новый ТаблицаЗначений;
	ТаблицаВидовДоговоров.Колонки.Добавить("Ссылка", Новый ОписаниеТипов("ПеречислениеСсылка.ВидыДоговоровССотрудниками"));
	ТаблицаВидовДоговоров.Колонки.Добавить("Синоним", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(256)));
	
	ГруппаДоговоров = Неопределено;
	Параметры.Отбор.Свойство("ГруппаДоговоров", ГруппаДоговоров);
	ДоступныеВидыДоговоров = ДоступныеВидыДоговоров(ГруппаДоговоров);
	
	Для Каждого ЗначениеПеречисления Из ДоступныеВидыДоговоров Цикл
		НоваяСтрока = ТаблицаВидовДоговоров.Добавить();
		НоваяСтрока.Ссылка = Перечисления.ВидыДоговоровССотрудниками[ЗначениеПеречисления.Имя];
		НоваяСтрока.Синоним = ЗначениеПеречисления.Синоним;
	КонецЦикла;
	
	// Отбор только по действующим категориям с учетом введенной строки.
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаВидовДоговоров", ТаблицаВидовДоговоров);
	Запрос.УстановитьПараметр("СтрокаПоиска", Строка(Параметры.СтрокаПоиска) + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступныеВидыДоговоров(ГруппаДоговоров)
	
	Если ГруппаДоговоров = Неопределено Тогда
		ГруппаДоговоров = "Все";
	КонецЕсли;
	
	// Формируем массив групп операций, по которым осуществляется отбор.
	ГруппыДоговоров = Новый Массив;
	Если ТипЗнч(ГруппаДоговоров) = Тип("ФиксированныйМассив") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ГруппыДоговоров, ГруппаДоговоров);
	Иначе
		ГруппыДоговоров.Добавить(ГруппаДоговоров);
	КонецЕсли;
	
	ДоступныеВидыДоговоров = Новый Массив;
	
	Если ГруппыДоговоров.Найти("Все") <> Неопределено Или ГруппыДоговоров.Найти("РаботникиИСлужащие") <> Неопределено Тогда
		
		ДоступныеВидыДоговоров.Добавить(Метаданные.Перечисления.ВидыДоговоровССотрудниками.ЗначенияПеречисления.ТрудовойДоговор);
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьГосударственнуюСлужбу") Тогда
			ДоступныеВидыДоговоров.Добавить(Метаданные.Перечисления.ВидыДоговоровССотрудниками.ЗначенияПеречисления.КонтрактГосслужащего);
		КонецЕсли;
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьМуниципальнуюСлужбу") Тогда
			ДоступныеВидыДоговоров.Добавить(Метаданные.Перечисления.ВидыДоговоровССотрудниками.ЗначенияПеречисления.ДоговорМуниципальногоСлужащего);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ГосударственнаяСлужба") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ГосударственнаяСлужба");
		Модуль.ДобавитьДоступныеВидыДоговоровССотрудникамиПоНастройкам(ДоступныеВидыДоговоров, ГруппыДоговоров);
	КонецЕсли;
	
	Возврат ДоступныеВидыДоговоров;
	
КонецФункции

Функция ВидыДоговоровВоеннойСлужбы() Экспорт
	
	МассивДоговоров = Новый Массив;
	МассивДоговоров.Добавить(Перечисления.ВидыДоговоровССотрудниками.ВоеннослужащийПоПризыву);
	МассивДоговоров.Добавить(Перечисления.ВидыДоговоровССотрудниками.КонтрактВоеннослужащего);
	
	Возврат МассивДоговоров;
	
КонецФункции

Функция ВидыДоговоровКромеВоеннойСлужбы() Экспорт
	
	ВидыДоговоровВоеннойСлужбы = ВидыДоговоровВоеннойСлужбы();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыДоговоровССотрудниками.Ссылка
	|ИЗ
	|	Перечисление.ВидыДоговоровССотрудниками КАК ВидыДоговоровССотрудниками
	|ГДЕ
	|	НЕ ВидыДоговоровССотрудниками.Ссылка В (&МассивВидовДоговоров)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровССотрудниками.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("МассивВидовДоговоров", ВидыДоговоровВоеннойСлужбы);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
		
КонецФункции

#КонецОбласти

#КонецЕсли