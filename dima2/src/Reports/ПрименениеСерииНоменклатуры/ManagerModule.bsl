#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Определяет применение серии номенклатуры - какие серии материалов использовались при производстве/сборке
//
// АЛГОРИТМ
// 1. Получить данные о расходе требуемой серии
// 1.1. Серия может быть израсходована следующими способами
//      - по графику производства - отражается маршрутными листами
//      - при распределении затрат - отражается документами "Распределение материалов и работ", "Списание затрат на выпуски без распоряжений"
//      - при сборке (разборке) товаров - отражается сборкой (разборкой)
//
// 2. Получить данные о приходе изделий с сериями или без серий
// 2.1. Приход изделий может произойти следующими способами
//      - если серия израсходована по графику производства, то
//         - при выполнении маршрутных листов - отражается маршрутными листами
//         - при выпуске продукции - отражается выпуском продукции по маршрутным листам 
//                                 (документы отбираются, если серии не регистрируются в МЛ)
//      - если серия израсходована при распределении затрат, то
//         - при выпуске продукции без распоряжений - отражается выпуском продукции без распоряжений
//      - если серия израсходована при сборке (разборке), то
//         - при сборке (разборке) - отражается сборкой (разборкой)
//
// Параметры:
//  Параметры	- Структура - содержит параметры необходимые для формирования структуры серии
//  АдресХранилища	- Строка - адрес хранилища в которое будут помещен результат
//
Функция ПрименениеСерииНоменклатуры(Параметры) Экспорт
	
	ПрименениеСерииНоменклатурыДерево = Новый ДеревоЗначений;
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Произведено", Новый ОписаниеТипов("Число"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("Строка"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("НоменклатураПредставление", Новый ОписаниеТипов("Строка"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("СерияПредставление", Новый ОписаниеТипов("Строка"));
	
	ПрименениеСерииКоллекция = ПрименениеСерииНоменклатурыДерево.Строки;
	
	СписокНоменклатуры = Новый ТаблицаЗначений;
	СписокНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СписокНоменклатуры.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СписокНоменклатуры.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	СписокНоменклатуры.Колонки.Добавить("Строки");
	
	НоваяСтрока = СписокНоменклатуры.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
	НоваяСтрока.Строки = ПрименениеСерииКоллекция;
	
	// Для предотвращения зацикливания
	ОтработаннаяНоменклатура = Новый ТаблицаЗначений;
	ОтработаннаяНоменклатура.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ОтработаннаяНоменклатура.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ОтработаннаяНоменклатура.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	НоваяСтрока = ОтработаннаяНоменклатура.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока СписокНоменклатуры.Количество() <> 0 Цикл
		
		НовыйСписокНоменклатуры = СписокНоменклатуры.СкопироватьКолонки();
		
		ПроизведенныеСерии = ПроизведенныеСерии(СписокНоменклатуры);
		Для каждого СтрокаНоменклатура Из СписокНоменклатуры Цикл
			СтруктураПоиска = Новый Структура("НоменклатураСписка,СерияСписка", СтрокаНоменклатура.Номенклатура, СтрокаНоменклатура.Серия);
	  		СписокСтрок = ПроизведенныеСерии.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаСерияИзделия Из СписокСтрок Цикл
				
				НоваяСерия = СтрокаНоменклатура.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСерия, СтрокаСерияИзделия);
				НоваяСерия.НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
																СтрокаСерияИзделия.НоменклатураПредставление,
																СтрокаСерияИзделия.ХарактеристикаПредставление);
																
				Если ЗначениеЗаполнено(НоваяСерия.Серия) Тогда
					// Для предотвращения зацикливания
					СтруктураПоиска = Новый Структура("Номенклатура,Характеристика,Серия", 
												НоваяСерия.Номенклатура, НоваяСерия.Характеристика, НоваяСерия.Серия);
				 	СписокСтрок = ОтработаннаяНоменклатура.НайтиСтроки(СтруктураПоиска);
					Если СписокСтрок.Количество() = 0 Тогда
						НоваяНоменклатура = НовыйСписокНоменклатуры.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяНоменклатура, НоваяСерия);
						НоваяНоменклатура.Строки = НоваяСерия.Строки;
					КонецЕсли; 
				КонецЕсли; 
				
			КонецЦикла; 
		КонецЦикла;
		
		СписокНоменклатуры = НовыйСписокНоменклатуры.Скопировать();
		
	КонецЦикла; 
	
	Возврат ПрименениеСерииНоменклатурыДерево;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПроизведенныеСерии(СписокНоменклатуры)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Серия КАК Справочник.СерииНоменклатуры) КАК Серия
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&СписокНоменклатуры КАК СписокНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Серия
	|;
	|";
	
	//++ НЕ УТ
	#Область РасходСерииПриПроизводстве
	Запрос.Текст = Запрос.Текст + 
	"
	//++ НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства) КАК Документ,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).Распоряжение КАК Распоряжение,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).КодСтроки КАК КодСтроки,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).КодСтрокиЭтапыГрафик КАК КодСтрокиЭтапыГрафик,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).Этап КАК Этап,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).НоменклатураПолуфабриката КАК НоменклатураПолуфабриката,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).НомерПартии КАК НомерПартии,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ РасходСерииПоГрафикуПроизводства
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			(Номенклатура, Характеристика, Серия) В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура,
	|						СписокНоменклатуры.Характеристика,
	|						СписокНоменклатуры.Серия
	|					ИЗ
	|						СписокНоменклатуры)
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.РасходМатериаловПриВыполненииМаршрутныхЛистов)) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтроки,
	|	НоменклатураПолуфабриката,
	|	ХарактеристикаПолуфабриката
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ДвижениеСерии.Документ КАК Документ,
	|	ДокументРаспределение.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДокументРаспределение.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	СУММА(ДокументРаспределение.Количество) КАК Количество
	|ПОМЕСТИТЬ РаспределениеЗатратНаЭтапыГрафика
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			(Номенклатура, Характеристика, Серия) В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура,
	|						СписокНоменклатуры.Характеристика,
	|						СписокНоменклатуры.Серия
	|					ИЗ
	|						СписокНоменклатуры)
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты)
	|				И Документ ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат) КАК ДвижениеСерии
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК ДокументРаспределение
	|	ПО ДокументРаспределение.Ссылка = ДвижениеСерии.Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеСерии.Номенклатура,
	|	ДвижениеСерии.Характеристика,
	|	ДвижениеСерии.Серия,
	|	ДвижениеСерии.Документ,
	|	ДокументРаспределение.ЗаказНаПроизводство,
	|	ДокументРаспределение.КодСтрокиПродукция
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаПроизводство,
	|	КодСтрокиПродукция
	|;
	|
	//-- НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ДвижениеСерии.Документ КАК Документ
	|ПОМЕСТИТЬ СписаниеЗатратНаВыпускБезРаспоряжений
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			(Номенклатура, Характеристика, Серия) В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура,
	|						СписокНоменклатуры.Характеристика,
	|						СписокНоменклатуры.Серия
	|					ИЗ
	|						СписокНоменклатуры)
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты)
	|				И Документ ССЫЛКА Документ.СписаниеЗатратНаВыпуск) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|";
	#КонецОбласти
	//-- НЕ УТ
	
	#Область РасходСерииПриСборке
	Запрос.Текст = Запрос.Текст + 
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.СборкаТоваров) КАК Документ,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ РасходСерииПриСборке
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			(Номенклатура, Характеристика, Серия) В
	|					(ВЫБРАТЬ
	|						СписокНоменклатуры.Номенклатура,
	|						СписокНоменклатуры.Характеристика,
	|						СписокНоменклатуры.Серия
	|					ИЗ
	|						СписокНоменклатуры КАК СписокНоменклатуры)
	|				И СкладскаяОперация В (ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектующихДляСборки),
	|										ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки))) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Документ
	|;
	|";
	#КонецОбласти
	
	//++ НЕ УТКА
	#Область ДокументыРасходаСерийПриПроизводстве
	Запрос.Текст = Запрос.Текст + 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходСерии.Номенклатура КАК Номенклатура,
	|	РасходСерии.Характеристика КАК Характеристика,
	|	РасходСерии.Серия КАК Серия,
	|	МаршрутныйЛистПроизводства.Ссылка КАК Ссылка,
	|	МаршрутныйЛистПроизводства.Распоряжение КАК Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки КАК КодСтроки,
	|	МаршрутныйЛистПроизводства.Этап КАК Этап
	|ПОМЕСТИТЬ МаршрутныеЛисты
	|ИЗ
	|	РасходСерииПоГрафикуПроизводства КАК РасходСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Распоряжение = РасходСерии.Распоряжение)
	|			И (МаршрутныйЛистПроизводства.КодСтроки = РасходСерии.КодСтроки)
	|			И (МаршрутныйЛистПроизводства.НомерПартии = РасходСерии.НомерПартии)
	|			И (МаршрутныйЛистПроизводства.НоменклатураПолуфабриката = РасходСерии.НоменклатураПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.ХарактеристикаПолуфабриката = РасходСерии.ХарактеристикаПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.Проведен)
	|			И (МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходСерии.Номенклатура КАК Номенклатура,
	|	РасходСерии.Характеристика КАК Характеристика,
	|	РасходСерии.Серия КАК Серия,
	|	МаршрутныйЛистПроизводства.Ссылка КАК Ссылка,
	|	МаршрутныйЛистПроизводства.Распоряжение КАК Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки КАК КодСтроки,
	|	МаршрутныйЛистПроизводства.Этап КАК Этап
	|ИЗ
	|	РаспределениеЗатратНаЭтапыГрафика КАК РасходСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Распоряжение = РасходСерии.ЗаказНаПроизводство)
	|			И (МаршрутныйЛистПроизводства.КодСтроки = РасходСерии.КодСтрокиПродукция)
	|			И (МаршрутныйЛистПроизводства.Проведен)
	|			И (МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МаршрутныеЛисты.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(МаршрутныеЛисты.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	МаршрутныеЛисты.Характеристика КАК Характеристика,
	|	МаршрутныеЛисты.Серия КАК Серия,
	|	МаршрутныеЛисты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ МаршрутныеЛистыСписка
	|ИЗ
	|	МаршрутныеЛисты КАК МаршрутныеЛисты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика
	|;
	|";
	#КонецОбласти
	//-- НЕ УТКА
		
	#Область ПриходСерий
	Запрос.Текст = Запрос.Текст + 
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходСерииПриСборке.Номенклатура КАК НоменклатураСписка,
	|	РасходСерииПриСборке.Характеристика КАК ХарактеристикаСписка,
	|	РасходСерииПриСборке.Серия КАК СерияСписка,
	|	ПоступлениеСерииПриСборке.Номенклатура КАК Номенклатура,
	|	ПоступлениеСерииПриСборке.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ПоступлениеСерииПриСборке.Характеристика КАК Характеристика,
	|	ПоступлениеСерииПриСборке.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ПоступлениеСерииПриСборке.Серия КАК Серия,
	|	ПоступлениеСерииПриСборке.Серия.Представление КАК СерияПредставление,
	|	ПоступлениеСерииПриСборке.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ПоступлениеСерииПриСборке.КоличествоОборот КАК Произведено
	|ИЗ 
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			Документ В
	|					(ВЫБРАТЬ
	|						РасходСерииПриСборке.Документ
	|					ИЗ
	|						РасходСерииПриСборке КАК РасходСерииПриСборке)
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства)) КАК ПоступлениеСерииПриСборке
	|	ЛЕВОЕ СОЕДИНЕНИЕ РасходСерииПриСборке КАК РасходСерииПриСборке
	|	ПО РасходСерииПриСборке.Документ = ПоступлениеСерииПриСборке.Документ
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	МаршрутныеЛистыСписка.Номенклатура КАК НоменклатураСписка,
	|	МаршрутныеЛистыСписка.Характеристика КАК ХарактеристикаСписка,
	|	МаршрутныеЛистыСписка.Серия КАК СерияСписка,
	|	ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпуск.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ТаблицаВыпуск.Характеристика КАК Характеристика,
	|	ТаблицаВыпуск.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия
	|		ИНАЧЕ ТаблицаСерии.Серия 
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия.Представление
	|		ИНАЧЕ ТаблицаСерии.Серия.Представление 
	|	КОНЕЦ КАК СерияПредставление,
	|	ТаблицаВыпуск.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ТаблицаСерии.КоличествоФакт, ТаблицаВыпуск.КоличествоФакт)) КАК Произведено
	|ИЗ 
	|	МаршрутныеЛистыСписка КАК МаршрутныеЛистыСписка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК ТаблицаВыпуск
	|		ПО ТаблицаВыпуск.Ссылка = МаршрутныеЛистыСписка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.МатериалыИУслуги КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТаблицаВыпуск.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаВыпуск.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаВыпуск.Характеристика
	|			И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|ГДЕ
	|	ТаблицаВыпуск.КоличествоФакт <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутныеЛистыСписка.Номенклатура,
	|	МаршрутныеЛистыСписка.Характеристика,
	|	МаршрутныеЛистыСписка.Серия,
	|	ТаблицаВыпуск.Ссылка,
	|	ТаблицаВыпуск.Номенклатура,
	|	ТаблицаВыпуск.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия
	|		ИНАЧЕ ТаблицаСерии.Серия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия.Представление
	|		ИНАЧЕ ТаблицаСерии.Серия.Представление 
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	МаршрутныеЛистыСписка.Номенклатура КАК НоменклатураСписка,
	|	МаршрутныеЛистыСписка.Характеристика КАК ХарактеристикаСписка,
	|	МаршрутныеЛистыСписка.Серия КАК СерияСписка,
	|	ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпуск.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ТаблицаВыпуск.Характеристика КАК Характеристика,
	|	ТаблицаВыпуск.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия
	|		ИНАЧЕ ТаблицаСерии.Серия 
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия.Представление
	|		ИНАЧЕ ТаблицаСерии.Серия.Представление 
	|	КОНЕЦ КАК СерияПредставление,
	|	ТаблицаВыпуск.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаВыпуск.Количество)) КАК Произведено
	|ИЗ 
	|	МаршрутныеЛистыСписка КАК МаршрутныеЛистыСписка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаВыпуск
	|		ПО ТаблицаВыпуск.Распоряжение = МаршрутныеЛистыСписка.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТаблицаВыпуск.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаВыпуск.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаВыпуск.Характеристика
	|			И ТаблицаСерии.Склад = ТаблицаВыпуск.Склад
	|			И ТаблицаСерии.Подразделение = ТаблицаВыпуск.Подразделение
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерийСклад
	|		ПО ПолитикиУчетаСерийСклад.Ссылка = МаршрутныеЛистыСписка.ВидНоменклатуры
	|			И ПолитикиУчетаСерийСклад.Склад = ТаблицаВыпуск.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерийПодразделение
	|		ПО ПолитикиУчетаСерийПодразделение.Ссылка = МаршрутныеЛистыСписка.ВидНоменклатуры
	|			И ПолитикиУчетаСерийПодразделение.Склад = ТаблицаВыпуск.Подразделение
	|
	|ГДЕ
	|	ТаблицаВыпуск.Ссылка.Проведен
	|	И НЕ ЕСТЬNULL(ПолитикиУчетаСерийСклад.ПолитикаУчетаСерий.УказыватьДляИзделийПриВыполненииМаршрутныхЛистов,
	|				ЕСТЬNULL(ПолитикиУчетаСерийПодразделение.ПолитикаУчетаСерий.УказыватьДляИзделийПриВыполненииМаршрутныхЛистов, ЛОЖЬ))
	|	И ЕСТЬNULL(ПолитикиУчетаСерийСклад.ПолитикаУчетаСерий.УказыватьПриПриемкеПродукцииИзПроизводства,
	|				ЕСТЬNULL(ПолитикиУчетаСерийПодразделение.ПолитикаУчетаСерий.УказыватьПриПриемкеПродукцииИзПроизводства, ЛОЖЬ))
	|
	|СГРУППИРОВАТЬ ПО
	|	МаршрутныеЛистыСписка.Номенклатура,
	|	МаршрутныеЛистыСписка.Характеристика,
	|	МаршрутныеЛистыСписка.Серия,
	|	ТаблицаВыпуск.Ссылка,
	|	ТаблицаВыпуск.Номенклатура,
	|	ТаблицаВыпуск.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия
	|		ИНАЧЕ ТаблицаСерии.Серия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия.Представление
	|		ИНАЧЕ ТаблицаСерии.Серия.Представление 
	|	КОНЕЦ
	//-- НЕ УТКА
	//++ НЕ УТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	СписаниеЗатратНаВыпускБезРаспоряжений.Номенклатура КАК НоменклатураСписка,
	|	СписаниеЗатратНаВыпускБезРаспоряжений.Характеристика КАК ХарактеристикаСписка,
	|	СписаниеЗатратНаВыпускБезРаспоряжений.Серия КАК СерияСписка,
	|	ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпуск.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ТаблицаВыпуск.Характеристика КАК Характеристика,
	|	ТаблицаВыпуск.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия
	|		ИНАЧЕ ТаблицаСерии.Серия 
	|	КОНЕЦ КАК Серия,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия.Представление
	|		ИНАЧЕ ТаблицаСерии.Серия.Представление 
	|	КОНЕЦ КАК СерияПредставление,
	|	ТаблицаВыпуск.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	СУММА(ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаВыпуск.Количество)) КАК Произведено
	|ИЗ 
	|	СписаниеЗатратНаВыпускБезРаспоряжений КАК СписаниеЗатратНаВыпускБезРаспоряжений
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ТаблицаВыходныеИзделия
	|		ПО ТаблицаВыходныеИзделия.Ссылка = СписаниеЗатратНаВыпускБезРаспоряжений.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаВыпуск
	|		ПО ТаблицаВыпуск.Ссылка = ТаблицаВыходныеИзделия.Распоряжение
	|			И ТаблицаВыпуск.Номенклатура = ТаблицаВыходныеИзделия.Номенклатура
	|			И ТаблицаВыпуск.Характеристика = ТаблицаВыходныеИзделия.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Серии КАК ТаблицаСерии
	|		ПО ТаблицаСерии.Ссылка = ТаблицаВыпуск.Ссылка
	|			И ТаблицаСерии.Номенклатура = ТаблицаВыпуск.Номенклатура
	|			И ТаблицаСерии.Характеристика = ТаблицаВыпуск.Характеристика
	|			И ТаблицаСерии.Склад = ТаблицаВыпуск.Склад
	|			И ТаблицаСерии.Подразделение = ТаблицаВыпуск.Подразделение
	|
	|СГРУППИРОВАТЬ ПО
	|	СписаниеЗатратНаВыпускБезРаспоряжений.Номенклатура,
	|	СписаниеЗатратНаВыпускБезРаспоряжений.Характеристика,
	|	СписаниеЗатратНаВыпускБезРаспоряжений.Серия,
	|	ТаблицаВыпуск.Ссылка,
	|	ТаблицаВыпуск.Номенклатура,
	|	ТаблицаВыпуск.Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия
	|		ИНАЧЕ ТаблицаСерии.Серия
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ТаблицаВыпуск.Серия.Представление
	|		ИНАЧЕ ТаблицаСерии.Серия.Представление 
	|	КОНЕЦ
	//-- НЕ УТ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураПредставление,
	|	ХарактеристикаПредставление,
	|	СерияПредставление";
	#КонецОбласти
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("НоменклатураСписка,СерияСписка");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли