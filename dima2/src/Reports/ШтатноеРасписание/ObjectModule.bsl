#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ИнициализироватьОтчет();

	КлючВарианта = ЗарплатаКадрыОтчеты.КлючВарианта(КомпоновщикНастроек);
	Если КлючВарианта = "ШтатноеРасписаниеНаПодпись"
		ИЛИ КлючВарианта = "СоблюдениеШтатногоРасписания" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		// Параметры документа
		ДокументРезультат.ТолькоПросмотр = Истина;
		ДокументРезультат.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ШтатноеРасписаниеНаПодпись";
		ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
		ДокументРезультат.Очистить();
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаАктуальности"));
		Если ЗначениеПараметра <> Неопределено Тогда
			Если ТипЗнч(ЗначениеПараметра.Значение) = Тип("Дата") 
				Или ТипЗнч(ЗначениеПараметра.Значение) = Тип("СтандартнаяДатаНачала") Тогда
				
				Если ТипЗнч(ЗначениеПараметра.Значение) = Тип("Дата") Тогда
					Дата = Дата(ЗначениеПараметра.Значение);
				Иначе
					Дата = Дата(ЗначениеПараметра.Значение.Дата);
					ЗначениеПараметра.Значение = Дата;
				КонецЕсли;
				
				Если Дата = '00010101' Тогда
					ЗначениеПараметра.Значение = ТекущаяДатаСеанса();
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
		ДатаОтчета = ЗначениеПараметра.Значение;
		
		Если ТипЗнч(ДатаОтчета) = Тип("СтандартнаяДатаНачала") Тогда
			ДатаОтчета = ДатаОтчета.Дата;
		КонецЕсли; 
		
		Если КлючВарианта = "ШтатноеРасписаниеНаПодпись" Тогда
			
			ПараметрыОтчета = Новый Структура;
		
			ПараметрыОтчета.Вставить("ФИОРуководителя", "");
			ПараметрыОтчета.Вставить("ФИООтветственного", "");
			
			ПараметрРуководитель = ОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Руководитель");
			Если ПараметрРуководитель.Использование Тогда
				РуководительСсылка = ПараметрРуководитель.Значение;
			Иначе
				РуководительСсылка = Справочники.ФизическиеЛица.ПустаяСсылка();
			КонецЕсли;
			
			ПараметрИсполнитель = ОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Исполнитель");
			Если ПараметрИсполнитель.Использование Тогда
				ИсполнительСсылка = ПараметрИсполнитель.Значение;
			Иначе
				ИсполнительСсылка = Справочники.ФизическиеЛица.ПустаяСсылка();
			КонецЕсли;
			
			МассивФизЛиц = Новый Массив;
			
			Если ЗначениеЗаполнено(РуководительСсылка) Тогда
				МассивФизЛиц.Добавить(РуководительСсылка);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ИсполнительСсылка) Тогда
				МассивФизЛиц.Добавить(ИсполнительСсылка);
			КонецЕсли;
			
			Если МассивФизЛиц.Количество() > 0 Тогда
				
				МассивФИО = КадровыйУчет.КадровыеДанныеФизическихЛиц(Истина, МассивФизЛиц, "ИОФамилия", ДатаОтчета);
				Для каждого ЭлементМассиваФИО Из МассивФИО Цикл
					
					Если ЭлементМассиваФИО.ФизическоеЛицо =  РуководительСсылка  Тогда
						ПараметрыОтчета.Вставить("ФИОРуководителя", ЭлементМассиваФИО.ИОФамилия);
					КонецЕсли;
					
					Если ЭлементМассиваФИО.ФизическоеЛицо =  ИсполнительСсылка  Тогда
						ПараметрыОтчета.Вставить("ФИООтветственного", ЭлементМассиваФИО.ИОФамилия);
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли; 

		КонецЕсли;
	
		Данные = Новый ДеревоЗначений;
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;

		МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиОтчета,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
		
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , , Истина);

		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВывода.УстановитьОбъект(Данные);
		
		ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		
		Для каждого СтрокаОрганизации Из Данные.Строки Цикл
			Если КлючВарианта = "ШтатноеРасписаниеНаПодпись" Тогда
				ВывестиОтчетШтатноеРасписаниеНаПодпись(ДокументРезультат, ПараметрыОтчета, СтрокаОрганизации);
			ИначеЕсли КлючВарианта = "СоблюдениеШтатногоРасписания" Тогда
				ВывестиОтчетСоблюдениеШтатногоРасписания(ДокументРезультат, СтрокаОрганизации);
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
		
		ЭлементОформления = НастройкиОтчета.УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", УправлениеШтатнымРасписанием.ФорматКоличестваСтавок());
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("КоличествоСтавок");
		
		ЗначениеПараметра = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаАктуальности"));

		Если ЗначениеПараметра <> Неопределено Тогда
			Если ТипЗнч(ЗначениеПараметра.Значение) = Тип("Дата") 
				Или ТипЗнч(ЗначениеПараметра.Значение) = Тип("СтандартнаяДатаНачала") Тогда
				ДатаАктуальности = Дата(ЗначениеПараметра.Значение);
				Если ДатаАктуальности = '00010101' Тогда
					ЗначениеПараметра.Значение = ТекущаяДатаСеанса();
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		СтандартнаяОбработка = Ложь;
		
		ДокументРезультат.Очистить();
		
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки = КомпоновщикМакета.Выполнить(ЭтотОбъект.СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
		
		// Создадим и инициализируем процессор компоновки.
		ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
		ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
		
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
		
		// Обозначим начало вывода
		ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	ЗарплатаКадрыОбщиеНаборыДанныхРасширенный.ДополнитьШтатноеРасписание(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ИнициализироватьОтчет();
	ЗначениеВДанныеФормы(ЭтотОбъект, Форма.Отчет);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Штатное расписание на подпись.

Процедура ВывестиОтчетШтатноеРасписаниеНаПодпись(ДокументРезультат, ПараметрыОтчета, Данные)
	
	Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	ИспользоватьРасчетЗарплатыРасширенная = ПолучитьФункциональнуюОпцию("ИспользоватьРасчетЗарплатыРасширенная");
	ИспользоватьВилкуСтавокВШтатномРасписании = ПолучитьФункциональнуюОпцию("ИспользоватьВилкуСтавокВШтатномРасписании");
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.ШтатноеРасписание.ПФ_MXL_ШтатноеРасписаниеНаПодпись");
	
	ТаблицаДолжностей = Новый ТаблицаЗначений;
	ТаблицаДолжностей.Колонки.Добавить("Должность", Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ТаблицаДолжностей.Колонки.Добавить("КоличествоСтавок", Новый ОписаниеТипов("Число"));
	ТаблицаДолжностей.Колонки.Добавить("ДолжностьРеквизитДопУпорядочивания", Новый ОписаниеТипов("Число"));
	ТаблицаДолжностей.Колонки.Добавить("ПлановыйФОТ", Новый ОписаниеТипов("Число"));
	ТаблицаДолжностей.Колонки.Добавить("ПлановыйФОТМин", Новый ОписаниеТипов("Число"));
	ТаблицаДолжностей.Колонки.Добавить("ПлановыйФОТМакс", Новый ОписаниеТипов("Число"));
	
	// Шапка
	
	СтрокаЗаголовка = ДокументРезультат.ВысотаТаблицы + 1;
	
	ШапкаДоФОТ = Макет.ПолучитьОбласть("Шапка|ДоФОТ");
	ШапкаДоФОТ.Параметры.Заполнить(Данные);
	ШапкаДоФОТ.Параметры.ДатаАктуальности = Формат(Данные.ПараметрыДанныхДатаАктуальности, "ДЛФ=D");
	ДокументРезультат.Вывести(ШапкаДоФОТ);
	
	Если ИспользоватьРасчетЗарплатыРасширенная Тогда
		Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
			ШапкаФОТ = Макет.ПолучитьОбласть("Шапка|ФОТМинМакс");
		Иначе
			ШапкаФОТ = Макет.ПолучитьОбласть("Шапка|ФОТ");
		КонецЕсли;
		ДокументРезультат.Присоединить(ШапкаФОТ);
	КонецЕсли; 
	
	ШапкаПослеФОТ = Макет.ПолучитьОбласть("Шапка|ПослеФОТ");
	ШапкаПослеФОТ.Параметры.ФИОРуководителя = ПараметрыОтчета.ФИОРуководителя;
	ДокументРезультат.Присоединить(ШапкаПослеФОТ);
	
	ДокументРезультат.Область(СтрокаЗаголовка, 1, СтрокаЗаголовка, ДокументРезультат.ШиринаТаблицы).Объединить();
	
	// Заголовок подразделения
	ПодразделенияЗаголовокДоФОТ = Макет.ПолучитьОбласть("ПодразделенияЗаголовок|ДоФОТ");
	ДокументРезультат.Вывести(ПодразделенияЗаголовокДоФОТ);
	
	Если ИспользоватьРасчетЗарплатыРасширенная Тогда
		Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
			ПодразделенияЗаголовокФОТ = Макет.ПолучитьОбласть("ПодразделенияЗаголовок|ФОТМинМакс");
		Иначе
			ПодразделенияЗаголовокФОТ = Макет.ПолучитьОбласть("ПодразделенияЗаголовок|ФОТ");
		КонецЕсли;
		ДокументРезультат.Присоединить(ПодразделенияЗаголовокФОТ);
	КонецЕсли; 
	
	ПодразделенияЗаголовокПослеФОТ = Макет.ПолучитьОбласть("ПодразделенияЗаголовок|ПослеФОТ");
	ДокументРезультат.Присоединить(ПодразделенияЗаголовокПослеФОТ);
	
	// Подразделения
	ПодразделенияДоФОТ = Макет.ПолучитьОбласть("Подразделения|ДоФОТ");
	ПодразделенияДеталиДоФОТ = Макет.ПолучитьОбласть("ПодразделенияДетали|ДоФОТ");
	
	Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
		ПодразделенияФОТ = Макет.ПолучитьОбласть("Подразделения|ФОТМинМакс");
		ПодразделенияДеталиФОТ = Макет.ПолучитьОбласть("ПодразделенияДетали|ФОТМинМакс");
	Иначе
		ПодразделенияФОТ = Макет.ПолучитьОбласть("Подразделения|ФОТ");
		ПодразделенияДеталиФОТ = Макет.ПолучитьОбласть("ПодразделенияДетали|ФОТ");
	КонецЕсли;
	
	ПодразделенияПослеФОТ = Макет.ПолучитьОбласть("Подразделения|ПослеФОТ");
	ПодразделенияДеталиПослеФОТ = Макет.ПолучитьОбласть("ПодразделенияДетали|ПослеФОТ");
	
	Если Данные.Владелец().Колонки.Найти("ПлановыйФОТ") <> НеОпределено Тогда
		ФОТДоступен = Истина;
	Иначе
		ФОТДоступен = Ложь;
	КонецЕсли;
	
	Для Каждого СтрокаПодразделения Из Данные.Строки Цикл
		
		ПодразделенияДоФОТ.Параметры.Заполнить(СтрокаПодразделения);
		УстановитьЗначениеПараметраКоличествоСтавок(ПодразделенияДоФОТ, СтрокаПодразделения.КоличествоСтавок);
		ДокументРезультат.Вывести(ПодразделенияДоФОТ);
		
		Если ИспользоватьРасчетЗарплатыРасширенная Тогда
			ПодразделенияФОТ.Параметры.Заполнить(СтрокаПодразделения);
			ДокументРезультат.Присоединить(ПодразделенияФОТ);
		КонецЕсли; 
		
		ПодразделенияПослеФОТ.Параметры.Заполнить(СтрокаПодразделения);
		ДокументРезультат.Присоединить(ПодразделенияПослеФОТ);
		
		Для каждого СтрокаДолжности Из СтрокаПодразделения.Строки Цикл
			
			ЗаполнитьЗначенияСвойств(ТаблицаДолжностей.Добавить(), СтрокаДолжности);
			
			ПодразделенияДеталиДоФОТ.Параметры.Заполнить(СтрокаДолжности);
			
			Если СтрокаДолжности.Владелец().Колонки.Найти("РазрядКатегория") <> Неопределено
				И ЗначениеЗаполнено(СтрокаДолжности.РазрядКатегория) Тогда
				
				ПодразделенияДеталиДоФОТ.Параметры.Должность = Строка(СтрокаДолжности.Должность) + ", " + СтрокаДолжности.РазрядКатегория;
				
			КонецЕсли;
			
			УстановитьЗначениеПараметраКоличествоСтавок(ПодразделенияДеталиДоФОТ, СтрокаДолжности.КоличествоСтавок);
			ДокументРезультат.Вывести(ПодразделенияДеталиДоФОТ);
			
			Если ИспользоватьРасчетЗарплатыРасширенная Тогда
				ПодразделенияДеталиФОТ.Параметры.Заполнить(СтрокаДолжности);
				ДокументРезультат.Присоединить(ПодразделенияДеталиФОТ);
			КонецЕсли; 
			
			ПодразделенияДеталиПослеФОТ.Параметры.Заполнить(СтрокаДолжности);
			ДокументРезультат.Присоединить(ПодразделенияДеталиПослеФОТ);
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Подразделения подвал
	ПодразделенияПодвалДоФОТ = Макет.ПолучитьОбласть("ПодразделенияПодвал|ДоФОТ");
	УстановитьЗначениеПараметраКоличествоСтавок(ПодразделенияПодвалДоФОТ, Данные.Строки.Итог("КоличествоСтавок"));
	ДокументРезультат.Вывести(ПодразделенияПодвалДоФОТ);
	
	Если ИспользоватьРасчетЗарплатыРасширенная Тогда
		Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
			ПодразделенияПодвалФОТ = Макет.ПолучитьОбласть("ПодразделенияПодвал|ФОТМинМакс");
			Если ФОТДоступен Тогда
				ПодразделенияПодвалФОТ.Параметры.ПлановыйФОТМин = Данные.Строки.Итог("ПлановыйФОТМин");
				ПодразделенияПодвалФОТ.Параметры.ПлановыйФОТМакс = Данные.Строки.Итог("ПлановыйФОТМакс");
			КонецЕсли;
		Иначе
			ПодразделенияПодвалФОТ = Макет.ПолучитьОбласть("ПодразделенияПодвал|ФОТ");
			Если ФОТДоступен Тогда
				ПодразделенияПодвалФОТ.Параметры.ПлановыйФОТ = Данные.Строки.Итог("ПлановыйФОТ");
			КонецЕсли;
		КонецЕсли;
		
		ДокументРезультат.Присоединить(ПодразделенияПодвалФОТ);
	КонецЕсли;
	
	ПодразделенияПодвалПослеФОТ = Макет.ПолучитьОбласть("ПодразделенияПодвал|ПослеФОТ");
	ДокументРезультат.Присоединить(ПодразделенияПодвалПослеФОТ);

	ТаблицаДолжностей.Свернуть("Должность,ДолжностьРеквизитДопУпорядочивания", "КоличествоСтавок,ПлановыйФОТ,ПлановыйФОТМин,ПлановыйФОТМакс");
	ТаблицаДолжностей.Сортировать("ДолжностьРеквизитДопУпорядочивания");
	
	// Заголовок должностей
	ДолжностиЗаголовокДоФОТ = Макет.ПолучитьОбласть("ДолжностиЗаголовок|ДоФОТ");
	ДокументРезультат.Вывести(ДолжностиЗаголовокДоФОТ);
	
	Если ИспользоватьРасчетЗарплатыРасширенная Тогда
		Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
			ДолжностиЗаголовокФОТ = Макет.ПолучитьОбласть("ДолжностиЗаголовок|ФОТМинМакс");
		Иначе
			ДолжностиЗаголовокФОТ = Макет.ПолучитьОбласть("ДолжностиЗаголовок|ФОТ");
		КонецЕсли;
		ДокументРезультат.Присоединить(ДолжностиЗаголовокФОТ);
	КонецЕсли; 
	
	ДолжностиЗаголовокПослеФОТ = Макет.ПолучитьОбласть("ДолжностиЗаголовок|ПослеФОТ");
	ДокументРезультат.Присоединить(ДолжностиЗаголовокПослеФОТ);
	
	// Детали должностей
	
	ДолжностиДоФОТ = Макет.ПолучитьОбласть("Должности|ДоФОТ");
	
	Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
		ДолжностиФОТ = Макет.ПолучитьОбласть("Должности|ФОТМинМакс");
	Иначе
		ДолжностиФОТ = Макет.ПолучитьОбласть("Должности|ФОТ");
	КонецЕсли;
	
	ДолжностиПослеФОТ = Макет.ПолучитьОбласть("Должности|ПослеФОТ");
	
	Для каждого СтрокаТаблицыДолжностей Из ТаблицаДолжностей Цикл
		
		ДолжностиДоФОТ.Параметры.Заполнить(СтрокаТаблицыДолжностей);
		УстановитьЗначениеПараметраКоличествоСтавок(ДолжностиДоФОТ, СтрокаТаблицыДолжностей.КоличествоСтавок);
		ДокументРезультат.Вывести(ДолжностиДоФОТ);
		
		Если ИспользоватьРасчетЗарплатыРасширенная Тогда
			ДолжностиФОТ.Параметры.Заполнить(СтрокаТаблицыДолжностей);
			ДокументРезультат.Присоединить(ДолжностиФОТ);
		КонецЕсли; 
		
		ДолжностиПослеФОТ.Параметры.Заполнить(СтрокаТаблицыДолжностей);
		ДокументРезультат.Присоединить(ДолжностиПослеФОТ);
		
	КонецЦикла;
	
	// Подвал должностей
	
	ДолжностиПодвалДоФОТ = Макет.ПолучитьОбласть("ДолжностиПодвал|ДоФОТ");
	УстановитьЗначениеПараметраКоличествоСтавок(ДолжностиПодвалДоФОТ, ТаблицаДолжностей.Итог("КоличествоСтавок"));
	ДокументРезультат.Вывести(ДолжностиПодвалДоФОТ);
	
	Если ИспользоватьРасчетЗарплатыРасширенная Тогда
		Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
			ДолжностиПодвалФОТ = Макет.ПолучитьОбласть("ДолжностиПодвал|ФОТМинМакс");
			Если ФОТДоступен Тогда
				ДолжностиПодвалФОТ.Параметры.ПлановыйФОТМин = ТаблицаДолжностей.Итог("ПлановыйФОТМин");
				ДолжностиПодвалФОТ.Параметры.ПлановыйФОТМакс = ТаблицаДолжностей.Итог("ПлановыйФОТМакс");
			КонецЕсли;
		Иначе
			ДолжностиПодвалФОТ = Макет.ПолучитьОбласть("ДолжностиПодвал|ФОТ");
			Если ФОТДоступен Тогда
				ДолжностиПодвалФОТ.Параметры.ПлановыйФОТ = ТаблицаДолжностей.Итог("ПлановыйФОТ");
			КонецЕсли;
		КонецЕсли;
		ДокументРезультат.Присоединить(ДолжностиПодвалФОТ);
	КонецЕсли; 
	
	ДолжностиПодвалПослеФОТ = Макет.ПолучитьОбласть("ДолжностиПодвал|ПослеФОТ");
	ДокументРезультат.Присоединить(ДолжностиПодвалПослеФОТ);
	
	// Подвал отчета
	
	ПодвалДоФОТ = Макет.ПолучитьОбласть("Подвал|ДоФОТ");
	ПодвалДоФОТ.Параметры.ФИООтветственного = ПараметрыОтчета.ФИООтветственного;
	ДокументРезультат.Вывести(ПодвалДоФОТ);
	
	Если ИспользоватьРасчетЗарплатыРасширенная Тогда
		Если ИспользоватьВилкуСтавокВШтатномРасписании Тогда
			ПодвалФОТ = Макет.ПолучитьОбласть("Подвал|ФОТМинМакс");
		Иначе
			ПодвалФОТ = Макет.ПолучитьОбласть("Подвал|ФОТ");
		КонецЕсли;
		ДокументРезультат.Вывести(ПодвалФОТ);
	КонецЕсли; 
	
	ПодвалПослеФОТ = Макет.ПолучитьОбласть("Подвал|ПослеФОТ");
	ДокументРезультат.Вывести(ПодвалПослеФОТ);
	
КонецПроцедуры

Процедура УстановитьЗначениеПараметраКоличествоСтавок(Макет, КоличествоСтавок)
	
	Макет.Параметры.КоличествоСтавок = Формат(КоличествоСтавок, УправлениеШтатнымРасписанием.ФорматКоличестваСтавок());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Соблюдение штатного расписания.

Процедура ВывестиОтчетСоблюдениеШтатногоРасписания(ДокументРезультат, Данные)
	
	Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
		ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.ШтатноеРасписание.ПФ_MXL_СоблюдениеШтатногоРасписания");
	
	ТаблицаДолжностей = Новый ТаблицаЗначений;
	ТаблицаДолжностей.Колонки.Добавить("Должность", Новый ОписаниеТипов("СправочникСсылка.Должности"));
	ТаблицаДолжностей.Колонки.Добавить("КоличествоСтавок", Новый ОписаниеТипов("Число"));
	ТаблицаДолжностей.Колонки.Добавить("КоличествоЗанятыхСтавок", Новый ОписаниеТипов("Число"));
	ТаблицаДолжностей.Колонки.Добавить("ДолжностьРеквизитДопУпорядочивания", Новый ОписаниеТипов("Число"));
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	Шапка.Параметры.Заполнить(Данные);
	Шапка.Параметры.ДатаАктуальности = Формат(Данные.ПараметрыДанныхДатаАктуальности, "ДЛФ=D");
	ДокументРезультат.Вывести(Шапка);
	
	ПодразделенияЗаголовок = Макет.ПолучитьОбласть("ПодразделенияЗаголовок");
	ДокументРезультат.Вывести(ПодразделенияЗаголовок);
	
	Для каждого СтрокаПодразделения Из Данные.Строки Цикл
		Подразделения = Макет.ПолучитьОбласть("Подразделения");
		Подразделения.Параметры.Заполнить(СтрокаПодразделения);
		УстановитьОтклонениеВМакете(Подразделения, СтрокаПодразделения.КоличествоСтавок, СтрокаПодразделения.КоличествоЗанятыхСтавок, Истина);
		ДокументРезультат.Вывести(Подразделения);
		Для каждого СтрокаДолжности Из СтрокаПодразделения.Строки Цикл
			ПодразделенияДетали = Макет.ПолучитьОбласть("ПодразделенияДетали");
			ЗаполнитьЗначенияСвойств(ТаблицаДолжностей.Добавить(), СтрокаДолжности);
			ПодразделенияДетали.Параметры.Заполнить(СтрокаДолжности);
			УстановитьОтклонениеВМакете(ПодразделенияДетали, СтрокаДолжности.КоличествоСтавок, СтрокаДолжности.КоличествоЗанятыхСтавок);
			ДокументРезультат.Вывести(ПодразделенияДетали);
		КонецЦикла;
	КонецЦикла;
	
	ТаблицаДолжностей.Свернуть("Должность,ДолжностьРеквизитДопУпорядочивания", "КоличествоСтавок,КоличествоЗанятыхСтавок");
	ТаблицаДолжностей.Сортировать("ДолжностьРеквизитДопУпорядочивания");
	
	ДолжностиЗаголовок = Макет.ПолучитьОбласть("ДолжностиЗаголовок");
	ДокументРезультат.Вывести(ДолжностиЗаголовок);
	
	Для каждого СтрокаТаблицыДолжностей Из ТаблицаДолжностей Цикл
		Должности = Макет.ПолучитьОбласть("Должности");
		Должности.Параметры.Заполнить(СтрокаТаблицыДолжностей);
		УстановитьОтклонениеВМакете(Должности, СтрокаТаблицыДолжностей.КоличествоСтавок, СтрокаТаблицыДолжностей.КоличествоЗанятыхСтавок);
		ДокументРезультат.Вывести(Должности);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьОтклонениеВМакете(Макет, КоличествоСтавок, КоличествоЗанятыхСтавок, МакетПодразделения = Ложь)
	
	Макет.Параметры.КоличествоСтавок = Формат(КоличествоСтавок, УправлениеШтатнымРасписанием.ФорматКоличестваСтавок());
	Макет.Параметры.КоличествоЗанятыхСтавок = Формат(КоличествоЗанятыхСтавок, УправлениеШтатнымРасписанием.ФорматКоличестваСтавок());
	
	Отклонение = ?(КоличествоСтавок = Null, 0, КоличествоСтавок)
		- ?(КоличествоЗанятыхСтавок = Null, 0, КоличествоЗанятыхСтавок);
		
	КомментарийКПозиции = "";
	
	Если Отклонение < 0 Тогда
		
		Если ?(КоличествоСтавок = Null, 0, КоличествоСтавок) = 0 Тогда
			Макет.Область(1, 1, 1, Макет.ШиринаТаблицы - 1).ЦветТекста = ЦветаСтиля.ПоясняющийОшибкуТекст;
			Если МакетПодразделения Тогда
				КомментарийКПозиции = НСтр("ru='Нет позиций штатного расписания';uk='Немає позицій штатного розкладу'");
			Иначе
				КомментарийКПозиции = НСтр("ru='Не используется в штатном расписании';uk='Не використовується в штатному розкладі'");
			КонецЕсли;
		Иначе
			КомментарийКПозиции = НСтр("ru='Превышена штатная численность';uk='Перевищена штатна чисельність'");
		КонецЕсли; 
		
	КонецЕсли;
	
	Макет.Параметры.Отклонение = Формат(Отклонение, УправлениеШтатнымРасписанием.ФорматКоличестваСтавок());
	Макет.Параметры.Комментарий = КомментарийКПозиции;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли