
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("МаршрутнаяКарта", Отчет.МаршрутнаяКарта) Тогда
		ПолучитьМаршрутнуюКартуИзНастроек = Ложь;
		СформироватьОтчет();
	Иначе
		ПолучитьМаршрутнуюКартуИзНастроек = Истина;
		УстановитьСостояниеОтчета(Элементы.ПолеТабличногоДокумента, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	КлючНастройки = "Отчет.МаршрутнаяКарта";
	Если НЕ ПолучитьМаршрутнуюКартуИзНастроек
		И НЕ Настройки.Получить(КлючНастройки) = Неопределено Тогда
		
		Настройки.Удалить(КлючНастройки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура МаршрутнаяКартаПриИзменении(Элемент)
	
	УстановитьСостояниеОтчета(Элементы.ПолеТабличногоДокумента, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВложенныеМаршрутыПриИзменении(Элемент)
	
	УстановитьСостояниеОтчета(Элементы.ПолеТабличногоДокумента, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчет();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьОтчет()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	ОтчетОбъект.СформироватьОтчет(ПолеТабличногоДокумента);
	
	УстановитьСостояниеОтчета(Элементы.ПолеТабличногоДокумента, Истина);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСостояниеОтчета(ПолеТабличногоДокумента, Сформирован)
	
	Если НЕ Сформирован Тогда
		Состояние = "НеАктуальность";
	Иначе
		Состояние = "НеИспользовать";
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ПолеТабличногоДокумента, Состояние);
	
КонецПроцедуры

#КонецОбласти
