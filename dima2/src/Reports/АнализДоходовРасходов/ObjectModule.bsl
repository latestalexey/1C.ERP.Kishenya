#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриОпределенииПараметровВыбора = Истина;
КонецПроцедуры

// Вызывается в форме отчета перед выводом настройки.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   СвойстваНастройки - Структура - Описание настройки отчета, которая будет выведена в форме отчета.
//       * ОписаниеТипов - ОписаниеТипов -
//           Тип настройки.
//       * ЗначенияДляВыбора - СписокЗначений -
//           Объекты, которые будут предложены пользователю в списке выбора.
//           Дополняет список объектов, уже выбранных пользователем ранее.
//       * ЗапросЗначенийВыбора - Запрос -
//           Возвращает объекты, которыми необходимо дополнить ЗначенияДляВыбора.
//           Первой колонкой (с 0м индексом) должен выбираться объект,
//           который следует добавить в ЗначенияДляВыбора.Значение.
//           Для отключения автозаполнения
//           в свойство ЗапросЗначенийВыбора.Текст следует записать пустую строку.
//       * ОграничиватьВыборУказаннымиЗначениями - Булево -
//           Когда Истина, то выбор пользователя будет ограничен значениями,
//           указанными в ЗначенияДляВыбора (его конечным состоянием).
//
Процедура ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки) Экспорт
	Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
		И СвойстваНастройки.ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных.ТипАнализа") Тогда
		НастроитьПараметрыОтборыПоФункциональнымОпциям(СвойстваНастройки);
	КонецЕсли;
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;

	УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы);
	
	//++ НЕ УТ
	СхемаКомпоновкиДанных.НаборыДанных.ДоходыИРасходы.Элементы.Доходы.Запрос = ТекстЗапросаДоходы();
	СхемаКомпоновкиДанных.НаборыДанных.ДоходыИРасходы.Элементы.Расходы.Запрос = ТекстЗапросаРасходы();
	СхемаКомпоновкиДанных.НаборыДанных.ДоходыИРасходы.Элементы.ТранспортноЗаготовительныеРасходы.Запрос = ТекстЗапросаТранспортноЗаготовительныеРасходы();
	СхемаКомпоновкиДанных.НаборыДанных.ДоходыИРасходы.Элементы.РасходыБудущихПериодов.Запрос = ТекстЗапросаРасходыБудущихПериодов();
	//-- НЕ УТ
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	КомпоновкаДанныхСервер.УстановитьЗаголовкиМакетаКомпоновки(СтруктураЗаголовковПолей(), МакетКомпоновки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КомпоновкаДанныхСервер.СкрытьВспомогательныеПараметрыОтчета(СхемаКомпоновкиДанных, КомпоновщикНастроек, ДокументРезультат, ВспомогательныеПараметрыОтчета());
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьОбязательныеНастройки(ПользовательскиеНастройкиМодифицированы)
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	ПараметрВалютаУправленческогоУчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВалютаУправленческогоУчета");
	Если ПараметрВалютаУправленческогоУчета <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ВалютаУправленческогоУчета", ВалютаУправленческогоУчета);
	КонецЕсли;
	
	ПараметрВалюта = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Валюта");
	Если ПараметрВалюта <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Валюта", ВалютаУправленческогоУчета);
	КонецЕсли;
	
	ПараметрПоказыватьПродажи = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПоказыватьПродажи");
	Если ПараметрПоказыватьПродажи <> Неопределено 
		И ПараметрПоказыватьПродажи.Значение = Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПоказыватьПродажи", 0);
	КонецЕсли;
	
	ПараметрВключаяЗапланированныеПродажи = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВключаяЗапланированныеПродажи");
	Если ПараметрВключаяЗапланированныеПродажи.Использование
	 И ПараметрВключаяЗапланированныеПродажи.Значение Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПланируемыеПродажи", Истина);
	Иначе
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПланируемыеПродажи", Ложь);
	КонецЕсли;
	
	ПараметрВключаяЗапланированныеРасходы = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВключаяЗапланированныеРасходы");
	Если ПараметрВключаяЗапланированныеРасходы.Использование
	 И ПараметрВключаяЗапланированныеРасходы.Значение Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПланируемыеРасходы", Истина);
	Иначе
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПланируемыеРасходы", Ложь);
	КонецЕсли;
	
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	ПользовательскиеНастройкиМодифицированы = Истина;
КонецПроцедуры

Функция СтруктураЗаголовковПолей()
	СтруктураЗаголовковПолей = Новый Структура;
	
	ПараметрТипАнализа = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ТипАнализа");
	ДоступнаяНастройка = ОтчетыКлиентСервер.НайтиДоступнуюНастройку(КомпоновщикНастроек.Настройки, ПараметрТипАнализа);
	Если ДоступнаяНастройка <> Неопределено Тогда
		ПредставлениеЗначенияПараметра = ДоступнаяНастройка.ДоступныеЗначения[ПараметрТипАнализа.Значение - 1];
		СтруктураЗаголовковПолей.Вставить("ТипАнализа", ПредставлениеЗначенияПараметра);
	КонецЕсли;
	
	ВалютаУправленческогоУчета = Константы.ВалютаУправленческогоУчета.Получить();
	СтруктураЗаголовковПолей.Вставить("ВалютаОтчета", 
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(%1)", ВалютаУправленческогоУчета));
	
	Возврат СтруктураЗаголовковПолей;
КонецФункции

Функция ВспомогательныеПараметрыОтчета()
	ВспомогательныеПараметры = Новый Массив;
	
	ВспомогательныеПараметры.Добавить("ТипАнализа");
	
	КомпоновкаДанныхСервер.ДобавитьВспомогательныеПараметрыОтчетаПоФункциональнымОпциям(ВспомогательныеПараметры);
	
	Возврат ВспомогательныеПараметры;
КонецФункции

//++ НЕ УТ
Функция ТекстЗапросаДоходы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сегменты.Партнер КАК Партнер,
	|	ИСТИНА КАК ИспользуетсяОтборПоСегментуПартнеров
	|ПОМЕСТИТЬ ОтборПоСегментуПартнеров
	|ИЗ
	|	РегистрСведений.ПартнерыСегмента КАК Сегменты
	|{ГДЕ
	|	Сегменты.Сегмент.* КАК СегментПартнеров,
	|	Сегменты.Партнер.* КАК Партнер}
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ИспользуетсяОтборПоСегментуПартнеров
	|;
	|	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаПрочиеДоходы КАК Раздел,
	|	&СтрокаДоходы КАК СтандартныйРаздел,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	ПрочиеДоходы.СтатьяДоходов,
	|	ПрочиеДоходы.АналитикаДоходов,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК СтатьяРасходов,
	|	ПрочиеДоходы.Организация,
	|	ПрочиеДоходы.Подразделение,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов.Склад
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка) КАК Заказ,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеДоходы.АналитикаДоходов.Сделка <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов.Сделка
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ КАК СделкаСКлиентами,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов.Менеджер
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов.Партнер
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Справочник.Партнеры
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов.Соглашение
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Соглашение,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов.Договор
	|		ИНАЧЕ NULL
	|	КОНЕЦ КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.АналитикаДоходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|				И ПрочиеДоходы.АналитикаДоходов <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			ТОГДА ПрочиеДоходы.АналитикаДоходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|	КОНЕЦ КАК ОбъектЭксплуатации,
	|	ПрочиеДоходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеДоходы.ПериодСекунда КАК Период,
	|	ПрочиеДоходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеДоходы.ПериодДень КАК ПериодДень,
	|	ПрочиеДоходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеДоходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеДоходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеДоходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеДоходы.ПериодГод КАК ПериодГод,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.Регистратор = НЕОПРЕДЕЛЕНО
	|			ТОГДА NULL
	|		ИНАЧЕ ПрочиеДоходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|
	|	ПрочиеДоходы.СуммаНачальныйОстаток КАК ОстатокДоходовПрошлогоПериода,
	|	ПрочиеДоходы.СуммаПриход КАК СуммаДоходов,
	|	ПрочиеДоходы.СуммаРасход КАК РаспределеноДоходов,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.СуммаНачальныйОстаток + ПрочиеДоходы.СуммаПриход >= ПрочиеДоходы.СуммаРасход
	|				ИЛИ ПрочиеДоходы.СуммаРасход = 0
	|			ТОГДА ПрочиеДоходы.СуммаКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НеРаспределеноДоходов,
	|	ВЫБОР
	|		КОГДА ПрочиеДоходы.СуммаНачальныйОстаток + ПрочиеДоходы.СуммаПриход < ПрочиеДоходы.СуммаРасход
	|				И ПрочиеДоходы.СуммаРасход <> 0
	|			ТОГДА -ПрочиеДоходы.СуммаКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РаспределениеПревышаетДоходы
	|ИЗ
	|	РегистрНакопления.ПрочиеДоходы.ОстаткиИОбороты(, , Авто, , {(АналитикаДоходов В
	|			        (ВЫБРАТЬ
	|			            ОтборПоСегментуПартнеров.Партнер
	|			        ИЗ
	|			            ОтборПоСегментуПартнеров
	|			        ГДЕ
	|			            ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров)
	|			    ИЛИ АналитикаДоходов.Партнер В
	|			        (ВЫБРАТЬ
	|			            ОтборПоСегментуПартнеров.Партнер
	|			        ИЗ
	|			            ОтборПоСегментуПартнеров
	|			        ГДЕ
	|			            ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров)) КАК Поле2}) КАК ПрочиеДоходы";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРасходы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сегменты.Партнер КАК Партнер,
	|	ИСТИНА КАК ИспользуетсяОтборПоСегментуПартнеров
	|ПОМЕСТИТЬ ОтборПоСегментуПартнеров
	|ИЗ
	|	РегистрСведений.ПартнерыСегмента КАК Сегменты
	|{ГДЕ
	|	Сегменты.Сегмент.* КАК СегментПартнеров,
	|	Сегменты.Партнер.* КАК Партнер}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ИспользуетсяОтборПоСегментуПартнеров
	|;
	|	
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаПрочиеРасходы КАК Раздел,
	|	&СтрокаРасходы КАК СтандартныйРаздел,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
	|	ПрочиеРасходы.СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов,
	|	
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Склад
	|		ИНАЧЕ Значение(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|		ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ Значение(Документ.ЗаказКлиента.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.СделкиСКлиентами
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Сделка
	|		ИНАЧЕ Значение(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ КАК СделкаСКлиентами,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Менеджер
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказНаПеремещение
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
	//++ НЕ УТКА
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказНаПроизводство
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	//-- НЕ УТКА
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПеремещениеТоваров
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Ответственный
	|		ИНАЧЕ Значение(Справочник.Пользователи.ПустаяСсылка)
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.Партнеры
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.СделкиСКлиентами
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Партнер
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Партнер
	|		ИНАЧЕ Значение(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.Номенклатура
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.ОбъектыЭксплуатации
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ОбъектыЭксплуатации.ПустаяСсылка)
	|	КОНЕЦ КАК ОбъектЭксплуатации,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.Регистратор = НЕОПРЕДЕЛЕНО
	|			ТОГДА NULL
	|		ИНАЧЕ ПрочиеРасходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	ПрочиеРасходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеРасходы.ПериодСекунда КАК Период,
	|	ПрочиеРасходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеРасходы.ПериодДень КАК ПериодДень,
	|	ПрочиеРасходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеРасходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеРасходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеРасходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеРасходы.ПериодГод КАК ПериодГод,
	|	
	|	ПрочиеРасходы.СуммаНачальныйОстаток КАК ОстатокРасходовПрошлогоПериода,
	|	ПрочиеРасходы.СуммаПриход КАК СуммаРасходов,
	|	ПрочиеРасходы.СуммаРасход КАК РаспределеноРасходов,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СуммаНачальныйОстаток + ПрочиеРасходы.СуммаПриход >= ПрочиеРасходы.СуммаРасход
	|				ИЛИ ПрочиеРасходы.СуммаРасход = 0
	|			ТОГДА ПрочиеРасходы.СуммаКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НеРаспределеноРасходов,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СуммаНачальныйОстаток + ПрочиеРасходы.СуммаПриход < ПрочиеРасходы.СуммаРасход
	|				И ПрочиеРасходы.СуммаРасход <> 0
	|			ТОГДА -ПрочиеРасходы.СуммаКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РаспределениеПревышаетРасходы
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты(, , Авто, , 
	|			СтатьяРасходов.ВариантРаспределенияРасходов В (
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы), 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НеРаспределять))
	|			{(АналитикаРасходов В
	|			        (ВЫБРАТЬ
	|			            ОтборПоСегментуПартнеров.Партнер
	|			        ИЗ
	|			            ОтборПоСегментуПартнеров
	|			        ГДЕ
	|			            ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров)
	|			    ИЛИ АналитикаРасходов.Партнер В
	|			        (ВЫБРАТЬ
	|			            ОтборПоСегментуПартнеров.Партнер
	|			        ИЗ
	|			            ОтборПоСегментуПартнеров
	|			        ГДЕ
	|			            ОтборПоСегментуПартнеров.ИспользуетсяОтборПоСегментуПартнеров = &ИспользуетсяОтборПоСегментуПартнеров))}) КАК ПрочиеРасходы";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаТранспортноЗаготовительныеРасходы()
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаТЗР КАК Раздел,
	|	&СтрокаРасходы КАК СтандартныйРаздел,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
	|	ПрочиеРасходы.СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов,
	|	
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Склад
	|		ИНАЧЕ Значение(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|		ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ Значение(Документ.ЗаказКлиента.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.СделкиСКлиентами
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Сделка
	|		ИНАЧЕ Значение(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ КАК СделкаСКлиентами,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Менеджер
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказНаПеремещение
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
	//++ НЕ УТКА
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказНаПроизводство
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	//-- НЕ УТКА
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПеремещениеТоваров
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Ответственный
	|		ИНАЧЕ Значение(Справочник.Пользователи.ПустаяСсылка)
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.Партнеры
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.СделкиСКлиентами
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Партнер
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Партнер
	|		ИНАЧЕ Значение(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.Номенклатура
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.Регистратор = НЕОПРЕДЕЛЕНО
	|			ТОГДА NULL
	|		ИНАЧЕ ПрочиеРасходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	ПрочиеРасходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеРасходы.ПериодСекунда КАК Период,
	|	ПрочиеРасходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеРасходы.ПериодДень КАК ПериодДень,
	|	ПрочиеРасходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеРасходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеРасходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеРасходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеРасходы.ПериодГод КАК ПериодГод,
	|	
	|	ПрочиеРасходы.СтоимостьПриход КАК СуммаРасходов,
	|	ПрочиеРасходы.СтоимостьРасход КАК РаспределеноРасходов,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СтоимостьНачальныйОстаток + ПрочиеРасходы.СтоимостьПриход >= ПрочиеРасходы.СтоимостьРасход
	|				ИЛИ ПрочиеРасходы.СтоимостьРасход = 0
	|			ТОГДА ПрочиеРасходы.СтоимостьКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК НеРаспределеноРасходов,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.СтоимостьНачальныйОстаток + ПрочиеРасходы.СтоимостьПриход < ПрочиеРасходы.СтоимостьРасход
	|				И ПрочиеРасходы.СтоимостьРасход <> 0
	|			ТОГДА -ПрочиеРасходы.СтоимостьКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РаспределениеПревышаетРасходы
	|ИЗ
	|	РегистрНакопления.ПартииПрочихРасходов.ОстаткиИОбороты(, , Авто, , СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)) КАК ПрочиеРасходы";
	
	Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаРасходыБудущихПериодов()
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&СтрокаРБП КАК Раздел,
	|	&СтрокаРасходы КАК СтандартныйРаздел,
	|	&ВалютаУправленческогоУчета КАК Валюта,
	|	
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиДоходов.ПустаяСсылка) КАК СтатьяДоходов,
	|	ПрочиеРасходы.СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов,
	|	
	|	ПрочиеРасходы.Организация,
	|	ПрочиеРасходы.Подразделение,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Склад
	|		ИНАЧЕ Значение(Справочник.Склады.ПустаяСсылка)
	|	КОНЕЦ КАК Склад,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|		ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ Значение(Документ.ЗаказКлиента.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.СделкиСКлиентами
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Сделка
	|		ИНАЧЕ Значение(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|	КОНЕЦ КАК СделкаСКлиентами,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Менеджер
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказНаПеремещение
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказНаПеремещение.ПустаяСсылка)
	//++ НЕ УТКА
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказНаПроизводство
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка)
	//-- НЕ УТКА
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПеремещениеТоваров
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Ответственный
	|		ИНАЧЕ Значение(Справочник.Пользователи.ПустаяСсылка)
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.Партнеры
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.СделкиСКлиентами
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Партнер
	|		КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказКлиента
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.РеализацияТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ЗаказПоставщику
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПередачаТоваровМеждуОрганизациями
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка)
	|			ИЛИ ПрочиеРасходы.АналитикаРасходов ССЫЛКА Документ.ПоступлениеТоваровУслуг
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Документ.ПоступлениеТоваровУслуг.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов.Партнер
	|		ИНАЧЕ Значение(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР КОГДА ПрочиеРасходы.АналитикаРасходов ССЫЛКА Справочник.Номенклатура
	|				И ПрочиеРасходы.АналитикаРасходов <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ПрочиеРасходы.АналитикаРасходов
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	КОНЕЦ КАК Номенклатура,
	|	
	|	ВЫБОР
	|		КОГДА ПрочиеРасходы.Регистратор = НЕОПРЕДЕЛЕНО
	|			ТОГДА NULL
	|		ИНАЧЕ ПрочиеРасходы.Регистратор
	|	КОНЕЦ КАК Регистратор,
	|	ПрочиеРасходы.ПериодСекунда КАК ПериодСекунда,
	|	ПрочиеРасходы.ПериодСекунда КАК Период,
	|	ПрочиеРасходы.ПериодМесяц КАК ПериодМесяц,
	|	ПрочиеРасходы.ПериодДень КАК ПериодДень,
	|	ПрочиеРасходы.ПериодНеделя КАК ПериодНеделя,
	|	ПрочиеРасходы.ПериодДекада КАК ПериодДекада,
	|	ПрочиеРасходы.ПериодКвартал КАК ПериодКвартал,
	|	ПрочиеРасходы.ПериодПолугодие КАК ПериодПолугодие,
	|	ПрочиеРасходы.ПериодГод КАК ПериодГод,
	|	
	|	ПрочиеРасходы.СуммаНачальныйОстаток КАК ОстатокРасходовПрошлогоПериода,
	|	ПрочиеРасходы.СуммаПриход КАК СуммаРасходов,
	|	ПрочиеРасходы.СуммаРасход КАК РаспределеноРасходов,
	|	ПрочиеРасходы.СуммаКонечныйОстаток КАК НеРаспределеноРасходов
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.ОстаткиИОбороты(, , Авто, , СтатьяРасходов.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов)) КАК ПрочиеРасходы";
	
	Возврат ТекстЗапроса;
КонецФункции
//-- НЕ УТ

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(СвойстваНастройки)
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(2);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодразделения") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(3);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(4);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ОтветственныйВДокументах") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(5);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(7);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСделкиСКлиентами") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(8);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		НайденныйЭлементСписка = СвойстваНастройки.ЗначенияДляВыбора.НайтиПоЗначению(10);
		СвойстваНастройки.ЗначенияДляВыбора.Удалить(НайденныйЭлементСписка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли