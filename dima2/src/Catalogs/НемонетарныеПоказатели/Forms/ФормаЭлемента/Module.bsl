
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Набор = РегистрыСведений.ЗначенияНемонетарныхПоказателей.СоздатьНаборЗаписей();
		Набор.Отбор.Показатель.Установить(Объект.Ссылка);
		Набор.Прочитать();
		ЗначенияПоказателя = Набор.Выгрузить();
		ЗначениеВРеквизитФормы(ЗначенияПоказателя,"НаборЗначений");
	КонецЕсли;
	
	ОбновитьЗаголовокФормы();
	БуферНаименования = Объект.Наименование;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НомерСтроки = 1;
	Для Каждого Стр Из НаборЗначений Цикл
		
		Если НЕ ЗначениеЗаполнено(Стр.Период) Тогда
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Не указана дата начала действия значения в строке №%1';uk='Не вказана дата початку дії значення в рядку №%1'"),
				НомерСтроки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				Текст,
				Объект.Ссылка,
				ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НаборЗначений", НомерСтроки, "Период"),
				,
				Отказ);
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(Стр.Автор) Тогда
			Стр.Автор = Пользователи.ТекущийПользователь();
		КонецЕсли;
	    НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	ОбновитьЗаголовокФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Набор = РегистрыСведений.ЗначенияНемонетарныхПоказателей.СоздатьНаборЗаписей();
	Набор.Отбор.Показатель.Установить(Объект.Ссылка);
	ЗначенияПоказателя = РеквизитФормыВЗначение("НаборЗначений");
	ЗначенияПоказателя.ЗаполнитьЗначения(Объект.Ссылка,"Показатель");
	Набор.Загрузить(ЗначенияПоказателя);
	Набор.Записать();

	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_НемонетарныйПоказатель", , Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если БуферНаименования = Объект.НаименованиеДляПечати ИЛИ ПустаяСтрока(Объект.НаименованиеДляПечати) Тогда
		Объект.НаименованиеДляПечати = Объект.Наименование;
	КонецЕсли;
	БуферНаименования = Объект.Наименование;
	
КонецПроцедуры

&НаКлиенте
Процедура КоментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНаборзначений

&НаКлиенте
Процедура НаборЗначенийПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		СтрокаТаблицы = Элементы.НаборЗначений.ТекущиеДанные;
		СтрокаТаблицы.Период = ТекущаяДата();				
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НаборЗначенийОрганизация.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗначений.Организация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<Использовать для всех>';uk='<Використовувати для всіх>'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НаборЗначенийПодразделение.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НаборЗначений.Подразделение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<Использовать для всех>';uk='<Використовувати для всіх>'"));

КонецПроцедуры

#Область Прочее

&НаСервере
Процедура ОбновитьЗаголовокФормы()
	
	ПредставлениеТипа = НСтр("ru='Немонетарный показатель';uk='Немонетарний показник'");
	ВидУчета = "";
	
	ПредставлениеТипа = ПредставлениеТипа + " " + ВидУчета;
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭтаФорма.Заголовок = ПредставлениеТипа + НСтр("ru=' (создание)';uk=' (створення)'"); ;
	Иначе
		ЭтаФорма.Заголовок = Объект.НаименованиеДляПечати + " (" + ПредставлениеТипа + ")";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
