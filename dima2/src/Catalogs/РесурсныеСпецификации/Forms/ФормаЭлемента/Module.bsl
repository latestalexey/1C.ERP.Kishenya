&НаКлиенте
Перем КэшированныеЗначения;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	ОсновнаяЕдиницаВремениБуфера = Справочники.ЭтапыПроизводства.ОсновнаяЕдиницаВремениБуфера();
	ОсновнаяЕдиницаВремениДлительностиЭтапаУББВ = Справочники.ЭтапыПроизводства.ОсновнаяЕдиницаВремениДлительностиЭтапаУББВ();
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
		
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
			И ТипЗнч(Параметры.ЗначениеКопирования) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
			ЗаполнитьЭтапыПоРесурснойСпецификации(Параметры.ЗначениеКопирования);
		КонецЕсли; 
	КонецЕсли;
	
	УправлениеДаннымиОбИзделиях.ИнициализацияАвтоподбораНоменклатуры(ЭтаФорма);
	УстановитьОтборВСпискеЭтапов();
	
	//++ НЕ УТКА
	Если Не ПравоДоступа("Изменение", Метаданные.Документы.РазрешениеНаЗаменуМатериалов) Тогда
		Элементы.МатериалыИУслугиВвестиРазрешениеНаЗамену.Доступность = Ложь;
	КонецЕсли;
	//-- НЕ УТКА
	
	Если НЕ ПолучитьФункциональнуюОпцию("УправлениеПредприятием") Тогда
		Элементы.СтраницыРазбиватьМаршрутныеЛисты.Видимость = Ложь;
		Элементы.ГруппаОптимальноеКоличествоПередачиМеждуЭтапамиМногоэтапный.Видимость = Ложь;
		Элементы.ГруппаДлительностьПроизводства2.Видимость = Ложь;
		Элементы.ВыходныеИзделияТребуетсяУказыватьСерии.Видимость = Ложь;
		Элементы.МатериалыИУслугиТребуетсяУказыватьСерии.Видимость = Ложь;
	КонецЕсли; 
	
	//++ НЕ УТКА
	Если НЕ ПравоДоступа("Просмотр", Метаданные.ОбщиеФормы.ПрименениеНоменклатурыВПроизводстве) Тогда
		Элементы.ВыходныеИзделияОткрытьСпецификацииИзделия.Видимость = Ложь;
	КонецЕсли; 
	//-- НЕ УТКА
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	ДополнительныеПараметры.Вставить("ОтложеннаяИнициализация", Истина);
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	// Обработчик подсистемы "Внешние обработки"
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	
	// МенюОтчеты
	МенюОтчеты.ПриСозданииНаСервере(ЭтаФорма, Элементы.ПодменюОтчеты,, Ложь);
	// Конец МенюОтчеты
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтаФорма);
	// Конец ИнтеграцияС1СДокументооборотом
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Запишем данные этапа если это одноэтапный процесс
	Если НЕ Объект.МногоэтапныйПроизводственныйПроцесс 
		И НЕ Объект.ПометкаУдаления Тогда
		
		ПроизводственныйПроцессОбъект = РеквизитФормыВЗначение("ПроизводственныйПроцесс");
		
		//++ НЕ УТКА
		УправлениеДаннымиОбИзделиях.СохранитьВидыРабочихЦентров(
					ВидыРабочихЦентров, 
					ПроизводственныйПроцессОбъект.ПланироватьРаботуВидовРабочихЦентров,
					ПроизводственныйПроцессОбъект.ВидыРабочихЦентров,
					ПроизводственныйПроцессОбъект.АльтернативныеВидыРабочихЦентров);
		//-- НЕ УТКА
		
		Если Объект.Статус = Перечисления.СтатусыСпецификаций.Действует Тогда
			
			РеквизитыЭтапа = Новый Структура;
			РеквизитыЭтапа.Вставить("ПроизводствоНаСтороне", ПроизводственныйПроцессОбъект.ПроизводствоНаСтороне);
			
			ТекущийОбъект.ПроверитьЗаполнениеСпецификацииПоРеквизитамЭтапов(Отказ, РеквизитыЭтапа);
			
			ПроизводственныйПроцессОбъект.ПроверитьЗаполнениеРеквизитов(Ложь, "ПроизводственныйПроцесс", Отказ);
			
		КонецЕсли; 
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли; 
		
		ПроизводственныйПроцессОбъект.ДополнительныеСвойства.Вставить("РазрешитьЗапись");
		
		ПроизводственныйПроцессОбъект.НомерЭтапа            = 1;
		ПроизводственныйПроцессОбъект.НомерСледующегоЭтапа  = 0;
		ПроизводственныйПроцессОбъект.Владелец              = Объект.Ссылка;
		
		Если ПустаяСтрока(ПроизводственныйПроцессОбъект.Наименование) Тогда
			ПроизводственныйПроцессОбъект.Наименование = НСтр("ru='Производство';uk='Виробництво'");
		КонецЕсли;
		
		ПроизводственныйПроцессОбъект.МаксимальноеКоличествоЕдиницПартийИзделия = МаксимальноеКоличествоЕдиницПартийИзделия;
		
		Если ТекущийОбъект.ЭтоНовый() Тогда
			СсылкаНового = Справочники.РесурсныеСпецификации.ПолучитьСсылку();
			ПроизводственныйПроцессОбъект.Владелец = СсылкаНового;
			ПроизводственныйПроцессОбъект.ОбменДанными.Загрузка = Истина;
			ТекущийОбъект.УстановитьСсылкуНового(СсылкаНового);
		КонецЕсли; 
		
		ПроизводственныйПроцессОбъект.Записать();
		
	КонецЕсли;
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	
	ПрочитатьДанныеПриОдноэтапномПроизводстве();
	
	ЗаполнитьСлужебныеРеквизиты();
	
	СтатусЗаписанногоОбъекта = ТекущийОбъект.Статус;
	Элементы.ЭтапыСписок.ИзменятьСоставСтрок = (Объект.Статус = Перечисления.СтатусыСпецификаций.ВРазработке);
	
	УстановитьОтборВСпискеЭтапов();
	
	УстановитьДоступностьКомандИзмененияСтатуса();
	
	Элементы.ГруппаОписание.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Описание);
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_РесурсныеСпецификации");
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	СтатусЗаписанногоОбъекта = ТекущийОбъект.Статус;
	Элементы.ЭтапыСписок.ИзменятьСоставСтрок = (Объект.Статус = Перечисления.СтатусыСпецификаций.ВРазработке);
	
	УстановитьДоступностьКомандИзмененияСтатуса();
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора <> Неопределено 
		И ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ФормаНастройкиСтатейКалькуляции" Тогда
		
		Модифицированность = Истина;
		
		ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение.СтатьиКалькуляции);
		
		ЗаполнитьПредставлениеСтатейКалькуляцииВзносов(ЭтаФорма);
		
	//++ НЕ УТКА
	ИначеЕсли ИсточникВыбора <> Неопределено 
		И ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И ИсточникВыбора.ИмяФормы = "ОбщаяФорма.НастройкаАвтовыбораМатериала" Тогда
		
		ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
		Если УправлениеДаннымиОбИзделияхКлиент.НастроитьАвтоподборНоменклатурыЗавершение(ВыбранноеЗначение, ТекущиеДанные, Объект.СоответствиеСвойств) Тогда
			Модифицированность = Истина;
		КонецЕсли;
		
	ИначеЕсли ИсточникВыбора <> Неопределено 
		И ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И ИсточникВыбора.ИмяФормы = "ОбщаяФорма.НастройкаПотребленияМатериала" Тогда
		
		ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
		Если УправлениеДаннымиОбИзделияхКлиент.НастроитьПотреблениеНоменклатурыЗавершение(ВыбранноеЗначение, ТекущиеДанные) Тогда
			Модифицированность = Истина;
		КонецЕсли;
	//-- НЕ УТКА
		
	ИначеЕсли ИсточникВыбора <> Неопределено 
		И ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ВыборДействующихСпецификаций" Тогда
		
		ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		Модифицированность = Истина;
		
		ТекущиеДанные.ИсточникПолученияПолуфабриката = ВыбранноеЗначение;
		
	ИначеЕсли ИсточникВыбора <> Неопределено 
		И ТипЗнч(ИсточникВыбора) = Тип("УправляемаяФорма") 
		И (ИсточникВыбора.ИмяФормы = "Справочник.РесурсныеСпецификации.Форма.ФормаВыбораЭтапаВыпуска") Тогда
		
		ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда 
			Возврат;
		КонецЕсли;
		
		Модифицированность = Истина;
		
		ОбработкаВыбораЭтапаВыпуска(ТекущиеДанные, ВыбранноеЗначение);
		
	Иначе
		
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ЭтапыПроизводства" Тогда
		
		ОбработатьИзменениеЭтапаПроизводства(Источник);
		
	ИначеЕсли ИмяСобытия = "КопированиеСтрокВБуферОбмена" Тогда
		
		УстановитьДоступностьКомандБуфераОбменаНаКлиенте();
		
	//++ НЕ УТКА		
	ИначеЕсли ИмяСобытия = "Запись_МаршрутныеКарты" И Источник = ПроизводственныйПроцесс.МаршрутнаяКарта Тогда
		
		ПрочитатьНастройкуРазбиватьМаршрутныеЛисты(ЭтаФорма);
	//-- НЕ УТКА
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс
			ИЛИ ПроизводственныйПроцесс.ПроизводствоНаСтороне
			ИЛИ НЕ РазбиватьМаршрутныеЛисты
			//++ НЕ УТКА
			ИЛИ НЕ ПроизводственныйПроцесс.МаршрутнаяКарта.Пустая() 
			//-- НЕ УТКА
			Тогда
			
		МассивНепроверяемыхРеквизитов.Добавить("МаксимальноеКоличествоЕдиницПартийИзделия");
		
	КонецЕсли; 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	// СтандартныеПодсистемы.Свойства
	Если ТекущаяСтраница.Имя = "ГруппаДополнительно"
		И Не ЭтотОбъект.ПараметрыСвойств.ВыполненаОтложеннаяИнициализация Тогда
		
		СвойстваВыполнитьОтложеннуюИнициализацию();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#Область ПроизводственныйПроцесс

&НаКлиенте
Процедура ПроизводственныйПроцессМаршрутнаяКартаПриИзменении(Элемент)
	
	//++ НЕ УТКА
	ПрочитатьНастройкуРазбиватьМаршрутныеЛисты(ЭтаФорма);
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияПредварительногоБуфераПриИзменении(Элемент)
	
	Если ПроизводственныйПроцесс.ЕдиницаИзмеренияПредварительногоБуфера.Пустая() Тогда
		ПроизводственныйПроцесс.ЕдиницаИзмеренияПредварительногоБуфера = ОсновнаяЕдиницаВремениБуфера;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЕдиницаИзмеренияЗавершающегоБуфераПриИзменении(Элемент)
	
	Если ПроизводственныйПроцесс.ЕдиницаИзмеренияЗавершающегоБуфера.Пустая() Тогда
		ПроизводственныйПроцесс.ЕдиницаИзмеренияЗавершающегоБуфера = ОсновнаяЕдиницаВремениБуфера;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПроизводственногоПроцессаПриИзменении(Элемент)
	
	Объект.МногоэтапныйПроизводственныйПроцесс = Булево(ВидПроизводственногоПроцесса);
	
	ПриИзмененииВидаПроизводственногоПроцесса();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПроизводстваПриИзменении(Элемент)
	
	ПроизводственныйПроцесс.ПроизводствоНаСтороне = Булево(СпособПроизводства);
	
	Если ПроизводственныйПроцесс.ПроизводствоНаСтороне Тогда
		
		ПроизводственныйПроцесс.ПланироватьРаботуВидовРабочихЦентров = Ложь;
		
	КонецЕсли;
	
	ПриИзмененииСпособаПроизводства();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныйПроцессПланироватьРаботуВидовРабочихЦентровПриИзменении(Элемент)
	
	УстановитьВидимостьПоЗначениюПланироватьРаботуВидовРабочихЦентров(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныйПроцессЕдиницаИзмеренияДлительностиЭтапаПриИзменении(Элемент)
	
	Если ПроизводственныйПроцесс.ЕдиницаИзмеренияДлительностиЭтапа.Пустая() Тогда
		ПроизводственныйПроцесс.ЕдиницаИзмеренияДлительностиЭтапа = ОсновнаяЕдиницаВремениДлительностиЭтапаУББВ;
	КонецЕсли; 
	
	УстановитьПодсказкуЕдиницыИзмеренияДлительностиЭтапа(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныйПроцессПодразделениеПриИзменении(Элемент)
	
	ПодразделениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныйПроцессПредварительныйБуферПриИзменении(Элемент)
	
	УстановитьОтметкуНезаполненногоДляБуферов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныйПроцессЗавершающийБуферПриИзменении(Элемент)
	
	УстановитьОтметкуНезаполненногоДляБуферов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроизводственныйПроцессУслугаПереработчикаПриИзменении(Элемент)
	
	ТекущаяСтрока = Новый Структура("Номенклатура, Характеристика, ХарактеристикиИспользуются");
	ТекущаяСтрока.Номенклатура = ПроизводственныйПроцесс.УслугаПереработчика;
	ТекущаяСтрока.Характеристика = ПроизводственныйПроцесс.ХарактеристикаУслугиПереработчика;
	ТекущаяСтрока.ХарактеристикиИспользуются = ХарактеристикиИспользуются;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ПроизводственныйПроцесс.ХарактеристикаУслугиПереработчика);
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
	ХарактеристикиИспользуются = ТекущаяСтрока.ХарактеристикиИспользуются;
	Элементы.ПроизводственныйПроцессХарактеристикаУслугиПереработчика.Доступность = ХарактеристикиИспользуются;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИзменитьСтатьиКалькуляцииВзносовНажатие(Элемент)
    
    СтатьиКалькуляции = Новый Структура("СтатьяКалькуляцииВзносов");
	ЗаполнитьЗначенияСвойств(СтатьиКалькуляции, Объект);

	ПараметрыФормы = Новый Структура("СтатьиКалькуляции", СтатьиКалькуляции);
	
	ПараметрыФормы.Вставить("ТолькоПросмотр", Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует") Или
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Закрыта"));
	
	ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ФормаНастройкиСтатейКалькуляции", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыходныеИзделия

&НаКлиенте
Процедура ВыходныеИзделияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборПоляВТаблице("ВыходныеИзделия", Поле.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ВсегоИзделий = Объект.ВыходныеИзделия.Количество() + Объект.ВозвратныеОтходы.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияНоменклатураПриИзменении(Элемент)

	ТекущаяСтрока = Элементы.ВыходныеИзделия.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакСерииИспользуются", Новый Структура("Номенклатура", "СерииИспользуются"));
	
	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "ВыходныеИзделия"));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВыходныеИзделия.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияУпаковкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВыходныеИзделия.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияТребуетсяУказыватьСерииПриИзменении(Элемент)
	
	ВыходныеИзделияТребуетсяУказыватьСерииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		ЗаполнитьКоличествоИзделийСТипомСтоимостиРассчитывается(Объект.ВыходныеИзделия, 
																Объект.МногоэтапныйПроизводственныйПроцесс, 
																Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияПослеУдаления(Элемент)
	
	ЗаполнитьКоличествоИзделийСТипомСтоимостиРассчитывается(Объект.ВыходныеИзделия, 
															Объект.МногоэтапныйПроизводственныйПроцесс, 
															Объект.Ссылка);
															
	ВсегоИзделий = Объект.ВыходныеИзделия.Количество() + Объект.ВозвратныеОтходы.Количество();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВозвратныеОтходы

&НаКлиенте
Процедура ВозвратныеОтходыНоменклатураПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыУпаковкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ВозвратныеОтходы.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ВсегоИзделий = Объект.ВыходныеИзделия.Количество() + Объект.ВозвратныеОтходы.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратныеОтходыПослеУдаления(Элемент)
	ВсегоИзделий = Объект.ВыходныеИзделия.Количество() + Объект.ВозвратныеОтходы.Количество();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыИУслуги

&НаКлиенте
Процедура МатериалыИУслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ОбработатьВыборПоляВТаблице("МатериалыИУслуги", Поле.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		СтрокаТабличнойЧасти = Элементы.МатериалыИУслуги.ТекущиеДанные;
		
		КлючСвязиИсточника = ?(Копирование, СтрокаТабличнойЧасти.КлючСвязи, Неопределено);
		
		СтрокаТабличнойЧасти.КлючСвязи = Новый УникальныйИдентификатор;
		
		ЗаполнитьСлужебныеРеквизитыВСтрокеМатериалыИУслуги(СтрокаТабличнойЧасти);
		
		УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиМатериалов(
					СтрокаТабличнойЧасти, 
					КлючСвязиИсточника,
					Объект.СоответствиеСвойств);
					
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПередУдалением(Элемент, Отказ)
	
	//++ НЕ УТКА
	УправлениеДаннымиОбИзделияхКлиент.ПередУдалениемМатериалов(
				Элементы.МатериалыИУслуги, 
				Объект.МатериалыИУслуги,
				Объект.СоответствиеСвойств);
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущиеДанные.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущиеДанные.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакСерииИспользуются", Новый Структура("Номенклатура", "СерииИспользуются"));
	
	СтруктураДействий.Вставить("НоменклатураПриИзмененииПереопределяемый", Новый Структура("ИмяФормы, ИмяТабличнойЧасти",
		ЭтаФорма.ИмяФормы, "МатериалыИУслуги"));
		
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
	УправлениеДаннымиОбИзделияхКлиентСервер.ПриИзмененииНоменклатурыВСтрокеМатериалов(ТекущиеДанные, Объект);
	УправлениеДаннымиОбИзделияхКлиентСервер.ПриИзмененииХарактеристикиВСтрокеМатериалов(ТекущиеДанные, Объект);
	
	Если ТекущиеДанные.ПроизводитсяВПроцессе И ЗначениеЗаполнено(ТекущиеДанные.ИсточникПолученияПолуфабриката) Тогда
		
		ТекущиеДанные.ПроизводитсяВПроцессе = Ложь;
		
		ТекущиеДанные.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
		
		ТекущиеДанные.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
		ТекущиеДанные.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиХарактеристикаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	УправлениеДаннымиОбИзделияхКлиентСервер.ПриИзмененииХарактеристикиВСтрокеМатериалов(ТекущаяСтрока, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиКоличествоУпаковокПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиУпаковкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПроизводитсяВПроцессеПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	Если Не ТекущаяСтрока.ПроизводитсяВПроцессе Тогда
		ТекущаяСтрока.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПустаяСсылка");
	Иначе
		ТекущаяСтрока.СтатьяКалькуляции = ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПолуфабрикатыПроизводимыеВПроцессе");
	КонецЕсли;
	
	ТекущаяСтрока.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
	ТекущаяСтрока.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиСпособПолученияПолуфабрикатаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	ТекущаяСтрока.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию(ТекущаяСтрока.СпособПолученияПолуфабриката);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиИсточникПолученияПолуфабрикатаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные.СпособПолученияПолуфабриката = ПредопределенноеЗначение("Перечисление.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
		ПараметрыФормы.Вставить("Характеристика", ТекущиеДанные.Характеристика);
		ПараметрыФормы.Вставить("НачалоПроизводства", Объект.НачалоДействия);
		ПараметрыФормы.Вставить("ПолучитьСписокДействующихСпецификаций", Истина);
		
		ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ВыборДействующихСпецификаций", ПараметрыФормы, ЭтаФорма);
		
	Иначе
		
		ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.ФормаВыбораЭтапаВыпуска", 
			ПолучитьПараметрыВыбораЭтапаВыпуска(), ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиЭтапРедактированиеПриИзменении(Элемент)
	
	ОбработатьИзменениеЭтапаВСтрокеМатериалыИУслуги();
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиТребуетсяУказыватьСерииПриИзменении(Элемент)
	
	МатериалыИУслугиТребуетсяУказыватьСерииПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТрудозатраты

&НаКлиенте
Процедура ТрудозатратыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборПоляВТаблице("Трудозатраты", Поле.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВидыРабочихЦентров

&НаКлиенте
Процедура ВидыРабочихЦентровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекущиеДанные = Элементы.ПроизводственныйПроцессВидыРабочихЦентров.ТекущиеДанные;
	
	УправлениеДаннымиОбИзделияхКлиент.ВидыРабочихЦентровПередНачаломДобавления(
				ТекущиеДанные, 
				ВидыРабочихЦентров, 
				Элементы.ПроизводственныйПроцессВидыРабочихЦентров, 
				Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыРабочихЦентровПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элементы.ПроизводственныйПроцессВидыРабочихЦентров.ТекущиеДанные;
		ТекущиеДанные.ЕдиницаИзмерения = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Минута");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыРабочихЦентровВидРабочегоЦентраПриИзменении(Элемент)
	
	//++ НЕ УТКА
	ВидыРабочихЦентровВидРабочегоЦентраПриИзмененииНаСервере();	
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыРабочихЦентровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборПоляВТаблице("ПроизводственныйПроцессВидыРабочихЦентров", Поле.Имя, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыРабочихЦентровВариантНаладкиПриИзменении(Элемент)
	
	//++ НЕ УТКА
	ВидыРабочихЦентровВариантНаладкиПриИзмененииНаСервере();
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтапыСписок

&НаКлиенте
Процедура ЭтапыСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если СтатусЗаписанногоОбъекта = СтатусДействующейСпецификации Тогда
		
		Отказ = Истина;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Отказ = Истина;
		
		Если Объект.Статус = СтатусДействующейСпецификации Тогда
			ТекстВопроса = НСтр("ru='Ввод этапов возможен только после записи спецификации со статусом ""В разработке"".
                                        |Изменить статус и записать спецификацию?'
                                        |;uk='Введення етапів можливе тільки після запису специфікації зі статусом ""У розробці"".
                                        |Змінити статус і записати специфікацію?'");
		Иначе
			ТекстВопроса = НСтр("ru='Ввод этапов возможен только после записи спецификации.
                                        |Записать спецификацию?'
                                        |;uk='Введення етапів можливе тільки після запису специфікації.
                                        |Записати специфікацію?'");
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросДобавитьЭтап", ЭтаФорма);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыСписокПередУдалением(Элемент, Отказ)
	
	Если СтатусЗаписанногоОбъекта = СтатусДействующейСпецификации Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыСписокПослеУдаления(Элемент)
	
	Оповестить("Запись_ЭтапыПроизводства");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазбитьСтроку(Команда)
	
	ТаблицаФормы  = Элементы.МатериалыИУслуги;
	ДанныеТаблицы = Объект.МатериалыИУслуги;
	
	Оповещение = Новый ОписаниеОповещения("РазбитьСтрокуЗавершение", ЭтотОбъект);
	ОбщегоНазначенияУТКлиент.РазбитьСтрокуТЧ(ДанныеТаблицы, ТаблицаФормы, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьСтрокуЗавершение(НоваяСтрока, ДополнительныеПараметры) Экспорт 
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	Если НоваяСтрока <> Неопределено Тогда
		
		НоваяСтрока.КлючСвязи = Новый УникальныйИдентификатор;
		
		СтруктураДействий = Новый Структура;
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
		
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(НоваяСтрока, СтруктураДействий, КэшированныеЗначения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВвестиРазрешениеНаЗамену(Команда)
	
	//++ НЕ УТКА
	
	Если НЕ ПроверитьЗаполнениеИзделия(НСтр("ru='Необходимо указать изделие.';uk='Необхідно зазначити виріб.'"))
		или Элементы.МатериалыИУслуги.ВыделенныеСтроки.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	Материалы = Новый Массив;
	
	Для каждого ЭлКоллекции из Элементы.МатериалыИУслуги.ВыделенныеСтроки Цикл
		
		ДанныеСтроки = Элементы.МатериалыИУслуги.ДанныеСтроки(ЭлКоллекции);
		
		Если ДанныеСтроки.СпособАвтовыбораХарактеристики <> ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ") или
			ДанныеСтроки.СпособАвтовыбораНоменклатуры <> ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ") Тогда
			
			Отказ = Истина;
			Прервать;
			
		КонецЕсли;
		
		СтруктураМатериала = Новый Структура;
		
		СтруктураМатериала.Вставить("ПрименениеМатериала", Строка(ДанныеСтроки.НомерСтроки) + ": " + ДанныеСтроки.ПрименениеМатериала);
		СтруктураМатериала.Вставить("Номенклатура", ДанныеСтроки.Номенклатура);
		СтруктураМатериала.Вставить("Характеристика", ДанныеСтроки.Характеристика);
		СтруктураМатериала.Вставить("КоличествоУпаковок", ДанныеСтроки.КоличествоУпаковок);
		СтруктураМатериала.Вставить("Количество", ДанныеСтроки.Количество);
		СтруктураМатериала.Вставить("Упаковка", ДанныеСтроки.Упаковка);
		СтруктураМатериала.Вставить("КлючСвязиСпецификация", ДанныеСтроки.КлючСвязи);
		
		Материалы.Добавить(СтруктураМатериала);
		
	КонецЦикла;
	
	Если Отказ Тогда
		
		ТекстСообщения = НСтр("ru='Выберите строки, для которых не используется автовыбор.';uk='Виділіть рядки, для яких не використовується автовибір.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.МатериалыИУслуги", "Объект");
		
		Возврат;
		
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Спецификация", Объект.Ссылка);
	ЗначенияЗаполнения.Вставить("Материалы", Материалы);
	
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.РазрешениеНаЗаменуМатериалов.ФормаОбъекта", ПараметрыФормы, , Истина);
	
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаКлиенте
Процедура КомандаНастроитьПодборНоменклатуры(Команда)
	
	//++ НЕ УТКА
	
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	ОчиститьСообщения();
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не выбрана строка таблицы ""Материалы и работы"".';uk='Не вибраний рядок таблиці ""Матеріали і роботи"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.МатериалыИУслуги", "Объект");
		
		Возврат;
	КонецЕсли;

	Если НЕ ПроверитьЗаполнениеИзделия(НСтр("ru='Для настройки автовыбора материала необходимо указать изделие.';uk='Для настройки автовибору матеріалу необхідно зазначити виріб.'")) Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = Объект.ВыходныеИзделия[0].Номенклатура;
	
	УправлениеДаннымиОбИзделияхКлиент.НастроитьАвтоподборНоменклатуры(
							ТекущиеДанные, 
							Объект.СоответствиеСвойств, 
							Номенклатура,
							НСтр("ru='Указывается в спецификации';uk='Вказується в специфікації'"),
							ЭтаФорма,
							НЕ ДоступностьРеквизитов);
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастроитьПотребление(Команда)
	
	//++ НЕ УТКА
	ТекущиеДанные = Элементы.МатериалыИУслуги.ТекущиеДанные;
	
	ОчиститьСообщения();
	
	Если ТекущиеДанные = Неопределено Тогда
		ТекстСообщения = НСтр("ru='Не выбрана строка таблицы ""Материалы и работы"".';uk='Не вибраний рядок таблиці ""Матеріали і роботи"".'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Объект.МатериалыИУслуги", "Объект");
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьЗаполнениеИзделия(НСтр("ru='Для настройки потребления необходимо указать изделие.';uk='Для настройки споживання необхідно зазначити виріб.'")) Тогда
		Возврат;
	КонецЕсли;
	
	Номенклатура = Объект.ВыходныеИзделия[0].Номенклатура;
	
	УправлениеДаннымиОбИзделияхКлиент.НастроитьПотреблениеНоменклатуры(
							ТекущиеДанные, 
							Номенклатура, 
							ЭтаФорма,
							НЕ ДоступностьРеквизитов);
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьСпецификацииИзделия(Команда)
	
	//++ НЕ УТКА
	
	ТекущиеДанные = Элементы.ВыходныеИзделия.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено 
		ИЛИ НЕ ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Основание,РежимСпецификацииИзделия,АктивизироватьСпецификацию", 
							ТекущиеДанные.Номенклатура, Истина, Объект.Ссылка);
	ОткрытьФорму("ОбщаяФорма.ПрименениеНоменклатурыВПроизводстве", ПараметрыФормы,, ТекущиеДанные.Номенклатура);
	
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоМаршрутнымКартамЭтапов(Команда)
	
	//++ НЕ УТКА
	Если НЕ Объект.МногоэтапныйПроизводственныйПроцесс 
		И ПроизводственныйПроцесс.МаршрутнаяКарта.Пустая() Тогда
		
		ТекстСообщения = НСтр("ru='Необходимо выбрать маршрутную карту';uk='Необхідно вибрати маршрутну карту'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ПроизводственныйПроцесс.МаршрутнаяКарта");
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru='Заполнить спецификацию по маршрутным картам этапов?';uk='Заповнити специфікацію за маршрутними картами етапів?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоМаршрутнымКартамЭтаповЗавершение", ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьВидыРабочихЦентровПоМаршрутнойКарте(Команда)
	
	//++ НЕ УТКА
	Если ПроизводственныйПроцесс.МаршрутнаяКарта.Пустая() Тогда
		ТекстСообщения = НСтр("ru='Необходимо выбрать маршрутную карту';uk='Необхідно вибрати маршрутну карту'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ПроизводственныйПроцесс.МаршрутнаяКарта");
		Возврат;
	КонецЕсли; 
	
	ЗаполнитьВидыРабочихЦентровПоМаршрутнойКарте();
	//-- НЕ УТКА
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусВРазработке(Команда)
	
	Если НЕ ДоступностьРеквизитов Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработчикУстановитьСтатусВРазработке", ЭтотОбъект);
		ОткрытьФорму("Справочник.РесурсныеСпецификации.Форма.РазблокированиеРеквизитов", 
								Новый Структура("Объект", Объект.Ссылка),,,,,
								ОписаниеОповещения,
								РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусДействует(Команда)
	
	УстановитьНовыйСтатусИЗаписать(ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьСтатусЗакрыта(Команда)
	
	УстановитьНовыйСтатусИЗаписать(ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Закрыта"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПроверитьЗаполнение(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнениеПринудительно() Тогда
		ТекстПредупреждения = НСтр("ru='Спецификацию можно сделать действующей.';uk='Специфікацію можна зробити чинною.'");
	Иначе
		ТекстПредупреждения = НСтр("ru='Спецификация не может быть действующей.
                                        |Необходимо заполнить указанные реквизиты.'
                                        |;uk='Специфікація не може бути діючою.
                                        |Необхідно заповнити зазначені реквізити.'");
	КонецЕсли;
	
	ПоказатьПредупреждение(,ТекстПредупреждения,, НСтр("ru='Проверка спецификации';uk='Перевірка специфікації'"));
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьПодборМатериалов(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	МассивТиповНоменклатуры = Новый Массив();
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
	ДоступныеТипыНоменклатуры = Новый ФиксированныйМассив(МассивТиповНоменклатуры);
	ПараметрыФормы.Вставить("Отбор", Новый Структура("ТипНоменклатуры", ДоступныеТипыНоменклатуры));
	
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВыбора", 
			ПараметрыФормы, 
			ЭтаФорма, 
			УникальныйИдентификатор,,,, 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСтроки(Команда)
	
	КоличествоТоваровДоВставки = Объект.МатериалыИУслуги.Количество();
	
	ПолучитьСтрокиИзБуфераОбмена();
	
	КоличествоВставленных = Объект.МатериалыИУслуги.Количество()-КоличествоТоваровДоВставки;
	КопированиеСтрокКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоВставленных);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьСтроки(Команда)
	
	Если КопированиеСтрокКлиент.ВозможноКопированиеСтрок(Элементы.МатериалыИУслуги.ТекущаяСтрока) Тогда
		СкопироватьСтрокиНаСервере();
		КопированиеСтрокКлиент.ОповеститьПользователяОКопированииСтрок(Элементы.МатериалыИУслуги.ВыделенныеСтроки.Количество());
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КомандаДобавитьАльтернативныйВидРЦ(Команда)
	
	ТекущиеДанные = Элементы.ПроизводственныйПроцессВидыРабочихЦентров.ТекущиеДанные;
	
	УправлениеДаннымиОбИзделияхКлиент.ДобавитьАльтернативныйВидРЦ(
				ТекущиеДанные, 
				ВидыРабочихЦентров, 
				Элементы.ПроизводственныйПроцессВидыРабочихЦентров);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаСервере
Процедура СвойстваВыполнитьОтложеннуюИнициализацию()
	УправлениеСвойствами.ЗаполнитьДополнительныеРеквизитыВФорме(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#Область ОбслуживаниеНастройкиПроизводственногоПроцесса

&НаСервере
Процедура ПриИзмененииВидаПроизводственногоПроцесса()
	
	ПрочитатьДанныеПриОдноэтапномПроизводстве();
	
	ЗаполнитьКоличествоИзделийСТипомСтоимостиРассчитывается(
		Объект.ВыходныеИзделия,
		Объект.МногоэтапныйПроизводственныйПроцесс, 
		Объект.Ссылка);
	
	ЗаполнитьИсточникПолученияПолуфабрикатаПриИзмененииВидаПроизводственногоПроцесса();
	
	ЗаполнитьСлужебныеРеквизитыМатериалыИУслуги();
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииСпособаПроизводства()
	
	УправлениеВидимостью();
	
	Для Каждого Строка Из Объект.МатериалыИУслуги Цикл
		
		Строка.ПроизводствоНаСтороне = ПроизводственныйПроцесс.ПроизводствоНаСтороне;
		
		Если Строка.ПроизводствоНаСтороне И Строка.ПроизводитсяВПроцессе Тогда
		
			Строка.ПроизводитсяВПроцессе = Ложь;
			
			Строка.СтатьяКалькуляции = Справочники.СтатьиКалькуляции.ПустаяСсылка();
			
			Строка.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
			Строка.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеПриОдноэтапномПроизводстве()

	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		Возврат
	КонецЕсли;
	
	ЭтапОдногоЭтапногоПроцесса = Справочники.РесурсныеСпецификации.ПолучитьЭтапОдногоЭтапногоПроцесса(Объект.Ссылка);
	
	Если ЭтапОдногоЭтапногоПроцесса <> Неопределено Тогда
		ПроизводственныйПроцессОбъект = ЭтапОдногоЭтапногоПроцесса.ПолучитьОбъект();
	Иначе	
		// Без проверки прав
		УстановитьПривилегированныйРежим(Истина);
		ПроизводственныйПроцессОбъект = Справочники.ЭтапыПроизводства.СоздатьЭлемент();
		ПроизводственныйПроцессОбъект.Заполнить(Новый Структура("Владелец", Объект.Ссылка));
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ПроизводственныйПроцессОбъект, "ПроизводственныйПроцесс");
	
	СпособПроизводства = Число(ПроизводственныйПроцесс.ПроизводствоНаСтороне);
	
	//++ НЕ УТКА
	ПрочитатьНастройкуРазбиватьМаршрутныеЛисты(ЭтаФорма);
	//-- НЕ УТКА
	
	УстановитьВидимостьПоЗначениюПланироватьРаботуВидовРабочихЦентров(ЭтаФорма);
	
	ПриЧтенииПодразделения(Истина);
	
	//++ НЕ УТКА
	ЗаполнитьВидыРабочихЦентров();
	//-- НЕ УТКА
	
	УправлениеДоступностью(ЭтаФорма);
	
	УстановитьПодсказкуЕдиницыИзмеренияДлительностиЭтапа(ЭтаФорма);
	
	УстановитьОтметкуНезаполненногоДляБуферов(ЭтаФорма);
	
	ХарактеристикиИспользуются = Справочники.Номенклатура.ХарактеристикиИспользуются(ПроизводственныйПроцесс.УслугаПереработчика);
	Элементы.ПроизводственныйПроцессХарактеристикаУслугиПереработчика.Доступность = ХарактеристикиИспользуются;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборВСпискеЭтапов()

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ЭтапыСписок, 
		"Владелец", 
		Объект.Ссылка, 
		ВидСравненияКомпоновкиДанных.Равно,
		, 
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьВыборЕдиницыИзмеренияБуферов(ЗаменитьЗначения)

	// Предварительный буфер
	СписокВыбора = Элементы.ПроизводственныйПроцессЕдиницаИзмеренияПредварительногоБуфера.СписокВыбора;
	Если СписокВыбора.НайтиПоЗначению(ПроизводственныйПроцесс.ЕдиницаИзмеренияПредварительногоБуфера) = Неопределено 
		И ЗначениеЗаполнено(ПроизводственныйПроцесс.ЕдиницаИзмеренияПредварительногоБуфера) Тогда
		
		Если ЗаменитьЗначения Тогда
			
			ПроизводственныйПроцесс.ПредварительныйБуфер = 0;
			ПроизводственныйПроцесс.ЕдиницаИзмеренияПредварительногоБуфера = Справочники.ЭтапыПроизводства.ОсновнаяЕдиницаВремениБуфера();
			
		ИначеЕсли ПроизводственныйПроцесс.ПланироватьРаботуВидовРабочихЦентров Тогда
			
			ТекстСообщения = НСтр("ru='Указана некорректная единица измерения предварительного буфера.';uk='Зазначена некоректна одиниця вимірювання попереднього буфера.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения,, 
						"ЕдиницаИзмеренияПредварительногоБуфера", 
						"ПроизводственныйПроцесс"); 
		КонецЕсли; 
		
	КонецЕсли; 

	// Завершающий буфер
	СписокВыбора = Элементы.ПроизводственныйПроцессЕдиницаИзмеренияЗавершающегоБуфера.СписокВыбора;
	Если СписокВыбора.НайтиПоЗначению(ПроизводственныйПроцесс.ЕдиницаИзмеренияЗавершающегоБуфера) = Неопределено
		И ЗначениеЗаполнено(ПроизводственныйПроцесс.ЕдиницаИзмеренияЗавершающегоБуфера) Тогда
		
		Если ЗаменитьЗначения Тогда
			
			ПроизводственныйПроцесс.ЗавершающийБуфер = 0;
			ПроизводственныйПроцесс.ЕдиницаИзмеренияЗавершающегоБуфера = Справочники.ЭтапыПроизводства.ОсновнаяЕдиницаВремениБуфера();
			
		ИначеЕсли ПроизводственныйПроцесс.ПланироватьРаботуВидовРабочихЦентров Тогда
			
			ТекстСообщения = НСтр("ru='Указана некорректная единица измерения завершающего буфера.';uk='Зазначена некоректна одиниця вимірювання завершального буфера.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						ТекстСообщения,, 
						"ЕдиницаИзмеренияЗавершающегоБуфера", 
						"ПроизводственныйПроцесс"); 
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьПоЗначениюПланироватьРаботуВидовРабочихЦентров(Форма)
	
	ЭлементыФормы = Форма.Элементы;
	
	ПланироватьРаботуВидовРабочихЦентров = Форма.ПроизводственныйПроцесс.ПланироватьРаботуВидовРабочихЦентров;
	
	Если ПланироватьРаботуВидовРабочихЦентров Тогда
		ЭлементыФормы.ПланироватьРаботуВидовРабочихЦентровСтраницы.ТекущаяСтраница = ЭлементыФормы.ПланироватьРаботуВидовРабочихЦентровПоРЦ;
	Иначе
		ЭлементыФормы.ПланироватьРаботуВидовРабочихЦентровСтраницы.ТекущаяСтраница = ЭлементыФормы.ПланироватьРаботуВидовРабочихЦентровБезРЦ;
	КонецЕсли; 
	
	ЭлементыФормы.ИнтервалПланирования.Видимость = ПланироватьРаботуВидовРабочихЦентров;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПодсказкуЕдиницыИзмеренияДлительностиЭтапа(Форма)
	
	Если Форма.ПроизводственныйПроцесс.ЕдиницаИзмеренияДлительностиЭтапа = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.День") Тогда
		ТекстПодсказки = НСтр("ru='При планировании используется календарь работы подразделения.';uk='При плануванні використовується календар роботи підрозділу.'");
	Иначе
		ТекстПодсказки = НСтр("ru='При планировании используется расписание работы подразделения.';uk='При плануванні використовується розклад роботи підрозділу.'");
	КонецЕсли;
	
	Форма.Элементы.ПодсказкаЕдиницаИзмеренияДлительностиЭтапа.Заголовок = ТекстПодсказки;
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере()

	ПриЧтенииПодразделения(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииПодразделения(ЭтоЧтениеОбъекта)

	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ПроизводственныйПроцесс.Подразделение);
	
	//++ НЕ УТКА
	ИнтервалПланирования = ПараметрыПодразделения.ИнтервалПланирования;
	
	Если НЕ ЭтоЧтениеОбъекта Тогда
		
		УправлениеДаннымиОбИзделиях.СохранитьВидыРабочихЦентров(
					ВидыРабочихЦентров, 
					ПроизводственныйПроцесс.ПланироватьРаботуВидовРабочихЦентров,
					ПроизводственныйПроцесс.ВидыРабочихЦентров,
					ПроизводственныйПроцесс.АльтернативныеВидыРабочихЦентров);
					
		Справочники.ЭтапыПроизводства.ПроверитьСоответствиеРеквизитовПодразделению(ПроизводственныйПроцесс);
		
		ЗаполнитьВидыРабочихЦентров();
		
	КонецЕсли; 
	//-- НЕ УТКА
	
	Справочники.ЭтапыПроизводства.ЗаполнитьВыборЕдиницыИзмеренияБуфера(
			ИнтервалПланирования, 
			Элементы.ПроизводственныйПроцессЕдиницаИзмеренияПредварительногоБуфера.СписокВыбора);
	
	Справочники.ЭтапыПроизводства.ЗаполнитьВыборЕдиницыИзмеренияБуфера(
			ИнтервалПланирования, 
			Элементы.ПроизводственныйПроцессЕдиницаИзмеренияЗавершающегоБуфера.СписокВыбора);
	
	ПроверитьВыборЕдиницыИзмеренияБуферов(НЕ ЭтоЧтениеОбъекта);

КонецПроцедуры

&НаКлиенте
Процедура РазбиватьМаршрутныеЛистыПриИзменении(Элемент)
	
	Если НЕ РазбиватьМаршрутныеЛисты Тогда
		Если МаксимальноеКоличествоЕдиницПартийИзделия <> 0 Тогда
			МаксимальноеКоличествоЕдиницПартийИзделия = 0;
		КонецЕсли; 
	Иначе
		Если МаксимальноеКоличествоЕдиницПартийИзделия = 0 Тогда
			МаксимальноеКоличествоЕдиницПартийИзделия = 1;
		КонецЕсли; 
	КонецЕсли;
	
	УправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтметкуНезаполненногоДляБуферов(Форма)

	Если НЕ ЗначениеЗаполнено(Форма.ПроизводственныйПроцесс.ЕдиницаИзмеренияПредварительногоБуфера)
		И Форма.ПроизводственныйПроцесс.ПредварительныйБуфер <> 0 Тогда
		Форма.Элементы.ПроизводственныйПроцессЕдиницаИзмеренияПредварительногоБуфера.ОтметкаНезаполненного = Истина;
	Иначе
		Форма.Элементы.ПроизводственныйПроцессЕдиницаИзмеренияПредварительногоБуфера.ОтметкаНезаполненного = Ложь;
	КонецЕсли; 	

	Если НЕ ЗначениеЗаполнено(Форма.ПроизводственныйПроцесс.ЕдиницаИзмеренияЗавершающегоБуфера)
		И Форма.ПроизводственныйПроцесс.ЗавершающийБуфер <> 0 Тогда
		Форма.Элементы.ПроизводственныйПроцессЕдиницаИзмеренияЗавершающегоБуфера.ОтметкаНезаполненного = Истина;
	Иначе
		Форма.Элементы.ПроизводственныйПроцессЕдиницаИзмеренияЗавершающегоБуфера.ОтметкаНезаполненного = Ложь;
	КонецЕсли; 	
	
КонецПроцедуры

//++ НЕ УТКА

&НаСервере
Процедура ЗаполнитьВидыРабочихЦентров()

	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		Возврат;
	КонецЕсли;
	
	УправлениеДаннымиОбИзделиях.ЗаполнитьВидыРабочихЦентров(
			ПроизводственныйПроцесс.ВидыРабочихЦентров,
			ПроизводственныйПроцесс.АльтернативныеВидыРабочихЦентров,
			ВидыРабочихЦентров);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПрочитатьНастройкуРазбиватьМаршрутныеЛисты(Форма)

	Если Форма.ПроизводственныйПроцесс.МаршрутнаяКарта.Пустая() Тогда
		
		Форма.РазбиватьМаршрутныеЛисты = (Форма.ПроизводственныйПроцесс.МаксимальноеКоличествоЕдиницПартийИзделия <> 0);
		Форма.МаксимальноеКоличествоЕдиницПартийИзделия = Форма.ПроизводственныйПроцесс.МаксимальноеКоличествоЕдиницПартийИзделия;
		Форма.Элементы.СтраницыРазбиватьМаршрутныеЛисты.ТекущаяСтраница = Форма.Элементы.СтраницаРазбиватьМаршрутныеЛисты;
		
	Иначе
		
		МаксимальноеКоличествоЕдиницПартийИзделия = ПараметрыМаршрутнойКарты(Форма.ПроизводственныйПроцесс.МаршрутнаяКарта);
		Форма.РазбиватьМаршрутныеЛистыПоМаршрутнойКарте = (МаксимальноеКоличествоЕдиницПартийИзделия <> 0);
		Форма.Элементы.СтраницыРазбиватьМаршрутныеЛисты.ТекущаяСтраница = Форма.Элементы.СтраницаРазбиватьМаршрутныеЛистыПоМаршрутнойКарте;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВидыРабочихЦентровВидРабочегоЦентраПриИзмененииНаСервере()

	ТекущаяСтрока = Элементы.ПроизводственныйПроцессВидыРабочихЦентров.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = ВидыРабочихЦентров.НайтиПоИдентификатору(ТекущаяСтрока);
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.ВариантНаладки = Справочники.ВариантыНаладки.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизитыВСтрокеВидовРЦ();
	
КонецПроцедуры

&НаСервере
Процедура ВидыРабочихЦентровВариантНаладкиПриИзмененииНаСервере()
	
	ЗаполнитьСлужебныеРеквизитыВСтрокеВидовРЦ();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыМаршрутнойКарты(МаршрутнаяКарта)

	МаксимальноеКоличествоЕдиницПартийИзделия = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(МаршрутнаяКарта, "МаксимальноеКоличествоЕдиницПартийИзделия");
	
	Возврат МаксимальноеКоличествоЕдиницПартийИзделия;

КонецФункции

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыВСтрокеВидовРЦ()

	ТекущаяСтрока = Элементы.ПроизводственныйПроцессВидыРабочихЦентров.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанные = ВидыРабочихЦентров.НайтиПоИдентификатору(ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	УправлениеДаннымиОбИзделиях.ЗаполнитьСлужебныеРеквизитыВСтрокеВидовРЦ(
				Неопределено, 
				Неопределено, 
				Истина, 
				ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыРабочихЦентровПоМаршрутнойКарте()

	Справочники.ЭтапыПроизводства.ЗаполнитьВидыРабочихЦентровПоМаршрутнойКарте(ПроизводственныйПроцесс);
	ПроверитьВыборЕдиницыИзмеренияБуферов(Истина);
	ЗаполнитьВидыРабочихЦентров();
	
КонецПроцедуры

//-- НЕ УТКА

#КонецОбласти

#Область РаботаСБуферомОбмена

&НаСервере
Процедура СкопироватьСтрокиНаСервере()
	
	КопированиеСтрокСервер.ПоместитьВыделенныеСтрокиВБуферОбмена(Элементы.МатериалыИУслуги.ВыделенныеСтроки, Объект.МатериалыИУслуги);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтрокиИзБуфераОбмена()
	
	МассивТиповНоменклатуры = Новый Массив;
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
	МассивТиповНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа"));
	
	ПараметрыОтбора = Новый Структура();
	ПараметрыОтбора.Вставить("ОтборПоТипуНоменклатуры", МассивТиповНоменклатуры);
	
	ТаблицаТоваров = КопированиеСтрокСервер.ПолучитьСтрокиИзБуфераОбмена(ПараметрыОтбора);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		ТекущаяСтрока = Объект.МатериалыИУслуги.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаТовара, "Номенклатура,Характеристика,Упаковка,КоличествоУпаковок,СтатьяКалькуляции");
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
		ТекущаяСтрока.КлючСвязи = Новый УникальныйИдентификатор;
		УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиМатериалов(ТекущаяСтрока);
		
	КонецЦикла;
	
	ЗаполнитьСлужебныеРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьКомандБуфераОбмена()
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("МатериалыИУслугиВставитьСтроки");
	МассивЭлементов.Добавить("МатериалыИУслугиКонтекстноеМенюВставитьСтроки");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(
			Элементы, 
			МассивЭлементов, 
			"Доступность", 
			НЕ ОбщегоНазначения.ПустойБуферОбмена("Строки") И ДоступностьРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКомандБуфераОбменаНаКлиенте()
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить("МатериалыИУслугиВставитьСтроки");
	МассивЭлементов.Добавить("МатериалыИУслугиКонтекстноеМенюВставитьСтроки");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Доступность", ДоступностьРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	
	СтатусДействующейСпецификации = Перечисления.СтатусыСпецификаций.Действует;
	СтатусСпецификацииВРазработке = Перечисления.СтатусыСпецификаций.ВРазработке;
	ТипСтоимостиРассчитывается    = Перечисления.ТипыСтоимостиВыходныхИзделий.Рассчитывается;
	
	ВидПроизводственногоПроцесса = Число(Объект.МногоэтапныйПроизводственныйПроцесс);
	
	ЗаполнитьСлужебныеРеквизиты();
	
	ПрочитатьДанныеПриОдноэтапномПроизводстве();
	
	Элементы.ГруппаОписание.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Описание);
	
	УправлениеВидимостью();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	// Стандартное оформление номенклатуры
	#Область СтандартноеОформление
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																   "ВыходныеИзделияНоменклатураЕдиницаИзмерения", 
                                                                   "Объект.ВыходныеИзделия.Упаковка");

	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																   "ВозвратныеОтходыНоменклатураЕдиницаИзмерения", 
                                                                   "Объект.ВозвратныеОтходы.Упаковка");

	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, 
																   "МатериалыИУслугиНоменклатураЕдиницаИзмерения", 
                                                                   "Объект.МатериалыИУслуги.Упаковка");
																   
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "ВыходныеИзделияХарактеристика",
																		     "Объект.ВыходныеИзделия.ХарактеристикиИспользуются");
																			 
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма, 
																			 "ВозвратныеОтходыХарактеристика",
																		     "Объект.ВозвратныеОтходы.ХарактеристикиИспользуются");
	#КонецОбласти

	// Жирный шрифт для первой строки изделий
	#Область ЖирнымПерваяСтрока
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыходныеИзделия.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыходныеИзделия.НомерСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1;

	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(WindowsШрифты.DefaultGUIFont, , , Истина, Ложь, Ложь, Ложь, ));
	#КонецОбласти

	// Текст <последний этап> или <первый этап>
	#Область ТекстПоследнийЭтапИлиПервыйЭтап
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыходныеИзделияЭтапРедактирование.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыходныеИзделия.ЭтапРедактирование");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<последний этап>';uk='<останній етап>'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВозвратныеОтходыЭтапРедактирование.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВозвратныеОтходы.ЭтапРедактирование");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<последний этап>';uk='<останній етап>'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиЭтапРедактирование.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ЭтапРедактирование");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<первый этап>';uk='<перший етап>'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТрудозатратыЭтапРедактирование.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Трудозатраты.ЭтапРедактирование");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<первый этап>';uk='<перший етап>'"));
	#КонецОбласти

	// Текст <любая характеристика>
	#Область ТекстЛюбаяХарактеристика
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыходныеИзделияХарактеристика.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыходныеИзделия.ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыходныеИзделия.Характеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<любая характеристика>';uk='<будь-яка характеристика>'"));
	#КонецОбласти

	// Если производство на стороне, то производство в процессе недоступно
	#Область ДоступностьПроизводитсяВПроцессе
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиПроизводитсяВПроцессе.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиИсточникПолученияПолуфабриката.Имя);

	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводствоНаСтороне");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти
	
	#Область ДоступностьСпособПолученияПолуфабриката
	
	// Для одноэтапной спецификации и для первых этапов способ получения полуфабриката 
	//  "производится в процессе на этапе" недоступен
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МногоэтапныйПроизводственныйПроцесс");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ЭтоПервыйЭтап");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	#КонецОбласти
	
	// Текст способа получения полуфабриката "на этапе" или "по спецификации"
	#Область ТекстСпособПолученияПолуфабриката
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.СпособПолученияПолуфабриката");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиНаЭтапе;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru=' на этапе';uk=' на етапі'"));

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.СпособПолученияПолуфабриката");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru=' по спецификации';uk=' по специфікації'"));
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиИсточникПолученияПолуфабриката.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	#КонецОбласти
	
	// Запрет редактирования времени работы, если применяется синхронная загрузка
	#Область ВремяРаботыПриСинхроннойЗагрузке
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровВремяРаботы.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровЕдиницаИзмерения.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.СинхроннаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти
	
	// Скрываем редактирование времени работы, если применяется синхронная загрузка, но не выбран вариант наладки
	#Область ВремяРаботыПриСинхроннойЗагрузке_Скрыть
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровВремяРаботы.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровЕдиницаИзмерения.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.СинхроннаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ИспользуетсяВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	#КонецОбласти
	
	// Скрываем надпись предназначенную для синхронной загрузки
	#Область ВидыРабочихЦентровВремяРаботыПриСинхроннойЗагрузке_Скрыть
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровВремяРаботыПриСинхроннойЗагрузке.Имя);

	ОтборГруппа = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.СинхроннаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ИспользуетсяВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ВидРабочегоЦентра");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	#КонецОбласти
	
	// Показываем надпись предназначенную для синхронной загрузки если вариант наладки не выбран
	#Область ВидыРабочихЦентровВремяРаботыПриСинхроннойЗагрузке_Показать
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровВремяРаботыПриСинхроннойЗагрузке.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.СинхроннаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ИспользуетсяВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ВидРабочегоЦентра");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<определяется вариантом наладки>';uk='<визначається варіантом наладки>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	#КонецОбласти
	
	// ОтметкаНезаполненного для поля Загрузка
	#Область Загрузка_ОтметкаНезаполненного
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроизводственныйПроцессВидыРабочихЦентровЗагрузка.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ПараллельнаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<не используется>';uk='<не використовується>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти
	
	// Запрет редактирования флага "Регистрировать серии"
	#Область СерииИспользуются
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыходныеИзделияТребуетсяУказыватьСерии.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыходныеИзделия.СерииИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиТребуетсяУказыватьСерии.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.СерииИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	#КонецОбласти
	
	// Прочее оформление
	#Область Прочее
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиИсточникПолученияПолуфабриката.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиИсточникПолученияПолуфабриката.Имя);

	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = ГруппаОтбора1.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ИсточникПолученияПолуфабриката");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиСтатьяКалькуляции.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ПроизводитсяВПроцессе");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыходныеИзделияДоляСтоимости.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВыходныеИзделия.КоличествоИзделийСТипомСтоимостиРассчитывается");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 2;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровВариантНаладки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.ИспользуетсяВариантНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru='<не используется>';uk='<не використовується>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВидыРабочихЦентровВидРабочегоЦентра.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидыРабочихЦентров.Альтернативный");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветАльтернативногоВидаРабочихЦентров);
	
	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПроизводственныйПроцессМаксимальноеКоличествоЕдиницПартийИзделия.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РазбиватьМаршрутныеЛисты");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	//
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(ЭтаФорма,
		"ПроизводственныйПроцессХарактеристикаУслугиПереработчика",
		"ХарактеристикиИспользуются");
		
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ИспользуетсяПроизводствоНаСтороне = Константы.ИспользоватьПроизводствоНаСтороне.Получить();
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.ВыходныеИзделия,
		Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются, ЗаполнитьПризнакАртикул,ЗаполнитьПризнакСерииИспользуются",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются"),
			Новый Структура("Номенклатура", "Артикул"),
			Новый Структура("Номенклатура", "СерииИспользуются")));
			
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.ВозвратныеОтходы,
		Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются, ЗаполнитьПризнакАртикул",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются"),
			Новый Структура("Номенклатура", "Артикул")));
			
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект.МатериалыИУслуги,
		Новый Структура("ЗаполнитьПризнакХарактеристикиИспользуются, ЗаполнитьПризнакАртикул,ЗаполнитьПризнакСерииИспользуются",
			Новый Структура("Номенклатура", "ХарактеристикиИспользуются"),
			Новый Структура("Номенклатура", "Артикул"),
			Новый Структура("Номенклатура", "СерииИспользуются")));
	
	//++ НЕ УТКА
	УправлениеДаннымиОбИзделиях.ЗаполнитьСлужебныеРеквизитыАвтоподбораНоменклатуры(Объект.МатериалыИУслуги);
	//-- НЕ УТКА
	
	ЗаполнитьСлужебныеРеквизитыМатериалыИУслуги();
	
	ЗаполнитьКоличествоИзделийСТипомСтоимостиРассчитывается(
					Объект.ВыходныеИзделия, 
					Объект.МногоэтапныйПроизводственныйПроцесс, 
					Объект.Ссылка);
	
	ВсегоИзделий = Объект.ВыходныеИзделия.Количество() + Объект.ВозвратныеОтходы.Количество();
	
	ЗаполнитьПредставлениеСтатейКалькуляцииВзносов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостью()
	
	Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Видимость = Ложь;
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		
		Элементы.ВыходныеИзделияЭтапРедактирование.Видимость  = Истина;
		Элементы.ВозвратныеОтходыЭтапРедактирование.Видимость = Истина;
		Элементы.МатериалыИУслугиЭтапРедактирование.Видимость = Истина;
		Элементы.ТрудозатратыЭтапРедактирование.Видимость     = Истина;
		
//++ НЕ УТКА
		Элементы.МатериалыИУслугиСпособПолученияПолуфабриката.Видимость = Истина;
//-- НЕ УТКА

	Иначе
		
		Элементы.ВыходныеИзделияЭтапРедактирование.Видимость  = Ложь;
		Элементы.ВозвратныеОтходыЭтапРедактирование.Видимость = Ложь;
		Элементы.МатериалыИУслугиЭтапРедактирование.Видимость = Ложь;
		Элементы.ТрудозатратыЭтапРедактирование.Видимость     = Ложь;
		
//++ НЕ УТКА
		УправлениеДаннымиОбИзделияхКлиентСервер.ВидимостьСпискаВидовРЦ(
			ПроизводственныйПроцесс.ПланироватьРаботуВидовРабочихЦентров,
			ПроизводственныйПроцесс.ПроизводствоНаСтороне,
			ЭтаФорма);
//-- НЕ УТКА
		
	КонецЕсли;
	
	Элементы.СпособПроизводства.Видимость = ИспользуетсяПроизводствоНаСтороне;
	
	Если ВидПроизводственногоПроцесса = 0 Тогда
		Элементы.ГруппаОписаниеПроизводственногоПроцессаСтраницы.ТекущаяСтраница = Элементы.СтраницаОдноэтапныйПроизводственныйПроцесс;
	Иначе
		Элементы.ГруппаОписаниеПроизводственногоПроцессаСтраницы.ТекущаяСтраница = Элементы.СтраницаМногоэтапныйПроизводственныйПроцесс;
	КонецЕсли;
	
	Если СпособПроизводства = 0 Тогда
		Элементы.ГруппаОдноэтапныйПроизводственныйПроцесс.ТекущаяСтраница = Элементы.СтраницаПроизводствоСобственнымиСилами;
	Иначе
		Элементы.ГруппаОдноэтапныйПроизводственныйПроцесс.ТекущаяСтраница = Элементы.СтраницаПроизводствоСиламиПереработчика;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКоличествоИзделийСТипомСтоимостиРассчитывается(
			ТабличнаяЧасть,
			МногоэтапныйПроизводственныйПроцесс,
			Ссылка)

	Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
		СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается = 0;
	КонецЦикла;
	
	Если МногоэтапныйПроизводственныйПроцесс Тогда
		СписокЭтапов = Новый Массив;
		КоличествоИзделийНевыбранныхЭтапов = 0;
		Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
			Если СписокЭтапов.Найти(СтрокаТаблицы.ЭтапРедактирование) = Неопределено Тогда
				СписокЭтапов.Добавить(СтрокаТаблицы.ЭтапРедактирование);
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ЭтапРедактирование) Тогда
				
				КоличествоИзделийНевыбранныхЭтапов = КоличествоИзделийНевыбранныхЭтапов + 1;
			КонецЕсли; 
		КонецЦикла;
		
		КоличествоИзделийПоследнегоЭтапа = 0;
		ПоследниеЭтапы = ПолучитьПоследниеЭтапы(Ссылка);
		Для каждого Этап Из ПоследниеЭтапы Цикл
			СписокСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("ЭтапРедактирование", Этап));
			КоличествоИзделийПоследнегоЭтапа = КоличествоИзделийПоследнегоЭтапа + СписокСтрок.Количество();
		КонецЦикла; 
		
		Для каждого Этап Из СписокЭтапов Цикл
			СписокСтрок = ТабличнаяЧасть.НайтиСтроки(Новый Структура("ЭтапРедактирование", Этап));
			Для каждого СтрокаТаблицы Из СписокСтрок Цикл
				СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается = СписокСтрок.Количество();
				
				// Добавим количество последних этапов
				Если НЕ ЗначениеЗаполнено(Этап) Тогда
					СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается = 
							СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается + КоличествоИзделийПоследнегоЭтапа;
							
				ИначеЕсли ПоследниеЭтапы.Найти(Этап) <> Неопределено Тогда
					СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается = 
							СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается + КоличествоИзделийНевыбранныхЭтапов;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Иначе
		Для каждого СтрокаТаблицы Из ТабличнаяЧасть Цикл
			СтрокаТаблицы.КоличествоИзделийСТипомСтоимостиРассчитывается = ТабличнаяЧасть.Количество();
		КонецЦикла;
	КонецЕсли; 

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПоследниеЭтапы(Ссылка)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Владелец = &Владелец
	|	И НЕ ЭтапыПроизводства.ПометкаУдаления
	|	И ЭтапыПроизводства.НомерСледующегоЭтапа = 0";
	
	Запрос.УстановитьПараметр("Владелец", Ссылка);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");

	Возврат Результат;

КонецФункции

&НаСервере
Процедура ОбработатьИзменениеЭтапаПроизводства(Источник)
	
	ОбработатьИзменениеЭтапаВТаблицеМатериалыИУслуги(Источник);
		
	ЗаполнитьКоличествоИзделийСТипомСтоимостиРассчитывается(Объект.ВыходныеИзделия,
		Объект.МногоэтапныйПроизводственныйПроцесс,
		Объект.Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСлужебныеРеквизитыЭтапа(Спецификация, Этап)

	Результат = Новый Структура("
		|ПроизводствоНаСтороне,
		|ЭтоПервыйЭтап");
	
	МассивЭтапов = Новый Массив;
	МассивЭтапов.Добавить(Этап);
	
	ЗаполнитьЗначенияСвойств(Результат, ПолучитьСлужебныеРеквизитыЭтапов(Спецификация, МассивЭтапов)[0]);

	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСлужебныеРеквизитыЭтапов(Владелец, МассивЭтапов = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Если МассивЭтапов = Неопределено Тогда
		
		ПолучитьПервыеЭтапы = Истина;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЭтапыПроизводства.Ссылка                КАК Этап,
			|	ЭтапыПроизводства.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
			|	ВЫБОР
			|		КОГДА ЭтапыПроизводства.НомерЭтапа = 1
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ                                   КАК ЭтоПервыйЭтап
			|ИЗ
			|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
			|ГДЕ
			|	ЭтапыПроизводства.Владелец = &Владелец";
		
	Иначе
		
		ПолучитьПервыеЭтапы = МассивЭтапов.Найти(Справочники.ЭтапыПроизводства.ПустаяСсылка()) <> Неопределено;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЭтапыПроизводства.Ссылка                КАК Этап,
			|	ЭтапыПроизводства.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
			|	ВЫБОР
			|		КОГДА ЭтапыПроизводства.НомерЭтапа = 1
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ                                   КАК ЭтоПервыйЭтап
			|ИЗ
			|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
			|ГДЕ
			|	ЭтапыПроизводства.Владелец = &Владелец
			|	И ЭтапыПроизводства.Ссылка В (&МассивЭтапов)";
			
		Запрос.УстановитьПараметр("МассивЭтапов", МассивЭтапов);
		
	КонецЕсли;
	
	Если ПолучитьПервыеЭтапы Тогда
		
		Запрос.Текст = Запрос.Текст + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ТПервыйЭтап.Этап                            КАК Этап,
			|	МАКСИМУМ(ТПервыйЭтап.ПроизводствоНаСтороне) КАК ПроизводствоНаСтороне,
			|	ИСТИНА                                      КАК ЭтоПервыйЭтап
			|ИЗ
			|	(ВЫБРАТЬ
			|		ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка) КАК Этап,
			|		МИНИМУМ(ЭтапыПроизводства.ПроизводствоНаСтороне)    КАК ПроизводствоНаСтороне
			|	ИЗ
			|		Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
			|	ГДЕ
			|		ЭтапыПроизводства.Владелец = &Владелец
			|		И ЭтапыПроизводства.НомерЭтапа = 1
			|		И НЕ ЭтапыПроизводства.ПометкаУдаления
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка),
			|		ЛОЖЬ) КАК ТПервыйЭтап
			|
			|СГРУППИРОВАТЬ ПО
			|	ТПервыйЭтап.Этап";
			
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСлужебныеРеквизитыВСтрокеМатериалыИУслуги(СтрокаТабличнойЧасти)
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, 
			ПолучитьСлужебныеРеквизитыЭтапа(Объект.Ссылка, СтрокаТабличнойЧасти.ЭтапРедактирование));
		
	Иначе
		
		СтрокаТабличнойЧасти.ЭтоПервыйЭтап         = Истина;
		СтрокаТабличнойЧасти.ПроизводствоНаСтороне = ПроизводственныйПроцесс.ПроизводствоНаСтороне;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыМатериалыИУслуги()
	
	ПараметрыОтбора = Новый Структура("Этап");
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
	
		РеквизитыЭтапов = ПолучитьСлужебныеРеквизитыЭтапов(Объект.Ссылка);
		
		Для Каждого Строка Из Объект.МатериалыИУслуги Цикл
			
			ПараметрыОтбора.Этап = Строка.ЭтапРедактирование;
			
			ЗаполнитьЗначенияСвойств(Строка, РеквизитыЭтапов.НайтиСтроки(ПараметрыОтбора)[0]);
			
			Если Строка.ПроизводитсяВПроцессе Тогда
				
				Строка.СпособПолученияПолуфабриката = ПолучитьСпособПолученияПолуфабриката(Строка.ИсточникПолученияПолуфабриката);
				
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		РеквизитыЭтапа = Новый Структура;
		РеквизитыЭтапа.Вставить("ЭтоПервыйЭтап", Истина);
		РеквизитыЭтапа.Вставить("ПроизводствоНаСтороне", ПроизводственныйПроцесс.ПроизводствоНаСтороне);
			
		Для Каждого Строка Из Объект.МатериалыИУслуги Цикл
			ЗаполнитьЗначенияСвойств(Строка, РеквизитыЭтапа);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеЭтапаВТаблицеМатериалыИУслуги(Источник)
	
	МассивЭтапов = Новый Массив;
	
	МассивЭтапов.Добавить(Источник);
	МассивЭтапов.Добавить(Справочники.ЭтапыПроизводства.ПустаяСсылка());
	
	ПараметрыОтбора = Новый Структура("ЭтапРедактирование");
	
	Этапы = ПолучитьСлужебныеРеквизитыЭтапов(Объект.Ссылка, МассивЭтапов);
	
	Для Каждого РеквизитыЭтапа Из Этапы Цикл
		
		ПараметрыОтбора.ЭтапРедактирование = РеквизитыЭтапа.Этап;
		
		Для Каждого Строка Из Объект.МатериалыИУслуги.НайтиСтроки(ПараметрыОтбора) Цикл
			
			ЗаполнитьЗначенияСвойств(Строка, РеквизитыЭтапа);
			
			Если Строка.ПроизводствоНаСтороне И Строка.ПроизводитсяВПроцессе Тогда
		
				Строка.ПроизводитсяВПроцессе = Ложь;
				
				Строка.СтатьяКалькуляции = Справочники.СтатьиКалькуляции.ПустаяСсылка();
				
				Строка.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
				Строка.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
				
			КонецЕсли;
			
			Если Строка.ЭтоПервыйЭтап
				И Строка.СпособПолученияПолуфабриката = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиНаЭтапе Тогда
				
				Строка.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
				Строка.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеЭтапаВСтрокеМатериалыИУслуги()
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущаяСтрока;
	ТекущиеДанные = Объект.МатериалыИУслуги.НайтиПоИдентификатору(ТекущаяСтрока);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, 
		ПолучитьСлужебныеРеквизитыЭтапа(Объект.Ссылка, ТекущиеДанные.ЭтапРедактирование));
	
	Если ТекущиеДанные.ПроизводитсяВПроцессе Тогда
		
		Если ТекущиеДанные.ПроизводствоНаСтороне Тогда
			
			ТекущиеДанные.ПроизводитсяВПроцессе = Ложь;
			
			ТекущиеДанные.СтатьяКалькуляции = Справочники.СтатьиКалькуляции.ПустаяСсылка();
			
			ТекущиеДанные.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
			ТекущиеДанные.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
			
		КонецЕсли;
		
		Если ТекущиеДанные.ЭтоПервыйЭтап
			И ТекущиеДанные.СпособПолученияПолуфабриката = Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиНаЭтапе Тогда
			
			ТекущиеДанные.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
			ТекущиеДанные.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоСтатусуСервер()
	
	Если ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли; 
	
	ДоступностьРеквизитов = (Объект.Статус = Перечисления.СтатусыСпецификаций.ВРазработке);
	СпецификацияЗакрыта = (Объект.Статус = Перечисления.СтатусыСпецификаций.Закрыта);
	
	МассивЭлементов = Справочники.РесурсныеСпецификации.ПолучитьБлокируемыеРеквизитыОбъекта();
	
	// Элементы одноэтапного производственного процесса
	Если НЕ Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		МассивЭлементовЭтапа = Справочники.ЭтапыПроизводства.ПолучитьБлокируемыеРеквизитыОбъекта();
		Для каждого ИмяЭлемента Из МассивЭлементовЭтапа Цикл
			МассивЭлементов.Добавить("ПроизводственныйПроцесс" + ИмяЭлемента);
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", НЕ ДоступностьРеквизитов);
	
	// Если спецификация закрыта то сделаем недоступными остальные реквизиты
	МассивЭлементов.Очистить();
	МассивЭлементов.Добавить("НачалоДействия");
	МассивЭлементов.Добавить("КонецДействия");
	МассивЭлементов.Добавить("ПечатьМаршрутнойКартыПоЭтапам");
	МассивЭлементов.Добавить("ПечатьМаршрутнойКартыПоМаршрутнымКартам");
	МассивЭлементов.Добавить("Ответственный");
	Если НЕ Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		МассивЭлементовЭтапа = Справочники.ЭтапыПроизводства.НедоступныеЭлементыЗакрытойСпецификации();
		Для каждого ИмяЭлемента Из МассивЭлементовЭтапа Цикл
			МассивЭлементов.Добавить("ПроизводственныйПроцесс" + ИмяЭлемента);
		КонецЦикла; 
	КонецЕсли;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", СпецификацияЗакрыта);
	
	// Элементы доступные только в действующей спецификации
	МассивЭлементов.Очистить();
	МассивЭлементов.Добавить("МатериалыИУслугиВвестиРазрешениеНаЗамену");
	
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", ДоступностьРеквизитов);
	
	УстановитьДоступностьКомандБуфераОбмена();
	
	Если НЕ СпецификацияЗакрыта Тогда
		Элементы.ПроизводственныйПроцессМаксимальноеКоличествоЕдиницПартийИзделия.ТолькоПросмотр = НЕ РазбиватьМаршрутныеЛисты;
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеПринудительно()

	ЕстьОшибки = Ложь;
	
	Если НЕ Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		ПроизводственныйПроцессОбъект = РеквизитФормыВЗначение("ПроизводственныйПроцесс");
		
		//++ НЕ УТКА
		УправлениеДаннымиОбИзделиях.СохранитьВидыРабочихЦентров(
					ВидыРабочихЦентров, 
					ПроизводственныйПроцессОбъект.ПланироватьРаботуВидовРабочихЦентров,
					ПроизводственныйПроцессОбъект.ВидыРабочихЦентров,
					ПроизводственныйПроцессОбъект.АльтернативныеВидыРабочихЦентров);
		//-- НЕ УТКА
		
		ПроизводственныйПроцессОбъект.ПроверитьЗаполнениеРеквизитов(Ложь, "ПроизводственныйПроцесс", ЕстьОшибки);
	КонецЕсли; 
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	СправочникОбъект.Статус = Перечисления.СтатусыСпецификаций.Действует;

	Если Не Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		
		РеквизитыЭтапа = Новый Структура;
		РеквизитыЭтапа.Вставить("ПроизводствоНаСтороне", ПроизводственныйПроцесс.ПроизводствоНаСтороне);
		
		ЕстьОшибки = ЕстьОшибки ИЛИ СправочникОбъект.ПроверитьЗаполнениеСпецификацииПоРеквизитамЭтапов(, РеквизитыЭтапа); 
		
	КонецЕсли;
	
	ЕстьОшибки = ЕстьОшибки ИЛИ НЕ СправочникОбъект.ПроверитьЗаполнение();
		
	Возврат НЕ ЕстьОшибки;
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьКомандИзмененияСтатуса()
	
	Элементы.УстановитьСтатусДействует.Доступность   = (Объект.Статус <> Перечисления.СтатусыСпецификаций.Действует);
	Элементы.УстановитьСтатусаЗакрыта.Доступность    = (Объект.Статус <> Перечисления.СтатусыСпецификаций.Закрыта);
	Элементы.УстановитьСтатусВРазработке.Доступность = (Объект.Статус <> Перечисления.СтатусыСпецификаций.ВРазработке);
	Элементы.ПроверитьЗаполнение.Доступность         = (Объект.Статус <> Перечисления.СтатусыСпецификаций.Действует);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьНовыйСтатусИЗаписать(Статус)

	Если Объект.ПометкаУдаления Тогда
		ТекстПредупреждения = НСтр("ru='Нельзя изменить статус помеченной на удаление спецификации.';uk='Не можна змінити статус позначеної на вилучення специфікації.'");
  		ПоказатьПредупреждение(,ТекстПредупреждения);
		Возврат;
	КонецЕсли; 
	
	ОчиститьСообщения();
	
	ТекущийСтатус = Объект.Статус;
	Объект.Статус = Статус;
	
	СтатусИзменен = Ложь;
	Попытка
		СтатусИзменен = Записать();
	Исключение
	
	КонецПопытки;
	
	Если НЕ СтатусИзменен Тогда
		Объект.Статус = ТекущийСтатус;
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура ВопросДобавитьЭтап(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Статус = СтатусДействующейСпецификации Тогда
		Объект.Статус = СтатусСпецификацииВРазработке;
	КонецЕсли;
	
	Если Записать() Тогда
		Элементы.ЭтапыСписок.ДобавитьСтроку();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикУстановитьСтатусВРазработке(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия = Истина Тогда
		
		УстановитьНовыйСтатусИЗаписать(ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.ВРазработке"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		СписокТоваров = ВыбранноеЗначение;
	Иначе
		СписокТоваров = Новый Массив;
		СписокТоваров.Добавить(ВыбранноеЗначение);
	КонецЕсли;
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакСерииИспользуются", Новый Структура("Номенклатура", "СерииИспользуются"));
	
	КэшированныеЗначения = ОбработкаТабличнойЧастиКлиентСервер.ПолучитьСтруктуруКэшируемыеЗначения();
	
	Для каждого СтрокаТовара Из СписокТоваров Цикл
		
		ТекущаяСтрока = Объект.МатериалыИУслуги.Добавить();
		ТекущаяСтрока.Номенклатура = СтрокаТовара;
		
		СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ТекущаяСтрока.Характеристика);
		СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ТекущаяСтрока.Упаковка);
		
		ОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);
		
		ТекущаяСтрока.КлючСвязи = Новый УникальныйИдентификатор;
		УправлениеДаннымиОбИзделияхКлиентСервер.ПриВводеНовойСтрокиМатериалов(ТекущаяСтрока);
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЭтапаВыпуска(ТекущиеДанные, ВыбранноеЗначение)
	
	ТекущиеДанные.ИсточникПолученияПолуфабриката = ВыбранноеЗначение.Этап;
	
	Если ВыбранноеЗначение.Свойство("ДобавитьПромежуточныйВыпускПриВыборе")
		И ВыбранноеЗначение.ДобавитьПромежуточныйВыпускПриВыборе Тогда
			
		ТекстВопроса = НСтр("ru='Добавить побочный выпуск?';uk='Додати побічний випуск?'");
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
		ПараметрыОповещения.Вставить("Характеристика", ТекущиеДанные.Характеристика);
		
		ПараметрыОповещения.Вставить("Артикул", ТекущиеДанные.Артикул);
		ПараметрыОповещения.Вставить("ХарактеристикиИспользуются", ТекущиеДанные.ХарактеристикиИспользуются);
		
		ПараметрыОповещения.Вставить("Упаковка", ТекущиеДанные.Упаковка);
		ПараметрыОповещения.Вставить("ЭтапРедактирование", ВыбранноеЗначение.Этап);
		ПараметрыОповещения.Вставить("СтатьяКалькуляции", ПредопределенноеЗначение("Справочник.СтатьиКалькуляции.ПолуфабрикатыПроизводимыеВПроцессе"));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораЭтапаВыпускаЗавершение", ЭтаФорма, ПараметрыОповещения);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораЭтапаВыпускаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ПромежуточныйВыпуск = Объект.ВозвратныеОтходы.Добавить();
	
	ЗаполнитьЗначенияСвойств(ПромежуточныйВыпуск, ДополнительныеПараметры);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ПроверитьХарактеристикуПоВладельцу", ПромежуточныйВыпуск.Характеристика);
	СтруктураДействий.Вставить("ПроверитьЗаполнитьУпаковкуПоВладельцу", ПромежуточныйВыпуск.Упаковка);
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", Новый Структура("НужноОкруглять", Ложь));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ПромежуточныйВыпуск, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭтапыПоРесурснойСпецификации(Спецификация)
	
	Если НЕ ЗначениеЗаполнено(Спецификация) ИЛИ Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		Возврат
	КонецЕсли;
	
	ЭтапОдногоЭтапногоПроцесса = Справочники.РесурсныеСпецификации.ПолучитьЭтапОдногоЭтапногоПроцесса(Спецификация);
	
	Если ЭтапОдногоЭтапногоПроцесса = Неопределено Тогда
		ПроизводственныйПроцессОбъект = Справочники.ЭтапыПроизводства.СоздатьЭлемент();
	Иначе
		ПроизводственныйПроцессОбъект = ЭтапОдногоЭтапногоПроцесса.Скопировать();
	КонецЕсли; 
	
	ПроизводственныйПроцессОбъект.Владелец = Объект.Ссылка;
	
	ЗначениеВРеквизитФормы(ПроизводственныйПроцессОбъект, "ПроизводственныйПроцесс");

КонецПроцедуры

//++ НЕ УТКА

&НаКлиенте
Функция ПроверитьЗаполнениеИзделия(ТекстСообщения)

	Если Объект.ВыходныеИзделия.Количество() = 0  Тогда
		Поле = "Объект.ВыходныеИзделия";
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ВыходныеИзделия[0].Номенклатура) Тогда
		Поле = "Объект.ВыходныеИзделия[0].Номенклатура";
	Иначе
		Возврат Истина
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле);
	
	Возврат Ложь;

КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоМаршрутнымКартамЭтаповЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоМаршрутнымКартамЭтапов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоМаршрутнымКартамЭтапов()

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц; //временные таблицы используются повторно
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		
		ТекстЗапросаСписокЭтапов = "ВЫБРАТЬ
		               |	ЭтапыПроизводства.Ссылка КАК Этап,
		               |	ЭтапыПроизводства.МаршрутнаяКарта КАК МаршрутнаяКарта,
		               |	ЭтапыПроизводства.НомерЭтапа,
		               |	ВЫБОР
		               |		КОГДА ЭтапыПроизводства.НомерЭтапа = 1
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК ЭтоПервыйЭтап,
		               |	ВЫБОР
		               |		КОГДА ЭтапыПроизводства.НомерСледующегоЭтапа = 0
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК ЭтоПоследнийЭтап
		               |ПОМЕСТИТЬ СписокЭтапов
		               |ИЗ
		               |	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		               |ГДЕ
		               |	ЭтапыПроизводства.Владелец = &Спецификация
		               |	И НЕ ЭтапыПроизводства.ПометкаУдаления";
					   
		Запрос.УстановитьПараметр("Спецификация", Объект.Ссылка);
		
	Иначе	
		
		ТекстЗапросаСписокЭтапов = "ВЫБРАТЬ
		               |	НЕОПРЕДЕЛЕНО КАК Этап,
		               |	&МаршрутнаяКарта КАК МаршрутнаяКарта,
		               |	1 КАК НомерЭтапа,
		               |	ИСТИНА КАК ЭтоПервыйЭтап,
		               |	ИСТИНА КАК ЭтоПоследнийЭтап
		               |ПОМЕСТИТЬ СписокЭтапов";
					   
		Запрос.УстановитьПараметр("МаршрутнаяКарта", ПроизводственныйПроцесс.МаршрутнаяКарта);
		
	КонецЕсли; 
	
	СтруктураДанных = Неопределено;
	ПолучитьДанныеМаршрутнойКарты(Запрос, ТекстЗапросаСписокЭтапов, СтруктураДанных);
	
	Запрос.МенеджерВременныхТаблиц.Закрыть();
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		
		// Нужно удалить строки где для этапа указана МК, а остальные оставить
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЭтапыПроизводства.Ссылка КАК Этап,
		|	ЭтапыПроизводства.МаршрутнаяКарта КАК МаршрутнаяКарта,
		|	ЭтапыПроизводства.МаршрутнаяКарта.ИспользоватьВыходныеИзделия КАК ИспользоватьВыходныеИзделия,
		|	ЭтапыПроизводства.МаршрутнаяКарта.ИспользоватьМатериалы КАК ИспользоватьМатериалы,
		|	ЭтапыПроизводства.МаршрутнаяКарта.ИспользоватьТрудозатраты КАК ИспользоватьТрудозатраты,
		|	ВЫБОР
		|		КОГДА ЭтапыПроизводства.НомерЭтапа = 1
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПервыйЭтап,
		|	ВЫБОР
		|		КОГДА ЭтапыПроизводства.НомерСледующегоЭтапа = 0
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоПоследнийЭтап
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|ГДЕ
		|	ЭтапыПроизводства.Владелец = &Спецификация
		|	И НЕ ЭтапыПроизводства.ПометкаУдаления
		|	И ЭтапыПроизводства.МаршрутнаяКарта <> ЗНАЧЕНИЕ(Справочник.МаршрутныеКарты.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Спецификация", Объект.Ссылка);
		ТаблицаЭтапы = Запрос.Выполнить().Выгрузить();
		
		УдалитьСтрокиГдеДляЭтаповУказаныМК(Объект.ВыходныеИзделия, ТаблицаЭтапы, "ЭтоПоследнийЭтап", "ИспользоватьВыходныеИзделия");
		УдалитьСтрокиГдеДляЭтаповУказаныМК(Объект.ВозвратныеОтходы, ТаблицаЭтапы, "ЭтоПоследнийЭтап", "ИспользоватьВыходныеИзделия");
		УдалитьСтрокиГдеДляЭтаповУказаныМК(Объект.МатериалыИУслуги, ТаблицаЭтапы, "ЭтоПервыйЭтап", "ИспользоватьМатериалы");
		УдалитьСтрокиГдеДляЭтаповУказаныМК(Объект.Трудозатраты, ТаблицаЭтапы, "ЭтоПервыйЭтап", "ИспользоватьТрудозатраты");
		
		СтрокиКУдалению = Новый Массив;
		Для каждого ДанныеСтроки Из Объект.СоответствиеСвойств Цикл
			СтруктураПоиска = Новый Структура("КлючСвязи", ДанныеСтроки.КлючСвязиМатериал);
   			СписокСтрок = Объект.МатериалыИУслуги.НайтиСтроки(СтруктураПоиска);
			Если СписокСтрок.Количество() = 0 Тогда
				СтрокиКУдалению.Добавить(ДанныеСтроки);
			КонецЕсли; 
		КонецЦикла; 
		Для каждого ДанныеСтроки Из СтрокиКУдалению Цикл
			Объект.СоответствиеСвойств.Удалить(ДанныеСтроки);
		КонецЦикла; 
		
	ИначеЕсли НЕ ПроизводственныйПроцесс.МаршрутнаяКарта.Пустая() Тогда
		
		СоставМаршрутнойКарты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
											ПроизводственныйПроцесс.МаршрутнаяКарта, 
											"ИспользоватьВыходныеИзделия,ИспользоватьМатериалы,ИспользоватьТрудозатраты");
														
		Если СоставМаршрутнойКарты.ИспользоватьВыходныеИзделия Тогда
			Объект.ВыходныеИзделия.Очистить();
			Объект.ВозвратныеОтходы.Очистить();
		КонецЕсли; 
		Если СоставМаршрутнойКарты.ИспользоватьМатериалы Тогда
			Объект.МатериалыИУслуги.Очистить();
			Объект.СоответствиеСвойств.Очистить();
		КонецЕсли; 
		Если СоставМаршрутнойКарты.ИспользоватьТрудозатраты Тогда
			Объект.Трудозатраты.Очистить();
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если СтруктураДанных.ВыходныеИзделия <> Неопределено Тогда
		Для каждого ЭлементКоллекции Из СтруктураДанных.ВыходныеИзделия Цикл
			ЗаполнитьЗначенияСвойств(Объект.ВыходныеИзделия.Добавить(), ЭлементКоллекции);
		КонецЦикла; 
	КонецЕсли;
	
	Если СтруктураДанных.ВозвратныеОтходы <> Неопределено Тогда
		Для каждого ЭлементКоллекции Из СтруктураДанных.ВозвратныеОтходы Цикл
			ЗаполнитьЗначенияСвойств(Объект.ВозвратныеОтходы.Добавить(), ЭлементКоллекции);
		КонецЦикла; 
	КонецЕсли;
	
	Если СтруктураДанных.МатериалыИУслуги <> Неопределено Тогда
		Для каждого ЭлементКоллекции Из СтруктураДанных.МатериалыИУслуги Цикл
			ЗаполнитьЗначенияСвойств(Объект.МатериалыИУслуги.Добавить(), ЭлементКоллекции);
		КонецЦикла; 
		Для каждого ЭлементКоллекции Из СтруктураДанных.СоответствиеСвойств Цикл
			ЗаполнитьЗначенияСвойств(Объект.СоответствиеСвойств.Добавить(), ЭлементКоллекции);
		КонецЦикла; 
	КонецЕсли;
	
	Если СтруктураДанных.Трудозатраты <> Неопределено Тогда
		Для каждого ЭлементКоллекции Из СтруктураДанных.Трудозатраты Цикл
			ЗаполнитьЗначенияСвойств(Объект.Трудозатраты.Добавить(), ЭлементКоллекции);
		КонецЦикла; 
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизиты();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокиГдеДляЭтаповУказаныМК(Таблица, ТаблицаЭтапы, ПолеАвтовыбораЭтапа, ИмяРеквизитаСостава)

	СтрокиКУдалению = Новый Массив;
	Для каждого ДанныеСтроки Из Таблица Цикл
		
		Если НЕ ДанныеСтроки.ЭтапРедактирование.Пустая() Тогда
			СтрокаЭтап = ТаблицаЭтапы.Найти(ДанныеСтроки.ЭтапРедактирование, "Этап");
		Иначе
			СтрокаЭтап = ТаблицаЭтапы.Найти(Истина, ПолеАвтовыбораЭтапа);
		КонецЕсли;
		
		Если СтрокаЭтап <> Неопределено 
			И НЕ СтрокаЭтап.МаршрутнаяКарта.Пустая() 
			И СтрокаЭтап[ИмяРеквизитаСостава] Тогда
			
			СтрокиКУдалению.Добавить(ДанныеСтроки);
		КонецЕсли; 
		
	КонецЦикла; 

	Для каждого ДанныеСтроки Из СтрокиКУдалению Цикл
		Таблица.Удалить(ДанныеСтроки);
	КонецЦикла; 
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьДанныеМаршрутнойКарты(Запрос, ТекстЗапросаСписокЭтапов, СтруктураДанных, ВключитьВложенныеМК = Истина)

	Если СтруктураДанных = Неопределено Тогда
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("ВыходныеИзделия",     Неопределено);
		СтруктураДанных.Вставить("ВозвратныеОтходы",    Неопределено);
		СтруктураДанных.Вставить("МатериалыИУслуги",    Неопределено);
		СтруктураДанных.Вставить("СоответствиеСвойств", Неопределено);
		СтруктураДанных.Вставить("Трудозатраты",        Неопределено);
	КонецЕсли;
	
	ИндексРезультатаЗапроса = 0;
	ТекстЗапроса = "";
	
	Если ТекстЗапросаСписокЭтапов <> Неопределено Тогда
		ИндексРезультатаЗапроса = 1;
		
		ТекстЗапроса = ТекстЗапросаСписокЭтапов 
				+ "
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|";
	КонецЕсли; 
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	СписокЭтапов.Этап,
	|	СписокЭтапов.ЭтоПервыйЭтап КАК ЭтоПервыйЭтап,
	|	СписокЭтапов.ЭтоПоследнийЭтап КАК ЭтоПоследнийЭтап,
	|	СписокЭтапов.МаршрутнаяКарта КАК МаршрутнаяКарта
	|ПОМЕСТИТЬ СписокМаршрутныхКарт
	|ИЗ
	|	СписокЭтапов КАК СписокЭтапов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	МаршрутнаяКарта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1. ВыходныеИзделия
	|ВЫБРАТЬ
	|	СписокМаршрутныхКарт.Этап,
	|	СписокМаршрутныхКарт.Этап КАК ЭтапРедактирование,
	|	СписокМаршрутныхКарт.ЭтоПоследнийЭтап КАК ЭтоПоследнийЭтап,
	|	СписокМаршрутныхКарт.МаршрутнаяКарта,
	|	МаршрутныеКартыВыходныеИзделия.Номенклатура,
	|	МаршрутныеКартыВыходныеИзделия.Характеристика,
	|	МаршрутныеКартыВыходныеИзделия.КоличествоУпаковок,
	|	МаршрутныеКартыВыходныеИзделия.Количество,
	|	МаршрутныеКартыВыходныеИзделия.Упаковка,
	|	МаршрутныеКартыВыходныеИзделия.ДоляСтоимости
	|ИЗ
	|	СписокМаршрутныхКарт КАК СписокМаршрутныхКарт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты КАК МаршрутныеКарты
	|		ПО (МаршрутныеКарты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты.ВыходныеИзделия КАК МаршрутныеКартыВыходныеИзделия
	|		ПО (МаршрутныеКартыВыходныеИзделия.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|ГДЕ
	|	МаршрутныеКарты.ИспользоватьВыходныеИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2. ВозвратныеОтходы
	|ВЫБРАТЬ
	|	СписокМаршрутныхКарт.Этап,
	|	СписокМаршрутныхКарт.Этап КАК ЭтапРедактирование,
	|	СписокМаршрутныхКарт.ЭтоПоследнийЭтап КАК ЭтоПоследнийЭтап,
	|	СписокМаршрутныхКарт.МаршрутнаяКарта,
	|	МаршрутныеКартыВозвратныеОтходы.Номенклатура,
	|	МаршрутныеКартыВозвратныеОтходы.Характеристика,
	|	МаршрутныеКартыВозвратныеОтходы.КоличествоУпаковок,
	|	МаршрутныеКартыВозвратныеОтходы.Количество,
	|	МаршрутныеКартыВозвратныеОтходы.Упаковка,
	|	МаршрутныеКартыВозвратныеОтходы.СтатьяКалькуляции,
	|	МаршрутныеКартыВозвратныеОтходы.ОписаниеИзделия
	|ИЗ
	|	СписокМаршрутныхКарт КАК СписокМаршрутныхКарт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты КАК МаршрутныеКарты
	|		ПО (МаршрутныеКарты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты.ВозвратныеОтходы КАК МаршрутныеКартыВозвратныеОтходы
	|		ПО (МаршрутныеКартыВозвратныеОтходы.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|ГДЕ
	|	МаршрутныеКарты.ИспользоватьВыходныеИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3. МатериалыИУслуги
	|ВЫБРАТЬ
	|		СписокМаршрутныхКарт.Этап,
	|		СписокМаршрутныхКарт.Этап КАК ЭтапРедактирование,
	|		МаршрутныеКартыМатериалыИУслуги.Номенклатура,
	|		МаршрутныеКартыМатериалыИУслуги.Характеристика,
	|		МаршрутныеКартыМатериалыИУслуги.КоличествоУпаковок,
	|		МаршрутныеКартыМатериалыИУслуги.Количество,
	|		МаршрутныеКартыМатериалыИУслуги.Упаковка,
	|		МаршрутныеКартыМатериалыИУслуги.СтатьяКалькуляции,
	|		МаршрутныеКартыМатериалыИУслуги.СпособАвтовыбораНоменклатуры,
	|		МаршрутныеКартыМатериалыИУслуги.СпособАвтовыбораХарактеристики,
	|		МаршрутныеКартыМатериалыИУслуги.СвойствоСодержащееНоменклатуру,
	|		МаршрутныеКартыМатериалыИУслуги.ОтборСвойство,
	|		МаршрутныеКартыМатериалыИУслуги.ОтборЗначениеСвойства,
	|		МаршрутныеКартыМатериалыИУслуги.АлгоритмРасчетаКоличества,
	|	ВЫБОР
	|		КОГДА МаршрутныеКартыМатериалыИУслуги.ПрименениеМатериала = """"
	|			ТОГДА МаршрутныеКартыМатериалыИУслуги.Операция
	|		ИНАЧЕ МаршрутныеКартыМатериалыИУслуги.ПрименениеМатериала
	|	КОНЕЦ КАК ПрименениеМатериала,
	|		МаршрутныеКартыМатериалыИУслуги.КлючСвязи
	|ИЗ
	|	СписокМаршрутныхКарт КАК СписокМаршрутныхКарт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты КАК МаршрутныеКарты
	|		ПО (МаршрутныеКарты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты.МатериалыИУслуги КАК МаршрутныеКартыМатериалыИУслуги
	|		ПО (МаршрутныеКартыМатериалыИУслуги.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|ГДЕ
	|	МаршрутныеКарты.ИспользоватьМатериалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 4. Трудозатраты
	|ВЫБРАТЬ
	|		СписокМаршрутныхКарт.Этап,
	|		СписокМаршрутныхКарт.Этап КАК ЭтапРедактирование,
	|		МаршрутныеКартыТрудозатраты.ВидРабот,
	|		МаршрутныеКартыТрудозатраты.Количество,
	|		МаршрутныеКартыТрудозатраты.СтатьяКалькуляции
	|ИЗ
	|	СписокМаршрутныхКарт КАК СписокМаршрутныхКарт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты КАК МаршрутныеКарты
	|		ПО (МаршрутныеКарты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты.Трудозатраты КАК МаршрутныеКартыТрудозатраты
	|		ПО (МаршрутныеКартыТрудозатраты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|ГДЕ
	|	МаршрутныеКарты.ИспользоватьТрудозатраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 5. СоответствиеСвойств
	|ВЫБРАТЬ
	|		МаршрутныеКартыСоответствиеСвойств.СвойствоМатериала,
	|		МаршрутныеКартыСоответствиеСвойств.СвойствоПродукции,
	|		МаршрутныеКартыСоответствиеСвойств.КлючСвязиМатериал
	|ИЗ
	|	СписокМаршрутныхКарт КАК СписокМаршрутныхКарт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты КАК МаршрутныеКарты
	|		ПО (МаршрутныеКарты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты.СоответствиеСвойств КАК МаршрутныеКартыСоответствиеСвойств
	|		ПО (МаршрутныеКартыСоответствиеСвойств.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|ГДЕ
	|	МаршрутныеКарты.ИспользоватьМатериалы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 6.
	|УНИЧТОЖИТЬ СписокЭтапов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 7. Тех. операции
	|ВЫБРАТЬ
	|	СписокМаршрутныхКарт.Этап,
	|	СписокМаршрутныхКарт.ЭтоПервыйЭтап КАК ЭтоПервыйЭтап,
	|	СписокМаршрутныхКарт.ЭтоПоследнийЭтап КАК ЭтоПоследнийЭтап,
	|	спрОперации.ВложенныйМаршрут КАК МаршрутнаяКарта,
	|	спрОперации.Наименование КАК НаименованиеОперации
	|ПОМЕСТИТЬ СписокЭтапов
	|ИЗ
	|	СписокМаршрутныхКарт КАК СписокМаршрутныхКарт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.МаршрутныеКарты КАК МаршрутныеКарты
	|		ПО (МаршрутныеКарты.Ссылка = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ТехнологическиеОперации КАК спрОперации
	|		ПО (спрОперации.Владелец = СписокМаршрутныхКарт.МаршрутнаяКарта)
	|			И спрОперации.СодержитВложенныйМаршрут
	|ГДЕ 
	|	НЕ спрОперации.Ссылка.ПометкаУдаления
	|	И МаршрутныеКарты.ИспользоватьОперации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СписокМаршрутныхКарт";
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.ВыполнитьПакет();
	// Результат[0] - СписокМаршрутныхКарт
	// Результат[1] - ВыходныеИзделия
	// Результат[2] - ВозвратныеОтходы
	// Результат[3] - МатериалыИУслуги
	// Результат[4] - Трудозатраты
	// Результат[5] - СоответствиеСвойств
	// Результат[7] - СписокЭтапов по данным вложенных МК, который будет использоваться на следующей итерации выполнения процедуры
	
	Если Результат[1 + ИндексРезультатаЗапроса].Пустой() И Результат[3 + ИндексРезультатаЗапроса].Пустой() Тогда
		Возврат;
	КонецЕсли; 
	
	ДобавитьДанныеВТаблицуСтруктурыДанных(Результат[1 + ИндексРезультатаЗапроса].Выгрузить(), СтруктураДанных.ВыходныеИзделия);
	ДобавитьДанныеВТаблицуСтруктурыДанных(Результат[2 + ИндексРезультатаЗапроса].Выгрузить(), СтруктураДанных.ВозвратныеОтходы);
	ДобавитьДанныеВТаблицуСтруктурыДанных(Результат[4 + ИндексРезультатаЗапроса].Выгрузить(), СтруктураДанных.Трудозатраты);
	ДобавитьДанныеВТаблицуСтруктурыДанных(Результат[3 + ИндексРезультатаЗапроса].Выгрузить(), СтруктураДанных.МатериалыИУслуги);
	ДобавитьДанныеВТаблицуСтруктурыДанных(Результат[5 + ИндексРезультатаЗапроса].Выгрузить(), СтруктураДанных.СоответствиеСвойств);
	
	Если ВключитьВложенныеМК Тогда
		ПолучитьДанныеМаршрутнойКарты(Запрос, Неопределено, СтруктураДанных);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьДанныеВТаблицуСтруктурыДанных(ТаблицаИсточник, ТаблицаСтруктурыДанных)

	Если ТаблицаСтруктурыДанных = Неопределено Тогда
		ТаблицаСтруктурыДанных = ТаблицаИсточник.СкопироватьКолонки();
	КонецЕсли; 
	Для каждого Выборка Из ТаблицаИсточник Цикл
		НоваяСтрока = ТаблицаСтруктурыДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	
КонецПроцедуры

//-- НЕ УТКА

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)
	
	Форма.Элементы.ПроизводственныйПроцессМаксимальноеКоличествоЕдиницПартийИзделия.ТолькоПросмотр = НЕ Форма.РазбиватьМаршрутныеЛисты;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборПоляВТаблице(ИмяТаблицы, ИмяПоля, СтандартнаяОбработка)

	Если НЕ ТолькоПросмотр И ДоступностьРеквизитов Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элементы[ИмяТаблицы].ТекущиеДанные;
	
	Если (ИмяПоля = "ВыходныеИзделияНоменклатура"
			ИЛИ ИмяПоля = "ВыходныеИзделияАртикул"
			ИЛИ ИмяПоля = "ВозвратныеОтходыНоменклатура"
			ИЛИ ИмяПоля = "ВозвратныеОтходыАртикул"
			ИЛИ ИмяПоля = "МатериалыИУслугиНоменклатура"
			ИЛИ ИмяПоля = "МатериалыИУслугиАртикул")
		И ЗначениеЗаполнено(ТекущиеДанные.Номенклатура)  Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.Номенклатура);
		
	ИначеЕсли (ИмяПоля = "ВыходныеИзделияЭтапРедактирование" 
			ИЛИ ИмяПоля = "МатериалыИУслугиЭтапРедактирование"
			ИЛИ ИмяПоля = "ТрудозатратыЭтапРедактирование")
		И ЗначениеЗаполнено(ТекущиеДанные.ЭтапРедактирование) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.ЭтапРедактирование);
		
	ИначеЕсли (ИмяПоля = "МатериалыИУслугиИсточникПолученияПолуфабриката")
		И ЗначениеЗаполнено(ТекущиеДанные.ИсточникПолученияПолуфабриката)  Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.ИсточникПолученияПолуфабриката);
		
	ИначеЕсли ИмяПоля = "ТрудозатратыСтатьяКалькуляции"
		И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции) Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.СтатьяКалькуляции);
		
	ИначеЕсли ИмяПоля = "МатериалыИУслугиСтатьяКалькуляции"
		И ЗначениеЗаполнено(ТекущиеДанные.СтатьяКалькуляции)
		И Не ТекущиеДанные.ПроизводитсяВПроцессе Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.СтатьяКалькуляции);
		
	ИначеЕсли (ИмяПоля = "ТрудозатратыВидРабот")
		И ЗначениеЗаполнено(ТекущиеДанные.ВидРабот)  Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.ВидРабот);
		
	ИначеЕсли (ИмяПоля = "ВидыРабочихЦентровВидРабочегоЦентра")
		И ЗначениеЗаполнено(ТекущиеДанные.ВидРабочегоЦентра)  Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.ВидРабочегоЦентра);
		
	ИначеЕсли (ИмяПоля = "ВидыРабочихЦентровВариантНаладки")
		И ЗначениеЗаполнено(ТекущиеДанные.ВариантНаладки)  Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, ТекущиеДанные.ВариантНаладки);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура МатериалыИУслугиТребуетсяУказыватьСерииПриИзмененииНаСервере()

	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущаяСтрока;
	ТекущиеДанные = Объект.МатериалыИУслуги.НайтиПоИдентификатору(ТекущаяСтрока);
	
	// Флаг должен быть одинаковым для всех строк в которых указан такой же материал
	СтруктураПоиска = Новый Структура("Номенклатура");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущиеДанные);
 	СписокСтрок = Объект.МатериалыИУслуги.НайтиСтроки(СтруктураПоиска);	
	Для каждого ДанныеСтроки Из СписокСтрок Цикл
		ДанныеСтроки.ТребуетсяУказыватьСерии = ТекущиеДанные.ТребуетсяУказыватьСерии;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыходныеИзделияТребуетсяУказыватьСерииПриИзмененииНаСервере()

	ТекущаяСтрока = Элементы.ВыходныеИзделия.ТекущаяСтрока;
	ТекущиеДанные = Объект.ВыходныеИзделия.НайтиПоИдентификатору(ТекущаяСтрока);
	
	// Флаг должен быть одинаковым для всех строк в которых указано такое же изделие
	СтруктураПоиска = Новый Структура("Номенклатура");
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущиеДанные);
 	СписокСтрок = Объект.ВыходныеИзделия.НайтиСтроки(СтруктураПоиска);	
	Для каждого ДанныеСтроки Из СписокСтрок Цикл
		ДанныеСтроки.ТребуетсяУказыватьСерии = ТекущиеДанные.ТребуетсяУказыватьСерии;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеСтатейКалькуляцииВзносов(Форма)
    
	Если Не ЗначениеЗаполнено(Форма.Объект.СтатьяКалькуляцииВзносов) Тогда
		Заголовок = НСтр("ru='Отражать страховые взносы по статье калькуляции оплаты труда.';uk='Відображати страхові внески за статтею калькуляції оплати праці.'");
	Иначе
        Заголовок = НСтр("ru='Отражать страховые взносы отдельной статьей калькуляции.';uk='Відображати страхові внески окремою статтею калькуляції.'");
    КонецЕсли;
	
	Форма.Элементы.ПредставлениеСтатейКалькуляцииВзносов.Заголовок = Заголовок;
	
КонецПроцедуры

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыВыбораЭтапаВыпуска()
	
	АдресХранилища = Неопределено;
	
	ТекущаяСтрока = Элементы.МатериалыИУслуги.ТекущаяСтрока;
	ТекущиеДанные = Объект.МатериалыИУслуги.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ЭтапРедактирование) Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура", ТекущиеДанные.Номенклатура);
		
		Если ТекущиеДанные.ХарактеристикиИспользуются Тогда
			
			ПараметрыОтбора.Вставить("Характеристика", ТекущиеДанные.Характеристика);
			
		КонецЕсли;
		
		ТаблицаПолуфабрикатов = Объект.ВозвратныеОтходы.Выгрузить(ПараметрыОтбора, "ЭтапРедактирование, Количество");
		
		ТаблицаПолуфабрикатов.Свернуть("ЭтапРедактирование", "Количество");
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВЫРАЗИТЬ(Таблица.ЭтапРедактирование КАК Справочник.ЭтапыПроизводства) КАК Этап,
		|	Таблица.Количество КАК Количество
		|ПОМЕСТИТЬ ВТТаблицаПолуфабрикатов
		|ИЗ
		|	&ТаблицаПолуфабрикатов КАК Таблица
		|ГДЕ
		|	Таблица.ЭтапРедактирование <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЭтапыПроизводства.Ссылка КАК Этап,
		|	ЭтапыПроизводства.Наименование КАК НаименованиеЭтапа,
		|	ЭтапыПроизводства.НомерЭтапа КАК НомерЭтапа,
		|	ЭтапыПроизводства.НомерСледующегоЭтапа КАК НомерСледующегоЭтапа,
		|	ЭтапыПроизводства.Подразделение КАК Подразделение,
		|	ЭтапыПроизводства.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
		|	ВЫБОР
		|		КОГДА &Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(&Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения).Представление
		|		ИНАЧЕ ВЫРАЗИТЬ(&Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения.Представление
		|	КОНЕЦ КАК УпаковкаПредставление,
		|	ЕСТЬNULL(ТаблицаПолуфабрикатов.Количество, 0) / ВЫБОР
		|		КОГДА &Упаковка <> ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|			ТОГДА &ТекстЗапросаКоэффициентУпаковки
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Количество,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ТаблицаПолуфабрикатов.Количество, 0) >= &Количество
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК КоличествоДостаточно,
		|	ВЫБОР
		|		КОГДА ТаблицаПолуфабрикатов.Количество ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДобавитьПромежуточныйВыпускПриВыборе
		|ИЗ
		|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаПолуфабрикатов КАК ТаблицаПолуфабрикатов
		|		ПО ЭтапыПроизводства.Ссылка = ТаблицаПолуфабрикатов.Этап
		|ГДЕ
		|	ЭтапыПроизводства.Владелец = &Владелец
		|	И НЕ ЭтапыПроизводства.ПометкаУдаления
		|	И НЕ ЭтапыПроизводства.ПроизводствоНаСтороне
		|	И ЭтапыПроизводства.НомерЭтапа < ВЫРАЗИТЬ(&Этап КАК Справочник.ЭтапыПроизводства).НомерЭтапа
		|	И ЭтапыПроизводства.НомерСледующегоЭтапа <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерЭтапа";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
									"&ТекстЗапросаКоэффициентУпаковки",
									Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("ВЫРАЗИТЬ(&Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения)",
																										"ВЫРАЗИТЬ(&Номенклатура КАК Справочник.Номенклатура)"));	
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Владелец", Объект.Ссылка);
		Запрос.УстановитьПараметр("Этап",     ТекущиеДанные.ЭтапРедактирование);
		
		Запрос.УстановитьПараметр("Номенклатура", ТекущиеДанные.Номенклатура);
		Запрос.УстановитьПараметр("Упаковка",     ТекущиеДанные.Упаковка);
		Запрос.УстановитьПараметр("Количество",   ТекущиеДанные.Количество);
		
		Запрос.УстановитьПараметр("ТаблицаПолуфабрикатов", ТаблицаПолуфабрикатов);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			АдресХранилища = ПоместитьВоВременноеХранилище(РезультатЗапроса.Выгрузить(), УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыВыбораЭтапа = Новый Структура;
	ПараметрыВыбораЭтапа.Вставить("Номенклатура",   ТекущиеДанные.Номенклатура);
	ПараметрыВыбораЭтапа.Вставить("Характеристика", ТекущиеДанные.Характеристика);
	ПараметрыВыбораЭтапа.Вставить("АдресХранилища", АдресХранилища);
	
	Возврат ПараметрыВыбораЭтапа;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьИсточникПолученияПолуфабрикатаПриИзмененииВидаПроизводственногоПроцесса()
	
	Если Объект.МногоэтапныйПроизводственныйПроцесс Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ПроизводитсяВПроцессе", Истина);
	ПараметрыОтбора.Вставить("СпособПолученияПолуфабриката", Перечисления.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиНаЭтапе);
	
	Для Каждого Строка Из Объект.МатериалыИУслуги.НайтиСтроки(ПараметрыОтбора) Цикл
		
		Строка.СпособПолученияПолуфабриката   = ПолучитьСпособПолученияПолуфабриката();
		Строка.ИсточникПолученияПолуфабриката = ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию();
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСпособПолученияПолуфабриката(ИсточникПолученияПолуфабриката = Неопределено)
	
//++ НЕ УТКА
	Если ИсточникПолученияПолуфабриката = Неопределено
		ИЛИ ТипЗнч(ИсточникПолученияПолуфабриката) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
//-- НЕ УТКА
		
		Возврат ПредопределенноеЗначение("Перечисление.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации");
		
//++ НЕ УТКА
	ИначеЕсли ТипЗнч(ИсточникПолученияПолуфабриката) = Тип("СправочникСсылка.ЭтапыПроизводства") Тогда
		
		Возврат ПредопределенноеЗначение("Перечисление.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиНаЭтапе");
		
	КонецЕсли;
//-- НЕ УТКА
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИсточникПолученияПолуфабрикатаПоУмолчанию(СпособПолученияПолуфабриката = Неопределено)
	
//++ НЕ УТКА
	Если СпособПолученияПолуфабриката = Неопределено 
		ИЛИ СпособПолученияПолуфабриката = ПредопределенноеЗначение("Перечисление.СпособыПолучениеМатериаловЭтапаПроизводства.ПроизвестиПоСпецификации") Тогда
//-- НЕ УТКА
		
		Возврат ПредопределенноеЗначение("Справочник.РесурсныеСпецификации.ПустаяСсылка");
		
//++ НЕ УТКА
	Иначе
		
		Возврат ПредопределенноеЗначение("Справочник.ЭтапыПроизводства.ПустаяСсылка");
		
	КонецЕсли;
//-- НЕ УТКА
	
КонецФункции

// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

////////////////////////////////////////////////////////////////////////////////
// Процедуры подсистемы свойств.

&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств(Команда)
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуОтчет(Команда)
	
	МенюОтчетыКлиент.ВыполнитьПодключаемуюКомандуОтчет(Команда, ЭтаФорма, Объект);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
