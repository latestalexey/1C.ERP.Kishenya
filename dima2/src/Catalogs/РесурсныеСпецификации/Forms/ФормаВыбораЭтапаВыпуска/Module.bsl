
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("АдресХранилища")
		И ЭтоАдресВременногоХранилища(Параметры.АдресХранилища) Тогда
		
		Этапы.Загрузить(ПолучитьИзВременногоХранилища(Параметры.АдресХранилища));
		
		Элементы.ФормаПоказатьВсе.Пометка = Этапы.НайтиСтроки(Новый Структура("КоличествоДостаточно", Истина)).ВГраница() = -1;
		
	КонецЕсли;
	
	Если Параметры.Свойство("Номенклатура", Номенклатура)
		И Параметры.Свойство("Характеристика", Характеристика)
		И ЗначениеЗаполнено(Номенклатура) Тогда
		
		ТекстШаблона = НСтр("ru='Этапы производства изделия %НоменклатураХарактеристика%';uk='Етапи виробництва виробу %НоменклатураХарактеристика%'");
		
		ЭтаФорма.Заголовок = СтрЗаменить(ТекстШаблона, "%НоменклатураХарактеристика%", 
							НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Номенклатура, Характеристика));
							
	Иначе
		
		ЭтаФорма.Заголовок = НСтр("ru='Этапы производства';uk='Етапи виробництва'");
		
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтапы

&НаКлиенте
Процедура ЭтапыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Этапы.ТекущиеДанные;
	
	ЗначениеВыбора = Новый Структура;
	ЗначениеВыбора.Вставить("Этап", ТекущиеДанные.Этап);
	ЗначениеВыбора.Вставить("ДобавитьПромежуточныйВыпускПриВыборе", ТекущиеДанные.ДобавитьПромежуточныйВыпускПриВыборе);
	
	ОповеститьОВыборе(ЗначениеВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсе(Команда)
	
	Элементы.ФормаПоказатьВсе.Пометка = Не Элементы.ФормаПоказатьВсе.Пометка;
	
	УправлениеФормой(ЭтаФорма, "УстановитьОтборСтрок");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма, Действие = "")
	
	ЭлементыФормы = Форма.Элементы;
	
	ВсеДействия = ПустаяСтрока(Действие);
	
	Если ВсеДействия Тогда
		
		ЭлементыФормы.ФормаПоказатьВсе.Видимость = Форма.Этапы.Количество() > 0;
		
	КонецЕсли;
	
	Если ВсеДействия Или Действие = "УстановитьОтборСтрок" Тогда
		
		Если ЭлементыФормы.ФормаПоказатьВсе.Пометка Тогда
			ОтборСтрок = Новый ФиксированнаяСтруктура;
		Иначе
			ОтборСтрок = Новый ФиксированнаяСтруктура("КоличествоДостаточно", Истина);
		КонецЕсли;
		
		ЭлементыФормы.Этапы.ОтборСтрок = ОтборСтрок;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Этапы.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этапы.КоличествоДостаточно");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);

КонецПроцедуры

#КонецОбласти
