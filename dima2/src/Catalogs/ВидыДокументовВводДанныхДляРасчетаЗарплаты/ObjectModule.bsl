#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение краткого состава документа.
	МаксимальнаяДлинаСтроки = 50;
	
	КраткийСоставСтрока = "";
	
	МассивПоказателей = ОбщегоНазначения.ВыгрузитьКолонку(Показатели, "Показатель", Истина);
	ИменаПоказателей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивПоказателей, "Наименование, КраткоеНаименование");
	Для Каждого Показатель Из МассивПоказателей Цикл
		Если Не ПустаяСтрока(КраткийСоставСтрока) Тогда
			КраткийСоставСтрока = КраткийСоставСтрока + ", ";
		КонецЕсли;
		ИмяПоказателя = ИменаПоказателей[Показатель];
		Если ИмяПоказателя <> Неопределено Тогда 
			КраткийСоставСтрока = КраткийСоставСтрока + ?(ЗначениеЗаполнено(ИмяПоказателя.КраткоеНаименование), ИмяПоказателя.КраткоеНаименование, ИмяПоказателя.Наименование);
		КонецЕсли;
	КонецЦикла;
	
	МассивВидовВремени = ОбщегоНазначения.ВыгрузитьКолонку(ВидыВремени, "ВидВремени", Истина);
	ИменаВидовВремени = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВидовВремени, "Наименование");
	Для Каждого ВидВремени Из МассивВидовВремени Цикл
		Если Не ПустаяСтрока(КраткийСоставСтрока) Тогда
			КраткийСоставСтрока = КраткийСоставСтрока + ", ";
		КонецЕсли;
		КраткийСоставСтрока = КраткийСоставСтрока + ИменаВидовВремени[ВидВремени];
	КонецЦикла;
	
	МассивВидовРабот = ОбщегоНазначения.ВыгрузитьКолонку(ВидыРабот, "ВидРабот", Истина);
	ИменаВидовРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВидовРабот, "Наименование");
	Для Каждого ВидРабот Из МассивВидовРабот Цикл
		Если Не ПустаяСтрока(КраткийСоставСтрока) Тогда
			КраткийСоставСтрока = КраткийСоставСтрока + ", ";
		КонецЕсли;
		КраткийСоставСтрока = КраткийСоставСтрока + ИменаВидовРабот[ВидРабот];
	КонецЦикла;
	
	Если СтрДлина(КраткийСоставСтрока) > МаксимальнаяДлинаСтроки Тогда
		КраткийСоставСтрока = Лев(КраткийСоставСтрока, МаксимальнаяДлинаСтроки - 3) + "...";
	КонецЕсли;
	
	КраткийСостав = КраткийСоставСтрока;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыРасширенный.УстановитьИспользованиеВводаДанныхДляРасчетаЗарплаты();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		// Для группы только стандартные проверки
		Возврат;
	КонецЕсли;
	
	Если Показатели.Количество() = 0
		И ВидыВремени.Количество() = 0
		И ВидыРабот.Количество() = 0 
		И Не ВидыРаботЗаполняютсяВДокументе Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Не выбраны ни показатели расчета зарплаты, ни виды времени, ни виды работ';uk='Не вибрані ні показники розрахунку зарплати, види ні часу, ні види робіт'"), , , , Отказ);
	КонецЕсли;
	
	// Проверка на удаление элемента из таблиц Показатели, Виды времени, Виды работ: Если существуют документы по текущему
	// шаблону с удаляемым показателем, видом времени, видом работы, то удалять запрещаем.
	// Проверка флага "НесколькоСотрудников": Если существуют документы, в которых заполнено несколько сотрудников, то
	// снимать флаг запрещаем.
	// Проверка флага "НесколькоПодразделений": Если существуют документы, в которых заполнено несколько подразделений в
	// показателях, то снимать флаг запрещаем.
	// Проверка флага "НесколькоОрганизаций": Если существуют документы, в которых заполнено несколько организаций в
	// показателях, то снимать флаг запрещаем.
	// Проверка флага "ОрганизацияЗаполняется": Если существуют документы с различными организациями, то устанавливать
	// флаг запрещаем.
	// Проверка флага "ОрганизацияЗаполненаНеверно": Если существуют документы с заполненной организацией, но она
	// отличается от выбранной организации в шаблоне, то устанавливать флаг запрещаем.
	// Проверка флага "ПодразделениеЗаполняется": Если существуют документы с различными подразделениями, то устанавливать
	// флаг запрещаем.
	// Проверка флага "ПодразделениеЗаполненоНеверно": Если существуют документы с заполненным подразделением, но оно
	// отличается от выбранного подразделения в шаблоне, то устанавливать флаг запрещаем.
	// Проверка флага "ПроверятьЗаполнениеПодразделения": Если существуют документы с пустым подразделением, то
	// устанавливать флаг запрещаем.
	// Проверка флага "ПоказыватьПодразделение": Если существуют документы с заполненным подразделением, то снимать флаг
	// запрещаем.
	// Проверка флага "ЗначенияПоказателейВводятсяНаРазныеДаты": Если существуют документы с заполненным показателями на
	// разные даты, то снимать флаг запрещаем.
	// Проверка флага "ИспользоватьПериодОкончания": Если существуют документы с заполненной датой окончания у показателя,
	// то снимать флаг запрещаем.
	// Проверка флага "ВремяВводитсяСводноИстина": Если существуют документы с детальным заполнением временем , то
	// устанавливать флаг запрещаем.
	// Проверка флага "ВремяВводитсяСводноЛожь": Если существуют документы с сводным заполнением временем , то снимать
	// флаг запрещаем.
	// Проверка флага "ВремяВводитсяЗаМесяц": Если существуют документы с заполненным временем на разные дни, то снимать
	// флаг запрещаем.
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Показатели", Показатели.Выгрузить());
	Запрос.УстановитьПараметр("ВидыВремени", ВидыВремени.Выгрузить());
	Запрос.УстановитьПараметр("ВидыРабот", ВидыРабот.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("НесколькоСотрудников", НесколькоСотрудников);
	Запрос.УстановитьПараметр("НесколькоПодразделений", НесколькоПодразделений);
	Запрос.УстановитьПараметр("НесколькоОрганизаций", НесколькоОрганизаций);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ПроверятьЗаполнениеПодразделения", ПроверятьЗаполнениеПодразделения);
	Запрос.УстановитьПараметр("ПоказыватьПодразделение", ПоказыватьПодразделение);
	Запрос.УстановитьПараметр("ЗначенияПоказателейВводятсяНаРазныеДаты", ЗначенияПоказателейВводятсяНаРазныеДаты);
	Запрос.УстановитьПараметр("ИспользоватьПериодОкончания", ИспользоватьПериодОкончания);
	Запрос.УстановитьПараметр("ВремяВводитсяСводно", ВремяВводитсяСводно);
	Запрос.УстановитьПараметр("ВремяВводитсяЗаМесяц", ВремяВводитсяЗаМесяц);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Показатели.Показатель КАК Показатель
	|ПОМЕСТИТЬ ВТПоказателиШаблона
	|ИЗ
	|	&Показатели КАК Показатели
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыВремени.ВидВремени КАК ВидВремени
	|ПОМЕСТИТЬ ВТВидыВремениШаблона
	|ИЗ
	|	&ВидыВремени КАК ВидыВремени
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыРабот.ВидРабот КАК ВидРабот
	|ПОМЕСТИТЬ ВТВидыРаботШаблона
	|ИЗ
	|	&ВидыРабот КАК ВидыРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.Ссылка КАК Шаблон,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.НесколькоСотрудников КАК НесколькоСотрудников,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.НесколькоПодразделений КАК НесколькоПодразделений,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.НесколькоОрганизаций КАК НесколькоОрганизаций,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.Организация КАК Организация,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.Подразделение КАК Подразделение,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.ПроверятьЗаполнениеПодразделения КАК ПроверятьЗаполнениеПодразделения,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.ПоказыватьПодразделение КАК ПоказыватьПодразделение,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.ЗначенияПоказателейВводятсяНаРазныеДаты КАК ЗначенияПоказателейВводятсяНаРазныеДаты,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.ИспользоватьПериодОкончания КАК ИспользоватьПериодОкончания,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВремяВводитсяСводно КАК ВремяВводитсяСводно,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВремяВводитсяЗаМесяц КАК ВремяВводитсяЗаМесяц,
	|	ДанныеДляРасчетаЗарплаты.Ссылка КАК Документ
	|ПОМЕСТИТЬ ВТДокументыШаблона
	|ИЗ
	|	Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты КАК ВидыДокументовВводДанныхДляРасчетаЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|		ПО ВидыДокументовВводДанныхДляРасчетаЗарплаты.Ссылка = ДанныеДляРасчетаЗарплаты.ВидДокумента
	|ГДЕ
	|	ВидыДокументовВводДанныхДляРасчетаЗарплаты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплатыПоказатели.Показатель КАК ЗначениеОшибки,
	|	""Показатели"" КАК ВидОшибки
	|ПОМЕСТИТЬ ВТОшибки
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.Показатели КАК ВидыДокументовВводДанныхДляРасчетаЗарплатыПоказатели
	|		ПО ДокументыШаблона.Шаблон = ВидыДокументовВводДанныхДляРасчетаЗарплатыПоказатели.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ДанныеДляРасчетаЗарплатыЗначенияПоказателей
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Ссылка
	|			И (ВидыДокументовВводДанныхДляРасчетаЗарплатыПоказатели.Показатель = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Показатель)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиШаблона КАК ПоказателиШаблона
	|		ПО (ВидыДокументовВводДанныхДляРасчетаЗарплатыПоказатели.Показатель = ПоказателиШаблона.Показатель)
	|ГДЕ
	|	ПоказателиШаблона.Показатель ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыВремени.Документ,
	|	ВидыВремени.ВидВремени,
	|	""ВидыВремени""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДокументыШаблона.Документ КАК Документ,
	|		ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.ВидВремени КАК ВидВремени
	|	ИЗ
	|		ВТДокументыШаблона КАК ДокументыШаблона
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВидыВремени КАК ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени
	|			ПО ДокументыШаблона.Шаблон = ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениСводно КАК ДанныеДляРасчетаЗарплатыДанныеОВремениСводно
	|			ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениСводно.Ссылка
	|				И (ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.ВидВремени = ДанныеДляРасчетаЗарплатыДанныеОВремениСводно.ВидВремени)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыВремениШаблона КАК ВидыВремениШаблона
	|			ПО (ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.ВидВремени = ВидыВремениШаблона.ВидВремени)
	|	ГДЕ
	|		ВидыВремениШаблона.ВидВремени ЕСТЬ NULL 
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДокументыШаблона.Документ,
	|		ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.ВидВремени
	|	ИЗ
	|		ВТДокументыШаблона КАК ДокументыШаблона
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВидыВремени КАК ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени
	|			ПО ДокументыШаблона.Шаблон = ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.Ссылка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеДляРасчетаЗарплатыДанныеОВремениДетально
	|			ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Ссылка
	|				И (ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.ВидВремени = ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.ВидВремени)
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыВремениШаблона КАК ВидыВремениШаблона
	|			ПО (ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыВремени.ВидВремени = ВидыВремениШаблона.ВидВремени)
	|	ГДЕ
	|		ВидыВремениШаблона.ВидВремени ЕСТЬ NULL ) КАК ВидыВремени
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыРабот.ВидРабот,
	|	""ВидыРабот""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовВводДанныхДляРасчетаЗарплаты.ВидыРабот КАК ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыРабот
	|		ПО ДокументыШаблона.Шаблон = ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыРабот.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ДанныеДляРасчетаЗарплатыВыполненныеРаботы
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыВыполненныеРаботы.Ссылка
	|			И (ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыРабот.ВидРабот = ДанныеДляРасчетаЗарплатыВыполненныеРаботы.ВидРабот)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВидыРаботШаблона КАК ВидыРаботШаблона
	|		ПО (ВидыДокументовВводДанныхДляРасчетаЗарплатыВидыРабот.ВидРабот = ВидыРаботШаблона.ВидРабот)
	|ГДЕ
	|	ВидыРаботШаблона.ВидРабот ЕСТЬ NULL 
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Сотрудник),
	|	""НесколькоСотрудников""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ДокументыШаблона.Документ КАК Документ,
	|			ДанныеДляРасчетаЗарплатыВыполненныеРаботы.Сотрудник КАК Сотрудник
	|		ИЗ
	|			ВТДокументыШаблона КАК ДокументыШаблона
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ВыполненныеРаботы КАК ДанныеДляРасчетаЗарплатыВыполненныеРаботы
	|				ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыВыполненныеРаботы.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументыШаблона.Документ,
	|			ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Сотрудник
	|		ИЗ
	|			ВТДокументыШаблона КАК ДокументыШаблона
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеДляРасчетаЗарплатыДанныеОВремениДетально
	|				ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументыШаблона.Документ,
	|			ДанныеДляРасчетаЗарплатыДанныеОВремениСводно.Сотрудник
	|		ИЗ
	|			ВТДокументыШаблона КАК ДокументыШаблона
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениСводно КАК ДанныеДляРасчетаЗарплатыДанныеОВремениСводно
	|				ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениСводно.Ссылка
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ДокументыШаблона.Документ,
	|			ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект
	|		ИЗ
	|			ВТДокументыШаблона КАК ДокументыШаблона
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ДанныеДляРасчетаЗарплатыЗначенияПоказателей
	|				ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Ссылка
	|					И (ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект ССЫЛКА Справочник.Сотрудники)) КАК Сотрудники
	|		ПО ДокументыШаблона.Документ = Сотрудники.Документ
	|			И (ДокументыШаблона.НесколькоСотрудников)
	|			И (НЕ &НесколькоСотрудников)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Сотрудники.Сотрудник) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект),
	|	""НесколькоПодразделений""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ДанныеДляРасчетаЗарплатыЗначенияПоказателей
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Ссылка
	|			И (ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект ССЫЛКА Справочник.ПодразделенияОрганизаций)
	|			И (ДокументыШаблона.НесколькоПодразделений)
	|			И (НЕ &НесколькоПодразделений)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект),
	|	""НесколькоОрганизаций""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ДанныеДляРасчетаЗарплатыЗначенияПоказателей
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Ссылка
	|			И (ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект ССЫЛКА Справочник.Организации)
	|			И (ДокументыШаблона.НесколькоОрганизаций)
	|			И (НЕ &НесколькоОрганизаций)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Объект) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплаты.Организация),
	|	""ОрганизацияЗаполняется""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|			И (ДокументыШаблона.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|			И (&Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплаты.Организация) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Организации.Документ,
	|	Организации.Организация,
	|	""ОрганизацияЗаполненаНеверно""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДляРасчетаЗарплаты.Ссылка КАК Документ,
	|		ДанныеДляРасчетаЗарплаты.Организация КАК Организация
	|	ИЗ
	|		ВТДокументыШаблона КАК ДокументыШаблона
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|			ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|				И (ДокументыШаблона.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|				И (&Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|				И (ДокументыШаблона.Организация <> &Организация)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДляРасчетаЗарплаты.Ссылка,
	|		ДанныеДляРасчетаЗарплаты.Организация
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплаты.Организация) = 1) КАК Организации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплаты.Подразделение),
	|	""ПодразделениеЗаполняется""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|			И (ДокументыШаблона.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (&Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплаты.Подразделение) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Подразделения.Документ,
	|	Подразделения.Подразделение,
	|	""ПодразделениеЗаполненоНеверно""
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДанныеДляРасчетаЗарплаты.Ссылка КАК Документ,
	|		ДанныеДляРасчетаЗарплаты.Подразделение КАК Подразделение
	|	ИЗ
	|		ВТДокументыШаблона КАК ДокументыШаблона
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|			ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|				И (ДокументыШаблона.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|				И (&Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|				И (ДокументыШаблона.Подразделение <> &Подразделение)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ДанныеДляРасчетаЗарплаты.Ссылка,
	|		ДанныеДляРасчетаЗарплаты.Подразделение
	|	
	|	ИМЕЮЩИЕ
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплаты.Подразделение) = 1) КАК Подразделения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	""ПроверятьЗаполнениеПодразделения""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|			И (ДанныеДляРасчетаЗарплаты.Подразделение = ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (НЕ ДокументыШаблона.ПроверятьЗаполнениеПодразделения)
	|			И (&ПроверятьЗаполнениеПодразделения)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка),
	|	""ПоказыватьПодразделение""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|			И (ДанныеДляРасчетаЗарплаты.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка))
	|			И (ДокументыШаблона.ПоказыватьПодразделение)
	|			И (НЕ &ПоказыватьПодразделение)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Период),
	|	""ЗначенияПоказателейВводятсяНаРазныеДаты""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ДанныеДляРасчетаЗарплатыЗначенияПоказателей
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Ссылка
	|			И (ДокументыШаблона.ЗначенияПоказателейВводятсяНаРазныеДаты)
	|			И (НЕ &ЗначенияПоказателейВводятсяНаРазныеДаты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Период) > 1
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеДляРасчетаЗарплатыЗначенияПоказателей.ПериодОкончания),
	|	""ИспользоватьПериодОкончания""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ЗначенияПоказателей КАК ДанныеДляРасчетаЗарплатыЗначенияПоказателей
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыЗначенияПоказателей.Ссылка
	|			И (ДанныеДляРасчетаЗарплатыЗначенияПоказателей.ПериодОкончания <> ДАТАВРЕМЯ(1, 1, 1))
	|			И (ДокументыШаблона.ИспользоватьПериодОкончания)
	|			И (НЕ &ИспользоватьПериодОкончания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""ВремяВводитсяСводноИстина""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеДляРасчетаЗарплатыДанныеОВремениДетально
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Ссылка
	|			И (НЕ ДокументыШаблона.ВремяВводитсяСводно)
	|			И (&ВремяВводитсяСводно)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""ВремяВводитсяСводноЛожь""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениСводно КАК ДанныеДляРасчетаЗарплатыДанныеОВремениСводно
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениСводно.Ссылка
	|			И (ДокументыШаблона.ВремяВводитсяСводно)
	|			И (НЕ &ВремяВводитсяСводно)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументыШаблона.Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыИспользованияРабочегоВремени.ПустаяСсылка),
	|	""ВремяВводитсяЗаМесяц""
	|ИЗ
	|	ВТДокументыШаблона КАК ДокументыШаблона
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты.ДанныеОВремениДетально КАК ДанныеДляРасчетаЗарплатыДанныеОВремениДетально
	|		ПО ДокументыШаблона.Документ = ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Ссылка
	|			И (ДокументыШаблона.ВремяВводитсяЗаМесяц)
	|			И (НЕ &ВремяВводитсяЗаМесяц)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументыШаблона.Документ,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов1,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов2,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов3,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов4,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов5,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов6,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов7,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов8,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов9,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов10,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов11,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов12,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов13,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов14,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов15,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов16,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов17,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов18,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов19,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов20,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов21,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов22,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов23,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов24,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов25,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов26,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов27,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов28,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов29,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов30,
	|	ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов31
	|
	|ИМЕЮЩИЕ
	|	ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов1 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов2 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов3 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов4 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов5 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов6 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов7 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов8 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов9 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов10 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов11 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов12 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов13 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов14 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов15 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов16 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов17 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов18 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов19 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов20 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов21 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов22 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов23 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов24 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов25 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов26 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов27 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов28 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов29 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов30 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ + ВЫБОР
	|		КОГДА ДанныеДляРасчетаЗарплатыДанныеОВремениДетально.Часов31 <> 0
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ошибки.Документ КАК Документ,
	|	Ошибки.ЗначениеОшибки,
	|	Ошибки.ВидОшибки,
	|	ДанныеДляРасчетаЗарплаты.Номер КАК НомерДокумента,
	|	ДанныеДляРасчетаЗарплаты.Дата КАК ДатаДокумента
	|ИЗ
	|	ВТОшибки КАК Ошибки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДанныеДляРасчетаЗарплаты КАК ДанныеДляРасчетаЗарплаты
	|		ПО Ошибки.Документ = ДанныеДляРасчетаЗарплаты.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Ошибки.Документ,
	|	Ошибки.ЗначениеОшибки,
	|	Ошибки.ВидОшибки,
	|	ДанныеДляРасчетаЗарплаты.Номер,
	|	ДанныеДляРасчетаЗарплаты.Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ошибки.ВидОшибки,
	|	Ошибки.Документ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru='Изменение шаблона невозможно. Выберите другие настройки или скопируйте шаблон';uk='Зміна шаблону неможлива. Виберіть інші параметри або скопіюйте шаблон'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, , , Отказ);
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ВидОшибки") Цикл
		
		КоличествоДокументовВОшибке = 0;
		ТекстОшибкиДокументов = "";
		Пока Выборка.СледующийПоЗначениюПоля("Документ") Цикл
			КоличествоДокументовВОшибке = КоличествоДокументовВОшибке + 1;
			Если КоличествоДокументовВОшибке > 2 Тогда
				СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(ТекстОшибкиДокументов, 2);
				ТекстОшибкиДокументов = ТекстОшибкиДокументов + " " + НСтр("ru='и других';uk='та інших'") + ", ";
				Прервать;
			КонецЕсли;
			ТекстОшибкиДокументов = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1%2 №%3 от %4г, ';uk='%1%2 №%3 від %4р, '"),
				ТекстОшибкиДокументов, ПредставлениеДокумента, Выборка.НомерДокумента, Формат(Выборка.ДатаДокумента, "ДЛФ=Д"))
		КонецЦикла;
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='В документ%1 %2';uk='В документ%1 %2'"),
			?(КоличествоДокументовВОшибке > 1, "ах", "е"), ТекстОшибкиДокументов);
		ПолеОшибки = "";
		Если Выборка.ВидОшибки = "Показатели" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используются удаляемые показатели:';uk='використовуються ці показники:'");
			ПолеОшибки = "Показатели";
		ИначеЕсли Выборка.ВидОшибки = "ВидыВремени" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используются удаляемые виды времени:';uk='використовуються ці види часу:'");
			ПолеОшибки = "ВидыРабочегоВремени";
		ИначеЕсли Выборка.ВидОшибки = "ВидыРабот" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используются удаляемые работы:';uk='використовуються роботи, що видаляються :'");
			ПолеОшибки = "Объект.ВидыРабот";
		ИначеЕсли Выборка.ВидОшибки = "НесколькоСотрудников" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение по нескольким сотрудникам';uk='використовується заповнення по декільком співробітникам'");
			ПолеОшибки = "Объект.НесколькоСотрудников";
		ИначеЕсли Выборка.ВидОшибки = "НесколькоПодразделений" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение по нескольким подразделениям';uk='використовується заповнення по кільком підрозділам'");
			ПолеОшибки = "Объект.НесколькоПодразделений";
		ИначеЕсли Выборка.ВидОшибки = "НесколькоОрганизаций" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение по нескольким организациям';uk='використовується заповнення по кільком організаціям'");
			ПолеОшибки = "Объект.НесколькоОрганизаций";
		ИначеЕсли Выборка.ВидОшибки = "ЗначенияПоказателейВводятсяНаРазныеДаты" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение значений показателей на разные даты';uk='використовується заповнення значень показників на різні дати'");
			ПолеОшибки = "Объект.ЗначенияПоказателейВводятсяНаРазныеДаты";
		ИначеЕсли Выборка.ВидОшибки = "ИспользоватьПериодОкончания" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение даты окончания для значений постоянных показателей';uk='використовується заповнення дати закінчення для постійних значень показників'");
			ПолеОшибки = "Объект.ИспользоватьПериодОкончания";
		ИначеЕсли Выборка.ВидОшибки = "ВремяВводитсяСводноИстина" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение времени детально по дням';uk='використовується заповнення часу детально по днях'");
			ПолеОшибки = "Объект.ВремяВводитсяСводно";
		ИначеЕсли Выборка.ВидОшибки = "ВремяВводитсяСводноЛожь" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение времени в целом за период';uk='використовується заповнення часу в цілому за період'");
			ПолеОшибки = "Объект.ВремяВводитсяСводно";
		ИначеЕсли Выборка.ВидОшибки = "ВремяВводитсяЗаМесяц" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется заполнение времени одним документом за весь месяц';uk='використовується заповнення часу одним документом за весь місяць'");
			ПолеОшибки = "Объект.ВремяВводитсяЗаМесяц";
		ИначеЕсли Выборка.ВидОшибки = "ОрганизацияЗаполненаНеверно" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется организация отличная от выбранной в шаблоне';uk='використовується організація відмінна від обраної в шаблоні'");
			ПолеОшибки = "Объект.Организация";
		ИначеЕсли Выборка.ВидОшибки = "ПодразделениеЗаполненоНеверно" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='используется подразделение отличное от выбранного в шаблоне';uk='використовується підрозділ відмінний від обраного шаблону'");
			ПолеОшибки = "Объект.Подразделение";
		ИначеЕсли Выборка.ВидОшибки = "ОрганизацияЗаполняется" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='указаны разные организации';uk='вказані різні організації'");
			ПолеОшибки = "ОрганизацияЗаполняется";
		ИначеЕсли Выборка.ВидОшибки = "ПодразделениеЗаполняется" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='указаны разные подразделения';uk='вказані різні підрозділи'");
			ПолеОшибки = "ПодразделениеЗаполняется";
		ИначеЕсли Выборка.ВидОшибки = "ПроверятьЗаполнениеПодразделения" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='не заполнены подразделения';uk='не заповнені підрозділу'");
			ПолеОшибки = "Объект.ПроверятьЗаполнениеПодразделения";
		ИначеЕсли Выборка.ВидОшибки = "ПоказыватьПодразделение" Тогда
			ТекстОшибки = ТекстОшибки + НСтр("ru='уже заполнены подразделения';uk='вже заповнені підрозділу'");
			ПолеОшибки = "Объект.ПоказыватьПодразделение";
		КонецЕсли;
		Если Выборка.ВидОшибки = "Показатели" Или Выборка.ВидОшибки = "ВидыВремени" Или Выборка.ВидОшибки = "ВидыРабот" Тогда
			Пока Выборка.Следующий() Цикл
				ТекстОшибки = ТекстОшибки + Символы.ПС + Символы.Таб + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1';uk='%1'"), Выборка.ЗначениеОшибки);
			КонецЦикла;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка, ПолеОшибки, , Отказ);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
