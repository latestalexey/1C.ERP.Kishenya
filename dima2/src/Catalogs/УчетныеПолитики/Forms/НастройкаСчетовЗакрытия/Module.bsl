#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("АдресНастройкиСчетов",АдресНастройкиСчетов) Тогда
		
		ПрочитатьДанныеНастройки();
		
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		Для Каждого Счет Из ВыбранноеЗначение Цикл
			ДобавитьСчет(Счет);
		КонецЦикла;
	Иначе
		ДобавитьСчет(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормы

&НаКлиенте
Процедура СчетаДоходовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ИмяТаблицыПодбора = "СчетаДоходов";
	ОткрытьФормуВыбораСчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаРасходовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ИмяТаблицыПодбора = "СчетаРасходов";
	ОткрытьФормуВыбораСчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура СчетаПрибылейУбытковПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ИмяТаблицыПодбора = "СчетаПрибылейУбытков";
	ОткрытьФормуВыбораСчетов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборСчетовДоходов(Команда)
	
	ИмяТаблицыПодбора = "СчетаДоходов";
	ПодборСчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСчетовРасходов(Команда)
	
	ИмяТаблицыПодбора = "СчетаРасходов";
	ПодборСчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСчетовПрибылейУбытков(Команда)
	
	ИмяТаблицыПодбора = "СчетаПрибылейУбытков";
	ПодборСчетов();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	СохранитьСчетаЗакрытия();
	Закрыть(РежимДиалогаВопрос.ОК);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодборСчетов()
	
	ОткрытьФормуВыбораСчетов(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСчетов(Подбор = Ложь)
	
	ПараметрыФормы = Новый Структура;
	// Стандартные параметры формы.
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",            НЕ Подбор);
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
	ПараметрыФормы.Вставить("ТолькоРазрешенные", Истина);
	
	// Стандартные параметры формы выбора (см. Расширение управляемой формы для динамического списка).
	ПараметрыФормы.Вставить("МножественныйВыбор",            Подбор);
	ПараметрыФормы.Вставить("РежимВыбора",                   Истина);
	// Предполагаемые реквизиты.
	ПараметрыФормы.Вставить("РежимОткрытияОкна",             РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ПараметрыФормы.Вставить("РазрешитьНачалоПеретаскивания", Ложь);
	ОткрытьФорму("ПланСчетов.Международный.ФормаВыбора",ПараметрыФормы,ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСчет(Счет)
	
	НоваяСтрока = ЭтотОбъект[ИмяТаблицыПодбора].Добавить();
	НоваяСтрока.Счет = Счет;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеНастройки()
	
	СчетаЗакрытия = ПолучитьИзВременногоХранилища(АдресНастройкиСчетов);
	ЗаполнитьЗначенияСвойств(ЭтаФорма,СчетаЗакрытия,,"СчетаДоходов,СчетаРасходов,СчетаПрибылейУбытков");
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СчетаЗакрытия.СчетаДоходов, СчетаДоходов);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СчетаЗакрытия.СчетаРасходов, СчетаРасходов);
	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(СчетаЗакрытия.СчетаПрибылейУбытков, СчетаПрибылейУбытков);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьСчетаЗакрытия()
	
	СчетаЗакрытия = Справочники.УчетныеПолитики.СчетаЗакрытия();
	ЗаполнитьЗначенияСвойств(СчетаЗакрытия,ЭтаФорма,,"СчетаДоходов,СчетаРасходов,СчетаПрибылейУбытков");
	СчетаЗакрытия.СчетаДоходов = РеквизитФормыВЗначение("СчетаДоходов");
	СчетаЗакрытия.СчетаРасходов = РеквизитФормыВЗначение("СчетаРасходов");
	СчетаЗакрытия.СчетаПрибылейУбытков = РеквизитФормыВЗначение("СчетаПрибылейУбытков");
	ПоместитьВоВременноеХранилище(СчетаЗакрытия, АдресНастройкиСчетов);
	
КонецПроцедуры

#КонецОбласти