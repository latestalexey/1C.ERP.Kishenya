
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Перем ЗначениеРеквизита;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеРемонтами")
		И (Параметры.Свойство("ПоказательАмортизации") И Параметры.ПоказательАмортизации) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если Параметры.Свойство("Класс", ЗначениеРеквизита)
		Или (Параметры.Свойство("Узел", ЗначениеРеквизита) И ЗначениеЗаполнено(ЗначениеРеквизита))
		Или Параметры.Свойство("ОбъектЭксплуатации", ЗначениеРеквизита) Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбора(
			ЗначениеРеквизита,
			(Параметры.Свойство("РегистрацияНаработки") И Параметры.РегистрацияНаработки),
			(Параметры.Свойство("ПоказательАмортизации") И Параметры.ПоказательАмортизации));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает список значений показателями наработок, принадлежащих классу объектов эксплуатации
//
Функция ДанныеВыбора(Знач ОбъектОтбора, ПолучатьИсточникиНаработки, ПоказательАмортизации)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"
	//++ НЕ УТКА
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ОбъектОтбора КАК Класс
	|ПОМЕСТИТЬ Классы
	|ГДЕ
	|	&ИспользоватьУправлениеРемонтами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Объекты.Класс
	|ИЗ
	|	Справочник.ОбъектыЭксплуатации КАК Объекты
	|ГДЕ
	|	Объекты.Ссылка = &ОбъектОтбора
	|	И &ИспользоватьУправлениеРемонтами
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Узлы.Класс
	|ИЗ
	|	Справочник.УзлыОбъектовЭксплуатации КАК Узлы
	|ГДЕ
	|	Узлы.Ссылка = &ОбъектОтбора
	|	И &ИспользоватьУправлениеРемонтами
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ПоказателиНаработки.ПоказательНаработки КАК ПоказательНаработки,
	|	ПоказателиНаработки.РегистрироватьОтИсточника КАК РегистрироватьОтИсточника
	|ПОМЕСТИТЬ Показатели
	|ИЗ
	|	Справочник.КлассыОбъектовЭксплуатации.ПоказателиНаработки КАК ПоказателиНаработки
	|ГДЕ
	|	ПоказателиНаработки.Ссылка В
	|			(ВЫБРАТЬ
	|				Классы.Класс
	|			ИЗ
	|				Классы)
	|	И ВЫБОР
	|			КОГДА &ПолучатьИсточникиНаработки
	|				ТОГДА НЕ ПоказателиНаработки.РегистрироватьОтИсточника
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//-- НЕ УТКА
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПервоначальныеСведенияОС.ПоказательНаработки КАК Значение,
	|	ПервоначальныеСведенияОС.ПоказательНаработки.ПометкаУдаления КАК ПометкаУдаления
	|ИЗ
	|	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			,
	|			ОсновноеСредство = &ОбъектОтбора
	//++ НЕ УТКА
	|				И НЕ ПоказательНаработки В
	|						(ВЫБРАТЬ
	|							Показатели.ПоказательНаработки
	|						ИЗ
	|							Показатели КАК Показатели)
	//-- НЕ УТКА
	|	) КАК ПервоначальныеСведенияОС
	|ГДЕ
	|	НЕ &ПоказательАмортизации
	//++ НЕ УТКА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Показатели.ПоказательНаработки,
	|	Показатели.ПоказательНаработки.ПометкаУдаления
	|ИЗ
	|	Показатели КАК Показатели
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ПолучатьИсточникиНаработки
	|				ТОГДА НЕ Показатели.РегистрироватьОтИсточника
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	//-- НЕ УТКА
	|";
	
	Запрос.УстановитьПараметр("ОбъектОтбора", ОбъектОтбора);
	Запрос.УстановитьПараметр("ПолучатьИсточникиНаработки", ПолучатьИсточникиНаработки);
	Запрос.УстановитьПараметр("ПоказательАмортизации", ПоказательАмортизации);
	Запрос.УстановитьПараметр("ИспользоватьУправлениеРемонтами", ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеРемонтами"));
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат ДанныеВыбора;
	КонецЕсли;
	
	ШаблонПолейСтруктуры = "Значение, ПометкаУдаления";
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураЭлемента = Новый Структура(ШаблонПолейСтруктуры);
		ЗаполнитьЗначенияСвойств(СтруктураЭлемента, Выборка);
		ДанныеВыбора.Добавить(СтруктураЭлемента);
		
	КонецЦикла;
	
	Возврат ДанныеВыбора;
	
КонецФункции

#КонецОбласти