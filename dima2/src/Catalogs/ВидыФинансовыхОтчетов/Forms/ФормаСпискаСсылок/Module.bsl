
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимУдаления Тогда
		Заголовок = НСтр("ru='Удаление элемента отчета';uk='Видалення елемента звіту'");
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru='Отмена';uk='Відмінити'");
	КонецЕсли;
	
	ДействиеСкопировать = 0;
	ДействиеПеренести = 1;
	ДействиеУдалить = 2;
	ОсновноеДействие = ДействиеСкопировать;
	
	ЭлементОтчета = Параметры.ЭлементОтчета;
	НаименованиеЭлемента = ЭлементОтчета.НаименованиеДляПечати;
	РежимУдаления = Параметры.РежимУдаления;
	УдалитьВсе = Параметры.УдалитьВсе;
	ОбновитьДеревоСсылок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкопироватьВсемПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитВсеПриИзменении(Элемент)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоссылокэлемента

&НаКлиенте
Процедура ДеревоСсылокЭлементаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура("Ключ",Элемент.ТекущиеДанные.ВидОтчета);
	ПараметрыФормы.Вставить("ТекущийЭлементОтчета", Элемент.ТекущиеДанные.ЭлементОтчета);
	ОткрытьФорму("Справочник.ВидыФинансовыхОтчетов.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСсылокЭлементаПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	Ответ = Неопределено;

	
	ПоказатьВопрос(Новый ОписаниеОповещения("КомандаВыполнитьЗавершение", ЭтотОбъект), НСтр("ru='Действие необратимо. Продолжить??';uk='Дія необоротно. Продовжити??'"),РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Ответ = РезультатВопроса;
    Если Ответ <> КодВозвратаДиалога.Да Тогда
        Возврат;
    КонецЕсли;
    
    ВыбранныйЭлемент = Неопределено;
    Если ОсновноеДействие = ДействиеПеренести Тогда
        Если Элементы.ДеревоСсылокЭлемента.ТекущаяСтрока = Неопределено Тогда
            ПоказатьПредупреждение(Неопределено, НСтр("ru='Укажите отчет новый источник ссылок.';uk='Зазначте звіт нове джерело посилань.'"));
            Возврат;
        КонецЕсли;
        ТекущиеДанные = Элементы.ДеревоСсылокЭлемента.ТекущиеДанные;
        ВыбранныйЭлемент = ТекущиеДанные.ЭлементОтчета;
        Если НЕ ЗначениеЗаполнено(ВыбранныйЭлемент) Тогда
            ПодчиненныеСтроки = ФинансоваяОтчетностьКлиентСервер.ПодчиненныеСтроки(ТекущиеДанные);
            ВыбранныйЭлемент = ПодчиненныеСтроки[0].ЭлементОтчета;
        КонецЕсли;
    КонецЕсли;
    ВыполнитьДействие(ВыбранныйЭлемент);
    
    Закрыть(Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПриИзмененииРеквизитов

&НаКлиенте
Процедура УстановитьДоступность()
	
	Если УдалитьВсе Тогда
		ОсновноеДействие = ДействиеУдалить;
		Элементы.СкопироватьВсем.Доступность = Ложь;
		Элементы.Перенести.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ВыполнитьДействие(ВыбранныйЭлемент = Неопределено)
	
	// в выбранный элемент скопируем данные оригинала и разорвем связь
	Если ВыбранныйЭлемент <> Неопределено Тогда
		НовыйОригинал = ВыбранныйЭлемент.ПолучитьОбъект();
		ЗаполнитьДанныеЭлемента(НовыйОригинал);
		НовыйОригинал.СвязанныйЭлемент = Неопределено;
		НовыйОригинал.Записать();
	КонецЕсли;
	
	ВидыОтчетов = ФинансоваяОтчетностьКлиентСервер.ПодчиненныеСтроки(ДеревоСсылокЭлемента);
	Для Каждого ВидОтчета Из ВидыОтчетов Цикл
		ЭлементыОтчета = ФинансоваяОтчетностьКлиентСервер.ПодчиненныеСтроки(ВидОтчета);
		Для Каждого СсылкаЭлемента Из ЭлементыОтчета Цикл
			
			Если ВыбранныйЭлемент <> Неопределено И СсылкаЭлемента.ЭлементОтчета = ВыбранныйЭлемент Тогда
				Продолжить;
			КонецЕсли;
			
			ОбъектЭлемента = СсылкаЭлемента.ЭлементОтчета.ПолучитьОбъект();
			Если ОсновноеДействие = ДействиеУдалить Тогда
				ОбъектЭлемента.УстановитьПометкуУдаления(Истина);
			КонецЕсли;
			ОбъектЭлемента.СвязанныйЭлемент = ВыбранныйЭлемент;
			Если ОсновноеДействие = ДействиеСкопировать Тогда
				ЗаполнитьДанныеЭлемента(ОбъектЭлемента);
			КонецЕсли;
			ОбъектЭлемента.Записать();
			
		КонецЦикла;// по элементам одного вида отчета
	КонецЦикла;// по видам отчетов
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеЭлемента(ОбъектПриемник)
	
	ЗаполнитьЗначенияСвойств(ОбъектПриемник, ЭлементОтчета,,"Код,Владелец,Родитель,НаименованиеДляПечати,Комментарий");
	
	ВыводитьЗаголовок = ОбъектПриемник.РеквизитыВидаЭлемента.Найти(ПланыВидовХарактеристик.РеквизитыЭлементовФинансовыхОтчетов.ВыводитьЗаголовокЭлемента);
	Если ВыводитьЗаголовок <> Неопределено Тогда
		ОбъектПриемник.ВидыФинансовыхОтчетов.Удалить(ВыводитьЗаголовок);
	КонецЕсли;
	
	ДопРеквизиты = ЭлементОтчета.РеквизитыВидаЭлемента.Выгрузить();
	Для Каждого Реквизит Из ДопРеквизиты Цикл
		Если Реквизит.Реквизит.ИмяПредопределенныхДанных = "КодСтрокиОтчета"
			ИЛИ Реквизит.Реквизит.ИмяПредопределенныхДанных = "Примечание" Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ОбъектПриемник.РеквизитыВидаЭлемента.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Реквизит);
	КонецЦикла;
	ОбъектПриемник.ОперандыФормулы.Загрузить(ЭлементОтчета.ОперандыФормулы.Выгрузить());
	ОбъектПриемник.ЭлементыТаблицы.Загрузить(ЭлементОтчета.ЭлементыТаблицы.Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоСсылок()
	
	ПараметрыДерева = МеждународнаяОтчетностьКлиентСервер.НовыеПараметрыДереваЭлементов();
	ПараметрыДерева.ИмяЭлементаДерева = "ДеревоСсылокЭлемента";
	ПараметрыДерева.Вставить("ЭлементОтчета", Параметры.ЭлементОтчета);
	ПараметрыДерева.Вставить("СчетПланаСчетов", Параметры.СчетПланаСчетов);
	
	МеждународнаяОтчетностьСервер.ОбновитьДеревоСсылокЭлемента(ЭтаФорма, ПараметрыДерева);
	
	КоличествоСсылок = ПараметрыДерева.КоличествоСсылок;
	ШаблонТекста = НСтр("ru='На элемент ""%1""
                        |обнаружены ссылки в других отчетах (%2 шт).
                        |Выберите действие:'
                        |;uk='На елемент ""%1""
                        |виявлені посилання в інших звітах (%2 шт).
                        |Виберіть дію:'");
	
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, НаименованиеЭлемента, КоличествоСсылок);
	Элементы.Пояснение.Заголовок = Текст;
	
	Элементы.Перенести.ТолькоПросмотр = КоличествоСсылок = 1;
	Если Элементы.Перенести.ТолькоПросмотр Тогда
		ОсновноеДействие = ДействиеСкопировать;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
