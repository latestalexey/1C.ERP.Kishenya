#Область ОбработчикиСобытий

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов


Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	УправлениеШтатнымРасписаниемВызовСервера.ОбработкаПолученияДанныхВыбораСправочникаШтатноеРасписание(ДанныеВыбора, Параметры, СтандартнаяОбработка);	
КонецПроцедуры

#КонецОбласти


#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область СлужебныйПрограммныйИнтерфейс

// Описание позиции штатного расписания.
//
Функция ДанныеПозицииШтатногоРасписания(ПозицияШтатногоРасписания) Экспорт
	
	ДанныеПозиции = Новый Структура(
		"Должность,
		|Подразделение,
		|ФОТ");
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ШтатноеРасписание.Ссылка,
		|	ШтатноеРасписание.Должность,
		|	ШтатноеРасписание.Подразделение,
		|	ШтатноеРасписание.ФОТ
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|ГДЕ
		|	ШтатноеРасписание.Ссылка = &Позиция");
		
	Запрос.УстановитьПараметр("Позиция", ПозицияШтатногоРасписания);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеПозиции, Выборка);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ПодборПерсонала");
		Модуль.ДополнитьДанныеПозицииШтатногоРасписания(ДанныеПозиции, ПозицияШтатногоРасписания);
	КонецЕсли;
	
	Возврат ДанныеПозиции;
	
КонецФункции

// используется при загрузке данных
Процедура РассчитатьФОТПозиции(ПозицияОбъект) Экспорт

	КоллекцияПозиций     = УправлениеШтатнымРасписанием.ПустаяТаблицаКоллекцииПозицийДляРасчетаФОТ();
	КоллекцияНачислений  = УправлениеШтатнымРасписанием.ПустаяТаблицаКоллекцииНачисленийДляРасчетаФОТ();
	КоллекцияПоказателей = УправлениеШтатнымРасписанием.ПустаяТаблицаКоллекцииПоказателейДляРасчетаФОТ();

	ЗаполнитьЗначенияСвойств(КоллекцияПозиций.Добавить(), ПозицияОбъект); 
	
	Для каждого СтрокаНачислений Из ПозицияОбъект.Начисления Цикл
		ЗаполнитьЗначенияСвойств(КоллекцияНачислений.Добавить(), СтрокаНачислений);
	КонецЦикла;
	
	Для каждого СтрокаПоказателей Из ПозицияОбъект.Показатели Цикл
		ЗаполнитьЗначенияСвойств(КоллекцияПоказателей.Добавить(), СтрокаПоказателей);
	КонецЦикла;
	
	ОписаниеПозиций = Новый Структура;
	ОписаниеПозиций.Вставить("Организация", ПозицияОбъект.Владелец);
	ОписаниеПозиций.Вставить("ДатаВступленияВСилу", ТекущаяДатаСеанса());
	ОписаниеПозиций.Вставить("Позиции", КоллекцияПозиций);
	ОписаниеПозиций.Вставить("Начисления", КоллекцияНачислений);
	ОписаниеПозиций.Вставить("Показатели", КоллекцияПоказателей);
	
	УправлениеШтатнымРасписанием.РассчитатьФОТНесколькихПозиций(ОписаниеПозиций);
	
	Для каждого СтруктураПозиции Из ОписаниеПозиций.Позиции Цикл
		
		ЗаполнитьЗначенияСвойств(ПозицияОбъект, СтруктураПозиции, "ОкладТариф,ОкладТарифМин,ОкладТарифМакс,ФОТ,ФОТМин,ФОТМакс");
		
		СтрокиНачислений = ОписаниеПозиций.Начисления.НайтиСтроки(Новый Структура("ИдентификаторСтрокиПозиции", СтруктураПозиции.ИдентификаторСтрокиПозиции));
		Для каждого СтрокаНачислений Из СтрокиНачислений Цикл
			СтрокиПозиции = ПозицияОбъект.Начисления.НайтиСтроки(Новый Структура("Начисление", СтрокаНачислений.Начисление));
			Для каждого СтрокаПозиции Из СтрокиПозиции Цикл
				ЗаполнитьЗначенияСвойств(СтрокаПозиции, СтрокаНачислений, "Размер,РазмерМин,РазмерМакс");
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.КадроваяИсторияСотрудников) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "ШтатноеРасписаниеНаПодпись";
		КомандаПечати.Представление = НСтр("ru='Штатное расписание на подпись';uk='Штатний розклад на підпис'");
		КомандаПечати.СписокФорм = "ФормаСписка";
		КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписание";
		КомандаПечати.Порядок = 20;
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Идентификатор = "СоблюдениеШтатногоРасписания";
		КомандаПечати.Представление = НСтр("ru='Соблюдение штатного расписания';uk='Дотримання штатного розкладу'");
		КомандаПечати.СписокФорм = "ФормаСписка";
		КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписание";
		КомандаПечати.Порядок = 30;
		
		ФОИспользоватьВилкуСтавокВШтатномРасписании = ПолучитьФункциональнуюОпцию("ИспользоватьВилкуСтавокВШтатномРасписании");
		Если ФОИспользоватьВилкуСтавокВШтатномРасписании Тогда
			
			КомандаПечати = КомандыПечати.Добавить();
			КомандаПечати.Идентификатор = "АнализШтатногоРасписания";
			КомандаПечати.Представление = НСтр("ru='Анализ штатного расписания';uk='Аналіз штатного розкладу'");
			КомандаПечати.СписокФорм = "ФормаСписка";
			КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписание";
			КомандаПечати.Порядок = 40;
			
		Иначе
			
			КомандаПечати = КомандыПечати.Добавить();
			КомандаПечати.Идентификатор = "АнализШтатногоРасписанияИспользуетсяВилкаСтавок";
			КомандаПечати.Представление = НСтр("ru='Анализ штатного расписания';uk='Аналіз штатного розкладу'");
			КомандаПечати.СписокФорм = "ФормаСписка";
			КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписание";
			КомандаПечати.Порядок = 40;
		
		КонецЕсли;
		
		Если ПравоДоступа("Чтение", Метаданные.РегистрыСведений.ИсторияНачисленийПоШтатномуРасписанию) Тогда
			
			Если ФОИспользоватьВилкуСтавокВШтатномРасписании Тогда
				
				КомандаПечати = КомандыПечати.Добавить();
				КомандаПечати.Идентификатор = "НачисленияПозицийШтатногоРасписанияИспользуетсяВилкаСтавок";
				КомандаПечати.Представление = НСтр("ru='Начисления позиций штатного расписания';uk='Нарахування позицій штатного розкладу'");
				КомандаПечати.СписокФорм = "ФормаСписка";
				КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписаниеНачисления";
				КомандаПечати.Порядок = 50;
				
				КомандаПечати = КомандыПечати.Добавить();
				КомандаПечати.Идентификатор = "ШтатноеРасписаниеКроссТаблицаИспользуетсяВилкаСтавок";
				КомандаПечати.Представление = НСтр("ru='Оклады, надбавки и ФОТ по штатному расписанию';uk='Оклади, надбавки та ФОП за штатним розкладом'");
				КомандаПечати.СписокФорм = "ФормаСписка";
				КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписаниеНачисления";
				КомандаПечати.Порядок = 60;
				
			Иначе
				
				КомандаПечати = КомандыПечати.Добавить();
				КомандаПечати.Идентификатор = "НачисленияПозицийШтатногоРасписания";
				КомандаПечати.Представление = НСтр("ru='Начисления позиций штатного расписания';uk='Нарахування позицій штатного розкладу'");
				КомандаПечати.СписокФорм = "ФормаСписка";
				КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписаниеНачисления";
				КомандаПечати.Порядок = 50;
				
				КомандаПечати = КомандыПечати.Добавить();
				КомандаПечати.Идентификатор = "ШтатноеРасписаниеКроссТаблица";
				КомандаПечати.Представление = НСтр("ru='Оклады, надбавки и ФОТ по штатному расписанию';uk='Оклади, надбавки та ФОП за штатним розкладом'");
				КомандаПечати.СписокФорм = "ФормаСписка";
				КомандаПечати.Обработчик = "УправлениеШтатнымРасписаниемКлиент.ОткрытьОтчетШтатноеРасписаниеНачисления";
				КомандаПечати.Порядок = 60;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	ИспользоватьМеханизмЗапретаРедактирования = Истина;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ОрганизационнаяСтруктура") Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("ОрганизационнаяСтруктура");
		ИспользоватьМеханизмЗапретаРедактирования = Модуль.СтруктураПредприятияСоответствуетСтруктуреЮридическихЛиц();
	КонецЕсли;
	
	Если ИспользоватьМеханизмЗапретаРедактирования Тогда 
		Результат.Добавить("Владелец;Владелец");
		Результат.Добавить("Подразделение");
	КонецЕсли;
	
	Результат.Добавить("Должность");
	
	Возврат Результат;	
	
КонецФункции	

Процедура ОбновитьСтруктуруШтатногоРасписанияПоПодразделениям() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Проверка и устранение задвоенных групп.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтатноеРасписание.Ссылка) КАК КоличествоСсылок,
		|	ШтатноеРасписание.Подразделение
		|ПОМЕСТИТЬ ВТКоличествоГруппПодразделений
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|ГДЕ
		|	ШтатноеРасписание.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	ШтатноеРасписание.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КоличествоГруппПодразделений.Подразделение
		|ПОМЕСТИТЬ ВТГруппыСПовторяющимисяПодразделениями
		|ИЗ
		|	ВТКоличествоГруппПодразделений КАК КоличествоГруппПодразделений
		|ГДЕ
		|	КоличествоГруппПодразделений.КоличествоСсылок > 1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГруппыСПовторяющимисяПодразделениями.Подразделение,
		|	ШтатноеРасписаниеРодители.Ссылка,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтатноеРасписаниеПодчиненные.Ссылка) КАК КоличествоПодчиненных
		|ПОМЕСТИТЬ ВТКоличествоПодчиненныхЭлементов
		|ИЗ
		|	ВТГруппыСПовторяющимисяПодразделениями КАК ГруппыСПовторяющимисяПодразделениями
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписаниеРодители
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписаниеПодчиненные
		|			ПО ШтатноеРасписаниеРодители.Ссылка = ШтатноеРасписаниеПодчиненные.Родитель
		|		ПО ГруппыСПовторяющимисяПодразделениями.Подразделение = ШтатноеРасписаниеРодители.Подразделение
		|			И (ШтатноеРасписаниеРодители.ЭтоГруппа)
		|
		|СГРУППИРОВАТЬ ПО
		|	ГруппыСПовторяющимисяПодразделениями.Подразделение,
		|	ШтатноеРасписаниеРодители.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КоличествоПодчиненныхЭлементов.Подразделение,
		|	КоличествоПодчиненныхЭлементов.Ссылка,
		|	МАКСИМУМ(КоличествоПодчиненныхЭлементов.КоличествоПодчиненных) КАК КоличествоПодчиненных
		|ПОМЕСТИТЬ ВТОставляемыеГруппыПредварительно
		|ИЗ
		|	ВТКоличествоПодчиненныхЭлементов КАК КоличествоПодчиненныхЭлементов
		|
		|СГРУППИРОВАТЬ ПО
		|	КоличествоПодчиненныхЭлементов.Подразделение,
		|	КоличествоПодчиненныхЭлементов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОставляемыеГруппыПредварительно.Подразделение,
		|	МИНИМУМ(ОставляемыеГруппыПредварительно.Ссылка) КАК Ссылка
		|ПОМЕСТИТЬ ВТОставляемыеПодразделения
		|ИЗ
		|	ВТОставляемыеГруппыПредварительно КАК ОставляемыеГруппыПредварительно
		|
		|СГРУППИРОВАТЬ ПО
		|	ОставляемыеГруппыПредварительно.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка,
		|	ОставляемыеПодразделения.Ссылка КАК Родитель
		|ИЗ
		|	ВТОставляемыеПодразделения КАК ОставляемыеПодразделения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ПО ОставляемыеПодразделения.Подразделение = ШтатноеРасписание.Подразделение
		|			И ОставляемыеПодразделения.Ссылка <> ШтатноеРасписание.Родитель
		|			И (НЕ ШтатноеРасписание.ЭтоГруппа)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбновляемыйОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ОбновляемыйОбъект.Родитель = Выборка.Родитель;
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ОбновляемыйОбъект);
			ОбновляемыйОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	КоличествоПодчиненныхЭлементов.Ссылка
		|ИЗ
		|	ВТКоличествоПодчиненныхЭлементов КАК КоличествоПодчиненныхЭлементов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОставляемыеПодразделения КАК ОставляемыеПодразделения
		|		ПО КоличествоПодчиненныхЭлементов.Ссылка = ОставляемыеПодразделения.Ссылка
		|ГДЕ
		|	ОставляемыеПодразделения.Ссылка ЕСТЬ NULL ";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ЛишняяГруппа = Выборка.Ссылка.ПолучитьОбъект();
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ЛишняяГруппа);
			ЛишняяГруппа.Удалить();
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Удаление лишних (пустых) групп.
	ПроверитьНеобходимостьУдаленияЛишнихГрупп = Истина;
	Пока ПроверитьНеобходимостьУдаленияЛишнихГрупп Цикл
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ШтатноеРасписание.Ссылка,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ШтатноеРасписаниеПодчиненные.Ссылка) КАК КоличествоПодчиненных
			|ПОМЕСТИТЬ ВТГруппыПозицийСКоличествомПодчиненных
			|ИЗ
			|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
			|			ШтатноеРасписание.Ссылка КАК Ссылка,
			|			ШтатноеРасписание.Родитель КАК Родитель
			|		ИЗ
			|			Справочник.ШтатноеРасписание КАК ШтатноеРасписание) КАК ШтатноеРасписаниеПодчиненные
			|		ПО ШтатноеРасписание.Ссылка = ШтатноеРасписаниеПодчиненные.Родитель
			|ГДЕ
			|	ШтатноеРасписание.ЭтоГруппа
			|
			|СГРУППИРОВАТЬ ПО
			|	ШтатноеРасписание.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ГруппыПозицийСКоличествомПодчиненных.Ссылка
			|ИЗ
			|	ВТГруппыПозицийСКоличествомПодчиненных КАК ГруппыПозицийСКоличествомПодчиненных
			|ГДЕ
			|	ГруппыПозицийСКоличествомПодчиненных.КоличествоПодчиненных = 0";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			ПроверитьНеобходимостьУдаленияЛишнихГрупп = Ложь;
		Иначе
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ЛишняяГруппа = Выборка.Ссылка.ПолучитьОбъект();
				УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ЛишняяГруппа);
				ЛишняяГруппа.Удалить();
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла; 
	
	// Создание недостающих групп позиций.
	ПроверитьНеобходимостьСозданияНовыхГрупп = Истина;	
	Пока ПроверитьНеобходимостьСозданияНовыхГрупп Цикл
		
		Запрос = Новый Запрос;
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ШтатноеРасписание.Подразделение КАК Подразделение,
			|	ШтатноеРасписание.Подразделение.Владелец КАК Владелец,
			|	ШтатноеРасписание.Подразделение.Родитель КАК ВышестоящееПодразделение
			|ПОМЕСТИТЬ ВТПодразделенияШтатногоРасписания
			|ИЗ
			|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|ГДЕ
			|	ШтатноеРасписание.Подразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПодразделенияШтатногоРасписания.Подразделение,
			|	ПодразделенияШтатногоРасписания.Владелец,
			|	ПодразделенияШтатногоРасписания.ВышестоящееПодразделение
			|ПОМЕСТИТЬ ВТОстатокСоздаваемых
			|ИЗ
			|	ВТПодразделенияШтатногоРасписания КАК ПодразделенияШтатногоРасписания
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ПО ПодразделенияШтатногоРасписания.Подразделение = ШтатноеРасписание.Подразделение
			|			И (ШтатноеРасписание.ЭтоГруппа)
			|ГДЕ
			|	ШтатноеРасписание.Ссылка ЕСТЬ NULL 
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПодразделенияШтатногоРасписания.ВышестоящееПодразделение,
			|	ПодразделенияШтатногоРасписания.Владелец,
			|	ВЫРАЗИТЬ(ПодразделенияШтатногоРасписания.ВышестоящееПодразделение КАК Справочник.ПодразделенияОрганизаций).Родитель
			|ИЗ
			|	ВТПодразделенияШтатногоРасписания КАК ПодразделенияШтатногоРасписания
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ПО ПодразделенияШтатногоРасписания.ВышестоящееПодразделение = ШтатноеРасписание.Подразделение
			|			И (ШтатноеРасписание.ЭтоГруппа)
			|ГДЕ
			|	ПодразделенияШтатногоРасписания.ВышестоящееПодразделение <> ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ОстатокСоздаваемых.Подразделение,
			|	ОстатокСоздаваемых.ВышестоящееПодразделение,
			|	ОстатокСоздаваемых.Владелец
			|ПОМЕСТИТЬ ВТПодразделенияШтатногоРасписанияКСозданию
			|ИЗ
			|	ВТОстатокСоздаваемых КАК ОстатокСоздаваемых
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ПодразделенияШтатногоРасписанияКСозданию.Подразделение,
			|	ПРЕДСТАВЛЕНИЕ(ПодразделенияШтатногоРасписанияКСозданию.Подразделение) КАК Наименование,
			|	ЕСТЬNULL(ШтатноеРасписание.Ссылка, ЗНАЧЕНИЕ(Справочник.ШтатноеРасписание.ПустаяСсылка)) КАК Родитель,
			|	ПодразделенияШтатногоРасписанияКСозданию.Владелец КАК Владелец
			|ИЗ
			|	ВТПодразделенияШтатногоРасписанияКСозданию КАК ПодразделенияШтатногоРасписанияКСозданию
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ПО ПодразделенияШтатногоРасписанияКСозданию.ВышестоящееПодразделение = ШтатноеРасписание.Подразделение
			|			И ПодразделенияШтатногоРасписанияКСозданию.Владелец = ШтатноеРасписание.Владелец
			|			И (ШтатноеРасписание.ЭтоГруппа)
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписаниеТекущийУровень
			|		ПО ПодразделенияШтатногоРасписанияКСозданию.Подразделение = ШтатноеРасписаниеТекущийУровень.Подразделение
			|			И ПодразделенияШтатногоРасписанияКСозданию.Владелец = ШтатноеРасписаниеТекущийУровень.Владелец
			|			И (ШтатноеРасписаниеТекущийУровень.ЭтоГруппа)
			|ГДЕ
			|	ШтатноеРасписаниеТекущийУровень.Ссылка ЕСТЬ NULL ";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			ПроверитьНеобходимостьСозданияНовыхГрупп = Ложь;
		Иначе
			
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				НоваяГруппа = Справочники.ШтатноеРасписание.СоздатьГруппу();
				ЗаполнитьЗначенияСвойств(НоваяГруппа, Выборка);
				УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(НоваяГруппа);
				НоваяГруппа.Записать();
				
			КонецЦикла;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	// Обновление иерархии групп
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка,
		|	ЕСТЬNULL(ГруппыШтатногоРасписания.Ссылка, ЗНАЧЕНИЕ(Справочник.ШтатноеРасписание.ПустаяСсылка)) КАК Родитель
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ГруппыШтатногоРасписания
		|		ПО ШтатноеРасписание.Подразделение.Родитель = ГруппыШтатногоРасписания.Подразделение
		|			И (ГруппыШтатногоРасписания.ЭтоГруппа)
		|			И ШтатноеРасписание.Ссылка <> ГруппыШтатногоРасписания.Ссылка
		|			И ШтатноеРасписание.Владелец = ГруппыШтатногоРасписания.Владелец
		|ГДЕ
		|	ШтатноеРасписание.Родитель <> ЕСТЬNULL(ГруппыШтатногоРасписания.Ссылка, ЗНАЧЕНИЕ(Справочник.ШтатноеРасписание.ПустаяСсылка))
		|	И ШтатноеРасписание.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбновляемыйОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ОбновляемыйОбъект.Родитель = Выборка.Родитель;
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ОбновляемыйОбъект);
			ОбновляемыйОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
	// Обновление иерархии элементов.
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка,
		|	ГруппыШтатногоРасписания.Ссылка КАК Родитель
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ГруппыШтатногоРасписания
		|		ПО ШтатноеРасписание.Подразделение = ГруппыШтатногоРасписания.Подразделение
		|			И (ГруппыШтатногоРасписания.ЭтоГруппа)
		|ГДЕ
		|	ШтатноеРасписание.Родитель <> ГруппыШтатногоРасписания.Ссылка
		|	И НЕ ШтатноеРасписание.ЭтоГруппа";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбновляемыйОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ОбновляемыйОбъект.Родитель = Выборка.Родитель;
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ОбновляемыйОбъект);
			ОбновляемыйОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
	// Обновление наименований групп штатного расписания.
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШтатноеРасписание.Подразделение,
		|	МИНИМУМ(ШтатноеРасписание.Закрыта) КАК Закрыта
		|ПОМЕСТИТЬ ВТЗакрытостьПодразделений
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|ГДЕ
		|	НЕ ШтатноеРасписание.ЭтоГруппа
		|
		|СГРУППИРОВАТЬ ПО
		|	ШтатноеРасписание.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтатноеРасписание.Ссылка КАК ГруппаШтатногоРасписания,
		|	ШтатноеРасписание.Родитель
		|ПОМЕСТИТЬ ВТНезакрытыеГруппы
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТЗакрытостьПодразделений КАК ЗакрытостьПодразделений
		|		ПО ШтатноеРасписание.Подразделение = ЗакрытостьПодразделений.Подразделение
		|			И (НЕ ЗакрытостьПодразделений.Закрыта)
		|ГДЕ
		|	ШтатноеРасписание.ЭтоГруппа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НезакрытыеГруппы.ГруппаШтатногоРасписания
		|ИЗ
		|	ВТНезакрытыеГруппы КАК НезакрытыеГруппы";
		
	РезультатЗапроса = Запрос.Выполнить();
	Пока Не РезультатЗапроса.Пустой() Цикл
		
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ШтатноеРасписание.Ссылка КАК ГруппаШтатногоРасписания,
			|	ШтатноеРасписание.Родитель КАК Родитель
			|ПОМЕСТИТЬ ВТНезакрытыеГруппыПредварительно
			|ИЗ
			|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТНезакрытыеГруппы КАК НезакрытыеГруппы
			|		ПО ШтатноеРасписание.Ссылка = НезакрытыеГруппы.Родитель
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНезакрытыеГруппы КАК НезакрытыеГруппыРассчитанные
			|		ПО ШтатноеРасписание.Ссылка = НезакрытыеГруппыРассчитанные.ГруппаШтатногоРасписания
			|ГДЕ
			|	ШтатноеРасписание.ЭтоГруппа
			|	И НезакрытыеГруппыРассчитанные.ГруппаШтатногоРасписания ЕСТЬ NULL 
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	НезакрытыеГруппыПредварительно.ГруппаШтатногоРасписания
			|ИЗ
			|	ВТНезакрытыеГруппыПредварительно КАК НезакрытыеГруппыПредварительно";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Запрос.Текст =
				"ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	НезакрытыеГруппыПредварительно.ГруппаШтатногоРасписания,
				|	НезакрытыеГруппыПредварительно.Родитель
				|ПОМЕСТИТЬ ВТНезакрытыеГруппыПромежуточная
				|ИЗ
				|	ВТНезакрытыеГруппыПредварительно КАК НезакрытыеГруппыПредварительно
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	НезакрытыеГруппы.ГруппаШтатногоРасписания,
				|	НезакрытыеГруппы.Родитель
				|ИЗ
				|	ВТНезакрытыеГруппы КАК НезакрытыеГруппы
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ВТНезакрытыеГруппыПредварительно
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ВТНезакрытыеГруппы
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	НезакрытыеГруппыПромежуточная.ГруппаШтатногоРасписания,
				|	НезакрытыеГруппыПромежуточная.Родитель
				|ПОМЕСТИТЬ ВТНезакрытыеГруппы
				|ИЗ
				|	ВТНезакрытыеГруппыПромежуточная КАК НезакрытыеГруппыПромежуточная
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|УНИЧТОЖИТЬ ВТНезакрытыеГруппыПромежуточная";
				
			Запрос.Выполнить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка,
		|	ШтатноеРасписание.Подразделение.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА НезакрытыеГруппы.ГруппаШтатногоРасписания ЕСТЬ NULL 
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Закрыта
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНезакрытыеГруппы КАК НезакрытыеГруппы
		|		ПО ШтатноеРасписание.Ссылка = НезакрытыеГруппы.ГруппаШтатногоРасписания
		|ГДЕ
		|	ШтатноеРасписание.ЭтоГруппа
		|	И (ШтатноеРасписание.Наименование <> ШтатноеРасписание.Подразделение.Наименование
		|			ИЛИ ШтатноеРасписание.Закрыта <> ВЫБОР
		|				КОГДА НезакрытыеГруппы.ГруппаШтатногоРасписания ЕСТЬ NULL 
		|					ТОГДА ИСТИНА
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ)";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка= РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ОбновляемыйОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ОбновляемыйОбъект.Наименование = Выборка.Наименование;
			ОбновляемыйОбъект.Закрыта = Выборка.Закрыта;
			УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ОбновляемыйОбъект);
			ОбновляемыйОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры


#Область ПервоначальноеЗаполнениеИОбновлениеИнформационнойБазы

Процедура ЗаполнитьКлассыУсловийТруда() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ШтатноеРасписание.Ссылка
	|ИЗ
	|	РегистрСведений.КлассыУсловийТрудаПоДолжностям КАК КлассыУсловийТрудаПоДолжностям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|		ПО КлассыУсловийТрудаПоДолжностям.Должность = ШтатноеРасписание.Ссылка";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		
		// Обновление уже выполнялось
		Возврат;
		
	КонецЕсли;
	
	Запрос.Текст =
	
	"ВЫБРАТЬ
	|	ШтатноеРасписание.Ссылка КАК Должность,
	|	КлассыУсловийТрудаПоДолжностям.Период,
	|	КлассыУсловийТрудаПоДолжностям.КлассУсловийТруда,
	|	КлассыУсловийТрудаПоДолжностям.ДатаРегистрацииИзменений
	|ИЗ
	|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.КлассыУсловийТрудаПоДолжностям КАК КлассыУсловийТрудаПоДолжностям
	|			ПО Должности.Ссылка = КлассыУсловийТрудаПоДолжностям.Должность
	|		ПО ШтатноеРасписание.Должность = Должности.Ссылка
	|ГДЕ
	|	КлассыУсловийТрудаПоДолжностям.КлассУсловийТруда ЕСТЬ НЕ NULL ";
	
	НаборЗаписей = РегистрыСведений.КлассыУсловийТрудаПоДолжностям.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(ЗаписьНабора, Выборка);
	КонецЦикла;
	
	НаборЗаписей.ОбменДанными.Загрузка = Истина;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти


#КонецЕсли
