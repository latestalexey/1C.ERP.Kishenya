#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий	
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	КатегорииЗастрахованныхЛицЕСВ.Ссылка
	|ИЗ
	|	Справочник.КатегорииЗастрахованныхЛицЕСВ КАК КатегорииЗастрахованныхЛицЕСВ
	|ГДЕ
	|	КатегорииЗастрахованныхЛицЕСВ.Ссылка НЕ В (&КатегорииВыключены)
	|	И КатегорииЗастрахованныхЛицЕСВ.Наименование ПОДОБНО &СтрокаПоиска";
	
	Если Параметры.СтрокаПоиска = Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И КатегорииЗастрахованныхЛицЕСВ.Наименование ПОДОБНО &СтрокаПоиска", "");
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СтрокаПоиска", Строка(Параметры.СтрокаПоиска) + "%");
	Запрос.УстановитьПараметр("КатегорииВыключены", КатегорииВыключены());
	РезультатЗапроса = Запрос.Выполнить();
	
	ДанныеВыбора = Новый СписокЗначений;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ДанныеВыбора.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	СтандартнаяОбработка = Ложь;	
	
КонецПроцедуры

Функция КатегорииВыключены() Экспорт
	
	МассивКатегорииВыключены = Новый Массив();
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуетсяТрудЧленовЛетныхЭкипажей") Тогда
		МассивКатегорииВыключены.Добавить(Справочники.КатегорииЗастрахованныхЛицЕСВ.НаемныйРаботникЛетчик);	
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользуютсяСпециальныеРаботы") Тогда
		МассивКатегорииВыключены.Добавить(Справочники.КатегорииЗастрахованныхЛицЕСВ.НаемныйРаботникИнвалидНаСпециальныхРаботах);
		МассивКатегорииВыключены.Добавить(Справочники.КатегорииЗастрахованныхЛицЕСВ.НаемныйРаботникНаСпециальныхРаботах);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении") Тогда
		МассивКатегорииВыключены.Добавить(Справочники.КатегорииЗастрахованныхЛицЕСВ.НаемныйРаботникБюджет);
	КонецЕсли;
	
	Возврат МассивКатегорииВыключены; 
	
КонецФункции	

#КонецОбласти


#КонецЕсли
