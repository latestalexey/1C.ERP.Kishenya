#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.ТолькоПросмотр Тогда
		Элементы.ФормаСохранитьНастройку.Доступность = Ложь;
	КонецЕсли; 
	
	Номенклатура = Параметры.Номенклатура;
	Если НЕ Номенклатура.Пустая() Тогда
		Элементы.СтраницыНоменклатура.ТекущаяСтраница = Элементы.СтраницаНоменклатураВыбрана;
		РеквизитыМатериала = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Номенклатура, "ВидНоменклатуры,ИспользованиеХарактеристик");
		ВидМатериала = РеквизитыМатериала.ВидНоменклатуры;
		ПродукцияИмеетХарактеристики = (РеквизитыМатериала.ИспользованиеХарактеристик 
											<> Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать);
	Иначе
		Элементы.СтраницыНоменклатура.ТекущаяСтраница = Элементы.СтраницаНоменклатураНеВыбрана;
		ПродукцияИмеетХарактеристики = Ложь;
	КонецЕсли;
	
	Характеристика = Параметры.Характеристика;
	Если НЕ Характеристика.Пустая() Тогда
		Элементы.СтраницыХарактеристика.ТекущаяСтраница = Элементы.СтраницаХарактеристикаВыбрана;
	Иначе
		Элементы.СтраницыХарактеристика.ТекущаяСтраница = Элементы.СтраницаХарактеристикаНеВыбрана;
	КонецЕсли;
	
	// Текущая настройка
	СпособАвтовыбораНоменклатуры   = Параметры.СпособАвтовыбораНоменклатуры;
	СпособАвтовыбораХарактеристики = Параметры.СпособАвтовыбораХарактеристики;
	СвойствоСодержащееНоменклатуру = Параметры.СвойствоСодержащееНоменклатуру;
	ПрименениеМатериала            = Параметры.ПрименениеМатериала;
	
	Для каждого НастройкаСоответствия Из Параметры.СоответствиеСвойств Цикл
		ЗаполнитьЗначенияСвойств(СоответствиеСвойств.Добавить(), НастройкаСоответствия);
	КонецЦикла; 
	
	// Определим вид изделий который содержит доступные свойства
	ВидИзделийИлиНоменклатура = Параметры.ВидИзделийИлиНоменклатура;
	Если ТипЗнч(ВидИзделийИлиНоменклатура) = Тип("СправочникСсылка.Номенклатура") Тогда
		ВидИзделий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидИзделийИлиНоменклатура, "ВидНоменклатуры");
	Иначе
		ВидИзделий = ВидИзделийИлиНоменклатура;
	КонецЕсли;
	
	СписокВсехДоступныхСвойств.Загрузить(УправлениеДаннымиОбИзделиях.ПолучитьСвойстваДляАвтоподбора(ВидИзделий));
	
	ЗаполнитьВыборСвойстваСодержащегоНоменклатуру();
	
	// Устаноим представление способа выбора, в зависимости от контекста настройки (спецификация или операция)
	ЗначениеВыбора = Элементы.МатериалУказываетсяВНСИ.СписокВыбора.НайтиПоЗначению(Перечисления.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ);
	ЗначениеВыбора.Представление = Параметры.НазваниеСвойстваУказываетсяВНСИ;
	
	ЗначениеВыбора = Элементы.ХарактеристикаУказываетсяВНСИ.СписокВыбора.НайтиПоЗначению(Перечисления.СпособыАвтовыбораХарактеристики.УказываетсяВНСИ);
	ЗначениеВыбора.Представление = Параметры.НазваниеСвойстваУказываетсяВНСИ;
	
	// Отключим видимость элементов связанных с характеристиками, если характеристики не используются
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	ИспользоватьДополнительныеРеквизитыИСведения = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеРеквизитыИСведения");
	
	// Элементы связанные с настройкой "Материал указывается в свойстве характеристики продукции"
	Если НЕ ИспользоватьХарактеристикиНоменклатуры ИЛИ НЕ ИспользоватьДополнительныеРеквизитыИСведения Тогда
		Элементы.ГруппаЗадаетсяВСвойствеПродукции.Видимость = Ложь;
	КонецЕсли; 
	
	Если НЕ ИспользоватьХарактеристикиНоменклатуры Тогда
		Элементы.ГруппаКакОпределяетсяХарактеристикаМатериала.Видимость = Ложь;
	КонецЕсли;
	
	// Элементы связанные с настройкой "Определяется по свойствам и характеристике продукции"
	Если НЕ ИспользоватьХарактеристикиНоменклатуры ИЛИ НЕ ИспользоватьДополнительныеРеквизитыИСведения Тогда
		Элементы.ГруппаХарактеристикаПодбираетсяПоСвойствамПродукции.Видимость = Ложь;
	КонецЕсли; 
	
	Если НЕ ИспользоватьХарактеристикиНоменклатуры Тогда
		Элементы.ПояснениеХарактеристика.Видимость = Ложь;
		Элементы.ПояснениеХарактеристикаПодбираетсяПоСвойствамПродукции.Видимость = Ложь;
		Элементы.ПояснениеНетХарактеристик.Видимость = Ложь;
		Элементы.ПояснениеХарактеристикаУточняетсяПриПроизводстве.Видимость = Ложь;
		Элементы.ДекорацияПустая.Видимость = Ложь;
	КонецЕсли; 
	
	ИспользоватьОбщиеДополнительныеЗначения = ПолучитьФункциональнуюОпцию("ИспользоватьОбщиеДополнительныеЗначения");
	Если НЕ ИспользоватьОбщиеДополнительныеЗначения Тогда
		Элементы.НастройкаСоответствияСвойств.Видимость = Ложь;
	КонецЕсли; 
	
	УправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область КакОпределяетсяМатериал

&НаКлиенте
Процедура МатериалУказываетсяВНСИПриИзменении(Элемент)
	
	ПриИзмененииАвтовыбораНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадаетсяВСвойствеПродукцииДоступнаПриИзменении(Элемент)
	
	ПриИзмененииАвтовыбораНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УточняетсяПриПроизводствеПриИзменении(Элемент)
	
	ПриИзмененииАвтовыбораНоменклатуры(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область КакОопределяетсяХарактеристикаМатериала

&НаКлиенте
Процедура ХарактеристикаУказываетсяВНСИПриИзменении(Элемент)
	
	УправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодбираетсяПоСвойствамПродукцииДоступнаПриИзменении(Элемент)
	
	УправлениеДоступностью(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСохранитьНастройку(Команда)
	
	ПараметрыФормы = Новый Структура("НоменклатураСтрокой,ХарактеристикаСтрокой,
									|СпособАвтовыбораНоменклатуры,СпособАвтовыбораХарактеристики,
									|СвойствоСодержащееНоменклатуру");
	
	ПараметрыФормы.Вставить("СпособАвтовыбораНоменклатуры",   СпособАвтовыбораНоменклатуры);
	ПараметрыФормы.Вставить("СпособАвтовыбораХарактеристики", СпособАвтовыбораХарактеристики);
	
	ПараметрыФормы.Вставить("ПрименениеМатериала", ПрименениеМатериала);
	
	Если СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции") Тогда
		ПараметрыФормы.Вставить("Номенклатура", Неопределено);
		
		НоменклатураСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='<указывается в свойстве продукции ""%1""';uk='<зазначається у властивості продукції ""%1""'"), 
									Строка(СвойствоСодержащееНоменклатуру));
				
		ПараметрыФормы.Вставить("НоменклатураСтрокой", НоменклатураСтрокой);
		
		ПараметрыФормы.Вставить("СвойствоСодержащееНоменклатуру", СвойствоСодержащееНоменклатуру);
		
	ИначеЕсли СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УточняетсяПриПроизводстве") Тогда
		ПараметрыФормы.Вставить("Номенклатура", Неопределено);
		ПараметрыФормы.Вставить("НоменклатураСтрокой", НСтр("ru='<уточняется при производстве>';uk='<уточнюється при виробництві>'"));
		
	Иначе
		ПараметрыФормы.Вставить("Номенклатура", Номенклатура);
		ПараметрыФормы.Вставить("НоменклатураСтрокой", "");
	КонецЕсли; 
	
	Если СпособАвтовыбораХарактеристики = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.ПодбираетсяПоСвойствамПродукции") Тогда
		ПараметрыФормы.Вставить("Характеристика", Неопределено);
		ПараметрыФормы.Вставить("ХарактеристикаСтрокой", НСтр("ru='<Определяется по характеристике продукции>';uk='<Визначається за характеристикою продукції>'"));
		
		ПараметрыФормы.Вставить("СоответствиеСвойств", СоответствиеСвойств);
		
	ИначеЕсли СпособАвтовыбораХарактеристики = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.УточняетсяПриПроизводстве") Тогда
		ПараметрыФормы.Вставить("Характеристика", Неопределено);
		ПараметрыФормы.Вставить("ХарактеристикаСтрокой", НСтр("ru='<уточняется при производстве>';uk='<уточнюється при виробництві>'"));
		
	Иначе
		ПараметрыФормы.Вставить("Характеристика", Характеристика);
		ПараметрыФормы.Вставить("ХарактеристикаСтрокой", "");
	КонецЕсли; 
	
	ОповеститьОВыборе(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНастройкаСоответствияСвойств(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидИзделий",          ВидИзделий);
	ПараметрыФормы.Вставить("ВидМатериала",        ВидМатериала);
	ПараметрыФормы.Вставить("СоответствиеСвойств", СоответствиеСвойств);
	ПараметрыФормы.Вставить("ТолькоПросмотр",      ТолькоПросмотр);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаСоответствияСвойствЗаверешение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.НастройкаСоответствияСвойств", ПараметрыФормы,,,,,ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СвойствоСодержащееНоменклатуру.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СпособАвтовыбораНоменклатуры");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаСоответствияСвойствЗаверешение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеСвойств.Очистить();
	Для каждого НастройкаСоответствия Из РезультатЗакрытия Цикл
		ЗаполнитьЗначенияСвойств(СоответствиеСвойств.Добавить(), НастройкаСоответствия);
	КонецЦикла; 
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВыборСвойстваСодержащегоНоменклатуру()

	Для каждого ДанныеСвойства Из СписокВсехДоступныхСвойств Цикл
		Если НЕ ДанныеСвойства.ПометкаУдаления 
			И ДанныеСвойства.Свойство.ТипЗначения.СодержитТип(Тип("СправочникСсылка.Номенклатура")) 
			ИЛИ ДанныеСвойства.Свойство = СвойствоСодержащееНоменклатуру Тогда
			Элементы.СвойствоСодержащееНоменклатуру.СписокВыбора.Добавить(ДанныеСвойства.Свойство, ДанныеСвойства.Представление);
		КонецЕсли;
	КонецЦикла;

	Если Элементы.СвойствоСодержащееНоменклатуру.СписокВыбора.Количество() = 0 Тогда
		Элементы.СтраницыЗадаетсяВСвойствеПродукции.ТекущаяСтраница = Элементы.СтраницаЗадаетсяВСвойствеПродукцииНеДоступна;
	Иначе
		Элементы.СтраницыЗадаетсяВСвойствеПродукции.ТекущаяСтраница = Элементы.СтраницаЗадаетсяВСвойствеПродукцииДоступна;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииАвтовыбораНоменклатуры(Форма)

	ПроверитьСпособыАвтовыбораХарактеристики(Форма);
	УправлениеДоступностью(Форма);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьСпособыАвтовыбораХарактеристики(Форма)
	
 	Если Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции") Тогда
		
		Если Форма.СпособАвтовыбораХарактеристики <> ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.УточняетсяПриПроизводстве") Тогда
			Форма.СпособАвтовыбораХарактеристики = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.УточняетсяПриПроизводстве");
		КонецЕсли; 
		
	ИначеЕсли Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УточняетсяПриПроизводстве") Тогда
		
		Если Форма.СпособАвтовыбораХарактеристики <> ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.УточняетсяПриПроизводстве") Тогда
			Форма.СпособАвтовыбораХарактеристики = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.УточняетсяПриПроизводстве");
		КонецЕсли; 
		
	КонецЕсли;

	Если ЗначениеЗаполнено(Форма.СвойствоСодержащееНоменклатуру)
		И Форма.СпособАвтовыбораНоменклатуры <> ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции") Тогда
		Форма.СвойствоСодержащееНоменклатуру = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)

	// СвойствоСодержащееНоменклатуру
	Если Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции") Тогда
		Форма.Элементы.СвойствоСодержащееНоменклатуру.ТолькоПросмотр = Ложь;
	Иначе
		Форма.Элементы.СвойствоСодержащееНоменклатуру.ТолькоПросмотр = Истина;
	КонецЕсли; 
	
	// ХарактеристикаУказываетсяВНСИ
	Если Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ") Тогда
		Форма.Элементы.ХарактеристикаУказываетсяВНСИ.ТолькоПросмотр = Ложь;
	Иначе
		Форма.Элементы.ХарактеристикаУказываетсяВНСИ.ТолькоПросмотр = Истина;
	КонецЕсли; 
	
	// ПодбираетсяПоСвойствамПродукции
	ВыборХарактеристикПоСвойствамДоступен = Истина;
	Если Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ")
		И Форма.Номенклатура.Пустая() Тогда
		
		ЗначениеВыбора = Форма.Элементы.ПодбираетсяПоСвойствамПродукцииНеДоступна.СписокВыбора[0];
		ЗначениеВыбора.Представление = НСтр("ru='Определяется по свойствам и характеристике продукции (необходимо указать материал)';uk='Визначається за властивостями та характеристиками продукції (необхідно зазначити матеріал)'");
		
		ВыборХарактеристикПоСвойствамДоступен = Ложь;
		
	ИначеЕсли Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УточняетсяПриПроизводстве") Тогда
		
		ЗначениеВыбора = Форма.Элементы.ПодбираетсяПоСвойствамПродукцииНеДоступна.СписокВыбора[0];
		ЗначениеВыбора.Представление = НСтр("ru='Определяется по свойствам и характеристике продукции (недоступно, т.к. материал уточняется в производстве)';uk='Визначається за властивостями та характеристиками продукції (недоступно, оскільки матеріал уточнюється у виробництві)'");
		
		ВыборХарактеристикПоСвойствамДоступен = Ложь;
		
	КонецЕсли;
	
	Если ВыборХарактеристикПоСвойствамДоступен Тогда
		Форма.Элементы.СтраницыХарактеристикаПодбираетсяПоСвойствамПродукции.ТекущаяСтраница = Форма.Элементы.СтраницаХарактеристикаПодбираетсяПоСвойствамПродукцииДоступна;
		Если Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УказываетсяВНСИ") Тогда
			Форма.Элементы.СтраницыНастройкаСоответствияСвойств.ТекущаяСтраница = Форма.Элементы.СтраницаНастройкаСоответствияСвойствНастройка;
		Иначе
			Форма.Элементы.СтраницыНастройкаСоответствияСвойств.ТекущаяСтраница = Форма.Элементы.СтраницаНастройкаСоответствияСвойствНастройкаНеТребуется;
		КонецЕсли; 
	Иначе
		Форма.Элементы.СтраницыХарактеристикаПодбираетсяПоСвойствамПродукции.ТекущаяСтраница = Форма.Элементы.СтраницаХарактеристикаПодбираетсяПоСвойствамПродукцииНеДоступна;
	КонецЕсли; 
	
	Если Форма.СпособАвтовыбораХарактеристики = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораХарактеристики.ПодбираетсяПоСвойствамПродукции") Тогда
		Форма.Элементы.НастройкаСоответствияСвойств.Доступность = Истина;
	Иначе
		Форма.Элементы.НастройкаСоответствияСвойств.Доступность = Ложь;
	КонецЕсли; 
	
	Если Форма.ИспользоватьХарактеристикиНоменклатуры Тогда
		Если Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.ЗадаетсяВСвойствеПродукции")
			ИЛИ Форма.СпособАвтовыбораНоменклатуры = ПредопределенноеЗначение("Перечисление.СпособыАвтовыбораНоменклатуры.УточняетсяПриПроизводстве") Тогда
			Форма.Элементы.СтраницыКакОпределяетсяХарактеристикаМатериала.ТекущаяСтраница = Форма.Элементы.СтраницаКакОпределяетсяХарактеристикаМатериалаМатериалВСвойстве;
		ИначеЕсли Форма.ПродукцияИмеетХарактеристики Тогда
			Форма.Элементы.СтраницыКакОпределяетсяХарактеристикаМатериала.ТекущаяСтраница = Форма.Элементы.СтраницаКакОпределяетсяХарактеристикаМатериалаЕстьХарактеристика;
		Иначе
			Форма.Элементы.СтраницыКакОпределяетсяХарактеристикаМатериала.ТекущаяСтраница = Форма.Элементы.СтраницаКакОпределяетсяХарактеристикаМатериалаНетХарактеристики;
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
