&НаКлиенте
Перем СтарыеЗначенияКонтролируемыхПолей;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		РаботаВБюджетномУчреждении = ПолучитьФункциональнуюОпцию("РаботаВБюджетномУчреждении");
		ИспользоватьСтатьиФинансирования = ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиФинансированияЗарплатаРасширенный");
	Иначе
		РаботаВБюджетномУчреждении = Ложь;
		ИспользоватьСтатьиФинансирования = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаКнопокПросмотр.Видимость			= ТолькоПросмотр;
	Элементы.ГруппаКнопокРедактирование.Видимость	= Не ТолькоПросмотр;
	Если ТолькоПросмотр Тогда
		Элементы.ФормаЗакрыть.КнопкаПоУмолчанию	= Истина;
	Иначе
		Элементы.ФормаОК.КнопкаПоУмолчанию		= Истина;
	КонецЕсли;
	
	Организация = Параметры.Организация;
	МесяцНачисления = Параметры.МесяцНачисления;
	ИмяДокумента = Параметры.ИмяДокумента;
	ОписаниеДокумента = Новый ФиксированнаяСтруктура(Параметры.ОписаниеДокумента);
	
	СписокСотрудников = Параметры.УчитываемыеСотрудники;
	Если ТипЗнч(СписокСотрудников) = Тип("Строка")
		И Не ПустаяСтрока(СписокСотрудников) Тогда
		
		СписокСотрудников = ПолучитьИзВременногоХранилища(СписокСотрудников);
		
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(СписокСотрудников) Тогда
		
		Если ТипЗнч(СписокСотрудников) <> Тип("Массив") Тогда
			СписокСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СписокСотрудников);
		КонецЕсли;
		
		Если СписокСотрудников.Количество() > 0 Тогда
			
			Если ТипЗнч(СписокСотрудников[0]) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				УчитываемыеСотрудники = Новый ФиксированныйМассив(СписокСотрудников);
			Иначе
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
				Запрос.Текст =
					"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
					|	Сотрудники.ФизическоеЛицо
					|ИЗ
					|	Справочник.Сотрудники КАК Сотрудники
					|ГДЕ
					|	Сотрудники.Ссылка В(&СписокСотрудников)
					|
					|ОБЪЕДИНИТЬ ВСЕ
					|
					|ВЫБРАТЬ РАЗЛИЧНЫЕ
					|	ФизическиеЛица.Ссылка
					|ИЗ
					|	Справочник.ФизическиеЛица КАК ФизическиеЛица
					|ГДЕ
					|	ФизическиеЛица.Ссылка В(&СписокСотрудников)";
					
				УчитываемыеСотрудники = Новый ФиксированныйМассив(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизическоеЛицо"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли; 
	
	
	ЗаполнитьФормуНаСервере(Параметры.СведенияОбНДФЛ, Истина);
	
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(НДФЛ, "НалоговыйПериод", "НалоговыйПериодСтрокой");	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	УчетНДФЛКлиент.ФормаПодробнееОРасчетеНДФЛОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы


#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыНДФЛ

&НаКлиенте
Процедура НДФЛПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		ЗаполнитьНовуюСтроку(Элементы.НДФЛ.ТекущиеДанные);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛНалоговыйПериодПриИзменении(Элемент)
	ЗарплатаКадрыКлиент.ВводМесяцаПриИзменении(Элементы.НДФЛ.ТекущиеДанные, "НалоговыйПериод", "НалоговыйПериодСтрокой", Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура НДФЛНалоговыйПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаНачалоВыбора(ЭтаФорма, Элементы.НДФЛ.ТекущиеДанные, "НалоговыйПериод", "НалоговыйПериодСтрокой");
КонецПроцедуры

&НаКлиенте
Процедура НДФЛНалоговыйПериодРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ЗарплатаКадрыКлиент.ВводМесяцаРегулирование(Элементы.НДФЛ.ТекущиеДанные, "НалоговыйПериод", "НалоговыйПериодСтрокой", Направление, Модифицированность);
КонецПроцедуры

&НаКлиенте
Процедура НДФЛНалоговыйПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаАвтоПодборТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НДФЛНалоговыйПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗарплатаКадрыКлиент.ВводМесяцаОкончаниеВводаТекста(Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры



#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ПеренестиИзмененияВОбъектФормыВладельца();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиИзмененияВОбъектФормыВладельца()
	
	Если Модифицированность Тогда
		
		Оповестить(
			"ИзмененыРезультатыРасчетаНДФЛ",
			ПоместитьИзмененныеДанныеВоВременноеХранилище(),
			ЭтаФорма);
		
		Модифицированность = Ложь;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НДФЛОтменитьИсправление(Команда)
	РасчетЗарплатыКлиент.ОтменитьИсправление(
		ЭтаФорма, УчетНДФЛКлиентСервер.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ());
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НДФЛОтменитьВсеИсправления(Команда)
	РасчетЗарплатыКлиент.ОтменитьВсеИсправления(
		ЭтаФорма, УчетНДФЛКлиентСервер.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ());
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьФиксРасчетСтрокНДФЛ(СтруктураПоиска)
	
	УчетНДФЛФормы.УстановитьФиксРасчетСтрокНДФЛ(ЭтаФорма, СтруктураПоиска);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьИзмененныеДанныеВоВременноеХранилище()
	
	ВозвращаемыеСведения = Новый Структура;
	ВозвращаемыеСведения.Вставить("НДФЛ", НДФЛ);
	
	
	Возврат ПоместитьВоВременноеХранилище(ВозвращаемыеСведения, Новый УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция ПолучитьКонтролируемыеПоля() Экспорт
	
	Возврат УчетНДФЛФормы.ФормаПодробнееОРасчетеНДФЛКонтролируемыеПоляДляФиксацииРезультатов();
	
КонецФункции

&НаКлиенте
Функция ПолучитьСтарыеЗначенияКонтролируемыхПолей() Экспорт
	Возврат СтарыеЗначенияКонтролируемыхПолей;
КонецФункции

&НаСервере
Функция ОписанияТаблицДляРаспределенияРезультата()

	ОписанияТаблиц = Новый Массив;
	ОписанияТаблиц.Добавить(УчетНДФЛКлиентСервер.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ());
	
	Возврат ОписанияТаблиц;

КонецФункции

&НаКлиенте
Процедура ПриИзмененииДанныхСтрокиНДФЛ()
	
	УчетНДФЛКлиент.НДФЛПриОкончанииРедактирования(ЭтаФорма);
	
	РасчетЗарплатыКлиент.СтрокаРасчетаПриОкончанииРедактирования(
		ЭтаФорма, УчетНДФЛКлиентСервер.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ(), , Ложь);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьНачисленияСотрудника(Сотрудники, СохранятьИсправления = Истина) Экспорт
	
	ВладелецФормы.ПерезаполнитьНачисленияСотрудника(Сотрудники, СохранятьИсправления);
	ЗарплатаКадрыКлиент.ПодключитьОбработчикОжиданияОбработкиСобытия(ЭтаФорма, "ПерезаполнитьФормуПоОбъекту");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьФормуПоОбъекту()
	
	ЗаполнитьФормуНаСервере(ВладелецФормы.СведенияОбНДФЛ(), Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФормуНаСервере(Знач СведенияОбНДФЛ, Знач ПервичноеЗаполнение)
	
	ТабличныеЧастиНДФЛ = ПолучитьИзВременногоХранилища(СведенияОбНДФЛ);
	ТабличныеЧастиНДФЛ.Свойство("АдресРаспределенияРезультатовВХранилище", АдресРаспределенияРезультатовВХранилище);
	
	Если ТолькоПросмотр И ТабличныеЧастиНДФЛ.НДФЛ.Количество() = 0
		Или УчитываемыеСотрудники = Неопределено ИЛИ УчитываемыеСотрудники.Количество() = 0 Тогда
		
		ВызватьИсключение  НСтр("ru='Нет сведений о расчете НДФЛ';uk='Немає відомостей про розрахунок ПДФО'");
		
	КонецЕсли; 
	
	ЕдинственныйСотрудник = УчитываемыеСотрудники.Количество() < 2;
	Если ПервичноеЗаполнение Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"НДФЛФизическоеЛицо",
			"ПараметрыВыбора",
			Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый ПараметрВыбора("Отбор.Ссылка", УчитываемыеСотрудники))));
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"НДФЛФизическоеЛицо",
			"Видимость",
			Не ЕдинственныйСотрудник);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"НДФЛПодразделение",
			"ПараметрыВыбора",
			Новый ФиксированныйМассив(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Новый ПараметрВыбора("Отбор.Владелец", Организация))));
		
		Элементы.НДФЛ.ТекущаяСтрока	= 0;
		
		Если ТолькоПросмотр Тогда
			Элементы.НДФЛ.ПоложениеКоманднойПанели	= ПоложениеКоманднойПанелиЭлементаФормы.Нет;
			Элементы.НДФЛ.ИзменятьСоставСтрок		= Ложь;
		КонецЕсли;
			
	КонецЕсли; 
	
	// Инициализация табличных частей
	НДФЛ.Загрузить(ТабличныеЧастиНДФЛ.НДФЛ);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(НДФЛ, "НалоговыйПериод", "НалоговыйПериодСтрокой");		
	
	
	Если ТабличныеЧастиНДФЛ.Свойство("Начисления") Тогда
		Начисления.Загрузить(ТабличныеЧастиНДФЛ.Начисления);
	КонецЕсли;
	
	
	ОписаниеТаблицы = УчетНДФЛКлиентСервер.ФормаПодробнееОРасчетеНДФЛОписаниеТаблицыНДФЛ();
	ОписаниеТаблицы.ОтменятьВсеИсправления = Не ЕдинственныйСотрудник;
	
	УчетНДФЛФормы.ФормаПодробнееОРасчетеНДФЛПриЗаполнении(ЭтаФорма, ОписаниеТаблицы, ОписанияТаблицДляРаспределенияРезультата());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНовуюСтроку(НоваяСтрока)
	
	НоваяСтрока.ФиксСтрока = Истина;
	
	Если УчитываемыеСотрудники.Количество() = 1 И ЕдинственныйСотрудник Тогда
		НоваяСтрока.ФизическоеЛицо = УчитываемыеСотрудники[0];
	КонецЕсли; 
	
	
	НоваяСтрока.НалоговыйПериод = НачалоМесяца(МесяцНачисления);
	ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДатеВТабличнойЧасти(НДФЛ, "НалоговыйПериод", "НалоговыйПериодСтрокой");

	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодходящееПодразделение(ФизическоеЛицо, Организация, МесяцНачисления)
	
	Подразделение = Неопределено;
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		
		ПараметрыПолученияСотрудников = КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоСпискуФизическихЛиц();
		ПараметрыПолученияСотрудников.Организация = Организация;
		ПараметрыПолученияСотрудников.НачалоПериода = МесяцНачисления;
		ПараметрыПолученияСотрудников.ОкончаниеПериода = КонецМесяца(МесяцНачисления);
		ПараметрыПолученияСотрудников.СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
		ПараметрыПолученияСотрудников.КадровыеДанные = "Подразделение";
		
		СотрудникиОрганизации = КадровыйУчет.СотрудникиОрганизации(Истина, ПараметрыПолученияСотрудников);
		Если СотрудникиОрганизации.Количество() > 0 Тогда
			Подразделение = СотрудникиОрганизации[0].Подразделение;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Подразделение;
	
КонецФункции

&НаКлиенте
Процедура РассчитатьСотрудника(Сотрудник, ОписаниеТаблицы) Экспорт
	
	ВладелецФормы.РассчитатьСотрудника(Сотрудник, ОписаниеТаблицы);
	
КонецПроцедуры

#КонецОбласти

СтарыеЗначенияКонтролируемыхПолей = Новый Соответствие;
