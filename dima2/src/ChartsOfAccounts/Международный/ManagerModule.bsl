#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает массив счетов с видом субконто заданным в качестве параметра
//
// Параметры:
// 		ВидыСубконто - Массив, ПланВидовХарактеристикСсылка.ВидыСубконтоМеждународные - Массив ссылок или ссылка на субконто, по которому необходимо получить счета
//
// Возвращаемое значение:
// 		Массив - Массив ссылок <ПланСчетовСсылка.Международный>, поддерживающих субконто заданные параметром функции
//
Функция СчетаУчетаСВидомСубконто(ВидыСубконто) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Международный.Ссылка
		|ИЗ
		|	ПланСчетов.Международный КАК Международный
		|ГДЕ
		|	Международный.ВидыСубконто.ВидСубконто В(&ВидСубконто)"
	);
	
	Запрос.УстановитьПараметр("ВидСубконто", ВидыСубконто);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	//++ НЕ УТКА
	КомандаОтчет = ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуИспользованиеСчетаМеждународногоУчета(КомандыОтчетов);
	Если КомандаОтчет <> Неопределено Тогда
		КомандаОтчет.СписокФорм = "ФормаСписка";
	КонецЕсли;
	//-- НЕ УТКА

КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// Монопольный обработчик обновления 2.1.3
// Заполняет предопределенный элемент "Служебный" плана счетов
//
Процедура ЗаполнитьПредопределенныйСлужебныйЭлемент() Экспорт
	
	ОбъектПланаСчетов = ПланыСчетов.Международный.Служебный.ПолучитьОбъект();
	
	Если ОбъектПланаСчетов.Код = "00000"
		И ОбъектПланаСчетов.Порядок = "00000"
		И ОбъектПланаСчетов.Вид = ВидСчета.АктивноПассивный
		И ОбъектПланаСчетов.Наименование = НСтр("ru='Служебный счет';uk='Службовий рахунок'")
		И ОбъектПланаСчетов.Описание = ""
		И ОбъектПланаСчетов.НаименованиеМеждународное = "" Тогда
		
		ОбъектПланаСчетов.НаименованиеМеждународное = НСтр("ru='Suspense account';uk='Suspense account'");
		ОбъектПланаСчетов.Описание = НСтр("ru='Временный счет для учета операций, платежей, поступлений, задолженности и т. д. до их окончательной классификации, в т.ч. счет для ввода начальных остатков.';uk='Тимчасовий рахунок для обліку операцій, платежів, надходжень, заборгованості і т. д. до їх остаточної класифікації, в т. ч. рахунок для введення початкових залишків.'");
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектПланаСчетов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВидыСубконто") Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ПланСчетов.Ссылка
			|ИЗ
			|	ПланСчетов.Международный КАК ПланСчетов
			|ГДЕ
			|	ПланСчетов.ВидыСубконто.ВидСубконто В(&ВидСубконто)");
		Запрос.УстановитьПараметр("ВидСубконто", Параметры.ВидыСубконто);
		Результат = Запрос.Выполнить();
		Параметры.Отбор.Вставить("Ссылка", Результат.Выгрузить().ВыгрузитьКолонку("Ссылка"));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


