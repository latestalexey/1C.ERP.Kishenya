#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначения.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияДляЗаполнения  = Новый Структура("Организация", "Организация");	
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = ЗарплатаКадры.ПерваяДоступнаяОрганизация();
	КонецЕсли; 
	
	ЗаполнитьПерерасчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_НачислениеЗарплаты" Тогда
		ЗаполнитьПерерасчетыЗарплаты();
	ИначеЕсли ИмяСобытия = "ДокументБольничныйЛистПослеЗаписи"
		Или ИмяСобытия = "ЗаписьДокументаКомандировка"
		Или ИмяСобытия = "Запись_ОплатаДнейУходаЗаДетьмиИнвалидами" 
		Или ИмяСобытия = "Запись_ОплатаПоСреднемуЗаработку" 
		Или ИмяСобытия = "ЗаписанДокументОтпуск" 
		Или ИмяСобытия = "Запись_ОтпускПоУходуЗаРебенком"
		Или ИмяСобытия = "Запись_Увольнение"
		Или ИмяСобытия = "Запись_УвольнениеСписком" Тогда
		
		ЗаполнитьПерерасчетыСреднегоЗаработка();
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарплатаДекорацияОбработкаНавигационнойСсылки(Элемент, 
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗарегистрироватьНеобходимостьПерерасчетаЗарплаты();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаЗарплата

&НаКлиенте
Процедура ТаблицаЗарплатаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаЗарплатаСотрудник" Тогда
		ПоказатьЗначение(, ТаблицаЗарплата.НайтиПоИдентификатору(ВыбраннаяСтрока)["Сотрудник"]);
	ИначеЕсли Поле.Имя = "ТаблицаЗарплатаПричина" Тогда
		ОткрытьПричиныПерерасчетаЗарплаты(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗарплатаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ЗарегистрироватьНеобходимостьПерерасчетаЗарплаты();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗарплатаПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ВыделенныеСтроки = Элементы.ТаблицаЗарплата.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Если ВыделенныеСтроки.Количество() = 1 Тогда
			
			ТекущиеДанные = ТаблицаЗарплата.НайтиПоИдентификатору(ВыделенныеСтроки[0]);
			
			ТекстВопроса = НСтр("ru='Удалить сведения о необходимости перерасчета зарплаты';uk='Видалити відомості про необхідність перерахунку зарплати'")
				+ Символы.ПС + НСтр("ru='сотрудника';uk='співробітника'") + " %1 " + НСтр("ru='в';uk='в'") + " %2 " + НСтр("ru='года';uk='року'") + "?";
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстВопроса,
				ТекущиеДанные.Сотрудник,
				Формат(ТекущиеДанные.Период, "ДФ='МММ гггг'"));
			
		Иначе
			ТекстВопроса = НСтр("ru='Удалить сведения о необходимости перерасчета зарплаты?';uk='Видалити відомості про необхідність перерахунку зарплати?'")
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура("УдаляемыеСтроки", ВыделенныеСтроки);
		
		Оповещение = Новый ОписаниеОповещения("ТаблицаЗарплатаПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСреднийЗаработок

&НаКлиенте
Процедура ТаблицаСреднийЗаработокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаСреднийЗаработокСотрудник" Тогда
		ПоказатьЗначение(, ТаблицаСреднийЗаработок.НайтиПоИдентификатору(ВыбраннаяСтрока)["Сотрудник"]);
	ИначеЕсли Поле.Имя = "ТаблицаСреднийЗаработокДокумент" Тогда
		
		ТекущиеДанные = ТаблицаСреднийЗаработок.НайтиПоИдентификатору(ВыбраннаяСтрока);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ",  ТекущиеДанные.Документ);
		ПараметрыОткрытия.Вставить("Сотрудник",  ТекущиеДанные.Сотрудник);
		
		ОткрытьФормуОбъекта(ТекущиеДанные.Документ, ПараметрыОткрытия);
		
	ИначеЕсли Поле.Имя = "ТаблицаСреднийЗаработокПричина" Тогда
		ОткрытьПричиныПерерасчетаЗарплаты(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСреднийЗаработокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ВыделенныеСтроки = Элементы.ТаблицаСреднийЗаработок.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() > 0 Тогда
		
		Если ВыделенныеСтроки.Количество() = 1 Тогда
			
			ТекущиеДанные = ТаблицаСреднийЗаработок.НайтиПоИдентификатору(ВыделенныеСтроки[0]);
			
			ТекстВопроса = НСтр("ru='Удалить сведения о необходимости перезаполнения сведений о среднем заработке и перерасчете документа';uk='Видалити відомості про необхідність перезаповнення даних про середній заробіток та перерахунку документу'") + Символы.ПС + "%1 " + НСтр("ru='по сотруднику';uk='по співробітнику'") + " %2?";
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстВопроса,
				ТекущиеДанные.Документ,
				ТекущиеДанные.Сотрудник);
			
		Иначе
			ТекстВопроса = НСтр("ru='Удалить сведения о необходимости перезаполнения сведений о среднем заработке и перерасчете документов';uk='Видалити відомості про необхідність перезаповнення даних про середній заробіток і перерахунок документів'") + "?";
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура("УдаляемыеСтроки", ВыделенныеСтроки);
		
		Оповещение = Новый ОписаниеОповещения("ТаблицаСреднийЗаработокПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьПерерасчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоначислитьСейчас(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаЗарплата.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДоступныеДоначисления = ПерерасчетЗарплатыВызовСервера.ДокументыПерерасчета(Организация, ТекущиеДанные.Период);
	
		ДополнительныеПараметры = Новый Структура("МесяцНачисления", ДоступныеДоначисления.МесяцНачисления);
		Если ДоступныеДоначисления.Документы.Количество() > 1 Тогда
			
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("РежимДоначисления", Истина);
			ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
			
			СтруктураОтбора = Новый Структура;
			СтруктураОтбора.Вставить("Организация", Организация);
			СтруктураОтбора.Вставить("МесяцНачисления", ДоступныеДоначисления.МесяцНачисления);
			СтруктураОтбора.Вставить("Ссылка", ДоступныеДоначисления.Документы);
			
			ПараметрыОткрытия.Вставить("Отбор", СтруктураОтбора);
			
			Оповещение = Новый ОписаниеОповещения("ДоначислитьСейчасЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			ОткрытьФорму("Документ.НачислениеЗарплаты.ФормаСписка",
				ПараметрыОткрытия, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		Иначе
			
			Если ДоступныеДоначисления.Документы.Количество() = 1 Тогда
				ДокументДоначисления = ДоступныеДоначисления.Документы[0];
			Иначе
				ДокументДоначисления = ПредопределенноеЗначение("Документ.НачислениеЗарплаты.ПустаяСсылка");
			КонецЕсли;
			
			ДоначислитьСейчасЗавершение(ДокументДоначисления, ДополнительныеПараметры);
			
		КонецЕсли; 
	
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура Исправить(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаСреднийЗаработок.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ИсправлениеДокументовЗарплатаКадрыКлиент.Исправить(ТекущиеДанные.Документ, ИмяОбъектаМетаданных(ТекущиеДанные.Документ));
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура Пересчитать(Команда)
	
	ТекущиеДанные = Элементы.ТаблицаСреднийЗаработок.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Документ", ТекущиеДанные.Документ);
		ДополнительныеПараметры.Вставить("Сотрудник", ТекущиеДанные.Сотрудник);
		
		Если ТекущиеДанные.Период < ОткрытыйПериодВыплатыЗарплаты(Организация, ТекущиеДанные.Сотрудник) Тогда
			
			ТекстВопроса = НСтр("ru='Зарплата за %1 г. уже выплачена. При пересчете документа изменится сальдо
                |расчетов с сотрудником %2. Чтобы избежать этого выберите команду ""Исправить""
                |Все равно пересчитать документ?'
                |;uk='Зарплата за %1 р. вже виплачена. При перерахунку документа зміниться сальдо
                |розрахунків з співробітником %2. Щоб уникнути цього виберіть команду ""Виправити""
                |Все одно перерахувати документ?'");
				
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстВопроса,
				Формат(ТекущиеДанные.Период, "ДФ='МММ гггг'"),
				ТекущиеДанные.Сотрудник);
			
			Оповещение = Новый ОписаниеОповещения("ПересчитатьЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			
		Иначе
			ПересчитатьЗавершение(КодВозвратаДиалога.Да, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗарегистрироватьНеобходимостьПерерасчетаЗарплаты()
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Организация", Организация);
		
		Оповещение = Новый ОписаниеОповещения("ЗарегистрироватьНеобходимостьПерерасчетаЗарплатыЗавершение", ЭтотОбъект);
		
		ОткрытьФорму("Обработка.УправлениеПерерасчетами.Форма.ФормаНовойЗаписиПерерасчетов", ПараметрыОткрытия, ЭтаФорма, , , , Оповещение);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьНеобходимостьПерерасчетаЗарплатыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьПерерасчетыЗарплаты(Результат.Сотрудник, Результат.Период);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", ДополнительныеПараметры.Документ);
		ПараметрыОткрытия.Вставить("ВыполнитьПерезаполнениеСведенийОСреднемЗаработке", Истина);
		ПараметрыОткрытия.Вставить("Сотрудник", ДополнительныеПараметры.Сотрудник);
		
		ОткрытьФормуОбъекта(ДополнительныеПараметры.Документ, ПараметрыОткрытия);
			
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиПользователя", "Организация", Организация);
	ЗаполнитьПерерасчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗарплатаПередУдалениемЗавершение(Результат, ДополнительныеСвойства) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьПерерасчетыТаблицыЗарплаты(ДополнительныеСвойства.УдаляемыеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПерерасчетыТаблицыЗарплаты(УдаляемыеСтроки)
	
	Если УдаляемыеСтроки.Количество() > 0 Тогда
		
		Для каждого ИдентификаторУдаляемойСтроки Из УдаляемыеСтроки Цикл
			
			ТекущиеДанные = ТаблицаЗарплата.НайтиПоИдентификатору(ИдентификаторУдаляемойСтроки);
			Если ТекущиеДанные <> Неопределено Тогда
				
				НаборЗаписей = РегистрыСведений.ПерерасчетЗарплаты.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Организация.Установить(Организация);
				НаборЗаписей.Отбор.Сотрудник.Установить(ТекущиеДанные.Сотрудник);
				НаборЗаписей.Отбор.ПериодДействия.Установить(ТекущиеДанные.Период);
				НаборЗаписей.Отбор.ДокументНачисления.Установить(ТекущиеДанные.ДокументНачисления);
				
				НаборЗаписей.Записать();
				
			КонецЕсли; 
			
		КонецЦикла;
		
		ЗаполнитьПерерасчетыЗарплаты();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСреднийЗаработокПередУдалениемЗавершение(Результат, ДополнительныеСвойства) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УдалитьПерерасчетыТаблицыСреднийЗаработок(ДополнительныеСвойства.УдаляемыеСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьПерерасчетыТаблицыСреднийЗаработок(УдаляемыеСтроки)
	
	Если УдаляемыеСтроки.Количество() > 0 Тогда
		
		Для каждого ИдентификаторУдаляемойСтроки Из УдаляемыеСтроки Цикл
			
			ТекущиеДанные = ТаблицаСреднийЗаработок.НайтиПоИдентификатору(ИдентификаторУдаляемойСтроки);
			Если ТекущиеДанные <> Неопределено Тогда
				
				НаборЗаписей = РегистрыСведений.ПерерасчетСреднегоЗаработка.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Организация.Установить(Организация);
				НаборЗаписей.Отбор.Сотрудник.Установить(ТекущиеДанные.Сотрудник);
				НаборЗаписей.Отбор.ДокументСреднегоЗаработка.Установить(ТекущиеДанные.Документ);
				
				НаборЗаписей.Записать();
				
			КонецЕсли; 
			
		КонецЦикла;
			
		ЗаполнитьПерерасчетыСреднегоЗаработка();
				
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПерерасчеты()
	
	ЗаполнитьПерерасчетыЗарплаты();
	ЗаполнитьПерерасчетыСреднегоЗаработка();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПерерасчетыЗарплаты(Сотрудник = Неопределено, Период = Неопределено)
	
	ТаблицаЗарплата.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПерерасчетЗарплаты.Организация,
		|	ПерерасчетЗарплаты.Сотрудник КАК Сотрудник,
		|	ПерерасчетЗарплаты.ПериодДействия КАК ПериодДействия,
		|	ПерерасчетЗарплаты.ДокументОснование КАК ДокументОснование,
		|	ПерерасчетЗарплаты.ДокументНачисления КАК ДокументНачисления
		|ИЗ
		|	РегистрСведений.ПерерасчетЗарплаты КАК ПерерасчетЗарплаты
		|ГДЕ
		|	ПерерасчетЗарплаты.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	ПериодДействия,
		|	ДокументНачисления,
		|	ДокументОснование";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			Пока Выборка.СледующийПоЗначениюПоля("ПериодДействия") Цикл
				
				Пока Выборка.СледующийПоЗначениюПоля("ДокументНачисления") Цикл
					
					НоваяСтрока = ТаблицаЗарплата.Добавить();
					НоваяСтрока.Сотрудник = Выборка.Сотрудник;
					НоваяСтрока.Период = Выборка.ПериодДействия;
					НоваяСтрока.ДокументНачисления = Выборка.ДокументНачисления;
					Пока Выборка.Следующий() Цикл
						Если ЗначениеЗаполнено(Выборка.ДокументОснование) Тогда
							НоваяСтрока.Причина.Добавить(Выборка.ДокументОснование, КраткоеПредставлениеДокумента(Выборка.ДокументОснование));
						КонецЕсли; 
					КонецЦикла; 
					НоваяСтрока.ПричинаНеизвестна = НоваяСтрока.Причина.Количество() = 0;
					
					Если Сотрудник = НоваяСтрока.Сотрудник
						И Период = НоваяСтрока.Период Тогда
						
						Элементы.ТаблицаЗарплата.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
						
					КонецЕсли; 
					
				КонецЦикла; 
			
			КонецЦикла; 
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	Если ТаблицаЗарплата.Количество() = 0 Тогда
		
		ТекстДекорации = НСтр("ru='Причин для перерасчета и доначисления зарплаты нет';uk='Причин для перерахунку та донарахування зарплати немає'");
		Если ЗначениеЗаполнено(Организация) Тогда
			
			ТекстДекорации = Новый ФорматированнаяСтрока(
				ТекстДекорации + " (",
				Новый ФорматированнаяСтрока(
					НСтр("ru='зарегистрировать необходимость перерасчета или доначисления зарплаты';uk='зареєструвати необхідність перерахунку або донарахування зарплати'"), , , , "ЗарегистрироватьПерерасчеты"),
				")");
			
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы,
			"ЗарплатаДекорация",
			"Заголовок",
			ТекстДекорации);
	
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ЗарплатаДекорация",
		"Видимость",
		ТаблицаЗарплата.Количество() = 0);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТаблицаЗарплатаГруппа",
		"Видимость",
		ТаблицаЗарплата.Количество() <> 0);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПерерасчетыСреднегоЗаработка()
	
	ТаблицаСреднийЗаработок.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПерерасчетСреднегоЗаработка.Организация,
		|	ПерерасчетСреднегоЗаработка.Сотрудник КАК Сотрудник,
		|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка КАК ДокументСреднегоЗаработка,
		|	ПерерасчетСреднегоЗаработка.ДокументСреднегоЗаработка.ПериодРегистрации КАК ПериодРегистрации,
		|	ПерерасчетСреднегоЗаработка.ДокументОснование КАК ДокументОснование
		|ИЗ
		|	РегистрСведений.ПерерасчетСреднегоЗаработка КАК ПерерасчетСреднегоЗаработка
		|ГДЕ
		|	ПерерасчетСреднегоЗаработка.Организация = &Организация
		|
		|УПОРЯДОЧИТЬ ПО
		|	Сотрудник,
		|	ДокументСреднегоЗаработка,
		|	ПериодРегистрации,
		|	ДокументОснование";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.СледующийПоЗначениюПоля("Сотрудник") Цикл
			
			Пока Выборка.СледующийПоЗначениюПоля("ДокументСреднегоЗаработка") Цикл
					
				Пока Выборка.СледующийПоЗначениюПоля("ПериодРегистрации") Цикл
					
					НоваяСтрока = ТаблицаСреднийЗаработок.Добавить();
					НоваяСтрока.Сотрудник = Выборка.Сотрудник;
					НоваяСтрока.Документ = Выборка.ДокументСреднегоЗаработка;
					НоваяСтрока.Период = Выборка.ПериодРегистрации;
					Пока Выборка.Следующий() Цикл
						Если ЗначениеЗаполнено(Выборка.ДокументОснование) Тогда
							НоваяСтрока.Причина.Добавить(Выборка.ДокументОснование, КраткоеПредставлениеДокумента(Выборка.ДокументОснование));
						КонецЕсли; 
					КонецЦикла; 
					
				КонецЦикла; 
				
			КонецЦикла; 
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СреднийЗаработокДекорация",
		"Видимость",
		ТаблицаСреднийЗаработок.Количество() = 0);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ТаблицаСреднийЗаработокГруппа",
		"Видимость",
		ТаблицаСреднийЗаработок.Количество() <> 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоначислитьСейчасЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Ключ", Результат);
		
		ЗначенияЗаполнения = Новый Структура;
		Если Не ЗначениеЗаполнено(Результат) Тогда
			ЗначенияЗаполнения.Вставить("Организация", Организация);
			ЗначенияЗаполнения.Вставить("МесяцНачисления", ДополнительныеПараметры.МесяцНачисления);
			ЗначенияЗаполнения.Вставить("РежимДоначисления", Истина);
		КонецЕсли; 
		
		ЗначенияЗаполнения.Вставить("ЗаполнитьПриОткрытии", Истина);
		
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
		ОткрытьФорму("Документ.НачислениеЗарплаты.ФормаОбъекта", ПараметрыОткрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПричиныПерерасчетаЗарплаты(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И ТекущиеДанные.Причина.Количество() > 0 Тогда
		
		Если ТекущиеДанные.Причина.Количество() = 1 Тогда
			ОткрытьПричиныПерерасчетаЗарплатыЗавершение(ТекущиеДанные.Причина[0]);
		Иначе
			
			Оповещение = Новый ОписаниеОповещения("ОткрытьПричиныПерерасчетаЗарплатыЗавершение", ЭтотОбъект);
			ПоказатьВыборИзСписка(Оповещение, ТекущиеДанные.Причина, Элемент);
			
		КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПричиныПерерасчетаЗарплатыЗавершение(Результат, ДополнительныеСвойства = Неопределено) Экспорт
	
	Если Результат <> Неопределено Тогда
		ОткрытьФормуОбъекта(Результат.Значение);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОбъекта(СсылкаНаОбъект, ПараметрыОткрытия = Неопределено)
	
	Если ПараметрыОткрытия = Неопределено Тогда
		ПараметрыОткрытия = Новый Структура("Ключ", СсылкаНаОбъект);
	КонецЕсли; 
	
	ОткрытьФорму("Документ." + ИмяОбъектаМетаданных(СсылкаНаОбъект) + ".ФормаОбъекта", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяОбъектаМетаданных(Ссылка)
	
	Возврат Ссылка.Метаданные().Имя;
	
КонецФункции

&НаСервере
Функция КраткоеПредставлениеДокумента(СсылкаНаДокумент)
	
	КраткоеПредставление = "";
	ИмяМетаданныхДокумента = ИмяОбъектаМетаданных(СсылкаНаДокумент);
	
	Если ИмяМетаданныхДокумента = "ИзменениеОплатыТруда" Тогда
		КраткоеПредставление = НСтр("ru='Изменение оплаты';uk='Зміна оплати'");
	ИначеЕсли ИмяМетаданныхДокумента = "КадровыйПеревод" Тогда
		КраткоеПредставление = НСтр("ru='Перевод';uk='Переведення'");
	ИначеЕсли ИмяМетаданныхДокумента = "КадровыйПереводСписком" Тогда
		КраткоеПредставление = НСтр("ru='Перевод списком';uk='Переведення списком'");
	ИначеЕсли ИмяМетаданныхДокумента = "ПриемНаРаботу" Тогда
		КраткоеПредставление = НСтр("ru='Прием';uk='Прийом'");
	ИначеЕсли ИмяМетаданныхДокумента = "ПриемНаРаботуСписком" Тогда
		КраткоеПредставление = НСтр("ru='Прием списком';uk='Прийом списком'");
	ИначеЕсли ИмяМетаданныхДокумента = "СторнированиеНачислений" Тогда
		КраткоеПредставление = НСтр("ru='Сторно';uk='Сторно'");
	ИначеЕсли ИмяМетаданныхДокумента = "ПростойСотрудников" Тогда
		КраткоеПредставление = НСтр("ru='Простой';uk='Простій'");
	ИначеЕсли ИмяМетаданныхДокумента = "Отпуск" Тогда
		КраткоеПредставление = НСтр("ru='Отпуск';uk='Відпустка'");
	ИначеЕсли ИмяМетаданныхДокумента = "ВходящаяСправкаОЗаработкеДляРасчетаПособий" Тогда
		КраткоеПредставление = НСтр("ru='Справка о зарплате';uk='Довідка про зарплату'");
	ИначеЕсли ИмяМетаданныхДокумента = "БольничныйЛист" Тогда
		КраткоеПредставление = НСтр("ru='Больничный';uk='Лікарняний'");
	ИначеЕсли ИмяМетаданныхДокумента = "ОплатаПоСреднемуЗаработку" Тогда
		КраткоеПредставление = НСтр("ru='Оплата по среднему';uk='Оплата за середнім'");
	ИначеЕсли ИмяМетаданныхДокумента = "ОплатаПоСреднемуЗаработку" Тогда
		КраткоеПредставление = НСтр("ru='Премия';uk='Премія'");
	ИначеЕсли ИмяМетаданныхДокумента = "ОплатаДнейУходаЗаДетьмиИнвалидами" Тогда
		КраткоеПредставление = НСтр("ru='Оплата дней ухода';uk='Оплата днів догляду'");
	ИначеЕсли ИмяМетаданныхДокумента = "НачислениеЗарплаты" Тогда
		КраткоеПредставление = НСтр("ru='Зарплата';uk='Зарплата'");
	ИначеЕсли ИмяМетаданныхДокумента = "РазовоеНачисление" Тогда
		КраткоеПредставление = НСтр("ru='Разовое начисление';uk='Разове нарахування'");
	ИначеЕсли ИмяМетаданныхДокумента = "Увольнение" Тогда
		КраткоеПредставление = НСтр("ru='Увольнение';uk='Звільнення'");
	ИначеЕсли ИмяМетаданныхДокумента = "УвольнениеСписком" Тогда
		КраткоеПредставление = НСтр("ru='Увольнение списком';uk='Звільнення списком'");
	ИначеЕсли ИмяМетаданныхДокумента = "ОтпускБезСохраненияОплаты" Тогда
		КраткоеПредставление = НСтр("ru='Отпуск без оплаты';uk='Відпустка без оплати'");
	ИначеЕсли ИмяМетаданныхДокумента = "ПрогулНеявка" Тогда
		КраткоеПредставление = НСтр("ru='Прогул';uk='Прогул'");
	ИначеЕсли ИмяМетаданныхДокумента = "ДоходВНатуральнойФорме" Тогда
		КраткоеПредставление = НСтр("ru='Натуральный доход';uk='Натуральний дохід'");
	ИначеЕсли ИмяМетаданныхДокумента = "Командировка" Тогда
		КраткоеПредставление = НСтр("ru='Командировка';uk='Відрядження'");
	ИначеЕсли ИмяМетаданныхДокумента = "МатериальнаяПомощь" Тогда
		КраткоеПредставление = НСтр("ru='Мат. помощь';uk='Мат. допомога'");
	ИначеЕсли ИмяМетаданныхДокумента = "ОтпускПоУходуЗаРебенком" Тогда
		КраткоеПредставление = НСтр("ru='Отпуск по уходу';uk='Відпустка по догляду'");
	ИначеЕсли ИмяМетаданныхДокумента = "ПереносДанных" Тогда
		КраткоеПредставление = НСтр("ru='Перенос';uk='Перенесення'");
	Иначе
		КраткоеПредставление = СсылкаНаДокумент.Метаданные().Синоним;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаНаДокумент, "Номер,Дата");
	
	КраткоеПредставление = КраткоеПредставление + " №" + ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(РеквизитыДокумента.Номер, Истина, Истина);
	КраткоеПредставление = КраткоеПредставление + " " + НСтр("ru='от';uk='від'") + " " + Формат(РеквизитыДокумента.Дата, "ДФ=dd.MM.yy");
	
	Возврат КраткоеПредставление;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОткрытыйПериодВыплатыЗарплаты(Организация, Сотрудник = Неопределено)
	
	ОткрытыйПериод = '00010101';
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
		
	Если Сотрудник = Неопределено Тогда
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	МАКСИМУМ(ЗарплатаКВыплате.ПериодВзаиморасчетов) КАК ПериодВзаиморасчетов
			|ИЗ
			|	РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
			|ГДЕ
			|	ЗарплатаКВыплате.Организация = &Организация
			|	И ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)";
		
	Иначе
		
		Если ТипЗнч(Сотрудник) = Тип("СправочникСсылка.Сотрудники") Тогда
			Запрос.УстановитьПараметр("Сотрудники", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сотрудник));
		Иначе
			Запрос.УстановитьПараметр("Сотрудники", Сотрудник);
		КонецЕсли;
		
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗарплатаКВыплате.Сотрудник,
			|	МАКСИМУМ(ЗарплатаКВыплате.ПериодВзаиморасчетов) КАК ПериодВзаиморасчетов
			|ИЗ
			|	РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
			|ГДЕ
			|	ЗарплатаКВыплате.Организация = &Организация
			|	И ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
			|	И ЗарплатаКВыплате.Сотрудник В(&Сотрудники)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗарплатаКВыплате.Сотрудник";
		
	КонецЕсли; 
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ОткрытыйПериод = КонецМесяца(Выборка.ПериодВзаиморасчетов) + 1;
		
	КонецЕсли; 
		
	Возврат ОткрытыйПериод;
	
КонецФункции

#КонецОбласти
