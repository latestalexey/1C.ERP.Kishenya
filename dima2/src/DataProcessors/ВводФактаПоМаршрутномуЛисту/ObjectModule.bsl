#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Получатель");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.ДоляСтоимости");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("ВыходныеИзделия.Характеристика");
	
	МассивНепроверяемыхРеквизитов.Добавить("Трудозатраты.Количество");
	
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.КоличествоУпаковок");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.Характеристика");
	МассивНепроверяемыхРеквизитов.Добавить("МатериалыИУслуги.ДатаРасхода");
	
	ПроверитьЗаполнениеКоличества(ПроверяемыеРеквизиты, Отказ);
	
	ПроверитьЗаполнениеДокументаВЗависимостиОтСтатуса(МассивНепроверяемыхРеквизитов, Отказ);
	
	ПроверитьЗаполнениеОтклонений("ВыходныеИзделия",  Отказ);
	ПроверитьЗаполнениеОтклонений("ВозвратныеОтходы", Отказ);
	ПроверитьЗаполнениеОтклонений("МатериалыИУслуги", Отказ);
	ПроверитьЗаполнениеОтклонений("Трудозатраты",     Отказ);
	
	ПроверитьНормативыТрудозатрат(Отказ);
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеСклада(Отказ)

	ШаблонСообщения = НСтр("ru='Не определено направление выпуска в строке %1 списка ""Продукция"".';uk='Не визначено напрям випуску в рядку %1 списку ""Продукція"".'");
	
	Для каждого СтрокаТаблицы Из ВыходныеИзделия Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Получатель) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", СтрокаТаблицы.НомерСтроки, "Склад");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЦикла;
	
	ШаблонСообщения = НСтр("ru='Не определено направление выпуска в строке %1 списка ""Возвратные отходы"".';uk='Не визначено напрям випуску в рядку %1 списку ""Зворотні відходи"".'");
	
	Для каждого СтрокаТаблицы Из ВозвратныеОтходы Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.Получатель) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", СтрокаТаблицы.НомерСтроки, "Склад");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДолиСтоимости(Отказ)
	
	Если ВыходныеИзделия.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru='Не заполнена колонка ""Доля стоимости"" в строке %1 списка ""Продукция""';uk='Не заповнена колонка ""Частка вартості"" в рядку %1 списку ""Продукція""'");
	Для каждого СтрокаТаблицы Из ВыходныеИзделия Цикл
		Если СтрокаТаблицы.ДоляСтоимости = 0 Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
																						Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
																						
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ВыходныеИзделия", СтрокаТаблицы.НомерСтроки, "ДоляСтоимости");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле,, Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеОтклонений(ИмяТаблицы, Отказ)
	
	Если НРег(ИмяТаблицы) = НРег("ВыходныеИзделия") Тогда
		ИмяРеквизитаКоличество = "КоличествоУпаковокФакт";
		ИмяРеквизита           = "КоличествоУпаковокОтклонение";
		
		ШаблонСообщения = НСтр("ru='Отклонение от норматива не должно приводить к отрицательному выпуску.';uk='Відхилення від нормативу не повинно призводити до від''ємного випуску.'");
		ШаблонСообщенияКоличество = НСтр("ru='Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Продукция"".';uk='Необхідно заповнити колонку ""Факт"" чи ""Відхилення"" в рядку %1 списку ""Продукція"".'");
		
	ИначеЕсли НРег(ИмяТаблицы) = НРег("ВозвратныеОтходы") Тогда
		ИмяРеквизитаКоличество = "КоличествоУпаковокФакт";
		ИмяРеквизита           = "КоличествоУпаковокОтклонение";
		
		ШаблонСообщения = НСтр("ru='Отклонение от норматива не должно приводить к отрицательному выпуску.';uk='Відхилення від нормативу не повинно призводити до від''ємного випуску.'");
		ШаблонСообщенияКоличество = НСтр("ru='Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Возвратные отходы"".';uk='Необхідно заповнити колонку ""Факт"" чи ""Відхилення"" в рядку %1 списку ""Зворотні відходи"".'");
		
	ИначеЕсли НРег(ИмяТаблицы) = НРег("МатериалыИУслуги") Тогда
		ИмяРеквизитаКоличество = "КоличествоУпаковокФакт";
		ИмяРеквизита           = "КоличествоУпаковокОтклонение";
		
		ШаблонСообщения = НСтр("ru='Отклонение от норматива не должно приводить к отрицательному потреблению.';uk='Відхилення від нормативу не повинно призводити до від''ємного споживання.'");
		ШаблонСообщенияКоличество = НСтр("ru='Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Материалы и работы"".';uk='Необхідно заповнити колонку ""Факт"" чи ""Відхилення"" в рядку %1 списку ""Матеріали і роботи"".'");
		
	ИначеЕсли НРег(ИмяТаблицы) = НРег("Трудозатраты") Тогда
		ИмяРеквизитаКоличество = "КоличествоФакт";
		ИмяРеквизита           = "КоличествоОтклонение";
		
		ШаблонСообщения = НСтр("ru='Отклонение от норматива не должно приводить к отрицательным трудозатратам.';uk='Відхилення від нормативу не повинно призводити до від''ємних трудовитрат.'");
		ШаблонСообщенияКоличество = НСтр("ru='Необходимо заполнить колонку ""Факт"" или ""Отклонение"" в строке %1 списка ""Трудозатраты"".';uk='Необхідно заповнити колонку ""Факт"" чи ""Відхилення"" в рядку %1 списку ""Трудовитрати"".'");
		
	КонецЕсли; 
	
	Для каждого ДанныеСтроки Из ЭтотОбъект[ИмяТаблицы] Цикл
		
		Если ДанныеСтроки.Количество + ДанныеСтроки.КоличествоОтклонение < 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ДанныеСтроки.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, ДанныеСтроки.НомерСтроки, ИмяРеквизита);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		ИначеЕсли ДанныеСтроки.КоличествоФакт = 0 И ДанныеСтроки.КоличествоОтклонение = 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщенияКоличество, ДанныеСтроки.НомерСтроки);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(ИмяТаблицы, ДанныеСтроки.НомерСтроки, ИмяРеквизитаКоличество);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеДокументаВЗависимостиОтСтатуса(МассивНепроверяемыхРеквизитов, Отказ)
	
	ПроверитьЗаполнениеСклада(Отказ);
	ПроверитьЗаполнениеДолиСтоимости(Отказ);
	
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "ВыходныеИзделия";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);

	ПараметрыПроверки.ИмяТЧ = "ВозвратныеОтходы";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);

	ПараметрыПроверки.ИмяТЧ = "МатериалыИУслуги";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(ЭтотОбъект, МассивНепроверяемыхРеквизитов, Отказ, ПараметрыПроверки);
	
	ПроверитьДатуРасходаМатериалов(Отказ);
	
	// Проверка заполнения серий
	ПараметрыУказанияСерий = Документы.МаршрутныйЛистПроизводства.ПараметрыУказанияСерий(ЭтотОбъект);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект, ПараметрыУказанияСерий.МатериалыИУслуги, Отказ, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПроверитьНормативыТрудозатрат(Отказ)

	ШаблонСообщения = НСтр("ru='Норматив по виду работ ""%1"" в сумме по всем бигадам должен быть равен %2 (сейчас сумма %3).';uk='Норматив по виду робіт ""%1"" в сумі по всіх бигадах повинен бути рівний %2 (зараз сума %3).'");
	
	Для каждого СтрокаТрудозатратыИсходные Из ТрудозатратыИсходные Цикл
		СтруктураПоиска = Новый Структура("ВидРабот", СтрокаТрудозатратыИсходные.ВидРабот);
		СписокСтрок = Трудозатраты.НайтиСтроки(СтруктураПоиска);
		Норматив = 0;
		Для каждого СтрокаТрудозатраты Из СписокСтрок Цикл
			Норматив = Норматив + СтрокаТрудозатраты.Количество;
		КонецЦикла;
		Если Норматив <> СтрокаТрудозатратыИсходные.Количество Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонСообщения, 
						СтрокаТрудозатратыИсходные.ВидРабот,
						Формат(СтрокаТрудозатратыИсходные.Количество, "ЧН=0; ЧГ="),
						Формат(Норматив, "ЧН=0; ЧГ="));
						
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,, "Объект", Отказ);
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПроверитьДатуРасходаМатериалов(Отказ)

	Если СтатусВыполненияОперации <> Перечисления.СтатусыВыполненияОпераций.Завершено Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения = НСтр("ru='Материал (работа) фактически израсходован, но не указана дата расхода в строке %1 списка ""Материалы и работы""';uk='Матеріал (робота) фактично витрачений, але не зазначена дата витрачання в рядку %1 списку ""Матеріали і роботи""'");
	
	Для каждого СтрокаТаблицы Из МатериалыИУслуги Цикл
		Если СтрокаТаблицы.ДатаРасхода = '000101010000'
			И СтрокаТаблицы.КоличествоФакт <> 0 Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, 
																						Формат(СтрокаТаблицы.НомерСтроки, "ЧГ="));
																						
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("МатериалыИУслуги", СтрокаТаблицы.НомерСтроки, "ДатаРасхода");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле,"Объект", Отказ);
		КонецЕсли; 	
	КонецЦикла; 
	
КонецПроцедуры

Процедура ПроверитьЗаполнениеКоличества(ПроверяемыеРеквизиты, Отказ)
	
	// Нужно удалить проверку количества, т.к. в документе особая логика проверки
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВыходныеИзделия.КоличествоУпаковокФакт"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВозвратныеОтходы.КоличествоУпаковокФакт"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("МатериалыИУслуги.КоличествоУпаковокФакт"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВыходныеИзделия.КоличествоУпаковок"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ВозвратныеОтходы.КоличествоУпаковок"));
	ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("МатериалыИУслуги.КоличествоУпаковок"));
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ВыходныеИзделия";
	ПараметрыПроверки.СуффиксДопРеквизита = "Факт";
	ПараметрыПроверки.УсловиеОтбораСтрокДляОкругления = "ВыходныеИзделия.Получатель ССЫЛКА Справочник.Склады";
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ВозвратныеОтходы";
	ПараметрыПроверки.СуффиксДопРеквизита = "Факт";
	ПараметрыПроверки.УсловиеОтбораСтрокДляОкругления = "ВозвратныеОтходы.Получатель ССЫЛКА Справочник.Склады";
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "МатериалыИУслуги";
	ПараметрыПроверки.ПроверитьВозможностьОкругления = Ложь;
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);

КонецПроцедуры

#КонецОбласти

#КонецЕсли