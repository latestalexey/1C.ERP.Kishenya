
&НаКлиенте
Перем ПредыдущийЭтап;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Отказ = Истина;
		СообщениеПользователю = НСтр("ru='Выполнять регламентные операции по закрытию месяца в подчиненном узле распределенной информационной не требуется,
            |они выполняются только в центральном узле информационной базы.'
            |;uk='Виконувати регламентні операції з закриття місяця в підпорядкованому вузлі розподіленої інформаційної не потрібно,
            |вони виконуються тільки в центральному вузлі інформаційної бази.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеПользователю);
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ПериодРегистрации", Объект.ПериодРегистрации);
	Если НЕ ЗначениеЗаполнено(Объект.ПериодРегистрации) Тогда
		Объект.ПериодРегистрации = НачалоМесяца(ТекущаяДата());
	КонецЕсли;
	
	Параметры.Свойство("Организация", Объект.Организация);
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Объект.Организация);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		Элементы.ГруппаАмортизация.Видимость = Ложь;
		Элементы.ГруппаПогашениеСтоимостиТМЦОбщая.Видимость = Ложь;
		Элементы.ГруппаНалогНаИмуществоОбщая.Видимость = Ложь;
		Элементы.ГруппаТранспортныйНалогОбщая.Видимость = Ложь;
		Элементы.ГруппаЗемельныйНалогОбщая.Видимость = Ложь;
		Элементы.ГруппаТорговыйСбор.Видимость = Ложь;
		Элементы.ГруппаПроизводственныеЗатраты.Видимость = Ложь;
		Элементы.ГруппаДолиСписанияКосвенныхРасходовОбщая.Видимость = Ложь;
		Элементы.ГруппаРасходыНаПродукциюОбщая.Видимость = Ложь;
		Элементы.ГруппаОтражениеВРеглУчете.Видимость = Ложь;
		Элементы.ГруппаФинансовыйРезультатОбщая.Видимость = Ложь;
		Элементы.ГруппаУСН.Видимость = Ложь;
		Элементы.ГруппаМеждународныйУчетОбщая.Видимость = Ложь;
		Элементы.ГруппаНачислениеОООбщая.Видимость = Ложь;
		Элементы.ГруппаОтражениеЗарплатыОбщая.Видимость = Ложь;
		Элементы.ГруппаРаспределениеМатериаловРаботНаПродукциюОбщая.Видимость = Ложь;
		Элементы.ГруппаНастройкаПоказателейРаспределенияРасходовОбщая.Видимость = Ложь;
		Элементы.ГруппаСписаниеЗатратНаВыпускОбщая.Видимость = Ложь;
		Элементы.ГруппаТорговыйСборОбщая.Видимость = Ложь;
        Элементы.ГруппаКорректировкаНалоговогоНазначенияКапитальныхИнвестицийОбщая.Видимость = Ложь;
        Элементы.ГруппаПерерасчетПропорциональногоНДСОбщая.Видимость = Ложь;
        Элементы.ГруппаКорректировкаУсловныхПродажПоПропорциональномуНДСОбщая.Видимость = Ложь;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		Элементы.ИсправлениеОтрицательныхОстатков.Заголовок = НСтр("ru='Исправление отрицательных остатков товаров организации';uk='Виправлення від''ємних залишків товарів організації'");
		Элементы.ПереоценкаДенежныхСредств.Заголовок = НСтр("ru='Переоценка денежных средств';uk='Переоцінка грошових коштів'");
	КонецЕсли;
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПартионныйУчет") Тогда
		Элементы.ПартииТоваров.Заголовок = НСтр("ru='Распределение доп. расходов по товарам';uk='Розподіл дод. витрат по товарах'");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("КомплекснаяАвтоматизация") Тогда
		Элементы.СписаниеЗатратНаВыпуск.Заголовок = НСтр("ru='Списание затрат на выпуск';uk='Списання витрат на випуск'");
		Элементы.ГруппаМеждународныйУчетОбщая.Видимость = Ложь;
		Элементы.ГруппаАмортизацияОСиНМАМеждународныйОбщая.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаОтчетСверкаДанныхПартионногоУчетаИСебестоимостиОткрыть.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	
	ВидПериодаРегистрации = Перечисления.ДоступныеПериодыОтчета.Месяц;
	СписокДоступныхОрганизаций = ДоступныеОрганизации(Объект.Организация);
	ПоказатьСвязанныеОрганизации();
	ОшибкиРасчета = Новый Структура;
	РезультатыРасчетов = Новый Структура;
	
	УчитыватьПрочиеДоходыИРасходы		 = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов");
	ФормироватьФинансовыйРезультат		 = ПолучитьФункциональнуюОпцию("ФормироватьФинансовыйРезультат");
	БазоваяВерсия						 = ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	ИспользоватьНесколькоВалют			 = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют");
	ИспользоватьПодарочныеСертификаты	 = ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты");
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	ИспользоватьКредитыДепозитыЗаймы	 = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыКредитовИДепозитов");
	ИспользоватьНачислениеЗарплаты		 = ПолучитьФункциональнуюОпцию("ИспользоватьНачислениеЗарплатыУТ");
	
	ИспользоватьМеждународныйУчет = Ложь;
	//++ НЕ УТКА
	ИспользоватьМеждународныйУчет		 = ПолучитьФункциональнуюОпцию("ИспользоватьМеждународныйФинансовыйУчет");
	//-- НЕ УТКА
	
	ЗаполнитьСписокВыбораОрганизации();
	СкрытьУказанныеЭтапы(ОтключенныеЭтапы());
	
	Этапы = ОбщегоНазначенияКлиентСервер.СократитьМассив(ЭтапыКРасчету(), ОтключенныеЭтапы());
	ЭтапыКРасчету.ЗагрузитьЗначения(Этапы);
	ВывестиСостоянияЭтапов();
	СкрытьОтобразитьДлительныеОперации(Ложь);
	
	Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Пометка = Истина;
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ФормаПоказатьСкрытьПоясненияКЭтапам","Заголовок",НСтр("ru='Скрыть пояснения к этапам';uk='Приховати пояснення до етапів'"));
	Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПредставлениеПериодаРегистрации = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Объект.ПериодРегистрации);
	ПредставлениеПериодРегистрацииРегламентный = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(ПериодРегистрацииРегламентный);
	ПредыдущийЭтап = "";
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	Если Параметры.Свойство("Организация") И ЗначениеЗаполнено(Параметры.Организация) Тогда
		Настройки.Вставить("Объект.Организация", Параметры.Организация);
	КонецЕсли;
	Если Параметры.Свойство("ПериодРегистрации") И ЗначениеЗаполнено(Параметры.ПериодРегистрации) Тогда
		Настройки.Вставить("Объект.ПериодРегистрации", Параметры.ПериодРегистрации);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	Если Настройки.Получить("ПериодРегистрации") <> Неопределено Тогда
		Объект.ПериодРегистрации = Настройки["ПериодРегистрации"];
	КонецЕсли;
	Если Настройки.Получить("Организация") <> Неопределено Тогда
		Объект.Организация = Настройки["Организация"];
	КонецЕсли;
	Если Настройки.Получить("СкрыватьЭтапыСоСтатусомНеТребуется") <> Неопределено Тогда
		СкрыватьЭтапыСоСтатусомНеТребуется = Настройки["СкрыватьЭтапыСоСтатусомНеТребуется"];
		Элементы.ФормаСкрыватьЭтапыСоСтатусомНеТребуется.Пометка = СкрыватьЭтапыСоСтатусомНеТребуется;
		СкрыватьЭтапыСоСтатусомНеТребуетсяСервер();
	КонецЕсли;
	Если Настройки.Получить("ВыводитьПоясненияКЭтапам") <> Неопределено Тогда
		Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Пометка = Не Настройки["ВыводитьПоясненияКЭтапам"];
	КонецЕсли;
	Если Настройки.Получить("РезультатыРасчетов") <> Неопределено Тогда
		РезультатыРасчетов = Настройки.Получить("РезультатыРасчетов");
	Иначе
		РезультатыРасчетов = Новый Структура;
	КонецЕсли;
	СписокДоступныхОрганизаций = ДоступныеОрганизации(Объект.Организация);
	ПоказатьСвязанныеОрганизации();
	ВывестиСостоянияЭтапов();
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Оповещение = Новый ОписаниеОповещения("ПредставлениеПериодаРегистрацииНачалоВыбораЗавершение", ЭтотОбъект);
	ОбщегоНазначенияУТКлиент.НачалоВыбораПредставленияПериодаРегистрации(Элемент,
		СтандартнаяОбработка,
		Объект.ПериодРегистрации,
		ЭтаФорма,
		Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииНачалоВыбораЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт 
	Если ВыбранныйПериод <> Неопределено Тогда
		Объект.ПериодРегистрации = ВыбранныйПериод;
		ПредставлениеПериодаРегистрации = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(Объект.ПериодРегистрации);
		ПоказатьСвязанныеОрганизации();
		ОтобразитьЭтапы(ЭтапыКРасчету);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаРегистрацииРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ОбщегоНазначенияУТКлиент.РегулированиеПредставленияПериодаРегистрации(Направление,
		СтандартнаяОбработка,
		Объект.ПериодРегистрации,
		ПредставлениеПериодаРегистрации);
	ПоказатьСвязанныеОрганизации();
	ОтобразитьЭтапы(ЭтапыКРасчету);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОрганизацияПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура АвтоматическиВыполнятьОперацииПоЗакрытиюМесяцаПриИзменении(Элемент)
	Если АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца И НЕ ЗначениеЗаполнено(ПериодРегистрацииРегламентный) Тогда
		ПериодРегистрацииРегламентный = Объект.ПериодРегистрации;
		ПредставлениеПериодРегистрацииРегламентный = 
				ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(ПериодРегистрацииРегламентный);
	КонецЕсли;
	СохранитьРеквизитыРегламентногоЗадания();
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодРегистрацииРегламентныйРегулирование(Элемент, Направление, СтандартнаяОбработка)
	ОбщегоНазначенияУТКлиент.РегулированиеПредставленияПериодаРегистрации(Направление,
		СтандартнаяОбработка,
		ПериодРегистрацииРегламентный,
		ПредставлениеПериодРегистрацииРегламентный);
	СохранитьРеквизитыРегламентногоЗадания();
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодРегистрацииРегламентныйНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПредставлениеПериодРегистрацииРегламентныйНачалоВыбораИзСпискаЗавершение", ЭтотОбъект);
	ОбщегоНазначенияУТКлиент.НачалоВыбораПредставленияПериодаРегистрации(Элемент,
		СтандартнаяОбработка,
		ПериодРегистрацииРегламентный,
		ЭтаФорма,
		Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодРегистрацииРегламентныйНачалоВыбораИзСпискаЗавершение(ВыбранныйПериод, ДополнительныеПараметры) Экспорт 
	
	Если ВыбранныйПериод <> Неопределено Тогда
		ПериодРегистрацииРегламентный = ВыбранныйПериод;
		
		ПредставлениеПериодРегистрацииРегламентный = ОбщегоНазначенияУТКлиент.ПолучитьПредставлениеПериодаРегистрации(ПериодРегистрацииРегламентный);
		СохранитьРеквизитыРегламентногоЗадания();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	ОрганизацияПриИзмененииСервер();
КонецПроцедуры

#Область ОбработчикиСобытийЭтапов

#Область ДействиеВыполнитьЭтап

&НаКлиенте
Процедура ИсправлениеОтрицательныхОстатковНажатие(Элемент)
	ВыполнитьЭтапыРасчета("ИсправлениеОтрицательныхОстатков")
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеМатериаловРаботНаПродукциюНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("РаспределениеМатериаловРаботНаПродукцию");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПоказателейРаспределенияРасходовНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("НастройкаПоказателейРаспределенияРасходов");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеРБПНажатие(Элемент)
	ВыполнитьЭтапыРасчета("РаспределениеРБП");
КонецПроцедуры

&НаКлиенте
Процедура СписаниеЗатратНаВыпускНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("СписаниеЗатратНаВыпуск");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ПередачаТоваровНажатие(Элемент)
	ВыполнитьЭтапыРасчета("ПередачаТоваров")
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыКомитентамОСписанииНажатие(Элемент)
	ВыполнитьЭтапыРасчета("ОтчетыКомитентамОСписании")
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПоКомиссииИнтеркампаниНажатие(Элемент)
	ВыполнитьЭтапыРасчета("ОтчетыПоКомиссииИнтеркампани")
КонецПроцедуры

&НаКлиенте
Процедура ДействиеФормированиеДвиженийПоРасчетуСПартнерамиНажатие(Элемент)
	ВыполнитьЭтапыРасчета("РасчетыСПартнерами", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ДействиеФормированиеДвиженийПоПартиямТоваровНажатие(Элемент)
	ВыполнитьЭтапыРасчета("ПартииТоваров", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ДействиеАмортизацияОСиНМАреглНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("АмортизацияОСиНМА");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПогашениеСтоимостиТМЦНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("ПогашениеСтоимостиТМЦ");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеАмортизацияОСиНМАМеждународныйНажатие(Элемент)
	//++ НЕ УТКА
	ВыполнитьЭтапыРасчета("АмортизацияОСиНМАМеждународный");
	//-- НЕ УТКА
	Возврат; // В УТ и КА данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеНачислениеООНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("НачислениеОО");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеОтражениеЗарплатыНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("ОтражениеЗарплаты");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеНачисленияПоКредитамДепозитамЗаймамНажатие(Элемент)
	ВыполнитьЭтапыРасчета("НачисленияПоКредитамДепозитам");
КонецПроцедуры

&НаКлиенте
Процедура ДействиеНастройкаРаспределенияРасходовНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("РаспределениеРасходов", Истина);
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетДолейСписанияКосвенныхРасходовНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("СписаниеКосвенныхРасходов", Истина);
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРаспределениеРасходовНаПродукциюАвтоматическиНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("РасходыНаПродукцию", Истина);
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеКорректировкаНалоговогоНазначенияКапитальныхИнвестицийНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("КорректировкаНалоговогоНазначенияКапитальныхИнвестиций", Истина);
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетСебестоимостиНажатие(Элемент)
	ВыполнитьЭтапыРасчета("Себестоимость", Истина);
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПереоценкаДенежныхСредствНажатие(Элемент)
	ВыполнитьЭтапыРасчета("ПереоценкаДенежныхСредств", Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ДействиеАннулированиеПодарочныхСертификатовНажатие(Элемент)
	ВыполнитьЭтапыРасчета("АннулированиеПодарочныхСертификатов");
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРаспределениеДоходовИРасходовПоНаправлениямНажатие(Элемент)
	ВыполнитьЭтапыРасчета("РаспределениеДоходовРасходов");
КонецПроцедуры

&НаКлиенте
Процедура ДействиеОтражениеДокументовВРеглУчетеНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("РеглУчет");
	//-- НЕ УТ
	Возврат; // В УТ данный обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеСписаниеКосвенныхРасходовНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("СписаниеКосвенныхРасходов");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеФормированиеРезервовПоСомнительнымДолгамНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("РезервыПоДолгам");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеФормированиеФинансовогоРезультатаНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("ФинансовыйРезультат");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетНалогаНаИмуществоНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("НалогНаИмущество");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетТранспортногоНалогаНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("ТранспортныйНалог");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетЗемельногоНалогаНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("ЗемельныйНалог");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеМеждународныйУчетНажатие(Элемент)
	//++ НЕ УТКА
	ВыполнитьЭтапыРасчета("МеждународныйУчет");
	//-- НЕ УТКА
	Возврат; // В УТ и КА обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ДействиеРасчетКурсовыхРазницРеглНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("РасчетКурсовыхРазницРегл");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ДействиеТорговыйСборНажатие(Элемент)
	//++ НЕ УТ
	ВыполнитьЭтапыРасчета("ТорговыйСбор");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ТорговыйСборНажатие(Элемент)
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ПомощникПоУчетуНДСНажатие(Элемент)
	
	Возврат; // Чтобы не был пустым обработчик

КонецПроцедуры

&НаКлиенте
Процедура УсловныеПродажиНажатие(Элемент)
	ВыполнитьЭтапыРасчета("УсловныеПродажи");
КонецПроцедуры

&НаКлиенте
Процедура ПерерасчетПропорциональногоНДСНажатие(Элемент)
    //++ НЕ УТ
	ВыполнитьЭтапыРасчета("ПерерасчетПропорциональногоНДС");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаУсловныхПродажПоПропорциональномуНДСНажатие(Элемент)
    //++ НЕ УТ
	ВыполнитьЭтапыРасчета("КорректировкаУсловныхПродажПоПропорциональномуНДС");
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

#КонецОбласти

#Область РасшифровкаЭтапа

&НаКлиенте
Процедура ПогашениеСтоимостиТМЦНажатие(Элемент)
	Возврат; // Для УТ11 обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура АмортизацияОСиНМАреглНажатие(Элемент)
	//++ НЕ УТ
	Если ОшибкиРасчета.Свойство("АмортизацияОСиНМА") Тогда
		ОтборПоУровню = Новый СписокЗначений;
		ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала", ОшибкиРасчета["АмортизацияОСиНМА"].НачалоРасчета);
		ПараметрыФормы.Вставить("ДатаОкончания", ОшибкиРасчета["АмортизацияОСиНМА"].КонецРасчета);
		ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
		ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
		ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить(НСтр("ru='Закрытие месяца. Начисление амортизации ОС и НМА.';uk='Закриття місяця. Нарахування амортизації ОЗ і НМА.'"));
		
		ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "ЗакрытиеМесяца");
	Иначе
		ТипыДокументов = Новый Массив;
		ТипыДокументов.Добавить(Тип("ДокументСсылка.АмортизацияОС"));
		ТипыДокументов.Добавить(Тип("ДокументСсылка.АмортизацияНМА"));
		Отбор = Новый Структура("Тип", ТипыДокументов);
		ПараметрыФормы = Новый Структура("Отбор", Отбор);
		ОткрытьФорму("ЖурналДокументов.РегламентныеДокументы.Форма.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
		//-- НЕ УТ
	Возврат; // В УТ11 обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеДвиженийПоПартиямТоваровНажатие(Элемент)
	ОтборПоУровню = Новый СписокЗначений;
	ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	
	ПартииТоваровВремяРасчета = Неопределено;
	РезультатыРасчетов.Свойство("ПартииТоваров", ПартииТоваровВремяРасчета);
	Если НЕ ЗначениеЗаполнено(ПартииТоваровВремяРасчета) Тогда
		ПартииТоваровВремяРасчета = Новый Структура("Начало, Конец", Дата("00010101000000"), Дата("00010101000000"));
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала", ПартииТоваровВремяРасчета.Начало);
	ПараметрыФормы.Вставить("ДатаОкончания", ПартииТоваровВремяРасчета.Конец);
	ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
	ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить(НСтр("ru='Партионный учет.Операции закрытия месяца.';uk='Партіонний облік.Операції закриття місяця.'"));
	
	ДополнитьОтборыРасчетаСебестоимостиДляЖурналаРегистрации(ПараметрыФормы);
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "Закрытие месяца");
	
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеДвиженийПоРасчетуСПартнерамиНажатие(Элемент)
	ОтборПоУровню = Новый СписокЗначений;
	ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала", ОшибкиРасчета["РасчетыСПартнерами"].НачалоРасчета);
	ПараметрыФормы.Вставить("ДатаОкончания", ОшибкиРасчета["РасчетыСПартнерами"].КонецРасчета);
	ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
	ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить("Расчеты с партнерами.Операции закрытия месяца.");
	ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить("ОтложенноеПроведениеПоРасчетам");
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "ЗакрытиеМесяца");
КонецПроцедуры

&НаКлиенте
Процедура ПереоценкаДенежныхСредствНажатие(Элемент)
	
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Отбор.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Отбор", Отбор);
	ОткрытьФорму("Документ.ПереоценкаВалютныхСредств.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура АннулированиеПодарочныхСертификатовНажатие(Элемент)
	ОтборПоУровню = Новый СписокЗначений;
	ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачала", ОшибкиРасчета["АннулированиеПодарочныхСертификатов"].НачалоРасчета);
	ПараметрыФормы.Вставить("ДатаОкончания", ОшибкиРасчета["АннулированиеПодарочныхСертификатов"].КонецРасчета);
	ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
	ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить("Аннулирование подарочных сертификатов.Операции закрытия месяца.");
	
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "ЗакрытиеМесяца");
КонецПроцедуры

&НаКлиенте
Процедура АмортизацияОСиНМАМеждународныйНажатие(Элемент)
	//++ НЕ УТКА
	Если ОшибкиРасчета.Свойство("АмортизацияОСиНМАМеждународный") Тогда
		ОтборПоУровню = Новый СписокЗначений;
		ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала", ОшибкиРасчета["АмортизацияОСиНМАМеждународный"].НачалоРасчета);
		ПараметрыФормы.Вставить("ДатаОкончания", ОшибкиРасчета["АмортизацияОСиНМАМеждународный"].КонецРасчета);
		ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
		ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
		ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить("Закрытие месяца. Начисление амортизации ОС и НМА (международный учет).");
		
		ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "ЗакрытиеМесяца");
	Иначе
		ТипыДокументов = Новый Массив;
		ТипыДокументов.Добавить(Тип("ДокументСсылка.АмортизацияОСМеждународныйУчет"));
		ТипыДокументов.Добавить(Тип("ДокументСсылка.АмортизацияНМАМеждународныйУчет"));
		Отбор = Новый Структура("Тип", ТипыДокументов);
		ПараметрыФормы = Новый Структура("Отбор", Отбор);
		ОткрытьФорму("ЖурналДокументов.РегламентныеДокументыМеждународныйУчет.Форма.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
	//-- НЕ УТКА
	Возврат; // В УТ11 и КА обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура РасчетДолейСписанияКосвенныхРасходовНажатие(Элемент)
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеРасходовНаПродукциюАвтоматическиНажатие(Элемент)
	//++ НЕ УТ
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПериодРегистрации", Объект.ПериодРегистрации);
	ПараметрыФормы.Вставить("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
	
	Форма = ПолучитьФорму("Документ.РаспределениеПрочихЗатрат.Форма.ФормаРабочееМесто",
					ПараметрыФормы,,ПредопределенноеЗначение("Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения"));
	Форма.Открыть();
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаНалоговогоНазначенияКапитальныхИнвестицийНажатие(Элемент)
	//++ НЕ УТ
	ДокументКорректировки = ПолучитьДокументКорректировкиНалоговогоНазначенияКапитальныхИнвестиций(Объект.Организация, Объект.ПериодРегистрации);
	Если ДокументКорректировки <> Неопределено Тогда
		ПоказатьЗначение(Неопределено, ДокументКорректировки); 
	Иначе
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, Объект.ПериодРегистрации);
		ОткрытьФорму("Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Форма.ФормаСписка", ПараметрыФормы);
	КонецЕсли;
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РасчетСебестоимостиНажатие(Элемент)
	ПартииТоваровВремяРасчета = Неопределено;
	СебестоимостьВремяРасчета = Неопределено;
	РезультатыРасчетов.Свойство("Себестоимость", СебестоимостьВремяРасчета);
	РезультатыРасчетов.Свойство("ПартииТоваров", ПартииТоваровВремяРасчета);
	Если НЕ ЗначениеЗаполнено(СебестоимостьВремяРасчета) Тогда
		СебестоимостьВремяРасчета = Новый Структура("Начало, Конец", Дата("00010101000000"), Дата("00010101000000"));
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ПартииТоваровВремяРасчета) Тогда
		ПартииТоваровВремяРасчета = Новый Структура("Начало, Конец", Дата("00010101000000"), Дата("00010101000000"));
	КонецЕсли;
 	Если СебестоимостьВремяРасчета.Конец >= ПартииТоваровВремяРасчета.Конец Тогда
		Начало = СебестоимостьВремяРасчета.Начало;
		Конец = СебестоимостьВремяРасчета.Конец;
	Иначе
		Начало = ПартииТоваровВремяРасчета.Начало;
		Конец = ПартииТоваровВремяРасчета.Конец;
	КонецЕсли;
	ОтборПоУровню = Новый СписокЗначений;
	ОтборПоУровню.Добавить("Ошибка", "Ошибка");
	ПараметрыФормы = Новый Структура("ДатаНачала, ДатаОкончания, Уровень", Начало, Конец + 600, ОтборПоУровню);
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
	ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить(НСтр("ru='Партионный учет.Операции закрытия месяца.';uk='Партіонний облік.Операції закриття місяця.'"));
	
	ДополнитьОтборыРасчетаСебестоимостиДляЖурналаРегистрации(ПараметрыФормы);
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "Расчет себестоимости");
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеДоходовИРасходовПоНаправлениямНажатие(Элемент)
	ПараметрыФормы = Новый Структура("ПериодРегистрации, Организация, НеПомеченныеНаУдаление",
		Объект.ПериодРегистрации, Объект.Организация, Истина);
	Форма = ПолучитьФорму("Документ.РаспределениеДоходовИРасходовПоНаправлениямДеятельности.Форма.ФормаСписка",
		ПараметрыФормы,
		,
		ПредопределенноеЗначение("Перечисление.СостоянияОперацийЗакрытияМесяца.НеВыполнено"));
	Форма.Открыть();
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеДокументовВРеглУчетеНажатие(Элемент)
	//++ НЕ УТ
	ПараметрыФормы = Новый Структура("ДатаОкончанияПериода, Организация", 
		КонецМесяца(Объект.ПериодРегистрации), Объект.Организация);
	Форма = ПолучитьФорму("Обработка.НастройкаОтраженияДокументовВРеглУчете.Форма.ФормаНастройкиОтраженияДокументовВРеглУчете", ПараметрыФормы);
	Форма.Открыть(); 
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура СписаниеКосвенныхРасходовНажатие(Элемент)
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеРезервовПоСомнительнымДолгамНажатие(Элемент)
	//++ НЕ УТ
	ОткрытьСписокРегламентныхОперацийЗаМесяц(ПредопределенноеЗначение("Перечисление.ТипыРегламентныхОпераций.ФормированиеРезервовПоСомнительнымДолгам"));
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РасчетКурсовыхРазницРеглНажатие(Элемент)
	//++ НЕ УТ
	ОткрытьСписокРегламентныхОперацийЗаМесяц(ПредопределенноеЗначение("Перечисление.ТипыРегламентныхОпераций.ПереоценкаВалютныхСредств"));
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеФинансовогоРезультатаНажатие(Элемент)
	//++ НЕ УТ
	Если ОшибкиРасчета.Свойство("ФинансовыйРезультат") Тогда
		ОтборПоУровню = Новый СписокЗначений;
		ОтборПоУровню.Добавить("Ошибка", "Ошибка");
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ДатаНачала", ОшибкиРасчета["ФинансовыйРезультат"].НачалоРасчета);
		ПараметрыФормы.Вставить("ДатаОкончания", ОшибкиРасчета["ФинансовыйРезультат"].КонецРасчета);
		ПараметрыФормы.Вставить("Уровень", ОтборПоУровню);
		ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", Новый Массив);
		ПараметрыФормы.СобытиеЖурналаРегистрации.Добавить(НСтр("ru='Формирование финансового результата';uk='Формування фінансового результату'"));
		
		ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормы, , "ЗакрытиеМесяца");
	Иначе
		МассивОпераций = Новый Массив();
		МассивОпераций.Добавить(ПредопределенноеЗначение("Перечисление.ТипыРегламентныхОпераций.ФормированиеФинансовогоРезультата"));
		ОткрытьСписокРегламентныхОперацийЗаМесяц(МассивОпераций);
	КонецЕсли;
	//-- НЕ УТ
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РасчетИмущественныхНалоговНажатие(Элемент)
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РасчетТранспортногоНалогаНажатие(Элемент)
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура РасчетЗемельногоНалогаНажатие(Элемент)
	Возврат; // Чтобы в УТ не был пустым обработчик
КонецПроцедуры

&НаКлиенте
Процедура НачислениеООНажатие(Элемент)
	//++ НЕ УТ
	ОткрытьФорму("Документ.НачислениеОценочныхОбязательствПоОтпускам.ФормаСписка",,,ЭтаФорма);
	//-- НЕ УТ
	Возврат; // Для УТ11 обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеЗарплатыНажатие(Элемент)
	//++ НЕ УТ
	ОткрытьФорму("Документ.ОтражениеЗарплатыВФинансовомУчете.ФормаСписка",,,ЭтаФорма);
	//-- НЕ УТ
	Возврат; // Для УТ11 обработчик пустой
КонецПроцедуры

&НаКлиенте
Процедура МеждународныйУчетНажатие(Элемент)
	
	//++ НЕ УТКА
	
	ПараметрыРасчета = Новый Структура("РучнойЗапуск", Истина);
	МеждународныйУчет(Ложь, ПараметрыРасчета);
	
	//-- НЕ УТКА
	
	Возврат; // Для УТ и КА обработчик пустой
	
КонецПроцедуры

&НаКлиенте
Процедура ФормированиеКУДиРНажатие(Элемент)
	Возврат; // В УТ обработчик пустой
	
КонецПроцедуры

#КонецОбласти // РасшифровкаЭтапа

#КонецОбласти // ОбработчикиСобытийЭтапов

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РассчитатьВсе(Команда)
	ГраницаРасчета = 0;
	ВыполнитьЭтапыРасчета(Неопределено, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеРегламентногоЗадания(Команда)
	Если РасписаниеРегламентногоЗадания = Неопределено Тогда
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
	КонецЕсли;
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеРегламентногоЗадания);
	Диалог.Показать(Новый ОписаниеОповещения("РедактированиеРасписанияРегламентногоЗаданияЗавершение", ЭтотОбъект, Новый Структура("Диалог", Диалог)));
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояния(Команда)
	ВывестиСостоянияЭтапов();
КонецПроцедуры

&НаКлиенте
Процедура ВывестиПояснения(Команда)
	ВыводитьПоясненияКЭтапам = Не ВыводитьПоясненияКЭтапам;
	Если ВыводитьПоясненияКЭтапам Тогда
		Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Пометка = Ложь;
	Иначе
		Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Пометка = Истина;
	КонецЕсли;
	УстановитьВидимостьПояснений();
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗадание(Команда)
	ОтключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте");
	ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
	ОтменитьФоновоеЗадание();
КонецПроцедуры

&НаКлиенте
Процедура СкрыватьЭтапыСоСтатусомНеТребуется(Команда)
	СкрыватьЭтапыСоСтатусомНеТребуется = Не СкрыватьЭтапыСоСтатусомНеТребуется;
	Элементы.ФормаСкрыватьЭтапыСоСтатусомНеТребуется.Пометка = СкрыватьЭтапыСоСтатусомНеТребуется;
	СкрыватьЭтапыСоСтатусомНеТребуетсяСервер();
КонецПроцедуры

&НаКлиенте
Процедура ОтчетСверкаДанныхПартионногоУчетаИСебестоимости(Команда)
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",
		Объект.Организация);
	ПараметрыОтчета.Вставить("Период",
		Новый СтандартныйПериод(НачалоМесяца(Объект.ПериодРегистрации), КонецМесяца(Объект.ПериодРегистрации)));
	
	ОткрытьФорму("Отчет.СверкаДанныхПартионногоУчетаИСебестоимости.ФормаОбъекта",
		Новый Структура("Отбор, СформироватьПриОткрытии", ПараметрыОтчета, Истина));
	
КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьКартинкуСтатусаЭтапа(Этап, НазваниеЭтапа)
	Если Не ЗначениеЗаполнено(Этап) Тогда
		Возврат;
	КонецЕсли;
	ИндексЗначения = Перечисления.СостоянияОперацийЗакрытияМесяца.Индекс(Этап);
	ИмяПеречисления = Метаданные.Перечисления.СостоянияОперацийЗакрытияМесяца.ЗначенияПеречисления[ИндексЗначения].Имя;
	Элементы["Картинки" + НазваниеЭтапа].ТекущаяСтраница = Элементы[НазваниеЭтапа + ИмяПеречисления];

	Если Этап = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Элементы["Действия" + НазваниеЭтапа].ТекущаяСтраница = Элементы["Действие" + НазваниеЭтапа+"НеТребуется"];
		Элементы["ГруппаПояснение" + НазваниеЭтапа].Видимость = Ложь;
	Иначе
		Элементы["Действия" + НазваниеЭтапа].ТекущаяСтраница = Элементы["Действие" + НазваниеЭтапа+"Выполнять"];
		Если ВыводитьПоясненияКЭтапам И Этап = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
			Элементы["ГруппаПояснение" + НазваниеЭтапа].Видимость = Истина;
		Иначе
			Элементы["ГруппаПояснение" + НазваниеЭтапа].Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиПодвала()
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Доступность 	= АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца;
	Элементы.ПредставлениеПериодРегистрацииРегламентный.Доступность = АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца;
	Элементы.СостояниеАвтоматическогоВыполненияОпераций.Доступность = АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца;
	ОбновитьПредставлениеРасписания();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПояснений()
	Для Каждого Этап Из СостоянияЭтапов Цикл
		СостояниеЭтапа = Этап.Значение;
		Если ВыводитьПоясненияКЭтапам 
			И (СостояниеЭтапа = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено
			   ИЛИ СостояниеЭтапа = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоСОшибками) Тогда
			Элементы["ГруппаПояснение"+Этап.Ключ].Видимость = Истина;
		Иначе
			Элементы["ГруппаПояснение"+Этап.Ключ].Видимость = Ложь;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СкрытьОтобразитьДлительныеОперации(Отобразить)
	Элементы.ГруппаДлительныеОперации.Видимость = Отобразить;
	Если Отобразить Тогда
		Элементы.ЗаголовокРасчетаВФоне.Заголовок = НСтр("ru='Выполняется операция';uk='Виконується операція'") + ": " + ЭтапРассчитываемыйВФоне;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьСнятьДоступностьЭлементов(Доступность)
	Элементы.ГруппаПараметры.Доступность = Доступность;
	Элементы.ФормаВыполнитьЗакрытие.Доступность = Доступность; 
	Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Доступность = Доступность;
	Элементы.ФормаОбновитьСостоянияОпераций.Доступность = Доступность;
	Элементы.ФормаСкрыватьЭтапыСоСтатусомНеТребуется.Доступность = Доступность;
	Элементы.ГруппаВыполнениеОпераций.Доступность = Доступность;
	Элементы.ГруппаЭтапы.Доступность = Доступность;
КонецПроцедуры

&НаСервере
Процедура ИзмененияВИнтерфейсеПриРаботеВФоне(ЗаданиеЗапущено)
	СкрытьОтобразитьДлительныеОперации(ЗаданиеЗапущено);	
	УстановитьСнятьДоступностьЭлементов(НЕ ЗаданиеЗапущено);
КонецПроцедуры

&НаСервере
Функция ДоступныеОрганизации(Организация = Неопределено, ДляВыбора = Ложь)
	
	// Для регламентных операций необходим полный список организаций,
	// а для списка выбора поля "Организация" - только доступные данному пользователю.
	Если НЕ ДляВыбора Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	СписокОрганизаций = Новый СписокЗначений;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ //РАЗРЕШЕННЫЕ
		|	ДанныеСправочника.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК ДанныеСправочника
		|ГДЕ
		|	(ДанныеСправочника.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация)
		|			ИЛИ &УчитыватьУпрОрганизацию)");
		Запрос.УстановитьПараметр("УчитыватьУпрОрганизацию",
			ПолучитьФункциональнуюОпцию("ИспользоватьУправленческуюОрганизацию"));
		
		Если ДляВыбора Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "//РАЗРЕШЕННЫЕ", "РАЗРЕШЕННЫЕ");
		КонецЕсли;
		
		МассивОрганизаций = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");
		СписокОрганизаций.ЗагрузитьЗначения(МассивОрганизаций);
	Иначе
		СписокОрганизаций.Добавить(Организация);
	КонецЕсли;
	
	Возврат СписокОрганизаций;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораОрганизации()
	СписокВыбора = Элементы.Организация.СписокВыбора;
	СписокВыбора.ЗагрузитьЗначения(ДоступныеОрганизации(, Истина).ВыгрузитьЗначения());
	СписокВыбора.Вставить(0, Справочники.Организации.ПустаяСсылка(), НСтр("ru='<По всем организациям>';uk='<За всіма організаціями>'"));
КонецПроцедуры

&НаСервере
Процедура СкрыватьЭтапыСоСтатусомНеТребуетсяСервер()
	ПараметрыОформления = Новый Структура("Статус");
	Для Каждого Состояние ИЗ СостоянияЭтапов Цикл
		Если Состояние.Значение = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
			ПараметрыОформления.Статус = Состояние.Значение;
			ОформлениеЭтапа(Состояние.Ключ, ПараметрыОформления);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВывестиСостоянияЭтапов()
	ОтобразитьЭтапы(ЭтапыКРасчету);
	ПолучитьСостояниеРегламентногоЗадания();
	УправлениеЭлементамиПодвала();
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЭтапВручную(Знач ИмяФормы, Знач ПараметрыФормы, Знач ПараметрыРасчета)
	Обработчик = Новый ОписаниеОповещения("РасчетЭтапаЗавершение", ЭтотОбъект, ПараметрыРасчета);
	Режим = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму(ИмяФормы, ПараметрыФормы,,,,, Обработчик, Режим);
КонецПроцедуры

&НаКлиенте
Процедура РасчетЭтапаЗавершение(Знач РезультатЗакрытия, Знач Параметры) Экспорт
	ОтобразитьЭтапы(ЭтапыКРасчету);
КонецПроцедуры

//++ НЕ УТ
&НаКлиенте
Процедура ОткрытьСписокРегламентныхОперацийЗаМесяц(ТипОперации = Неопределено)
	СтруктураПериод = Новый Структура("ДатаНачала, ДатаОкончания",
		НачалоМесяца(Объект.ПериодРегистрации), КонецМесяца(Объект.ПериодРегистрации));
	ПараметрыФормы = Новый Структура("Период, ДатаОкончанияПериода",
		СтруктураПериод, КонецМесяца(Объект.ПериодРегистрации));
	Отбор = Новый Структура;
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		Отбор.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	Если ЗначениеЗаполнено(ТипОперации) Тогда
		Отбор.Вставить("ТипОперации", ТипОперации);
	КонецЕсли;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ОткрытьФорму("Документ.РегламентнаяОперация.Форма.ФормаСписка", ПараметрыФормы);
КонецПроцедуры
//-- НЕ УТ

&НаСервере
Процедура ОрганизацияПриИзмененииСервер()
	СписокДоступныхОрганизаций = ДоступныеОрганизации(Объект.Организация);
	ПоказатьСвязанныеОрганизации();
	ОтобразитьЭтапы(ЭтапыКРасчету);
	ПолучитьСостояниеРегламентногоЗадания();
	УправлениеЭлементамиПодвала();
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	УсловноеОформление.Элементы.Очистить();
	Для Каждого Этап Из ЭтапыКРасчету Цикл
		ДобавитьУсловноеОформление(Этап);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловноеОформление(НаименованиеЭтапа)
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы[НаименованиеЭтапа].Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(НаименованиеЭтапа);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеактуальногоСписка);
	
КонецПроцедуры

//++ НЕ УТ
&НаСервереБезКонтекста
Функция ПолучитьДокументКорректировкиНалоговогоНазначенияКапитальныхИнвестиций(Организация, ПериодРегистрации)
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций КАК КорректировкаНалоговогоНазначенияКапитальныхИнвестиций
	|ГДЕ
	|	(КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания)
	|	И НЕ КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ПометкаУдаления
	|	И (КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Организация = &Организация
	|		ИЛИ &ПоВсемОрганизациям)
	|");
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", НЕ ЗначениеЗаполнено(Организация));
	
	Результат = Запрос.Выполнить().Выбрать();
	
	ДокументКорректировки = Неопределено;
	Пока Результат.Следующий() Цикл
		ДокументКорректировки = Результат.Ссылка;
	КонецЦикла;
	
	Возврат ДокументКорректировки;
КонецФункции
//-- НЕ УТ

#Область ФоновоеЗаданиеРасчета

&НаСервере
Функция ФоновоеЗаданиеЗапустить(КРасчету, ПараметрыЗадания, Отказ)

	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ПараметрыЗадания.Вставить("ЭтапыКРасчету", КРасчету);
	ПараметрыЗадания.Вставить("СписокОрганизаций", СписокДоступныхОрганизаций.ВыгрузитьЗначения());
	ПараметрыЗадания.Вставить("Отказ", Ложь);
	ПараметрыЗадания.Вставить("АдресХранилища", АдресХранилища);
	
	ЗапускУспешен = ЗакрытиеМесяцаУТВызовСервера.РассчитатьВФоновомЗадании(ПараметрыЗадания);
	
	ИзмененияВИнтерфейсеПриРаботеВФоне(ЗапускУспешен);
	Возврат ЗапускУспешен;
КонецФункции

&НаКлиенте
Функция ФоновоеЗаданиеВыполнено()
	ЗаданиеВыполнено = Ложь;
	ПараметрыОформления = Новый Структура("Статус, ИмяЭтапа");
	Попытка
		Результат = ЗакрытиеМесяцаУТВызовСервера.СостояниеЗаданияЗакрытияМесяца();
		Если Результат.УспешноВыполнено Тогда
			ЗаданиеВыполнено = Истина;
			РезультатРасчета = ПолучитьИзВременногоХранилища(АдресХранилища);
			ОчиститьОшибкиРасчетаЭтапа(РезультатРасчета.РасчитанЭтап);
			Если Не РезультатРасчета.Отказ Тогда
				ОтключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте");
				ЗагрузитьРезультат(РезультатРасчета.РасчитанЭтап, Результат);
				НомерЭтапа = ОпределитьНомерЭтапа(РезультатРасчета.РасчитанЭтап);
				ПараметрыОформления.ИмяЭтапа = РезультатРасчета.РасчитанЭтап;
				ПараметрыОформления.Статус = СостояниеЭтапа(СостоянияЭтапов, РезультатРасчета.РасчитанЭтап);
				СостоянияЭтапов[РезультатРасчета.РасчитанЭтап] = ПараметрыОформления.Статус;
				ОформлениеЭтапа(РезультатРасчета.РасчитанЭтап, ПараметрыОформления);
				Если ПараметрыОформления.Статус = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЗакрытияМесяца.НеВыполнено")
					И ОстанавливатьсяПоОшибке 
				Тогда
					// этап не рассчитан, завершаем работу
					ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
					ОтобразитьЭтапы(ЭтапыКРасчету);
					ГраницаРасчета = 0;
					ТекстСообщения = НСтр("ru='Ошибка при расчете этапа';uk='Помилка при розрахунку етапу'")+ ": " + ЭтапРассчитываемыйВФоне;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
					Возврат ЗаданиеВыполнено;
				КонецЕсли;
				Если НомерЭтапа < ГраницаРасчета Тогда 
					// продолжаем расчет этапов
					ВыполнитьЭтапыРасчета(ГраницаРасчета, Ложь, НомерЭтапа+1);
				Иначе
					ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
					ОтобразитьЭтапы(ЭтапыКРасчету);
				КонецЕсли;
			ИначеЕсли НЕ ОстанавливатьсяПоОшибке Тогда
				СведенияРасчета = Новый Структура("НачалоРасчета, КонецРасчета");
				ЗаполнитьЗначенияСвойств(СведенияРасчета, РезультатРасчета);
				ОшибкиРасчета.Вставить(РезультатРасчета.РасчитанЭтап, СведенияРасчета);
				ПараметрыОформления.ИмяЭтапа = РезультатРасчета.РасчитанЭтап;
				ПараметрыОформления.Статус = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЗакрытияМесяца.НеВыполнено");
				ОформлениеЭтапа(РезультатРасчета.РасчитанЭтап, ПараметрыОформления);
				НомерЭтапа = ОпределитьНомерЭтапа(РезультатРасчета.РасчитанЭтап);
				ЗагрузитьРезультат(РезультатРасчета.РасчитанЭтап, Результат);
				Если НомерЭтапа + 2 <= ЭтапыКРасчету.Количество() Тогда
					СледующийЭтап = ЭтапыКРасчету.Получить(НомерЭтапа+1);
					ВыполнитьЭтапыРасчета(СледующийЭтап.Значение, Ложь, НомерЭтапа + 1);
				Иначе
					ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
					ОбновитьФорму();
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли Результат.ЕщеВыполняется Тогда
			ПараметрыОбработчика = Новый Структура("Интервал", 1);
			ПодключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте", ПараметрыОбработчика.Интервал, Истина);
		ИначеЕсли Результат.ВыполненоСОшибками Тогда
			ОтключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте");
			ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
			ТекстСобытия = НСтр("ru='Закрытие месяца';uk='Закриття місяця'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
			ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(ТекстСобытия, "Ошибка", Результат.ТекстОшибки);
			ПараметрыРасчета = Новый Структура("НачалоРасчета, КонецРасчета, СведенияОбОшибке",);
			ОбновитьФорму();
		КонецЕсли;
	Исключение
		ТекстСобытия = НСтр("ru='Закрытие месяца';uk='Закриття місяця'",ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(ТекстСобытия, "Ошибка", ТекстОшибки);
		ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
		ОбновитьФорму();
	КонецПопытки;
	Возврат ЗаданиеВыполнено;
КонецФункции

&НаКлиенте
Процедура ОчиститьОшибкиРасчетаЭтапа(Этап)
	Если ОшибкиРасчета.Свойство(Этап) Тогда
		ОшибкиРасчета.Удалить(Этап);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФоновоеЗаданиеПроверитьНаКлиенте()
	Если НЕ ФоновоеЗаданиеВыполнено() Тогда
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("Интервал", 1);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте", ПараметрыОбработчика.Интервал, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьРезультат(РассчиталиЭтап, Результат)
	ВремяРасчета = Новый Структура("Начало, Конец");
	ЗаполнитьЗначенияСвойств(ВремяРасчета, Результат);
	РезультатыРасчетов.Вставить(РассчиталиЭтап, ВремяРасчета);
КонецПроцедуры

&НаСервере
Процедура ОтменитьФоновоеЗадание()
	ЗакрытиеМесяцаУТВызовСервера.ОтменитьВыполнениеЗадания("ЗакрытиеМесяца");
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму()
	ВывестиСостоянияЭтапов();
КонецПроцедуры
#КонецОбласти // ФоновоеЗаданиеРасчета

// Метод выполняет анализ массива этапов для определения их состояния.
// Параметры:
//		МассивЭтапов - Массив - Массив, содержащий в себе имена этапов.
//						Заполняется в методе МассивЭтаповКВыполнению()
// ВозвращаемоеЗначение - Соответствие - В ключе содержится имя этапа,
//						в значении его статус.
&НаСервере
Функция СостоянияЭтапов(СписокЭтапов) 
	УстановитьПривилегированныйРежим(Истина);
	СостоянияЭтапов = Новый Структура;
	Для Каждого Этап Из СписокЭтапов Цикл
		СостояниеЭтапа = СостояниеЭтапа(СостоянияЭтапов, Этап.Значение);
		СостоянияЭтапов.Вставить(Этап.Значение, СостояниеЭтапа);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	Возврат СостоянияЭтапов;
КонецФункции

Функция ОпределитьНомерЭтапа(Этап)
	Элемент = ЭтапыКРасчету.НайтиПоЗначению(Этап);
	Если Элемент = Неопределено Тогда
		НомерЭтапа = ЭтапыКРасчету.Количество() - 1;
	Иначе
		НомерЭтапа = ЭтапыКРасчету.Индекс(Элемент);
	КонецЕсли;
	Возврат НомерЭтапа;
КонецФункции

&НаКлиенте
Процедура ВыполнитьЭтапыРасчета(ИмяЭтапаРасчета = Неопределено, РучнойЗапуск = Истина, НачалоРасчета = 0)
	Отказ = Ложь;
	РасчетВФоне = Ложь;
	ПараметрыРасчета = Новый Структура("РучнойЗапуск, Статус, Этап", РучнойЗапуск);
	СтатусНеТребуется = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЗакрытияМесяца.НеТребуется");
	СтатусВыполненоУспешно = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно");
	
	Если ГраницаРасчета = 0 ИЛИ РучнойЗапуск Тогда
		ГраницаРасчета = ОпределитьНомерЭтапа(ИмяЭтапаРасчета);
	КонецЕсли;
	Пока НачалоРасчета <= ГраницаРасчета Цикл
		Если Отказ Тогда
			Прервать; // при расчете предыдущего этапа произошла критическая ошибка
		КонецЕсли;
		// Пропускаем все ручные операции кроме текущего
		Если НачалоРасчета <> ГраницаРасчета Тогда
			ПараметрыРасчета.РучнойЗапуск = Ложь;
		ИначеЕсли НачалоРасчета = ГраницаРасчета
			И ГраницаРасчета = ЭтапыКРасчету.Количество() - 1
		Тогда
			ПараметрыРасчета.РучнойЗапуск = РучнойЗапуск;
		Иначе
			ПараметрыРасчета.РучнойЗапуск = Истина;
		КонецЕсли;

		ЭтапКРасчету = ЭтапыКРасчету.Получить(НачалоРасчета).Значение;
		Если РезультатыРасчетов.Свойство(ЭтапКРасчету) И Не РучнойЗапуск Тогда
			РезультатыРасчетов.Удалить(ЭтапКРасчету);
		КонецЕсли;
		ПараметрыРасчета.Этап = ЭтапКРасчету;
		ПараметрыРасчета.Статус = СостояниеЭтапа(СостоянияЭтапов, ЭтапКРасчету);
		СостоянияЭтапов.Вставить(ЭтапКРасчету, ПараметрыРасчета.Статус);
		Если (ПараметрыРасчета.Статус = СтатусНеТребуется
				ИЛИ ПараметрыРасчета.Статус = СтатусВыполненоУспешно
				ИЛИ ПараметрыРасчета.Статус = Неопределено)
		Тогда
			СостояниеЭтапа = ПараметрыРасчета.Статус; // это финальный статус этапа
			ОформлениеЭтапа(ПараметрыРасчета.Этап, ПараметрыРасчета);
		Иначе
			РассчитатьЭтап(ЭтапКРасчету, Отказ, РасчетВФоне, ПараметрыРасчета);
			Если РасчетВФоне ИЛИ ПараметрыРасчета.РучнойЗапуск Тогда
				Прервать; // запущен обработчик ожидания, позже будет перезапуск расчета
			КонецЕсли;
			СостояниеЭтапа = СостояниеЭтапа(СостоянияЭтапов, ЭтапКРасчету);
		КонецЕсли; 
		СостоянияЭтапов.Вставить(ЭтапКРасчету, СостояниеЭтапа);
		НачалоРасчета = НачалоРасчета + 1;
	КонецЦикла;
	Если НачалоРасчета >= ГраницаРасчета Тогда
		ГраницаРасчета = 0;
		Если Не РасчетВФоне Тогда
			ИзмененияВИнтерфейсеПриРаботеВФоне(Ложь);
			ВывестиСостоянияЭтапов();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ)
	РасчетВФоне = Истина;
	ЗапускУспешен = ФоновоеЗаданиеЗапустить(КРасчету, ПараметрыРасчета, Отказ);
	Если ЗапускУспешен Тогда
		ПараметрыОбработчика = Новый Структура("Интервал", 1);
		ПодключитьОбработчикОжидания("ФоновоеЗаданиеПроверитьНаКлиенте", ПараметрыОбработчика.Интервал, Истина);
	КонецЕсли;
КонецПроцедуры

Функция ИнициализироватьПараметрыРасчета()
	ПараметрыРасчета = Новый Структура("Организация, Период", Объект.Организация, Объект.ПериодРегистрации);
	Возврат ПараметрыРасчета;
КонецФункции

#Область НастройкаРегламентногоЗадания

&НаСервере
Процедура СохранитьРеквизитыРегламентногоЗадания()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	РасписаниеЗадания.РегламентноеЗадание КАК ИдентификаторЗадания
	|ИЗ
	|	РегистрСведений.РегламентныеЗаданияЗакрытияМесяца КАК РасписаниеЗадания
	|ГДЕ
	|	РасписаниеЗадания.Организация = &Организация
	|");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Результат = Запрос.Выполнить();
	
	ИдентификаторЗадания = Неопределено;
	Если Не Результат.Пустой() Тогда
		Расписание = Результат.Выгрузить();
		ИдентификаторЗадания = Расписание[0].ИдентификаторЗадания;
	КонецЕсли;
	
	ОрганизацииКРасчету = Новый Массив();
	ОрганизацииКРасчету.Добавить(Объект.Организация);
	
	ПараметрыЗадания = Новый Структура("Использование, Параметры", АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца, ОрганизацииКРасчету);
	Если РасписаниеРегламентногоЗадания <> Неопределено Тогда
		ПараметрыЗадания.Вставить("Расписание", РасписаниеРегламентногоЗадания);
	КонецЕсли;
	
	ОбновитьЗаданиеЗакрытияМесяца(ИдентификаторЗадания, ПараметрыЗадания);
	
	Если НЕ ЗначениеЗаполнено(ПериодРегистрацииРегламентный) Тогда
		ПериодРегистрацииРегламентный = НачалоМесяца(ТекущаяДата());
 		ПредставлениеПериодРегистрацииРегламентный = Локализация.ФорматДляИнтерфейса(ПериодРегистрацииРегламентный, "ДФ='MMMM yyyy'");
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.РегламентныеЗаданияЗакрытияМесяца.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Организация.Установить(Объект.Организация);
	Запись = НаборЗаписей.Добавить();
	Запись.Организация = Объект.Организация;
	Запись.РегламентноеЗадание = ИдентификаторЗадания;
	Запись.ЗакрываемыйПериод = ПериодРегистрацииРегламентный;
	
	НаборЗаписей.Записать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	УправлениеЭлементамиПодвала();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗаданиеЗакрытияМесяца(ИдентификаторЗадания, ПараметрыЗадания)
	Задание = Неопределено;
	Если ИдентификаторЗадания <> Неопределено Тогда
		Отбор = Новый Структура("УникальныйИдентификатор", ИдентификаторЗадания);
		Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторЗадания);
	КонецЕсли;
	
	Если Задание = Неопределено Тогда // идентификатор не заполнен или задание не нашли
		СтрокаНаименования = НСтр("ru='Закрытие месяца по организации: %1';uk='Закриття місяця по організації: %1'");
		Если Объект.Организация = Справочники.Организации.ПустаяСсылка() Тогда
			СтрокаНаименования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаНаименования,
										НСтр("ru='По всем организациям.';uk='По всіх організаціях.'"));
		Иначе
			СтрокаНаименования = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СтрокаНаименования, СокрЛП(Объект.Организация));
		КонецЕсли;
		ПараметрыЗадания.Вставить("Метаданные",Метаданные.РегламентныеЗадания.ЗакрытиеМесяца);
		ПараметрыЗадания.Вставить("Наименование", СтрокаНаименования);
		Задание = РегламентныеЗаданияСервер.ДобавитьЗадание(ПараметрыЗадания);
	Иначе
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задание, ПараметрыЗадания);
	КонецЕсли;
	ИдентификаторЗадания = РегламентныеЗаданияСервер.УникальныйИдентификатор(Задание);
КонецПроцедуры

&НаСервере
Процедура ОбновитьПредставлениеРасписания()
	
	ПредставлениеРасписания = Строка(РасписаниеРегламентногоЗадания);
	Если ПредставлениеРасписания = Строка(Новый РасписаниеРегламентногоЗадания) Тогда
		 ПредставлениеРасписания = НСтр("ru='Расписание не задано';uk='Розклад не заданий'");
	КонецЕсли;
	Элементы.НастроитьРасписаниеРегламентногоЗадания.Заголовок = ПредставлениеРасписания;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеРасписанияРегламентногоЗаданияЗавершение(Расписание, ДополнительныеПараметры) Экспорт
	Диалог = ДополнительныеПараметры.Диалог;
	Если Расписание <> Неопределено Тогда
		РасписаниеРегламентногоЗадания = Диалог.Расписание;
		СохранитьРеквизитыРегламентногоЗадания();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПолучитьСостояниеРегламентногоЗадания()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИдентификаторЗадания = Неопределено;
	ПериодРегистрацииРегламентный = Дата("00010101000000");

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Задание.РегламентноеЗадание,
	|	Задание.ЗакрываемыйПериод
	|ИЗ
	|	РегистрСведений.РегламентныеЗаданияЗакрытияМесяца КАК Задание
	|ГДЕ
	|	Задание.Организация = &Организация");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ИдентификаторЗадания = ВыборкаДетальныеЗаписи.РегламентноеЗадание;
		ПериодРегистрацииРегламентный = ВыборкаДетальныеЗаписи.ЗакрываемыйПериод;
	КонецЦикла;
	
	ПредставлениеПериодРегистрацииРегламентный = Формат(ПериодРегистрацииРегламентный, "ДФ='MMMM yyyy'");
	Если ИдентификаторЗадания <> Неопределено Тогда
		Задание = РегламентныеЗаданияСервер.Задание(ИдентификаторЗадания);
		Если Задание <> Неопределено Тогда
			РасписаниеРегламентногоЗадания	= Задание.Расписание;
			АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца = Задание.Использование;
			
			СвойстваПоследнегоФоновогоЗадания = 
				РегламентныеЗаданияСлужебный.ПолучитьСвойстваПоследнегоФоновогоЗаданияВыполненияРегламентногоЗадания(Задание);
			Если СвойстваПоследнегоФоновогоЗадания = Неопределено Тогда
				СостояниеАвтоматическогоВыполненияОпераций = НСтр("ru='Не выполнялось';uk='Не виконувалось'");
			Иначе
				СостояниеАвтоматическогоВыполненияОпераций = Строка(СвойстваПоследнегоФоновогоЗадания.Состояние) + ": "
				+ Строка(СвойстваПоследнегоФоновогоЗадания.Конец);
			КонецЕсли;
		Иначе
			РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
			АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца = Ложь;
			СостояниеАвтоматическогоВыполненияОпераций = "";
		КонецЕсли;
	Иначе
		РасписаниеРегламентногоЗадания = Новый РасписаниеРегламентногоЗадания;
		АвтоматическиВыполнятьОперацииПоЗакрытиюМесяца = Ложь;
		СостояниеАвтоматическогоВыполненияОпераций = "";
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ВнутреннийИнтерфейс
// Метод возвращает массив этапов к расчету,
// упорядоченных по приоритету выполнения (номеру индекса).
// Элементы с большим индексом зависят от элементов с меньшим индексом.
// Для добавления нового этапа необходимо добавить новый элемент в массив
// и прописать вызов соответствующего метода в методе ВыполнитьЭтапыРасчета()
Функция ЭтапыКРасчету()
	ОбработкаОъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОъект.ЭтапыКРасчету();
КонецФункции

// Метод возвращает массив этапов, исключаемых из расчета.
Функция ОтключенныеЭтапы()
	ОбработкаОъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОъект.ОтключенныеЭтапы();
КонецФункции

&НаКлиенте
Процедура РассчитатьЭтап(ИмяЭтапаРасчета, Отказ = Ложь, РасчетВФоне = Ложь, ПараметрыРасчета)
	Если ИмяЭтапаРасчета = "ИсправлениеОтрицательныхОстатков" Тогда
		ИсправлениеОтрицательныхОстатков(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "ПередачаТоваров" Тогда
		ПередачаВозвратТоваров(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "ОтчетыПоКомиссииИнтеркампани" Тогда
		ОтчетыПоКомиссииИнтеркампани(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "ОтчетыКомитентамОСписании" Тогда
		ОтчетыКомитентамОСписании(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "ПомощникПоУчетуНДС" Тогда
		ОперацииПоУчетуНДС(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "РасчетыСПартнерами" Тогда
		РасчетыСПартнерами(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "РаспределениеРБП" Тогда
		РаспределениеРБП(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "НачисленияПоКредитамДепозитам" Тогда
		НачисленияПоКредитамДепозитам(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "АннулированиеПодарочныхСертификатов" Тогда
		АннулированиеПодарочныхСертификатов(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "ПартииТоваров" Тогда
		РасчетПартийТоваров(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	//++ НЕ УТ
	ИначеЕсли ИмяЭтапаРасчета = "КорректировкаНалоговогоНазначенияКапитальныхИнвестиций" Тогда
		КорректировкаНалоговогоНазначенияКапитальныхИнвестиций(ИмяЭтапаРасчета, РасчетВФоне, Отказ, ПараметрыРасчета);
	//-- НЕ УТ
	ИначеЕсли ИмяЭтапаРасчета = "Себестоимость" Тогда
		РасчетСебестоимости(ИмяЭтапаРасчета, РасчетВФоне, Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "ПереоценкаДенежныхСредств" Тогда
		ПереоценкаДенежныхСредств(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "РаспределениеДоходовРасходов" Тогда
		РаспределениеДоходовРасходовПоНаправлениям(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	//++ НЕ УТ
	ИначеЕсли ИмяЭтапаРасчета = "АмортизацияОСиНМА" Тогда
		АмортизацияОСиНМА(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	//++ НЕ УТКА
	ИначеЕсли ИмяЭтапаРасчета = "АмортизацияОСиНМАМеждународный" Тогда
		АмортизацияОСиНМАМеждународный(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	//-- НЕ УТКА
	ИначеЕсли ИмяЭтапаРасчета = "РаспределениеРасходов" Тогда
		НастройкаРаспределенияРасходов(ИмяЭтапаРасчета, РасчетВФоне, Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "НастройкаПоказателейРаспределенияРасходов" Тогда
		НастройкаПоказателейРаспределенияРасходов(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "СписаниеЗатратНаВыпуск" Тогда
		СписаниеЗатратНаВыпуск(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "РаспределениеМатериаловРаботНаПродукцию" Тогда
		РаспределениеМатериаловРаботНаПродукцию(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "РасходыНаПродукцию" Тогда
		РаспределениеРасходовНаПродукцию(ИмяЭтапаРасчета, РасчетВФоне, Отказ, ПараметрыРасчета);
	//++ НЕ УТКА
	ИначеЕсли ИмяЭтапаРасчета = "МеждународныйУчет" Тогда
		МеждународныйУчет(Отказ, ПараметрыРасчета);
	//-- НЕ УТКА
	ИначеЕсли ИмяЭтапаРасчета = "РеглУчет" Тогда
		РеглУчет(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "СписаниеКосвенныхРасходов" Тогда
		СписаниеКосвенныхРасходов(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "РезервыПоДолгам" Тогда
		РезервыПоДолгам(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "РасчетКурсовыхРазницРегл" Тогда
		РасчетКурсовыхРазницРегл(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "ФинансовыйРезультат" Тогда
		ФинансовыйРезультат(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "НачислениеОО" Тогда
		НачислениеОО(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	ИначеЕсли ИмяЭтапаРасчета = "ОтражениеЗарплаты" Тогда
		ОтражениеЗарплаты(ИмяЭтапаРасчета, РасчетВФоне, Отказ);
	//-- НЕ УТ
	ИначеЕсли ИмяЭтапаРасчета = "УсловныеПродажи" Тогда
		УсловныеПродажи(Отказ, ПараметрыРасчета);
    //++ НЕ УТ
	ИначеЕсли ИмяЭтапаРасчета = "ПерерасчетПропорциональногоНДС" Тогда
		ПерерасчетПропорциональногоНДС(Отказ, ПараметрыРасчета);
	ИначеЕсли ИмяЭтапаРасчета = "КорректировкаУсловныхПродажПоПропорциональномуНДС" Тогда
		КорректировкаУсловныхПродажПоПропорциональномуНДС(Отказ, ПараметрыРасчета);
    //-- НЕ УТ    
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СостояниеЭтапа(ПредыдущиеЭтапы, ИмяЭтапаРасчета)
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыРасчета = Новый Структура("СписокОрганизаций, СписокПодразделений, Период",
						СписокДоступныхОрганизаций.ВыгрузитьЗначения(), СписокПодразделений(), Объект.ПериодРегистрации, Объект.Организация);
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Состояние = ОбработкаОбъект.СостояниеЭтапа(ПредыдущиеЭтапы, ПараметрыРасчета, ИмяЭтапаРасчета);
	Возврат Состояние;
КонецФункции

&НаСервере
Процедура ОформлениеЭтапа(Этап, ПараметрыОформления)
	Если Этап = "ИсправлениеОтрицательныхОстатков" Тогда
		ОформлениеИсправлениеОтрицательныхОстатков(ПараметрыОформления);
	ИначеЕсли Этап = "ПередачаТоваров" Тогда
		ОформлениеПередачаВозвратТоваров(ПараметрыОформления);
	ИначеЕсли Этап = "ОтчетыПоКомиссииИнтеркампани" Тогда
		ОформлениеОтчетыПоКомиссииИнтеркампани(ПараметрыОформления);
	ИначеЕсли Этап = "ОтчетыКомитентамОСписании" Тогда
		ОформлениеОтчетыКомитентамОСписании(ПараметрыОформления);
	ИначеЕсли Этап = "ПомощникПоУчетуНДС" Тогда
		ОформлениеОперацииПоУчетуНДС(ПараметрыОформления);
	ИначеЕсли Этап = "РасчетыСПартнерами" Тогда
		ОформлениеРасчетыСПартнерами(ПараметрыОформления);
	ИначеЕсли Этап = "РаспределениеРБП" Тогда
		ОформлениеРаспределениеРБП(ПараметрыОформления);
	ИначеЕсли Этап = "НачисленияПоКредитамДепозитам" Тогда
		ОформлениеНачисленияПоКредитамДепозитам(ПараметрыОформления);
	ИначеЕсли Этап = "АннулированиеПодарочныхСертификатов" Тогда
		ОформлениеАннулированиеПодарочныхСертификатов(ПараметрыОформления);
	ИначеЕсли Этап = "ПартииТоваров" Тогда
		ОформлениеПартииТоваров(ПараметрыОформления);
	//++ НЕ УТ
	ИначеЕсли Этап = "КорректировкаНалоговогоНазначенияКапитальныхИнвестиций" Тогда
		ОформлениеКорректировкаНалоговогоНазначенияКапитальныхИнвестиций(ПараметрыОформления);
	//-- НЕ УТ
	ИначеЕсли Этап = "Себестоимость" Тогда
		ОформлениеРасчетСебестоимости(ПараметрыОформления);
	ИначеЕсли Этап = "ПереоценкаДенежныхСредств" Тогда
		ОформлениеПереоценкаДенежныхСредств(ПараметрыОформления);
	ИначеЕсли Этап = "РаспределениеДоходовРасходов" Тогда
		ОформлениеРаспределениеДоходовРасходов(ПараметрыОформления);
	//++ НЕ УТ
	ИначеЕсли Этап = "АмортизацияОСиНМА" Тогда
		ОформлениеАмортизацияОСиНМА(ПараметрыОформления);
	//++ НЕ УТКА
	ИначеЕсли Этап = "АмортизацияОСиНМАМеждународный" Тогда
		ОформлениеАмортизацияОСиНМАМеждународный(ПараметрыОформления);
	//-- НЕ УТКА
	ИначеЕсли Этап = "РаспределениеРасходов" Тогда
		ОформлениеРаспределениеРасходов(ПараметрыОформления);
	ИначеЕсли Этап = "НастройкаПоказателейРаспределенияРасходов" Тогда
		ОформлениеНастройкаПоказателейРаспределенияРасходов(ПараметрыОформления);
	ИначеЕсли Этап = "СписаниеЗатратНаВыпуск" Тогда
		ОформлениеСписаниеЗатратНаВыпуск(ПараметрыОформления);
	ИначеЕсли Этап = "РаспределениеМатериаловРаботНаПродукцию" Тогда
		ОформлениеРаспределениеМатериаловРаботНаПродукцию(ПараметрыОформления);
	ИначеЕсли Этап = "РасходыНаПродукцию" Тогда
		ОформлениеРасходыНаПродукцию(ПараметрыОформления);
	ИначеЕсли Этап = "РеглУчет" Тогда
		ОформлениеРеглУчет(ПараметрыОформления);
	ИначеЕсли Этап = "СписаниеКосвенныхРасходов" Тогда
		ОформлениеСписаниеКосвенныхРасходов(ПараметрыОформления);
	ИначеЕсли Этап = "РезервыПоДолгам" Тогда
		ОформлениеРезервыПоДолгам(ПараметрыОформления);
	ИначеЕсли Этап = "РасчетКурсовыхРазницРегл" Тогда
		ОформлениеРасчетКурсовыхРазницРегл(ПараметрыОформления);
	ИначеЕсли Этап = "ФинансовыйРезультат" Тогда
		ОформлениеФинансовыйРезультат(ПараметрыОформления);
	//++ НЕ УТКА
	ИначеЕсли Этап = "МеждународныйУчет" Тогда
		ОформлениеМеждународныйУчет(ПараметрыОформления);
	//-- НЕ УТКА
	ИначеЕсли Этап = "НачислениеОО" Тогда
		ОформлениеНачислениеОО(ПараметрыОформления);
	ИначеЕсли Этап = "ОтражениеЗарплаты" Тогда
		ОформлениеОтражениеЗарплаты(ПараметрыОформления);
	//-- НЕ УТ
	ИначеЕсли Этап = "УсловныеПродажи" Тогда
		ОформлениеУсловныеПродажи(ПараметрыОформления);
    //++ НЕ УТ
	ИначеЕсли Этап = "ПерерасчетПропорциональногоНДС" Тогда
		ОформлениеПерерасчетПропорциональногоНДС(ПараметрыОформления);
	ИначеЕсли Этап = "КорректировкаУсловныхПродажПоПропорциональномуНДС" Тогда
		ОформлениеКорректировкаУсловныхПродажПоПропорциональномуНДС(ПараметрыОформления);
    //-- НЕ УТ    
	КонецЕсли;
КонецПроцедуры

#Область ОбработчикиЭтапов

#Область СлужебныеМетодыОбработчиковЭтапов

&НаСервере
Процедура СкрытьУказанныеЭтапы(МассивЭтапов)
	ПараметрыОформления = Новый Структура("Скрыть, Статус", Истина, Неопределено);
	Для Каждого Этап Из МассивЭтапов Цикл
		ОформлениеЭтапа(Этап, ПараметрыОформления)
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОтобразитьЭтапы(Этапы)
	
	СостоянияЭтапов = СостоянияЭтапов(Этапы);
	ПараметрыОформления = Новый Структура("ИмяЭтапа, Статус", "", Неопределено);
	
	ВыполненоУспешно = 0;
	НеТребуется		 = 0;
	НеВыполнено		 = 0;

	Для Каждого Состояние Из СостоянияЭтапов Цикл
		ПараметрыОформления.Статус = Состояние.Значение;
		ПараметрыОформления.ИмяЭтапа = Состояние.Ключ;
		ОформлениеЭтапа(Состояние.Ключ, ПараметрыОформления);
		Если Состояние.Значение = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
			НеВыполнено = НеВыполнено + 1;
		ИначеЕсли Состояние.Значение = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
			НеТребуется = НеТребуется + 1;
		ИначеЕсли Состояние.Значение = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно Тогда
			ВыполненоУспешно = ВыполненоУспешно + 1;
		КонецЕсли;
	КонецЦикла;
	Если ВыполненоУспешно > 0 И НеВыполнено = 0 Тогда
		Статус = НСтр("ru='Операции выполнены успешно';uk='Операції виконані успішно'");
		Элементы.ДекорацияВыполнено.Видимость= Истина;
		Элементы.ДекорацияНеВыполнено.Видимость = Ложь;
		Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Видимость = Ложь;
	ИначеЕсли НеТребуется > 0 И НеВыполнено = 0 Тогда
		Статус = "";
		Элементы.ДекорацияВыполнено.Видимость= Ложь;
		Элементы.ДекорацияНеВыполнено.Видимость = Ложь;
		Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Видимость = Ложь;
	Иначе
		Статус = НСтр("ru='Существуют невыполненные операции';uk='Існують невиконані операції'");
		Элементы.ДекорацияВыполнено.Видимость= Ложь;
		Элементы.ДекорацияНеВыполнено.Видимость = Истина;
		Элементы.ФормаПоказатьСкрытьПоясненияКЭтапам.Видимость = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СписокПодразделений()
	Запрос = Новый Запрос("
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтруктураПредприятия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия");
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
КонецФункции

&НаСервере
Функция АктивноЗаданиеРасчетаПартий()
	Отбор = Новый Структура("Ключ, Состояние", "ПартионныйУчет", СостояниеФоновогоЗадания.Активно);
	АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	Возврат АктивныеЗадания.Количество() > 0;
КонецФункции

&НаКлиенте
Процедура ПроверитьЗавершениеРасчетаПартий()
	Если АктивноЗаданиеРасчетаПартий() Тогда
		ПараметрыОбработчика = Новый Структура;
		ПараметрыОбработчика.Вставить("Интервал", 1);
		ПодключитьОбработчикОжидания("ПроверитьЗавершениеРасчетаПартий", ПараметрыОбработчика.Интервал, Истина);
	Иначе
		РасчетВФоне = Ложь;
		Отказ = Ложь;
		РасчетПартийТоваров("ПартионныйУчет", РасчетВФоне, Отказ)
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПоказатьСвязанныеОрганизации()
	
	СвязанныеОрганизации.Очистить();
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Элементы.ГруппаПояснениеСвязанныеОрганизации.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	СхемаРасчета = ПартионныйУчет.СхемаРасчетаПартий(КонецМесяца(Объект.ПериодРегистрации), Объект.Организация);
	Если СхемаРасчета.Количество() = 0 Тогда
		МассивОрганизаций = Новый Массив;
	Иначе
		МассивОрганизаций = СхемаРасчета[СхемаРасчета.Количество() - 1].Организации;
	КонецЕсли;
		
	СвязанныеОрганизации.ЗагрузитьЗначения(МассивОрганизаций);
	
	Если СвязанныеОрганизации.Количество() < 2 Тогда
		// Это организация "одиночка" - нет связанных с ней по Интеркампани организаций
		Элементы.ГруппаПояснениеСвязанныеОрганизации.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	СвязанныеОрганизации.СортироватьПоЗначению();
	
	// Перенесем организацию-отбор на первую позицию в списке
	ЭлементОрганизацииВСписке = СвязанныеОрганизации.НайтиПоЗначению(Объект.Организация);
	Если ЭлементОрганизацииВСписке <> Неопределено Тогда
		Если СвязанныеОрганизации.Индекс(ЭлементОрганизацииВСписке) > 0 Тогда
			СвязанныеОрганизации.Удалить(ЭлементОрганизацииВСписке);
			СвязанныеОрганизации.Вставить(0, Объект.Организация);
		КонецЕсли;
	КонецЕсли;
	
	// Сформируем текст для вывода в форме
	ПояснениеСвязанныеОрганизации = "";
	
	Для Каждого ТекущаяОрганизация Из СвязанныеОрганизации Цикл
		ПояснениеСвязанныеОрганизации = ПояснениеСвязанныеОрганизации
			+ ?(ПояснениеСвязанныеОрганизации = "", "", "," + Символы.ПС) + "- " + СокрЛП(ТекущаяОрганизация.Значение);
	КонецЦикла;
	
	ПояснениеСвязанныеОрганизации =
		НСтр("ru='Будет выполнен расчет для следующих организаций:';uk='Буде виконаний розрахунок для наступних організацій:'") + Символы.ПС + ПояснениеСвязанныеОрганизации;
				
	Элементы.ПояснениеСвязанныеОрганизации.Высота = СвязанныеОрганизации.Количество() + 1;
	Элементы.ГруппаПояснениеСвязанныеОрганизации.Видимость = Истина;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДополнитьОтборыРасчетаСебестоимостиДляЖурналаРегистрации(ПараметрыФормы)
	РасчетСебестоимости.ДополнитьОтборыДляЖурналаРегистрации(ПараметрыФормы.Уровень, ПараметрыФормы.СобытиеЖурналаРегистрации);
КонецПроцедуры

#КонецОбласти

#Область Прочее
&НаКлиенте
Процедура ИсправлениеОтрицательныхОстатков(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус <> "НеТребуется" Тогда
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, Объект.ПериодРегистрации);
		ИмяФормыРасчета = "Обработка.ПомощникИсправленияОстатковТоваровОрганизаций.Форма.ФормаМониторСостоянияТоваровОрганизаций";
		РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеИсправлениеОтрицательныхОстатков(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
			ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,"ИсправлениеОтрицательныхОстатковТоваров","Заголовок", НСтр("ru='Исправление отрицательных остатков товаров организации';uk='Виправлення від''ємних залишків товарів організації'"));
		КонецЕсли;
		Элементы.ГруппаИсправлениеОтрицательныхОстатковОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ИсправлениеОтрицательныхОстатков.Имя);
	Иначе
		Элементы.ГруппаИсправлениеОтрицательныхОстатковОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередачаВозвратТоваров(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус <> "НеТребуется" Тогда
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, Объект.ПериодРегистрации);
		ОткрытьФорму("ЖурналДокументов.ПередачиВозвратыТоваровМеждуОрганизациями.Форма.ФормаРабочееМесто", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеПередачаВозвратТоваров(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.ГруппаПередачаТоваровОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПередачаТоваров.Имя);
	Иначе
		Элементы.ГруппаПередачаТоваровОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыКомитентамОСписании(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус <> "НеТребуется" Тогда
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, Объект.ПериодРегистрации);
		ОткрытьФорму("ЖурналДокументов.ОтчетыКомитентам.Форма.ФормаСпискаДокументов", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеОтчетыКомитентамОСписании(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.ГруппаОтчетыКомитентамОСписанииОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПередачаТоваров.Имя);
	Иначе
		Элементы.ГруппаОтчетыКомитентамОСписанииОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПоКомиссииИнтеркампани(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус <> "НеТребуется" Тогда
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, Объект.ПериодРегистрации);
		ОткрытьФорму("ЖурналДокументов.ОтчетыПоКомиссииМеждуОрганизациями.Форма.ФормаСпискаДокументов", ПараметрыФормы);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеОтчетыПоКомиссииИнтеркампани(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.ГруппаОтчетыПоКомиссииИнтеркампаниОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПередачаТоваров.Имя);
	Иначе
		Элементы.ГруппаОтчетыПоКомиссииИнтеркампаниОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСПартнерами(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ПараметрыРасчета.Вставить("Организация", СписокДоступныхОрганизаций);
	ЭтапРассчитываемыйВФоне = НСтр("ru='формирование движений по расчетам с партнерами и переоценка расчетов.';uk='формування рухів по розрахунках з партнерами і переоцінка розрахунків.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРасчетыСПартнерами(ПараметрыОформления)
	ИспользоватьПартнеровКакКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровКакКонтрагентов");
	ПартнерыИКонтрагенты.ЗаголовокРеквизитаВЗависимостиОтФОИспользоватьПартнеровКакКонтрагентов(ЭтотОбъект,
		"РасчетыСПартнерами",
		НСтр("ru='Формирование движений по расчетам с контрагентами';uk='Формування рухів за розрахунками з контрагентами'"),
		ИспользоватьПартнеровКакКонтрагентов);
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаРасчетыСПартнерамиОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Если ИспользоватьПартнеровКакКонтрагентов Тогда
			ПояснениеФормированиеДвиженийПоРасчетуСПартнерами = НСтр("ru='Необходимо выполнить формирование движений по взаиморасчетам с контрагентами.';uk='Необхідно виконати формування рухів по взаєморозрахунках з контрагентами.'");
		Иначе
			ПояснениеФормированиеДвиженийПоРасчетуСПартнерами = НСтр("ru='Необходимо выполнить формирование движений по взаиморасчетам с партнерами.';uk='Необхідно виконати формування рухів по взаєморозрахунках з партнерами.'");
		КонецЕсли;
		Если ОшибкиРасчета.Свойство("РасчетыСПартнерами") Тогда 
			Элементы.РасчетыСПартнерами.Гиперссылка = Истина;
		Иначе
			Элементы.РасчетыСПартнерами.Гиперссылка = Ложь;
		КонецЕсли;
	КонецЕсли;
	Элементы.ГруппаРасчетыСПартнерамиОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РасчетыСПартнерами.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПоУчетуНДС(Отказ, ПараметрыРасчета)
	
	
КонецПроцедуры

&НаСервере
Процедура ОформлениеОперацииПоУчетуНДС(ПараметрыОформления)
	
	Элементы.ГруппаПомощникПоУчетуНДСОбщая.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УсловныеПродажи(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус = "ОжидаетВыполненияПредыдущихОпераций" Тогда
		Возврат
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Период, Организация", Объект.ПериодРегистрации, Объект.Организация);
	ИмяФормыРасчета = "Обработка.НДСФормированиеУсловныхПродаж.Форма.Форма";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеУсловныеПродажи(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда	
		ПояснениеУсловныеПродажи = НСтр("ru='Требуется сформировать налоговые документы по условным продажам';uk='Потрібно сформувати податкові документи по умовних продажах'");
		Элементы.УсловныеПродажи.Гиперссылка = Истина;
		Элементы.ГруппаУсловныеПродажиОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.УсловныеПродажи.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ОжидаетВыполненияПредыдущихОпераций Тогда
		ПояснениеУсловныеПродажи = НСтр("ru='Необходимо расчитать себестоимость для заполнения регистра по условным продажам';uk='Необхідно розрахувати собівартість для заповнення регістра по умовним продажам'");
		Элементы.УсловныеПродажи.Гиперссылка = Истина;
		Элементы.ГруппаУсловныеПродажиОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.УсловныеПродажи.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоСОшибками Тогда
		ПояснениеУсловныеПродажи = НСтр("ru='Есть расхождения учетной суммы НДС и в сформированных документах';uk='Є розбіжності облікової суми ПДВ і в сформованих документах'");
		Элементы.ГруппаУсловныеПродажиОбщая.Видимость = Истина;
		Элементы.УсловныеПродажи.Гиперссылка = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.УсловныеПродажи.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаУсловныеПродажиОбщая.Видимость = Истина;
		Возврат;
	Иначе
		Элементы.ГруппаУсловныеПродажиОбщая.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

//++ НЕ УТ

&НаКлиенте
Процедура ПерерасчетПропорциональногоНДС(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус = "ОжидаетВыполненияПредыдущихОпераций" Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Период, Организация", Объект.ПериодРегистрации, Объект.Организация);
    ИмяФормыРасчета = "Документ.ПерерасчетПропорциональногоНДСпоТоварамИОС.ФормаСписка";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаУсловныхПродажПоПропорциональномуНДС(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ПараметрыРасчета.Статус = "ОжидаетВыполненияПредыдущихОпераций" Тогда
		Возврат;
    КонецЕсли;
	ПараметрыФормы = Новый Структура("Период, Организация", Объект.ПериодРегистрации, Объект.Организация);
	ИмяФормыРасчета = "Обработка.НДСФормированиеКорректировокКУсловнымПродажамПоПропорциональномуНДС.Форма.Форма";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
    Возврат;
КонецПроцедуры

&НаСервере
Процедура ОформлениеПерерасчетПропорциональногоНДС(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда	
		ПояснениеПерерасчетПропорциональногоНДС = НСтр("ru='Требуется сформировать документ ""Перерасчет пропорционального НДС по товарам и ОС""';uk='Потрібно сформувати документ ""Перерахунок пропорційного ПДВ по товарах і ОЗ""'");
		Элементы.ПерерасчетПропорциональногоНДС.Гиперссылка = Истина;
		Элементы.ГруппаПерерасчетПропорциональногоНДСОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПерерасчетПропорциональногоНДС.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ОжидаетВыполненияПредыдущихОпераций Тогда
		ПояснениеПерерасчетПропорциональногоНДС = НСтр("ru='Необходимо сформировать налоговые документы по условным продажам для заполнения документа ""Перерасчет пропорционального НДС по товарам и ОС""';uk='Необхідно сформувати податкові документи по умовним продажам для заповнення документа ""Перерахунок пропорційного ПДВ по товарах і ОС""'");
		Элементы.ПерерасчетПропорциональногоНДС.Гиперссылка = Истина;
		Элементы.ГруппаПерерасчетПропорциональногоНДСОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПерерасчетПропорциональногоНДС.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаПерерасчетПропорциональногоНДСОбщая.Видимость = Истина;
		Возврат;
	Иначе
		Элементы.ГруппаПерерасчетПропорциональногоНДСОбщая.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформлениеКорректировкаУсловныхПродажПоПропорциональномуНДС(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда	
		ПояснениеКорректировкаУсловныхПродажПоПропорциональномуНДС = НСтр("ru='Требуется сформировать налоговые документы по условным продажам по пропорциональному НДС';uk='Потрібно сформувати податкові документи по умовним продажам за пропорційним ПДВ'");
		Элементы.КорректировкаУсловныхПродажПоПропорциональномуНДС.Гиперссылка = Истина;
		Элементы.ГруппаКорректировкаУсловныхПродажПоПропорциональномуНДСОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.КорректировкаУсловныхПродажПоПропорциональномуНДС.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ОжидаетВыполненияПредыдущихОпераций Тогда
		ПояснениеКорректировкаУсловныхПродажПоПропорциональномуНДС = НСтр("ru='Необходимо сформировать документ ""Перерасчет пропорционального НДС по товарам и ОС"" для заполнения регистра по условным продажам по пропорциональному НДС';uk='Необхідно сформувати документ ""Перерахунок пропорційного ПДВ по товарах і ОС"" для заповнення регістра по умовним продажам за пропорційним ПДВ'");
		Элементы.КорректировкаУсловныхПродажПоПропорциональномуНДС.Гиперссылка = Истина;
		Элементы.ГруппаКорректировкаУсловныхПродажПоПропорциональномуНДСОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.КорректировкаУсловныхПродажПоПропорциональномуНДС.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоСОшибками Тогда
		ПояснениеКорректировкаУсловныхПродажПоПропорциональномуНДС = НСтр("ru='Есть расхождения учетной суммы НДС и суммы НДС в сформированных документах';uk='Є розбіжності облікової суми ПДВ і суми ПДВ у сформованих документах'");
		Элементы.КорректировкаУсловныхПродажПоПропорциональномуНДС.Гиперссылка = Истина;
		Элементы.ГруппаКорректировкаУсловныхПродажПоПропорциональномуНДСОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.КорректировкаУсловныхПродажПоПропорциональномуНДС.Имя);	
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаКорректировкаУсловныхПродажПоПропорциональномуНДСОбщая.Видимость = Истина;
		Возврат;
	Иначе
		Элементы.ГруппаКорректировкаУсловныхПродажПоПропорциональномуНДСОбщая.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

//-- НЕ УТ



#КонецОбласти

#Область Затраты

//++ НЕ УТ

&НаКлиенте
Процедура НачислениеОО(ИмяЭтапа, РасчетВФоне, Отказ)
	
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='начисление оценочных обязательств и резервов по отпускам.';uk='нарахування оціночних зобов''язань і резервів по відпустках.'");
	ПараметрыРасчета.Вставить("СписокОрганизаций", СписокДоступныхОрганизаций);
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
	
КонецПроцедуры

&НаСервере
Процедура ОформлениеНачислениеОО(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаНачислениеОООбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеНачислениеОО = НСтр("ru='Существуют неначисленные оценочные обязательства (резервы) по отпускам.';uk='Існують нараховані оціночні зобов''язання (резерви) по відпустках.'");
		Элементы.НачислениеОО.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаНачислениеОООбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.НачислениеОО.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаНачислениеОООбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.НачислениеОО.Имя);
КонецПроцедуры

&НаСервере
Процедура ОформлениеОтражениеЗарплаты(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаОтражениеЗарплатыОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеОтражениеЗарплаты = НСтр("ru='Существуют начисления к отражению в финансовом учете';uk='Існують нарахування до відображення в фінансовому обліку'");
		Элементы.ОтражениеЗарплаты.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаОтражениеЗарплатыОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ОтражениеЗарплаты.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаОтражениеЗарплатыОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ОтражениеЗарплаты.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ОтражениеЗарплаты(ИмяЭтапа, РасчетВФоне, Отказ)
	
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='отражение зарплаты в финансовом учете.';uk='відображення зарплати в фінансовому обліку.'");
	ПараметрыРасчета.Вставить("СписокОрганизаций", СписокДоступныхОрганизаций);
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
	
КонецПроцедуры

//-- НЕ УТ

&НаКлиенте
Процедура РаспределениеРБП(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, Объект.ПериодРегистрации);
	ИмяФормыРасчета = "Документ.РаспределениеРасходовБудущихПериодов.Форма.ФормаСпискаДокументов";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРаспределениеРБП(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеРаспределениеРБП = НСтр("ru='Существуют не распределенные расходы будущих периодов.';uk='Існують нерозподілені витрати майбутніх періодів.'");
		Элементы.ГруппаРаспределениеРБПОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РаспределениеРБП.Имя);
	Иначе
		Элементы.ГруппаРаспределениеРБПОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПоКредитамДепозитам(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Организация", Объект.Организация);
	ПараметрыФормы.Вставить("Период", Объект.ПериодРегистрации);
	ИмяФормыРасчета = "Документ.НачисленияКредитовИДепозитов.Форма.ФормаСписка";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеНачисленияПоКредитамДепозитам(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется 
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаНачисленияПоКредитамДепозитамОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеНачисленияПоКредитамДепозитамЗаймам = НСтр("ru='Существуют договора кредитов, депозитов или займов, по которым не выполнено начисление.';uk='Існують договору кредитів, депозитів або позик, за якими не виконано нарахування.'");
	Иначе
		Элементы.ГруппаНачисленияПоКредитамДепозитамОбщая.Видимость = Истина;
	КонецЕсли;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.НачисленияПоКредитамДепозитам.Имя);
КонецПроцедуры

&НаКлиенте
Процедура АннулированиеПодарочныхСертификатов(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='аннулирование подарочных сертификатов.';uk='анулювання подарункових сертифікатів.'");
	ПараметрыРасчета.Вставить("СписокОрганизаций", СписокДоступныхОрганизаций);
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеАннулированиеПодарочныхСертификатов(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаАннулированиеПодарочныхСертификатовОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеАннулированиеПодарочныхСертификатов = НСтр("ru='Необходимо выполнить аннулирование подарочных сертификатов.';uk='Необхідно виконати анулювання подарункових сертифікатів.'");
		Если ОшибкиРасчета.Свойство(ПараметрыОформления.ИмяЭтапа) Тогда 
			Элементы.АннулированиеПодарочныхСертификатов.Гиперссылка = Истина;
		Иначе
			Элементы.АннулированиеПодарочныхСертификатов.Гиперссылка = Ложь;
		КонецЕсли;
	Иначе
		Элементы.АннулированиеПодарочныхСертификатов.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаАннулированиеПодарочныхСертификатовОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.АннулированиеПодарочныхСертификатов.Имя);
КонецПроцедуры

//++ НЕ УТ
&НаКлиенте
Процедура АмортизацияОСиНМА(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='амортизация ОС и НМА (регламентированный учет).';uk='амортизація ОЗ і НМА (регламентований облік).'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеАмортизацияОСиНМА(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаАмортизацияОСиНМАОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.АмортизацияОСиНМА.Гиперссылка = Истина;
		ПояснениеАмортизацияОСиНМАрегл = НСтр("ru='Необходимо начислить амортизацию по регламентированному учету';uk='Необхідно нарахувати амортизацію по регламентованому обліку'");
	Иначе
		Элементы.АмортизацияОСиНМА.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаАмортизацияОСиНМАОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.АмортизацияОСиНМА.Имя);
КонецПроцедуры

//++ НЕ УТКА

&НаКлиенте
Процедура АмортизацияОСиНМАМеждународный(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='амортизация ОС и НМА (международный учет).';uk='амортизація ОЗ і НМА (міжнародний облік).'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеАмортизацияОСиНМАМеждународный(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаАмортизацияОСиНМАМеждународныйОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.АмортизацияОСиНМАМеждународный.Гиперссылка = Истина;
		ПояснениеАмортизацияОСиНМАМеждународный = НСтр("ru='Необходимо начислить амортизацию по международному учету';uk='Необхідно нарахувати амортизацію по міжнародному обліку'");
	Иначе
		Элементы.АмортизацияОСиНМАМеждународный.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаАмортизацияОСиНМАМеждународныйОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.АмортизацияОСиНМАМеждународный.Имя);
КонецПроцедуры

//-- НЕ УТКА

//-- НЕ УТ

#КонецОбласти

//++ НЕ УТ
#Область ПроизводственныеЗатраты

&НаКлиенте
Процедура НастройкаРаспределенияРасходов(ИмяЭтапа, РасчетВФоне, Отказ, ПараметрыРасчета)
	Если ПараметрыРасчета.РучнойЗапуск И РезультатыРасчетов.Свойство("РаспределениеРасходов") Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Состояние", ПредопределенноеЗначение("Перечисление.СостоянияРаспределенияРасходов.ТребуетсяНастройкаРаспределения"));
		ПараметрыФормы.Вставить("ПериодРегистрации", Объект.ПериодРегистрации);
		ПараметрыФормы.Вставить("Организация", Объект.Организация);
		ПараметрыФормы.Вставить("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
		ИмяФормыРасчета = "Документ.РаспределениеПрочихЗатрат.Форма.ФормаРабочееМесто";
		РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
	Иначе
		КРасчету = Новый Массив();
		КРасчету.Добавить(ИмяЭтапа);
		Состояние = ПредопределенноеЗначение("Перечисление.СостоянияРаспределенияРасходов.ТребуетсяНастройкаРаспределения");
		ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
		ПараметрыРасчета.Вставить("СписокПодразделений", СписокПодразделений());
		ПараметрыРасчета.Вставить("Состояние", Состояние);
		ЭтапРассчитываемыйВФоне = НСтр("ru='настройка распределения расходов.';uk='настройка розподілу витрат.'");
		РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеРаспределениеРасходов(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаРаспределениеРасходовОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеНастройкаРаспределенияРасходов = НСтр("ru='Существуют производственные расходы, не полностью распределенные по статьям калькуляции.';uk='Існують виробничі витрати, не повністю розподілені за статтями калькуляції.'");
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаРаспределениеРасходовОбщая.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	Элементы.ГруппаРаспределениеРасходовОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РаспределениеРасходов.Имя);
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПоказателейРаспределенияРасходов(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = ИнициализироватьПараметрыРасчета();
	ПараметрыФормы.Вставить("ТолькоНезаполненные", Истина);
	ИмяФормыРасчета = "Справочник.ПравилаРаспределенияРасходов.Форма.ФормаСпискаВручную";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеНастройкаПоказателейРаспределенияРасходов(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеНастройкаПоказателейДляРаспределенияРасходовНаПродукцию =  НСтр("ru='Существуют показатели, необходимые для расчета базы распределения расходов,
            | значения которых не заданы в данном месяце.'
            |;uk='Існують показники, які необхідні для розрахунку бази розподілу витрат,
            |значення яких не задані в даному місяці.'");
		Элементы.ГруппаНастройкаПоказателейРаспределенияРасходовОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.НастройкаПоказателейРаспределенияРасходов.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаНастройкаПоказателейРаспределенияРасходовОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ГруппаНастройкаПоказателейРаспределенияРасходовОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписаниеЗатратНаВыпуск(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Состояние, Период, Организация", "ТребуетсяСписать", , Объект.Организация);
	ИмяФормыРасчета = "Документ.СписаниеЗатратНаВыпуск.Форма.ФормаСпискаДокументов";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеСписаниеЗатратНаВыпуск(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеСписаниеЗатратНаВыпуск = 
			НСтр("ru='Требуется списать материалы (работы) и распределить трудозатраты на выпуски продукции без распоряжений';uk='Потрібно списати матеріали (роботи) і розподілити трудовитрати на випуск продукції без розпоряджень'");
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.СписаниеЗатратНаВыпуск.Имя);
		Элементы.ГруппаСписаниеЗатратНаВыпускОбщая.Видимость = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаСписаниеЗатратНаВыпускОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ГруппаСписаниеЗатратНаВыпускОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеМатериаловРаботНаПродукцию(Элемент, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Состояние", 		 "ТребуетсяНастройкаРаспределения");
	ПараметрыФормы.Вставить("ПериодРегистрации", Объект.ПериодРегистрации);
	ПараметрыФормы.Вставить("Организация", 		 Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение", 	 ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
	Если СостоянияЭтапов.СписаниеЗатратНаВыпуск = ПредопределенноеЗначение("Перечисление.СостоянияОперацийЗакрытияМесяца.НеВыполнено") Тогда
		ПараметрыФормы.Вставить("ТребуетсяСписатьЗатраты", Истина);
	КонецЕсли;
	ИмяФормыРасчета = "Документ.РаспределениеПроизводственныхЗатрат.Форма.ФормаРабочееМесто";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРаспределениеМатериаловРаботНаПродукцию(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеРаспределениеМатериаловИРаботНаПродукцию = 
			НСтр("ru='Требуется распределить материалы и работы на этапы производства.';uk='Потрібно розподілити матеріали і роботи на етапи виробництва.'");
		Элементы.ГруппаРаспределениеМатериаловРаботНаПродукциюОбщая.Видимость = Истина;
		УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РаспределениеМатериаловРаботНаПродукцию.Имя);
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаРаспределениеМатериаловРаботНаПродукциюОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ГруппаРаспределениеМатериаловРаботНаПродукциюОбщая.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
//-- НЕ УТ

#Область Себестоимость

&НаКлиенте
Процедура РасчетПартийТоваров(ИмяЭтапа, РасчетВФоне, Отказ)
	Если Не АктивноЗаданиеРасчетаПартий() Тогда
		КРасчету = Новый Массив();
		КРасчету.Добавить(ИмяЭтапа);
		ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
		ЭтапРассчитываемыйВФоне = НСтр("ru='расчет себестоимости.';uk='розрахунок собівартості.'");
		РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
	Иначе
		ПодключитьОбработчикОжидания("ПроверитьЗавершениеРасчетаПартий", 1);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеПартииТоваров(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаПартииТоваровОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Если ОшибкиРасчета.Свойство("ПартииТоваров") Тогда 
			Элементы.ПартииТоваров.Гиперссылка = Истина;
		Иначе
			Элементы.ПартииТоваров.Гиперссылка = Ложь;
		КонецЕсли;
		ПояснениеФормированиеДвиженийПоПартиямТоваров = 
			НСтр("ru='Формирование движений не выполнялось или при распределении партий товаров возникли ошибки.';uk='Формування рухів не виконувалося або при розподілі партій товарів виникли помилки.'");
	Иначе
		Элементы.ПартииТоваров.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаПартииТоваровОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПартииТоваров.Имя);
КонецПроцедуры

//++ НЕ УТ
&НаКлиенте
Процедура КорректировкаНалоговогоНазначенияКапитальныхИнвестиций(ИмяЭтапа, РасчетВФоне, Отказ, ПараметрыРасчета)
	ДокументКорректировки = ПолучитьДокументКорректировкиНалоговогоНазначенияКапитальныхИнвестиций(Объект.Организация, Объект.ПериодРегистрации);
	Если ДокументКорректировки <> Неопределено ИЛИ НЕ РезультатыРасчетов.Свойство("Себестоимость") 
	Тогда
		Если УчитыватьПрочиеДоходыИРасходы Тогда
			КРасчету = Новый Массив();
			КРасчету.Добавить(ИмяЭтапа);
			ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
			ЭтапРассчитываемыйВФоне = НСтр("ru='корректировка налогового назначения капитальных инвестиций.';uk='коригування податкового призначення капітальних інвестицій.'");
			РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(Объект.Организация) Тогда
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, КонецМесяца(Объект.ПериодРегистрации));
		ИмяФормыРасчета = "Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ФормаОбъекта";
		РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
	Иначе
		ПараметрыФормы = Новый Структура("Организация, ПериодРегистрации", Объект.Организация, КонецМесяца(Объект.ПериодРегистрации));
		ИмяФормыРасчета = "Документ.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.ФормаСписка";
		РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеКорректировкаНалоговогоНазначенияКапитальныхИнвестиций(ПараметрыОформления)
	Если ПараметрыОформления.Свойство("Скрыть") Тогда
		Элементы.ГруппаКорректировкаНалоговогоНазначенияКапитальныхИнвестицийОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Элементы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Гиперссылка = Ложь;
		Если СкрыватьЭтапыСоСтатусомНеТребуется Тогда
			Элементы.ГруппаКорректировкаНалоговогоНазначенияКапитальныхИнвестицийОбщая.Видимость = Ложь;
			Возврат;
		КонецЕсли;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно Тогда
		ПояснениеКорректировкаНалоговогоНазначенияКапитальныхИнвестиций = НСтр("ru='Выполнена корректировка налогового назначения капитальных инвестиций';uk='Виконано коригування податкового призначення капітальних інвестицій'");
		Элементы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Гиперссылка = Ложь;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеКорректировкаНалоговогоНазначенияКапитальныхИнвестиций = НСтр("ru='Необходимо сформировать документ корректировки налогового назначения капитальных инвестиций';uk='Необхідно сформувати документ коригування податкового призначення капітальних інвестицій'");
		Элементы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Гиперссылка = Истина;
	КонецЕсли;
	Элементы.ГруппаКорректировкаНалоговогоНазначенияКапитальныхИнвестицийОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.КорректировкаНалоговогоНазначенияКапитальныхИнвестиций.Имя);
КонецПроцедуры
//-- НЕ УТ

//++ НЕ УТ

&НаКлиенте
Процедура РаспределениеРасходовНаПродукцию(ИмяЭтапа, РасчетВФоне, Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск И РезультатыРасчетов.Свойство("ПартииТоваров") Тогда
		Возврат;
	КонецЕсли;
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ПараметрыРасчета.Вставить("СписокПодразделений", СписокПодразделений());
	ПараметрыРасчета.Вставить("СписокОрганизаций", СписокДоступныхОрганизаций);
	ПараметрыРасчета.Вставить("РучнойЗапуск", Истина);
	ЭтапРассчитываемыйВФоне = НСтр("ru='распределение расходов на себестоимость продукции.';uk='розподіл витрат на собівартість продукції.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРасходыНаПродукцию(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Элементы.РасходыНаПродукцию.Гиперссылка = Ложь;
		Если СкрыватьЭтапыСоСтатусомНеТребуется Тогда
			Элементы.ГруппаРасходыНаПродукциюОбщая.Видимость = Ложь;
			Возврат;
		КонецЕсли;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно Тогда
		Элементы.РасходыНаПродукцию.Гиперссылка = Ложь;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоСОшибками Тогда
		ПояснениеРаспределениеРасходовНаПродукциюАвтоматически = НСтр("ru='Существуют производственные расходы,
            | для распределения которых не удалось сформировать базу распределения.'
            |;uk='Існують виробничі витрати,
            |для розподілу яких не вдалося сформувати базу розподілу.'");
		Элементы.РасходыНаПродукцию.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус =Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеРаспределениеРасходовНаПродукциюАвтоматически = 
			НСтр("ru='Существуют производственные расходы, требующие распределения пропорционально показателям.';uk='Існують виробничі витрати, що  потребують розподілу пропорційно показникам.'");
		Элементы.РасходыНаПродукцию.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаРасходыНаПродукциюОбщая.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	Элементы.ГруппаРасходыНаПродукциюОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РасходыНаПродукцию.Имя);
КонецПроцедуры
//-- НЕ УТ

&НаКлиенте
Процедура РасчетСебестоимости(ИмяЭтапа, РасчетВФоне, Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск И РезультатыРасчетов.Свойство("ПартииТоваров") Тогда
		Возврат;
	КонецЕсли;
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ПараметрыРасчета.Вставить("РучнойЗапуск", Истина);
	ЭтапРассчитываемыйВФоне = НСтр("ru='расчет себестоимости.';uk='розрахунок собівартості.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРасчетСебестоимости(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется Тогда
		Элементы.Себестоимость.Гиперссылка = Ложь;
		Если СкрыватьЭтапыСоСтатусомНеТребуется Тогда
			Элементы.ГруппаСебестоимостьОбщая.Видимость = Ложь;
			Возврат;
		КонецЕсли;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно Тогда
		Элементы.Себестоимость.Гиперссылка = Ложь;
	Иначе
		Элементы.Себестоимость.Гиперссылка = ?(РезультатыРасчетов.Свойство("Себестоимость") ИЛИ РезультатыРасчетов.Свойство("ПартииТоваров"), Истина, Ложь);
		ПояснениеРасчетСебестоимости = НСтр("ru='Необходимо ввести документ расчета себестоимости с вариантом расчета - фактический.';uk='Необхідно ввести документ розрахунку собівартості з варіантом розрахунку - фактичний.'");
	КонецЕсли;
	Элементы.ГруппаСебестоимостьОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.Себестоимость.Имя);
	

КонецПроцедуры

#КонецОбласти

#Область ФинансовыйРезультат

&НаКлиенте
Процедура ПереоценкаДенежныхСредств(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='переоценка денежных средств.';uk='переоцінка грошових коштів.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеПереоценкаДенежныхСредств(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаПереоценкаДенежныхСредствОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.ПереоценкаДенежныхСредств.Гиперссылка = Истина;
		ПояснениеПереоценкаДенежныхСредств = НСтр("ru='Необходимо выполнить переоценку остатков денежных средств.';uk='Необхідно виконати переоцінку залишків грошових коштів.'");
	Иначе
		Элементы.ПереоценкаДенежныхСредств.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаПереоценкаДенежныхСредствОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ПереоценкаДенежныхСредств.Имя);
КонецПроцедуры

&НаКлиенте
Процедура РаспределениеДоходовРасходовПоНаправлениям(ИмяЭтапа, РасчетВФоне, Отказ)
	Если УчитыватьПрочиеДоходыИРасходы И (ФормироватьФинансовыйРезультат ИЛИ БазоваяВерсия) Тогда
		КРасчету = Новый Массив();
		КРасчету.Добавить(ИмяЭтапа);
		ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
		ЭтапРассчитываемыйВФоне = НСтр("ru='распределение доходов и расходов по направлениям деятельности.';uk='розподіл доходів і витрат за напрямами діяльності.'");
		РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОформлениеРаспределениеДоходовРасходов(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаРаспределениеДоходовРасходовОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.РаспределениеДоходовРасходов.Гиперссылка = Истина;
		ПояснениеРаспределениеДоходовИРасходовПоНаправлениям = НСтр("ru='Необходимо выполнить распределение доходов и расходов по направлениям деятельности.';uk='Необхідно виконати розподіл доходів і видатків за напрямами діяльності.'");
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаРаспределениеДоходовРасходовОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.РаспределениеДоходовРасходов.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаРаспределениеДоходовРасходовОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РаспределениеДоходовРасходов.Имя);
КонецПроцедуры

#КонецОбласти


#Область РеглУчет

//++ НЕ УТ

&НаКлиенте
Процедура РеглУчет(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='отражение документов в регламентированном учете.';uk='відображення документів в регламентованому обліку.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРеглУчет(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаРеглУчетОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоСОшибками Тогда
		ПояснениеОтражениеДокументовВРеглУчете = НСтр("ru='При отражении документов в регламентированном учете произошли ошибки';uk='При відображенні документів в регламентованому обліку сталися помилки'");
		Элементы.РеглУчет.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеОтражениеДокументовВРеглУчете = НСтр("ru='Существуют документы, не отраженные в регламентированном учете';uk='Існують документи, які не відображені в регламентованому обліку'");
		Элементы.РеглУчет.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно Тогда
		Элементы.РеглУчет.Гиперссылка = Ложь;
	Иначе
		Элементы.РеглУчет.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаРеглУчетОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РеглУчет.Имя);
КонецПроцедуры

&НаКлиенте
Процедура СписаниеКосвенныхРасходов(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='списание косвенных расходов на финансовый результат.';uk='списання непрямих витрат на фінансовий результат.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеСписаниеКосвенныхРасходов(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаСписаниеКосвенныхРасходовОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеСписаниеКосвенныхРасходов = НСтр("ru='Необходимо ввести документ списания косвенных расходов.';uk='Необхідно ввести документ списання непрямих витрат.'");
		Элементы.СписаниеКосвенныхРасходов.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаСписаниеКосвенныхРасходовОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.СписаниеКосвенныхРасходов.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаСписаниеКосвенныхРасходовОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.СписаниеКосвенныхРасходов.Имя);
КонецПроцедуры

&НаКлиенте
Процедура РезервыПоДолгам(ИмяЭтапа, РасчетВФоне, Отказ);
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='формирование резервов по сомнительным долгам.';uk='формування резервів по сумнівних боргах.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРезервыПоДолгам(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаРезервыПоДолгамОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		ПояснениеФормированиеРезервовПоСомнительнымДолгам = НСтр("ru='Необходимо ввести документ формирования резервов по сомнительным долгам.';uk='Необхідно ввести документ формування резервів по сумнівних боргах.'");
		Элементы.РезервыПоДолгам.Гиперссылка = Истина;
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаРезервыПоДолгамОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.РезервыПоДолгам.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаРезервыПоДолгамОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РезервыПоДолгам.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ФинансовыйРезультат(ИмяЭтапа, РасчетВФоне, Отказ);
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='формирование финансового результата.';uk='формування фінансового результату.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеФинансовыйРезультат(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаФинансовыйРезультатОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.ФинансовыйРезультат.Гиперссылка = Истина;
        ПояснениеФормированиеФинансовогоРезультата = НСтр("ru='Необходимо сформировать финансовый результат по регламентированному учету.';uk='Необхідно сформувати фінансовий результат по регламентованому обліку.'");
	ИначеЕсли ПараметрыОформления.Статус = Неопределено Тогда
		Элементы.ГруппаФинансовыйРезультатОбщая.Видимость = Ложь;
		Возврат;
	Иначе
		Элементы.ФинансовыйРезультат.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаФинансовыйРезультатОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.ФинансовыйРезультат.Имя);	
КонецПроцедуры

//++ НЕ УТКА

&НаКлиенте
Процедура МеждународныйУчет(Отказ, ПараметрыРасчета)
	Если Не ПараметрыРасчета.РучнойЗапуск Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = Новый Структура("Статус, Организация, ПериодРегистрации", Статус, Объект.Организация, Объект.ПериодРегистрации);
	ИмяФормыРасчета = "Обработка.ОперацииЗакрытияМесяцаМеждународныйУчет.Форма";
	РассчитатьЭтапВручную(ИмяФормыРасчета, ПараметрыФормы, ПараметрыРасчета);
КонецПроцедуры

&НаСервере
Процедура ОформлениеМеждународныйУчет(ПараметрыОформления)
	Если (ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно
		И СкрыватьЭтапыСоСтатусомНеТребуется) ИЛИ ПараметрыОформления.Свойство("Скрыть")
	Тогда
		Элементы.ГруппаМеждународныйУчетОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус <> Перечисления.СостоянияОперацийЗакрытияМесяца.ВыполненоУспешно Тогда
		Элементы.МеждународныйУчет.Гиперссылка = Истина;
		ПояснениеМеждународныйУчет = НСтр("ru='Перейти к закрытию месяца по международному учету';uk='Перейти до закриття місяця з міжнародного обліку'");
	КонецЕсли;
	Элементы.ГруппаМеждународныйУчетОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.МеждународныйУчет.Имя);	
КонецПроцедуры

//-- НЕ УТКА

&НаКлиенте
Процедура РасчетКурсовыхРазницРегл(ИмяЭтапа, РасчетВФоне, Отказ)
	КРасчету = Новый Массив();
	КРасчету.Добавить(ИмяЭтапа);
	ПараметрыРасчета = ИнициализироватьПараметрыРасчета();
	ЭтапРассчитываемыйВФоне = НСтр("ru='расчет курсовых разниц.';uk='розрахунок курсових різниць.'");
	РассчитатьЭтапВФоновомЗадании(КРасчету, ПараметрыРасчета, РасчетВФоне, Отказ);
КонецПроцедуры

&НаСервере
Процедура ОформлениеРасчетКурсовыхРазницРегл(ПараметрыОформления)
	Если ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеТребуется
		И СкрыватьЭтапыСоСтатусомНеТребуется
	Тогда
		Элементы.ГруппаРасчетКурсовыхРазницРеглОбщая.Видимость = Ложь;
		Возврат;
	ИначеЕсли ПараметрыОформления.Статус = Перечисления.СостоянияОперацийЗакрытияМесяца.НеВыполнено Тогда
		Элементы.РасчетКурсовыхРазницРегл.Гиперссылка = Истина;
		ПояснениеРасчетКурсовыхРазницРегл = НСтр("ru='Необходимо выполнить расчет курсовых разниц по регламентированному учету.';uk='Необхідно виконати розрахунок курсових різниць по регламентованому обліку.'");
	Иначе
		Элементы.РасчетКурсовыхРазницРегл.Гиперссылка = Ложь;
	КонецЕсли;
	Элементы.ГруппаРасчетКурсовыхРазницРеглОбщая.Видимость = Истина;
	УстановитьКартинкуСтатусаЭтапа(ПараметрыОформления.Статус, Элементы.РасчетКурсовыхРазницРегл.Имя);
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти


#КонецОбласти

#КонецОбласти
