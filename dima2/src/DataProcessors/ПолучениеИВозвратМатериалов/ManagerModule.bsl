#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Подготавливает данные для вывода в рабочее место
//
// Параметры:
//  Параметры	- Структура - содержит необходимые параметры
//  АдресХранилища	- Строка - адрес хранилища в которое будут помещен результат
//
Процедура ПолучитьДанные(Параметры, АдресХранилища) Экспорт

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОстаткиВПодразделении.Организация КАК Организация,
	|	ОстаткиВПодразделении.Подразделение КАК Подразделение,
	|	ОстаткиВПодразделении.Номенклатура КАК Номенклатура,
	|	ОстаткиВПодразделении.Характеристика КАК Характеристика,
	|	ОстаткиВПодразделении.Назначение КАК Назначение,
	|	СУММА(ОстаткиВПодразделении.КоличествоОстаток) КАК КоличествоОстаток
	|ПОМЕСТИТЬ ОстаткиВПодразделении
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(
	|		,
	|		Подразделение ССЫЛКА Справочник.СтруктураПредприятия
	|		И (&НеИспользоватьПодразделение
	|			ИЛИ Подразделение = &Подразделение)
	|		И (&НеИспользоватьОрганизацию
	|			ИЛИ Организация = &Организация)) КАК ОстаткиВПодразделении
	|
	|ГДЕ
	|	ОстаткиВПодразделении.КоличествоОстаток <> 0
	|	И ОстаткиВПодразделении.Номенклатура.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|															ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиВПодразделении.Организация,
	|	ОстаткиВПодразделении.Номенклатура,
	|	ОстаткиВПодразделении.Характеристика,
	|	ОстаткиВПодразделении.Назначение,
	|	ОстаткиВПодразделении.Подразделение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Подразделение
	|;
	|
	//++ НЕ УТКА
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкаПередачиМатериаловВПроизводство.Номенклатура КАК Номенклатура,
	|	ЕСТЬNULL(ХарактеристикиНоменклатуры.Ссылка, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
	|	НастройкаПередачиМатериаловВПроизводство.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление
	|ИЗ
	|	РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиМатериаловВПроизводство
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО (СправочникНоменклатура.Ссылка = НастройкаПередачиМатериаловВПроизводство.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО (СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|					И ХарактеристикиНоменклатуры.Владелец = СправочникНоменклатура.Ссылка
	|				ИЛИ СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
	|					И ХарактеристикиНоменклатуры.Владелец = СправочникНоменклатура.ВидНоменклатуры
	|				ИЛИ СправочникНоменклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
	|					И ХарактеристикиНоменклатуры.Владелец = СправочникНоменклатура.ВладелецХарактеристик)
	|			И (ХарактеристикиНоменклатуры.Ссылка = НастройкаПередачиМатериаловВПроизводство.Характеристика
	|				ИЛИ НастройкаПередачиМатериаловВПроизводство.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|ГДЕ
	|	НастройкаПередачиМатериаловВПроизводство.ОснованиеДляПолучения = ЗНАЧЕНИЕ(Перечисление.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаВнутреннееПотребление)
	|	И НастройкаПередачиМатериаловВПроизводство.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И (&НеИспользоватьПодразделение
	|			ИЛИ НастройкаПередачиМатериаловВПроизводство.Подразделение = &Подразделение)
	|	И (&НеИспользоватьСклад
	|			ИЛИ НастройкаПередачиМатериаловВПроизводство.Склад = &Склад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ПотребностьПоМаршрутнымЛистам
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПотребностьВМатериалах.Организация КАК Организация,
	|	ПотребностьВМатериалах.Подразделение КАК Подразделение,
	|	ПотребностьВМатериалах.Номенклатура КАК Номенклатура,
	|	ПотребностьВМатериалах.Характеристика КАК Характеристика,
	|	ПотребностьВМатериалах.Назначение КАК Назначение,
	|	ПотребностьВМатериалах.КоличествоОборот КАК ТекущаяПотребность
	|ПОМЕСТИТЬ ПотребностьПоМаршрутнымЛистам
	|ИЗ
	|	РегистрНакопления.ПотребностьВМатериалахВПроизводстве.Обороты(
	|		,
	|		,
	|		,
	|		(&НеИспользоватьПодразделение
	|			ИЛИ Подразделение = &Подразделение)
	|		И (&НеИспользоватьОрганизацию
	|			ИЛИ Организация = &Организация)) КАК ПотребностьВМатериалах
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// МатериалыНеНачавшихсяЭтапов
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ГрафикЭтаповПроизводстваОстатки.Распоряжение           КАК Распоряжение,
	|	ЗаказНаПроизводствоЭтапыГрафикВсе.КодСтроки            КАК КодСтрокиЭтапыГрафик,
	|	ЗаказНаПроизводствоМатериалыИУслуги.КодСтроки          КАК КодСтрокиРаспоряжения
	|ПОМЕСТИТЬ МатериалыНеНачавшихсяЭтапов
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства КАК ГрафикЭтаповПроизводстваОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ЭтапыГрафик КАК ЗаказНаПроизводствоЭтапыГрафик
	|		ПО (ЗаказНаПроизводствоЭтапыГрафик.Ссылка = ГрафикЭтаповПроизводстваОстатки.Распоряжение)
	|			И (ЗаказНаПроизводствоЭтапыГрафик.КодСтроки = ГрафикЭтаповПроизводстваОстатки.КодСтрокиЭтапыГрафик)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ЭтапыГрафик КАК ЗаказНаПроизводствоЭтапыГрафикВсе
	|		ПО (ЗаказНаПроизводствоЭтапыГрафикВсе.Ссылка = ЗаказНаПроизводствоЭтапыГрафик.Ссылка)
	|			И (ЗаказНаПроизводствоЭтапыГрафикВсе.КлючСвязиЭтапы = ЗаказНаПроизводствоЭтапыГрафик.КлючСвязиЭтапы)
	|			И (ЗаказНаПроизводствоЭтапыГрафикВсе.КлючСвязиПродукция = ЗаказНаПроизводствоЭтапыГрафик.КлючСвязиПродукция)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Этапы КАК ЗаказНаПроизводствоЭтапы
	|		ПО (ЗаказНаПроизводствоЭтапы.Ссылка = ЗаказНаПроизводствоЭтапыГрафик.Ссылка)
	|			И (ЗаказНаПроизводствоЭтапы.КлючСвязи = ЗаказНаПроизводствоЭтапыГрафик.КлючСвязиЭтапы)
	|			И (ЗаказНаПроизводствоЭтапы.КлючСвязиПродукция = ЗаказНаПроизводствоЭтапыГрафик.КлючСвязиПродукция)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ЗаказНаПроизводствоМатериалыИУслуги
	|		ПО (ЗаказНаПроизводствоМатериалыИУслуги.Ссылка = ЗаказНаПроизводствоЭтапы.Ссылка)
	|			И (ЗаказНаПроизводствоМатериалыИУслуги.КлючСвязиЭтапы = ЗаказНаПроизводствоЭтапы.КлючСвязи)
	|			И (ЗаказНаПроизводствоМатериалыИУслуги.КлючСвязиПродукция = ЗаказНаПроизводствоЭтапы.КлючСвязиПродукция)
	|ГДЕ
	|	НЕ ЗаказНаПроизводствоЭтапы.ПроизводствоНаСтороне
	|	И (&НеИспользоватьПодразделение
	|		ИЛИ ГрафикЭтаповПроизводстваОстатки.Подразделение = &Подразделение)
	|	И ГрафикЭтаповПроизводстваОстатки.Запланировано > 0
	|	И (ГрафикЭтаповПроизводстваОстатки.НачалоЭтапа <= &ЗаказыПоДатуКонецДня
	|		ИЛИ &ЗаказыПоДатуКонецДня = ДАТАВРЕМЯ(1, 1, 1))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказНаПроизводство.Ссылка                      КАК Распоряжение,
	|	0                                               КАК КодСтрокиЭтапыГрафик,
	|	ЗаказНаПроизводствоМатериалыИУслуги.КодСтроки   КАК КодСтрокиРаспоряжения
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Этапы КАК ЗаказНаПроизводствоЭтапы
	|		ПО (ЗаказНаПроизводствоЭтапы.Ссылка = ЗаказНаПроизводство.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ЭтапыГрафик КАК ЗаказНаПроизводствоЭтапыГрафик
	|		ПО (ЗаказНаПроизводствоЭтапыГрафик.Ссылка = ЗаказНаПроизводствоЭтапы.Ссылка)
	|			И (ЗаказНаПроизводствоЭтапыГрафик.КлючСвязиЭтапы = ЗаказНаПроизводствоЭтапы.КлючСвязи)
	|			И (ЗаказНаПроизводствоЭтапыГрафик.КлючСвязиПродукция = ЗаказНаПроизводствоЭтапы.КлючСвязиПродукция)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства КАК ГрафикЭтаповПроизводстваОстатки
	|		ПО ГрафикЭтаповПроизводстваОстатки.Распоряжение = ЗаказНаПроизводствоЭтапыГрафик.Ссылка
	|			И ГрафикЭтаповПроизводстваОстатки.КодСтрокиЭтапыГрафик = ЗаказНаПроизводствоЭтапыГрафик.КодСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ЗаказНаПроизводствоМатериалыИУслуги
	|		ПО (ЗаказНаПроизводствоМатериалыИУслуги.Ссылка = ЗаказНаПроизводствоЭтапы.Ссылка)
	|			И (ЗаказНаПроизводствоМатериалыИУслуги.КлючСвязиЭтапы = ЗаказНаПроизводствоЭтапы.КлючСвязи)
	|			И (ЗаказНаПроизводствоЭтапы.КлючСвязиПродукция = ЗаказНаПроизводствоЭтапы.КлючСвязиПродукция)
	|ГДЕ
	|	ЗаказНаПроизводство.Статус В (
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.КПроизводству), 
	|			ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт))
	|	И ЗаказНаПроизводство.Проведен
	|	И ЗаказНаПроизводство.СтатусГрафикаПроизводства = ЗНАЧЕНИЕ(Перечисление.СтатусыГрафикаПроизводстваВЗаказеНаПроизводство.ТребуетсяРассчитать)
	|	И НЕ ЗаказНаПроизводствоЭтапы.ПроизводствоНаСтороне
	|	И (&НеИспользоватьПодразделение
	|		ИЛИ ЗаказНаПроизводствоЭтапы.Подразделение = &Подразделение)
	|	И ГрафикЭтаповПроизводстваОстатки.Распоряжение ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтрокиРаспоряжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// СозданныеЗаказы
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ЗаказНаПроизводство.Ссылка КАК Распоряжение
	|ПОМЕСТИТЬ СозданныеЗаказы
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Создан)
	|	И ЗаказНаПроизводство.Проведен
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ПотребностьПоЗаказамНаПроизводство
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.Организация       КАК Организация,
	|	ВложенныйЗапрос.Подразделение     КАК Подразделение,
	|	ВложенныйЗапрос.Номенклатура      КАК Номенклатура,
	|	ВложенныйЗапрос.Характеристика    КАК Характеристика,
	|	ВложенныйЗапрос.Назначение        КАК Назначение,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
	|ПОМЕСТИТЬ ПотребностьПоЗаказамНаПроизводство
	|
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗаказыМатериаловСУчетомКорректировок.Распоряжение.Организация КАК Организация,
	|		ЗаказыМатериаловСУчетомКорректировок.Подразделение     КАК Подразделение,
	|		ЗаказыМатериаловСУчетомКорректировок.Номенклатура      КАК Номенклатура,
	|		ЗаказыМатериаловСУчетомКорректировок.Характеристика    КАК Характеристика,
	|		ЗаказыМатериаловСУчетомКорректировок.Назначение        КАК Назначение,
	|		ЗаказыМатериаловСУчетомКорректировок.КоличествоОборот  КАК Количество
	|	ИЗ
	|		РегистрНакопления.ЗаказыМатериаловСУчетомКорректировок.Обороты(
	|				,,,
	|				НЕ Отменено
	|				И НЕ ПроизводствоНаСтороне 
	|				И (Распоряжение, КодСтрокиРаспоряжения) В
	|					(ВЫБРАТЬ
	|						МатериалыНеНачавшихсяЭтапов.Распоряжение,
	|						МатериалыНеНачавшихсяЭтапов.КодСтрокиРаспоряжения
	|					 ИЗ МатериалыНеНачавшихсяЭтапов)) КАК ЗаказыМатериаловСУчетомКорректировок
	|	ГДЕ
	|		ЗаказыМатериаловСУчетомКорректировок.КоличествоОборот <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыМатериаловСУчетомКорректировок.Распоряжение.Организация КАК Организация,
	|		ЗаказыМатериаловСУчетомКорректировок.Подразделение     КАК Подразделение,
	|		ЗаказыМатериаловСУчетомКорректировок.Номенклатура      КАК Номенклатура,
	|		ЗаказыМатериаловСУчетомКорректировок.Характеристика    КАК Характеристика,
	|		ЗаказыМатериаловСУчетомКорректировок.Назначение        КАК Назначение,
	|		ЗаказыМатериаловСУчетомКорректировок.КоличествоОборот  КАК Количество
	|	ИЗ
	|		РегистрНакопления.ЗаказыМатериаловСУчетомКорректировок.Обороты(
	|				,,,
	|				НЕ Отменено
	|				И НЕ ПроизводствоНаСтороне 
	|				И (&НеИспользоватьПодразделение
	|					ИЛИ Подразделение = &Подразделение)
	|				И Распоряжение В
	|					(ВЫБРАТЬ
	|						СозданныеЗаказы.Распоряжение
	|					 ИЗ СозданныеЗаказы)) КАК ЗаказыМатериаловСУчетомКорректировок
	|	ГДЕ
	|		ЗаказыМатериаловСУчетомКорректировок.КоличествоОборот <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МаршрутныйЛистПроизводства.Организация    КАК Организация,
	|		МаршрутныйЛистПроизводства.Подразделение  КАК Подразделение,
	|		ТаблицаМатериалыИУслуги.Номенклатура      КАК Номенклатура,
	|		ТаблицаМатериалыИУслуги.Характеристика    КАК Характеристика,
	|		ТаблицаМатериалыИУслуги.Назначение        КАК Назначение,
	|		-ТаблицаМатериалыИУслуги.Количество       КАК Количество
	|	ИЗ
	|		Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|			ПО ТаблицаМатериалыИУслуги.Ссылка = МаршрутныйЛистПроизводства.Ссылка
	|	ГДЕ
	|		МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен)
	|		И МаршрутныйЛистПроизводства.Проведен
	|		И (МаршрутныйЛистПроизводства.Распоряжение, МаршрутныйЛистПроизводства.КодСтрокиЭтапыГрафик) В
	|					(ВЫБРАТЬ
	|						МатериалыНеНачавшихсяЭтапов.Распоряжение,
	|						МатериалыНеНачавшихсяЭтапов.КодСтрокиЭтапыГрафик
	|					 ИЗ МатериалыНеНачавшихсяЭтапов)
	|
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.Подразделение,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение
	|
	|ИМЕЮЩИЕ СУММА(ВложенныйЗапрос.Количество) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Подразделение
	|;
	//-- НЕ УТКА
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ЗаказыМатериаловОстаткиПоРаспоряжениям
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыМатериаловОстатки.Распоряжение.Организация     КАК Организация,
	|	ЗаказыМатериаловОстатки.Распоряжение                 КАК Распоряжение,
	|	ЗаказыМатериаловОстатки.КодСтроки                    КАК КодСтроки,
	|	ЗаказыМатериаловОстатки.Номенклатура                 КАК Номенклатура,
	|	ЗаказыМатериаловОстатки.Характеристика               КАК Характеристика,
	|	ЗаказыМатериаловОстатки.Назначение                   КАК Назначение,
	|	ЗаказыМатериаловОстатки.Подразделение                КАК Подразделение,
	|	ЗаказыМатериаловОстатки.Склад                        КАК Склад,
	|	ЗаказыМатериаловОстатки.ЗаказаноОстаток              КАК Заказано,
	|	ЗаказыМатериаловОстатки.ВРезервеОстаток              КАК ВРезерве,
	|	ЗаказыМатериаловОстатки.КОформлениюОстаток           КАК КОтгрузке
	|ПОМЕСТИТЬ ЗаказыМатериаловОстаткиПоРаспоряжениям
	|ИЗ
	|	РегистрНакопления.ЗаказыМатериаловВПроизводство.Остатки(
	|			,
	|			(&НеИспользоватьПодразделение
	|				ИЛИ Подразделение = &Подразделение)
	|			И (&НеИспользоватьСклад
	|				ИЛИ Склад = &Склад)) КАК ЗаказыМатериаловОстатки
	|ГДЕ
	|	&НеИспользоватьОрганизацию
	|		ИЛИ ЗаказыМатериаловОстатки.Распоряжение.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// МатериалыКОтгрузке
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Организация         КАК Организация,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Номенклатура        КАК Номенклатура,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Характеристика      КАК Характеристика,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Назначение          КАК Назначение,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Подразделение       КАК Подразделение,
	|	СУММА(ЗаказыМатериаловОстаткиПоРаспоряжениям.КОтгрузке)    КАК КОтгрузке
	|ПОМЕСТИТЬ МатериалыКОтгрузке
	|ИЗ
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям КАК ЗаказыМатериаловОстаткиПоРаспоряжениям
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Организация,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Подразделение,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Номенклатура,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Характеристика,
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям.Назначение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ЗаказыМатериаловОстаткиКДате
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыМатериаловОстатки.Организация                  КАК Организация,
	|	ЗаказыМатериаловОстатки.Номенклатура                 КАК Номенклатура,
	|	ЗаказыМатериаловОстатки.Характеристика               КАК Характеристика,
	|	ЗаказыМатериаловОстатки.Назначение                   КАК Назначение,
	|	ЗаказыМатериаловОстатки.Подразделение                КАК Подразделение,
	|	ЗаказыМатериаловОстатки.Склад                        КАК Склад,
	|	ЗаказыМатериаловОстатки.Заказано                     КАК Заказано,
	|	ЗаказыМатериаловОстатки.ВРезерве                     КАК ВРезерве
	|ПОМЕСТИТЬ ЗаказыМатериаловОстаткиКДате
	|ИЗ
	|	ЗаказыМатериаловОстаткиПоРаспоряжениям КАК ЗаказыМатериаловОстатки 
	|
	|ГДЕ
	|	// сюда добавляется условие которое формируется программно
	|	&УсловиеЗаказыМатериаловОстаткиКДате
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ЗаказыМатериаловОстатки
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыМатериаловОстатки.Организация                  КАК Организация,
	|	ЗаказыМатериаловОстатки.Номенклатура                 КАК Номенклатура,
	|	ЗаказыМатериаловОстатки.Характеристика               КАК Характеристика,
	|	ЗаказыМатериаловОстатки.Назначение                   КАК Назначение,
	|	ЗаказыМатериаловОстатки.Подразделение                КАК Подразделение,
	|	СУММА(ЗаказыМатериаловОстатки.Заказано)              КАК Заказано,
	|	СУММА(ЗаказыМатериаловОстатки.ВРезерве)              КАК ВРезерве,
	|	МАКСИМУМ(ЗаказыМатериаловОстатки.Склад)              КАК Склад,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЗаказыМатериаловОстатки.Склад)  КАК КоличествоСкладов
	|ПОМЕСТИТЬ ЗаказыМатериаловОстатки
	|ИЗ
	|	ЗаказыМатериаловОстаткиКДате КАК ЗаказыМатериаловОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыМатериаловОстатки.Организация,
	|	ЗаказыМатериаловОстатки.Подразделение,
	|	ЗаказыМатериаловОстатки.Номенклатура,
	|	ЗаказыМатериаловОстатки.Характеристика,
	|	ЗаказыМатериаловОстатки.Назначение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// СписокНомеклатуры
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокНомеклатуры.Организация,
	|	СписокНомеклатуры.Номенклатура,
	|	СписокНомеклатуры.Характеристика,
	|	СписокНомеклатуры.Назначение,
	|	СписокНомеклатуры.Подразделение
	|ПОМЕСТИТЬ СписокНомеклатуры
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОстаткиВПодразделении.Организация КАК Организация,
	|		ОстаткиВПодразделении.Номенклатура КАК Номенклатура,
	|		ОстаткиВПодразделении.Характеристика КАК Характеристика,
	|		ОстаткиВПодразделении.Назначение КАК Назначение,
	|		ОстаткиВПодразделении.Подразделение КАК Подразделение
	|	ИЗ
	|		ОстаткиВПодразделении КАК ОстаткиВПодразделении
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗаказыМатериаловОстатки.Организация,
	|		ЗаказыМатериаловОстатки.Номенклатура,
	|		ЗаказыМатериаловОстатки.Характеристика,
	|		ЗаказыМатериаловОстатки.Назначение,
	|		ЗаказыМатериаловОстатки.Подразделение
	|	ИЗ
	|		ЗаказыМатериаловОстатки КАК ЗаказыМатериаловОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МатериалыКОтгрузке.Организация,
	|		МатериалыКОтгрузке.Номенклатура,
	|		МатериалыКОтгрузке.Характеристика,
	|		МатериалыКОтгрузке.Назначение,
	|		МатериалыКОтгрузке.Подразделение
	|	ИЗ
	|		МатериалыКОтгрузке КАК МатериалыКОтгрузке
	|
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПотребностьПоМаршрутнымЛистам.Организация,
	|		ПотребностьПоМаршрутнымЛистам.Номенклатура,
	|		ПотребностьПоМаршрутнымЛистам.Характеристика,
	|		ПотребностьПоМаршрутнымЛистам.Назначение,
	|		ПотребностьПоМаршрутнымЛистам.Подразделение
	|	ИЗ
	|		ПотребностьПоМаршрутнымЛистам КАК ПотребностьПоМаршрутнымЛистам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ПотребностьПоЗаказамНаПроизводство.Организация,
	|		ПотребностьПоЗаказамНаПроизводство.Номенклатура,
	|		ПотребностьПоЗаказамНаПроизводство.Характеристика,
	|		ПотребностьПоЗаказамНаПроизводство.Назначение,
	|		ПотребностьПоЗаказамНаПроизводство.Подразделение
	|	ИЗ
	|		ПотребностьПоЗаказамНаПроизводство КАК ПотребностьПоЗаказамНаПроизводство
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		&Организация,
	|		НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Номенклатура,
	|		НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка),
	|		НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Подразделение
	|	ИЗ
	|		НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление КАК НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление
	|			ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиВПодразделении КАК ОстаткиВПодразделении
	|			ПО ОстаткиВПодразделении.Номенклатура = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Номенклатура
	|				И ОстаткиВПодразделении.Характеристика = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Характеристика
	|				И ОстаткиВПодразделении.Подразделение = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Подразделение
	|			ЛЕВОЕ СОЕДИНЕНИЕ ЗаказыМатериаловОстатки КАК ЗаказыМатериаловОстатки
	|			ПО ЗаказыМатериаловОстатки.Номенклатура = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Номенклатура
	|				И ЗаказыМатериаловОстатки.Характеристика = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Характеристика
	|				И ЗаказыМатериаловОстатки.Подразделение = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Подразделение
	|			ЛЕВОЕ СОЕДИНЕНИЕ МатериалыКОтгрузке КАК МатериалыКОтгрузке
	|			ПО МатериалыКОтгрузке.Номенклатура = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Номенклатура
	|				И МатериалыКОтгрузке.Характеристика = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Характеристика
	|				И МатериалыКОтгрузке.Подразделение = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Подразделение
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПотребностьПоМаршрутнымЛистам КАК ПотребностьПоМаршрутнымЛистам
	|			ПО ПотребностьПоМаршрутнымЛистам.Номенклатура = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Номенклатура
	|				И ПотребностьПоМаршрутнымЛистам.Характеристика = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Характеристика
	|				И ПотребностьПоМаршрутнымЛистам.Подразделение = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Подразделение
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПотребностьПоЗаказамНаПроизводство КАК ПотребностьПоЗаказамНаПроизводство
	|			ПО ПотребностьПоЗаказамНаПроизводство.Номенклатура = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Номенклатура
	|				И ПотребностьПоЗаказамНаПроизводство.Характеристика = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Характеристика
	|				И ПотребностьПоЗаказамНаПроизводство.Подразделение = НоменклатураПередаваемаяПоЗаказуНаВнутреннееПотребление.Подразделение
	|	ГДЕ
	|		ОстаткиВПодразделении.Номенклатура ЕСТЬ NULL
	|		И ЗаказыМатериаловОстатки.Номенклатура ЕСТЬ NULL
	|		И МатериалыКОтгрузке.Номенклатура ЕСТЬ NULL
	|		И ПотребностьПоМаршрутнымЛистам.Номенклатура ЕСТЬ NULL
	|		И ПотребностьПоЗаказамНаПроизводство.Номенклатура ЕСТЬ NULL
	|	
	//-- НЕ УТКА
	|
	|	) КАК СписокНомеклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// ЗаказыМатериаловОстаткиПоСкладам
	|ВЫБРАТЬ
	|	ЗаказыМатериаловОстатки.Организация            КАК Организация,
	|	ЗаказыМатериаловОстатки.Номенклатура           КАК Номенклатура,
	|	ЗаказыМатериаловОстатки.Характеристика         КАК Характеристика,
	|	ЗаказыМатериаловОстатки.Назначение             КАК Назначение,
	|	ЗаказыМатериаловОстатки.Подразделение          КАК Подразделение,
	|	ЗаказыМатериаловОстатки.Склад                  КАК Склад
	|ИЗ
	|	ЗаказыМатериаловОстаткиКДате КАК ЗаказыМатериаловОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказыМатериаловОстатки.Организация,
	|	ЗаказыМатериаловОстатки.Номенклатура,
	|	ЗаказыМатериаловОстатки.Характеристика,
	|	ЗаказыМатериаловОстатки.Назначение,
	|	ЗаказыМатериаловОстатки.Подразделение,
	|	ЗаказыМатериаловОстатки.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СписокНомеклатуры.Организация,
	|	СписокНомеклатуры.Подразделение,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ЗаказыМатериаловОстатки.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ТОГДА 
	|				ВЫБОР КОГДА ЕСТЬNULL(ЗаказыМатериаловОстатки.КоличествоСкладов, 0) = 1
	|					ТОГДА ЗаказыМатериаловОстатки.Склад
	|					ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				КОНЕЦ
	|	ИНАЧЕ 
	//++ НЕ УТКА
	|		ЕСТЬNULL(НастройкаПередачиМатериаловХарактеристика.Склад,
	|			ЕСТЬNULL(НастройкаПередачиМатериаловНоменклатура.Склад, 
	//-- НЕ УТКА
	|			ЕСТЬNULL(НастройкаПередачиМатериаловСклад.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	//++ НЕ УТКА
	|		))
	//-- НЕ УТКА
	|	КОНЕЦ КАК Склад,
	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ЗаказыМатериаловОстатки.КоличествоСкладов, 0) > 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НесколькоСкладов,
	
	//++ НЕ УТКА
	|	ЕСТЬNULL(НастройкаПередачиМатериаловХарактеристика.Склад,
	|			ЕСТЬNULL(НастройкаПередачиМатериаловНоменклатура.Склад, 
	//-- НЕ УТКА
	|			ЕСТЬNULL(НастройкаПередачиМатериаловСклад.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	//++ НЕ УТКА
	|)) 
	//-- НЕ УТКА
	|		КАК СкладПоУмолчанию,
	
	|	ЕСТЬNULL(ЗаказыМатериаловОстатки.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК СкладИзРаспоряжений,
	|	ЕСТЬNULL(ЗаказыМатериаловОстатки.КоличествоСкладов, 0) КАК КоличествоСкладов,
	
	//++ НЕ УТКА
	|	ЕСТЬNULL(НастройкаПередачиМатериаловХарактеристика.ОснованиеДляПолучения, 
	|			ЕСТЬNULL(НастройкаПередачиМатериаловНоменклатура.ОснованиеДляПолучения, 
	|			ЕСТЬNULL(НастройкаПередачиМатериаловСклад.ОснованиеДляПолучения, 
	|			ЗНАЧЕНИЕ(Перечисление.ОснованияДляПолученияМатериаловВПроизводстве.ПустаяСсылка)))) КАК ОснованиеДляПолучения,
	//-- НЕ УТКА
	|	СписокНомеклатуры.Номенклатура,
	|	СписокНомеклатуры.Характеристика,
	|	СписокНомеклатуры.Назначение,
	|	ВЫРАЗИТЬ(СписокНомеклатуры.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения.ТипИзмеряемойВеличины КАК ТипИзмеряемойВеличины,
	|	ЕСТЬNULL(ОстаткиВПодразделении.КоличествоОстаток, 0) КАК Остаток,
	|	ЕСТЬNULL(МатериалыКОтгрузке.КОтгрузке, 0) КАК КПолучению,
	|	ЕСТЬNULL(ЗаказыМатериаловОстатки.ВРезерве, 0) КАК ВРезерве,
	
	//++ НЕ УТКА
	|	ЕСТЬNULL(ПотребностьПоМаршрутнымЛистам.ТекущаяПотребность, 0) КАК ПотребностьПоМаршрутнымЛистам,
	//-- НЕ УТКА
	
	|	ЕСТЬNULL(ОстаткиВПодразделении.КоличествоОстаток, 0)
	|		+ ЕСТЬNULL(МатериалыКОтгрузке.КОтгрузке, 0) 
	//++ НЕ УТКА
	|		- ЕСТЬNULL(ПотребностьПоМаршрутнымЛистам.ТекущаяПотребность, 0)
	//-- НЕ УТКА
	|	КАК ТекущийИзлишекДефицит,
	
	|	ЕСТЬNULL(ЗаказыМатериаловОстатки.Заказано, 0) 
	|		- ЕСТЬNULL(МатериалыКОтгрузке.КОтгрузке, 0) КАК ОжидаетсяКПолучению,
	
	//++ НЕ УТКА
	|	ЕСТЬNULL(ПотребностьПоЗаказамНаПроизводство.Количество, 
	//-- НЕ УТКА
	|		0
	//++ НЕ УТКА
	|		) 
	//-- НЕ УТКА
	|		КАК ПотребностьПоЗаказамНаПроизводство,
	
	|	ЕСТЬNULL(ОстаткиВПодразделении.КоличествоОстаток, 0)
	|		+ ЕСТЬNULL(ЗаказыМатериаловОстатки.Заказано, 0)
	//++ НЕ УТКА
	|		- ЕСТЬNULL(ПотребностьПоМаршрутнымЛистам.ТекущаяПотребность, 0)
	|		- ЕСТЬNULL(ПотребностьПоЗаказамНаПроизводство.Количество, 0)
	//-- НЕ УТКА
	|		КАК ОжидаемыйИзлишекДефицит,
	
	|	ВЫБОР
	|		КОГДА СписокНомеклатуры.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры)
	|				ИЛИ СписокНомеклатуры.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры)
	|				ИЛИ СписокНомеклатуры.Номенклатура.ИспользованиеХарактеристик = ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются
	|ИЗ
	|	СписокНомеклатуры КАК СписокНомеклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиВПодразделении КАК ОстаткиВПодразделении
	|		ПО (ОстаткиВПодразделении.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (ОстаткиВПодразделении.Характеристика = СписокНомеклатуры.Характеристика)
	|			И (ОстаткиВПодразделении.Назначение = СписокНомеклатуры.Назначение)
	|			И (ОстаткиВПодразделении.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (ОстаткиВПодразделении.Организация = СписокНомеклатуры.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЗаказыМатериаловОстатки КАК ЗаказыМатериаловОстатки
	|		ПО (ЗаказыМатериаловОстатки.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (ЗаказыМатериаловОстатки.Характеристика = СписокНомеклатуры.Характеристика)
	|			И (ЗаказыМатериаловОстатки.Назначение = СписокНомеклатуры.Назначение)
	|			И (ЗаказыМатериаловОстатки.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (ЗаказыМатериаловОстатки.Организация = СписокНомеклатуры.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ МатериалыКОтгрузке КАК МатериалыКОтгрузке
	|		ПО (МатериалыКОтгрузке.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (МатериалыКОтгрузке.Характеристика = СписокНомеклатуры.Характеристика)
	|			И (МатериалыКОтгрузке.Назначение = СписокНомеклатуры.Назначение)
	|			И (МатериалыКОтгрузке.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (МатериалыКОтгрузке.Организация = СписокНомеклатуры.Организация)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиМатериаловСклад
	|		ПО (НастройкаПередачиМатериаловСклад.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (НастройкаПередачиМатериаловСклад.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))
	|			И (НастройкаПередачиМатериаловСклад.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (&НеИспользоватьСклад ИЛИ НастройкаПередачиМатериаловСклад.Склад = &Склад)
	//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностьПоМаршрутнымЛистам КАК ПотребностьПоМаршрутнымЛистам
	|		ПО (ПотребностьПоМаршрутнымЛистам.Организация = СписокНомеклатуры.Организация)
	|			И (ПотребностьПоМаршрутнымЛистам.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (ПотребностьПоМаршрутнымЛистам.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (ПотребностьПоМаршрутнымЛистам.Характеристика = СписокНомеклатуры.Характеристика)
	|			И (ПотребностьПоМаршрутнымЛистам.Назначение = СписокНомеклатуры.Назначение)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПотребностьПоЗаказамНаПроизводство КАК ПотребностьПоЗаказамНаПроизводство
	|		ПО (ПотребностьПоЗаказамНаПроизводство.Организация = СписокНомеклатуры.Организация)
	|			И (ПотребностьПоЗаказамНаПроизводство.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (ПотребностьПоЗаказамНаПроизводство.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (ПотребностьПоЗаказамНаПроизводство.Характеристика = СписокНомеклатуры.Характеристика)
	|			И (ПотребностьПоЗаказамНаПроизводство.Назначение = СписокНомеклатуры.Назначение)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиМатериаловНоменклатура
	|		ПО (НастройкаПередачиМатериаловНоменклатура.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (НастройкаПередачиМатериаловНоменклатура.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (НастройкаПередачиМатериаловНоменклатура.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка))
	|			И (&НеИспользоватьСклад ИЛИ НастройкаПередачиМатериаловНоменклатура.Склад = &Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаПередачиМатериаловВПроизводство КАК НастройкаПередачиМатериаловХарактеристика
	|		ПО (НастройкаПередачиМатериаловХарактеристика.Подразделение = СписокНомеклатуры.Подразделение)
	|			И (НастройкаПередачиМатериаловХарактеристика.Номенклатура = СписокНомеклатуры.Номенклатура)
	|			И (НастройкаПередачиМатериаловХарактеристика.Характеристика = СписокНомеклатуры.Характеристика)
	|			И (&НеИспользоватьСклад ИЛИ НастройкаПередачиМатериаловХарактеристика.Склад = &Склад)
	//-- НЕ УТКА
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписокНомеклатуры.Номенклатура.Наименование,
	|	СписокНомеклатуры.Характеристика.Наименование";
	
	Если Параметры.ЗаказыПоДату <> '000101010000' Тогда
		УсловиеЗаказыМатериаловОстаткиКДате = 
		"(ЗаказыМатериаловОстатки.Распоряжение, ЗаказыМатериаловОстатки.КодСтроки) В (
		|	ВЫБРАТЬ
		|		ДанныеЗаказов.Распоряжение КАК Распоряжение,
		|		ДанныеЗаказов.КодСтроки КАК КодСтроки
		|	ИЗ	
		|		РегистрНакопления.ЗаказыМатериаловСУчетомКорректировок.Обороты(,,,
		|			(Распоряжение, КодСтрокиРаспоряжения) В (
		|				ВЫБРАТЬ
		|					Фильтр.Распоряжение,
		|					Фильтр.КодСтроки
		|				ИЗ
		|					ЗаказыМатериаловОстаткиПоРаспоряжениям КАК Фильтр)) КАК ДанныеЗаказов
		//++ НЕ УТКА
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ТаблицаЗаказНаПроизводствоМатериалы
		|			ПО ТаблицаЗаказНаПроизводствоМатериалы.Ссылка = ДанныеЗаказов.Распоряжение
		|				И ТаблицаЗаказНаПроизводствоМатериалы.КодСтроки = ДанныеЗаказов.КодСтрокиРаспоряжения
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Этапы КАК ТаблицаЭтапы
		|			ПО (ТаблицаЭтапы.Ссылка = ТаблицаЗаказНаПроизводствоМатериалы.Ссылка)
		|				И (ТаблицаЭтапы.КлючСвязи = ТаблицаЗаказНаПроизводствоМатериалы.КлючСвязиЭтапы)
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.ЭтапыГрафик КАК ТаблицаЭтапыГрафик
		|			ПО (ТаблицаЭтапыГрафик.Ссылка = ТаблицаЭтапы.Ссылка)
		|				И (ТаблицаЭтапыГрафик.КлючСвязиПродукция = ТаблицаЭтапы.КлючСвязиПродукция)
		|				И (ТаблицаЭтапыГрафик.КлючСвязиЭтапы = ТаблицаЭтапы.КлючСвязи)
		//-- НЕ УТКА
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказМатериаловВПроизводство.Товары КАК ТаблицаЗаказМатериаловМатериалы
		|			ПО ТаблицаЗаказМатериаловМатериалы.Ссылка = ДанныеЗаказов.Распоряжение
		|				И ТаблицаЗаказМатериаловМатериалы.КодСтроки = ДанныеЗаказов.КодСтрокиРаспоряжения
		|	СГРУППИРОВАТЬ ПО
		|		ДанныеЗаказов.Распоряжение,
		|		ДанныеЗаказов.КодСтроки
		|	ИМЕЮЩИЕ
		|		МИНИМУМ(
		//++ НЕ УТКА
		|					ЕСТЬNULL(ТаблицаЭтапыГрафик.НачалоПредварительногоБуфера, 
		|					ЕСТЬNULL(ТаблицаЗаказНаПроизводствоМатериалы.ДатаПотребности, 
		//-- НЕ УТКА
		|					ЕСТЬNULL(ТаблицаЗаказМатериаловМатериалы.ДатаОтгрузки, ДАТАВРЕМЯ(1, 1, 1))
		//++ НЕ УТКА
		|					))
		//-- НЕ УТКА
		|				) <= &ЗаказыПоДатуКонецДня)";
	Иначе
		УсловиеЗаказыМатериаловОстаткиКДате = "ИСТИНА";
	КонецЕсли; 
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеЗаказыМатериаловОстаткиКДате", УсловиеЗаказыМатериаловОстаткиКДате);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Подразделение", Параметры.Подразделение);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("Склад", Параметры.Склад);
	Запрос.УстановитьПараметр("НеИспользоватьПодразделение", Параметры.Подразделение.Пустая());
	Запрос.УстановитьПараметр("НеИспользоватьОрганизацию", Параметры.Организация.Пустая());
	Запрос.УстановитьПараметр("НеИспользоватьСклад", Параметры.Склад.Пустая());
	Запрос.УстановитьПараметр("ЗаказыПоДатуКонецДня", ?(Параметры.ЗаказыПоДату <> '000101010000', КонецДня(Параметры.ЗаказыПоДату), '000101010000'));
	
	Результат = Запрос.ВыполнитьПакет();
	
	ЗаказыМатериаловПоСкладам = Результат[Результат.ВГраница()-1].Выгрузить();
	
	ТаблицаПолучениеИВозвратМатериалов = Результат[Результат.ВГраница()].Выгрузить();
	ТаблицаПолучениеИВозвратМатериалов.Колонки.Добавить("ОстатокНаСкладе", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 3));
	ТаблицаПолучениеИВозвратМатериалов.Колонки.Добавить("ИдентификаторСтроки", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 0));
	
	СкладПоУмолчанию = Справочники.Склады.СкладПоУмолчанию();
	
	СтрокиКУдалению = Новый Массив;
	Для каждого СтрокаТаблицы Из ТаблицаПолучениеИВозвратМатериалов Цикл
		
		// Удаление строк по которым нет данных
		Если СтрокаТаблицы.Остаток = 0
			И СтрокаТаблицы.КПолучению = 0 
			И СтрокаТаблицы.ТекущийИзлишекДефицит = 0 
			И СтрокаТаблицы.ОжидаетсяКПолучению = 0 
			И СтрокаТаблицы.ОжидаемыйИзлишекДефицит = 0
			//++ НЕ УТКА
			И СтрокаТаблицы.ОснованиеДляПолучения <> Перечисления.ОснованияДляПолученияМатериаловВПроизводстве.ПоЗаказуНаВнутреннееПотребление 
			//-- НЕ УТКА
			Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицы);
			Продолжить;
		КонецЕсли; 
		
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Склад) Тогда
			СтрокаТаблицы.Склад = СкладПоУмолчанию;
		КонецЕсли; 
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СкладПоУмолчанию) Тогда
			СтрокаТаблицы.СкладПоУмолчанию = СкладПоУмолчанию;
		КонецЕсли; 
		
		Если НЕ Параметры.Склад.Пустая() И СтрокаТаблицы.Склад <> Параметры.Склад Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицы);
		КонецЕсли; 
		
	КонецЦикла; 
	Для каждого СтрокаТаблицы Из СтрокиКУдалению Цикл
		ТаблицаПолучениеИВозвратМатериалов.Удалить(СтрокаТаблицы);
	КонецЦикла; 
	
	// Формирование таблицы аналитики остатков в списке "Получение и возврат".
	ТаблицаДляОбеспечения = Новый ТаблицаЗначений();
	ТаблицаДляОбеспечения.Колонки.Добавить("Номенклатура",   Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаДляОбеспечения.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаДляОбеспечения.Колонки.Добавить("Склад",          Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаДляОбеспечения.Колонки.Добавить("Назначение",     Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	ТаблицаДляОбеспечения.Колонки.Добавить("Подразделение",  Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	ТаблицаДляОбеспечения.Колонки.Добавить("Идентификатор",  ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 0));
	ТаблицаДляОбеспечения.Колонки.Добавить("ИдентификаторСтроки", ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 0));
	ТаблицаДляОбеспечения.Колонки.Добавить("КПолучению",     ОбщегоНазначенияУТ.ПолучитьОписаниеТиповЧисла(15, 3));
	ТаблицаДляОбеспечения.Индексы.Добавить("ИдентификаторСтроки");
	
	Идентификатор = 1;
	Для Каждого СтрокаТаблицы Из ТаблицаПолучениеИВозвратМатериалов Цикл
		СтрокаТаблицы.ИдентификаторСтроки = Идентификатор;
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Склад) Тогда
			СтруктураПоиска = Новый Структура("Номенклатура,Характеристика,Назначение,Подразделение,Организация");
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицы);
   			СписокСтрок = ЗаказыМатериаловПоСкладам.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаЗаказМатериала Из СписокСтрок Цикл
				НоваяСтрока = ТаблицаДляОбеспечения.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
				НоваяСтрока.ИдентификаторСтроки = СтрокаТаблицы.ИдентификаторСтроки;
				НоваяСтрока.Идентификатор = Идентификатор;
				НоваяСтрока.Склад = СтрокаЗаказМатериала.Склад;
				Идентификатор = Идентификатор + 1;
			КонецЦикла; 
			Если СписокСтрок.Количество() = 0 Тогда
				Идентификатор = Идентификатор + 1;
			КонецЕсли; 
		Иначе
			НоваяСтрока = ТаблицаДляОбеспечения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.ИдентификаторСтроки = СтрокаТаблицы.ИдентификаторСтроки;
			НоваяСтрока.Идентификатор = Идентификатор;
			Идентификатор = Идентификатор + 1;
		КонецЕсли; 
	КонецЦикла;
	
	// Получение остатков склада.
	ТаблицаОстатков = СвободныйОстатокНаСкладе(ТаблицаДляОбеспечения);
	
	// Загрузка остатков в список "Получение и возврат".
	Для Каждого СтрокаТаблицы Из ТаблицаОстатков Цикл
		СтрокаОбеспечения = ТаблицаДляОбеспечения.Найти(СтрокаТаблицы.Идентификатор, "Идентификатор");
		СтрокаПолучениеИВозврат = ТаблицаПолучениеИВозвратМатериалов.Найти(СтрокаОбеспечения.ИдентификаторСтроки, "ИдентификаторСтроки");
		СтрокаПолучениеИВозврат.ОстатокНаСкладе = СтрокаПолучениеИВозврат.ОстатокНаСкладе + СтрокаТаблицы.Количество;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТаблицаПолучениеИВозвратМатериалов, АдресХранилища);
	
КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОснованииСписокДокументыПроизводства(КомандыСоздатьНаОсновании) Экспорт
	
	СписокКоманд = Документы.ПередачаМатериаловВПроизводство.ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании);
	Для каждого Команда Из СписокКоманд Цикл
		Если Команда <> Неопределено Тогда
			Команда.ИмяСписка = "ДокументыПроизводства";	
		КонецЕсли; 
	КонецЦикла; 

	СписокКоманд = Документы.ВозвратМатериаловИзПроизводства.ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании);
	Для каждого Команда Из СписокКоманд Цикл
		Если Команда <> Неопределено Тогда
			Команда.ИмяСписка = "ДокументыПроизводства";	
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОснованииСписокЗаказыНаПередачуМатериалов(КомандыСоздатьНаОсновании) Экспорт
	
	СписокКоманд = Документы.ЗаказМатериаловВПроизводство.ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании);
	
	Для каждого Команда Из СписокКоманд Цикл
		Если Команда <> Неопределено Тогда
			Команда.ИмяСписка = "ЗаказыНаПередачуМатериалов";	
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СвободныйОстатокНаСкладе(ТаблицаДляОбеспечения) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТаблицаДляОбеспечения", ТаблицаДляОбеспечения);
	Запрос.Текст =
		// Строки списка "Получение и возврат".
		"ВЫБРАТЬ
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Назначение     КАК Назначение,
		|	Таблица.Подразделение  КАК Подразделение,
		|	Таблица.Идентификатор  КАК Идентификатор,
		|	Таблица.КПолучению     КАК Количество
		|ПОМЕСТИТЬ ВтТовары
		|ИЗ
		|	&ТаблицаДляОбеспечения КАК Таблица
		|
		|;
		|/////////////////////////////////////////////////////////
		// Аналитика потребностей строк списка "Получение и возврат".
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Назначение     КАК Назначение,
		|	Таблица.Подразделение  КАК Подразделение
		|ПОМЕСТИТЬ ВтТоварыПодразделений
		|ИЗ
		|	ВтТовары КАК Таблица
		|ИНДЕКСИРОВАТЬ ПО
		|	Подразделение,
		|	Номенклатура, Характеристика, Склад, Назначение
		|
		|;
		|/////////////////////////////////////////////////////////
		// Материалы заказов заданной аналитики
		|ВЫБРАТЬ
		|	Набор.Подразделение         КАК Подразделение,
		|	Набор.Распоряжение          КАК Распоряжение,
		|
		|	Набор.Номенклатура          КАК Номенклатура,
		|	Набор.Характеристика        КАК Характеристика,
		|	Набор.Склад                 КАК Склад,
		|	Набор.Назначение            КАК Назначение,
		|
		|	СУММА(Набор.КоличествоОборот) КАК Количество,
		|
		|	ВЫБОР КОГДА Набор.ВариантОбеспечения В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ                           КАК ЗарезервированоНаСкладе,
		|
		|	ВЫБОР КОГДА Набор.ВариантОбеспечения В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно)) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ                           КАК ЗарезервированоКДате,
		|
		|	ВЫБОР КОГДА Набор.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов) ТОГДА
		|				Набор.ДатаПотребности
		|			КОНЕЦ                       КАК ДатаОтгрузки
		|
		|ПОМЕСТИТЬ ВтПотребностиПодразделений
		|ИЗ(
		// Материалы заказов на производство заданной аналитики.
		|	ВЫБРАТЬ
		|		РегистрЗаказы.Подразделение         КАК Подразделение,
		|		РегистрЗаказы.Распоряжение          КАК Распоряжение,
		|
		|		РегистрЗаказы.Номенклатура          КАК Номенклатура,
		|		РегистрЗаказы.Характеристика        КАК Характеристика,
		|		РегистрЗаказы.Склад                 КАК Склад,
		|		РегистрЗаказы.Назначение            КАК Назначение,
		|
		|		РегистрЗаказы.КоличествоОборот      КАК КоличествоОборот,
		|
		|		РегистрЗаказы.ВариантОбеспечения    КАК ВариантОбеспечения,
		|		РегистрЗаказы.ДатаПотребности       КАК ДатаПотребности
		|	ИЗ
		|		РегистрНакопления.ЗаказыМатериаловСУчетомКорректировок.Обороты(,,,
		|			(Номенклатура, Характеристика, Склад, Назначение, Подразделение) В(
		|				ВЫБРАТЬ
		|					Фильтр.Номенклатура   КАК Номенклатура,
		|					Фильтр.Характеристика КАК Характеристика,
		|					Фильтр.Склад          КАК Склад,
		|					Фильтр.Назначение     КАК Назначение,
		|					Фильтр.Подразделение  КАК Подразделение
		|				ИЗ
		|					ВтТоварыПодразделений КАК Фильтр)) КАК РегистрЗаказы
		// Материалы заказов на внутреннее потребление заданной аналитики.
		// ...
		|) КАК Набор
		|СГРУППИРОВАТЬ ПО
		|	Набор.Подразделение, Набор.Распоряжение,
		|	Набор.Номенклатура, Набор.Характеристика, Набор.Склад, Набор.Назначение,
		|
		|	ВЫБОР КОГДА Набор.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов) ТОГДА
		|			Набор.ДатаПотребности
		|		КОНЕЦ,
		|
		|	ВЫБОР КОГДА Набор.ВариантОбеспечения В(
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ,
		|
		|	ВЫБОР КОГДА Набор.ВариантОбеспечения В (
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов),
		|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно)) ТОГДА
		|				ИСТИНА
		|			ИНАЧЕ
		|				ЛОЖЬ
		|		КОНЕЦ
		|
		|;
		|///////////////////////////////////////////////////////////
		// Материалы заказов на внутреннее потреление заданной аналитики.
		|ВЫБРАТЬ
		|	Таблица.Номенклатура   КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад          КАК Склад,
		|	Таблица.Назначение     КАК Назначение,
		|	Таблица.Подразделение  КАК Подразделение,
		|	Таблица.Идентификатор  КАК Идентификатор,
		|
		|	ТаблицаПотребностей.ДатаОтгрузки КАК ДатаОтгрузки,
		|
		|	ЕСТЬNULL(ТаблицаПотребностей.ЗарезервированоНаСкладе, ЛОЖЬ) КАК ЗарезервированоНаСкладе,
		|	ЕСТЬNULL(ТаблицаПотребностей.ЗарезервированоКДате,    ЛОЖЬ) КАК ЗарезервированоКДате,
		|
		|	МАКСИМУМ(ЕСТЬNULL(ТаблицаПотребностей.Количество, 0))
		|		- СУММА(ВЫБОР КОГДА ТаблицаПотребностей.ЗарезервированоНаСкладе И НЕ РегистрКОтгрузке.КОтгрузке ЕСТЬ NULL ТОГДА // вычет отгруженного.
		|					РегистрКОтгрузке.КОтгрузке
		|				ИНАЧЕ
		|					0
		|			КОНЕЦ)         КАК Количество
		|ИЗ
		|	ВтТовары КАК Таблица
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтПотребностиПодразделений КАК ТаблицаПотребностей
		|		ПО ТаблицаПотребностей.Номенклатура     = Таблица.Номенклатура
		|		 И ТаблицаПотребностей.Характеристика   = Таблица.Характеристика
		|		 И ТаблицаПотребностей.Склад            = Таблица.Склад
		|		 И ТаблицаПотребностей.Назначение       = Таблица.Назначение
		|		 И ТаблицаПотребностей.Подразделение    = Таблица.Подразделение
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке КАК РегистрКОтгрузке
		|		ПО РегистрКОтгрузке.ДокументОтгрузки = ТаблицаПотребностей.Распоряжение
		|		 И РегистрКОтгрузке.Номенклатура     = ТаблицаПотребностей.Номенклатура
		|		 И РегистрКОтгрузке.Характеристика   = ТаблицаПотребностей.Характеристика
		|		 И РегистрКОтгрузке.Склад            = ТаблицаПотребностей.Склад
		|		 И РегистрКОтгрузке.Назначение       = ТаблицаПотребностей.Назначение
		|		 И РегистрКОтгрузке.Получатель       = ТаблицаПотребностей.Подразделение
		|		 И РегистрКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		 И РегистрКОтгрузке.КОтгрузке <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Идентификатор, Таблица.Подразделение,
		|	Таблица.Номенклатура, Таблица.Характеристика, Таблица.Склад, Таблица.Назначение,
		|	ТаблицаПотребностей.ДатаОтгрузки, ТаблицаПотребностей.Распоряжение,
		|	ЕСТЬNULL(ТаблицаПотребностей.ЗарезервированоНаСкладе, ЛОЖЬ),
		|	ЕСТЬNULL(ТаблицаПотребностей.ЗарезервированоКДате, ЛОЖЬ)
		|УПОРЯДОЧИТЬ ПО
		|	Таблица.Идентификатор";
	
	ТаблицаПотребностей = Запрос.Выполнить().Выгрузить();
	
	ТаблицаСостояниеОбеспечения = ОбеспечениеСервер.СостояниеОбеспеченияНаличием(ТаблицаПотребностей);
	
	Возврат ТаблицаСостояниеОбеспечения;
	
КонецФункции

#КонецОбласти

#КонецЕсли