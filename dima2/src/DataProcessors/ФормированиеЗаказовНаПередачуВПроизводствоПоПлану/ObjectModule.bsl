#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ПериодПоступленияНачало = '000101010000' И ПериодПоступленияОкончание = '000101010000'
		И ПериодЗаказаНачало = '000101010000' И ПериодЗаказаОкончание = '000101010000' Тогда
		
		ТекстСообщения = НСтр("ru='Необходимо указать либо период заказа, либо период поступления.';uk='Необхідно вказати період замовлення, або період надходження.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ПериодПоступленияНачало",, Отказ); 
		
	КонецЕсли; 
	
	Для каждого РеквизитТЧ Из Метаданные().ТабличныеЧасти.ФормированиеЗаказов.Реквизиты Цикл
		Если РеквизитТЧ.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ФормированиеЗаказов." + РеквизитТЧ.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ РежимЗаполненияДанных Тогда
		
		ШаблонСообщения = НСтр("ru='Не заполнена колонка ""%1"" в строке %2.';uk='Не заповнена колонка ""%1"" в рядку %2.'");
		Для каждого СтрокаДанныеЗаказа Из ФормированиеЗаказов Цикл
			Если НЕ СтрокаДанныеЗаказа.ОтметитьКЗаказу ИЛИ СтрокаДанныеЗаказа.КоличествоУпаковок = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Для каждого РеквизитТЧ Из Метаданные().ТабличныеЧасти.ФормированиеЗаказов.Реквизиты Цикл
				Если РеквизитТЧ.ПроверкаЗаполнения = ПроверкаЗаполнения.ВыдаватьОшибку 
					И НЕ ЗначениеЗаполнено(СтрокаДанныеЗаказа[РеквизитТЧ.Имя]) Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												ШаблонСообщения, 
												РеквизитТЧ.Синоним,
												СтрокаДанныеЗаказа.НомерСтроки);
												
					Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ФормированиеЗаказов", СтрокаДанныеЗаказа.НомерСтроки, РеквизитТЧ.Имя);
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
