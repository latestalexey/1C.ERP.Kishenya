
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.ТипЗначения = Новый ОписаниеТипов;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеЕдиницыИзмеренияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипыЗначения", Объект.ТипЗначения);
	ПараметрыФормы.Вставить("Количество", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикОповещенияОВыбореПризнака", ЭтаФорма, "Количество");
	ОткрытьФорму("ОбщаяФорма.ВыборНастроекПризнакаУчета", ПараметрыФормы, ,,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнениеВалютыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипыЗначения", Объект.ТипЗначения);
	ПараметрыФормы.Вставить("Валюта", Истина);
	
	Оповещение = Новый ОписаниеОповещения("ОбработчикОповещенияОВыбореПризнака", ЭтаФорма, "Валюта");
	ОткрытьФорму("ОбщаяФорма.ВыборНастроекПризнакаУчета", ПараметрыФормы, ,,,, Оповещение);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_АналитикиСтатейБюджетов", , Объект.Ссылка);

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастроитьТипАналитики(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипЗначения", Объект.ТипЗначения);
	ПараметрыФормы.Вставить("ДополнительноеСвойство", Объект.ДополнительноеСвойство);
	
	ОписаниеОповещение = Новый ОписаниеОповещения("ОбработкаНастройкиТипаАналитики", ЭтотОбъект);
	ОткрытьФорму("ПланВидовХарактеристик.АналитикиСтатейБюджетов.Форма.НастройкаТипаАналитики", 
		ПараметрыФормы, ЭтаФорма, , , , ОписаниеОповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ОбщегоНазначенияУТКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаКлиенте
Процедура ОбработчикОповещенияОВыбореПризнака(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры = "Валюта" Тогда
		Объект.ПредставлениеВалюты = Результат.Представление;
		Объект.ЗаполнениеВалюты = Результат.Значение;
	Иначе
		Объект.ПредставлениеЕдиницыИзмерения = Результат.Представление;
		Объект.ЗаполнениеЕдиницыИзмерения = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ЗаполнениеЕдиницыИзмерения.Доступность = Форма.Объект.УчитыватьПоКоличеству;
	Форма.Элементы.ЗаполнениеЕдиницыИзмерения.АвтоотметкаНезаполненного = Форма.Объект.УчитыватьПоКоличеству;
	
	Форма.Элементы.ЗаполнениеВалюты.Доступность = Форма.Объект.УчитыватьПоВалюте;
	Форма.Элементы.ЗаполнениеВалюты.АвтоотметкаНезаполненного = Форма.Объект.УчитыватьПоВалюте;
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьПоКоличествуПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьПоВалютеПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНастройкиТипаАналитики(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Объект.ТипЗначения = Результат.ТипЗначения;
		Объект.ДополнительноеСвойство = Результат.ДополнительноеСвойство;
		ЗаполнитьПредставлениеТипаЗначения();
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПредставлениеТипаЗначения()
	
	Если ЗначениеЗаполнено(Объект.ДополнительноеСвойство) Тогда
		Объект.ПредставлениеТипаЗначения = Строка(Объект.ДополнительноеСвойство);
	Иначе
		Объект.ПредставлениеТипаЗначения = Строка(Объект.ТипЗначения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
