#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Имена реквизитов, от значений которых зависят параметры указания серий
//
//	Возвращаемое значение:
//		Строка - имена реквизитов, перечисленные через запятую
//
Функция ИменаРеквизитовДляЗаполненияПараметровУказанияСерий() Экспорт
	ИменаРеквизитов = "Подразделение,Дата";
	
	Возврат ИменаРеквизитов;
КонецФункции

//Возвращает параметры указания серий для товаров, указанных в документе
//
//	Параметры
//			Объект - Структура - структура значений реквизитов объекта, необходимых для заполнения параметров указания серий
//	Возвращаемое значение
//			Тип Структура
//				Состав полей задается в функции ОбработкаТабличнойЧастиКлиентСервер.ПараметрыУказанияСерий
//
Функция ПараметрыУказанияСерий(Объект) Экспорт
	
	ПараметрыСерий = СкладыСервер.ИспользованиеСерийВПодразделении(Объект.Подразделение);
	
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ПолноеИмяОбъекта = "Документ.РаспределениеПроизводственныхЗатрат";
	
	ПараметрыУказанияСерий.ИмяПоляСклад = "Подразделение";
	
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = ПараметрыСерий.ИспользоватьСерииНоменклатуры;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = ПараметрыСерий.УчитыватьСебестоимостьПоСериям;
	
	ПараметрыУказанияСерий.СкладскиеОперации.Добавить(Перечисления.СкладскиеОперации.СписаниеМатериаловНаЗатраты);
	
	ПараметрыУказанияСерий.ПоляСвязи.Добавить("Назначение");
	
	ПараметрыУказанияСерий.ЭтоНакладная = Истина;
	
	ПараметрыУказанияСерий.ТоварВШапке = Истина;
	ПараметрыУказанияСерий.ИмяТЧТовары = "";
	ПараметрыУказанияСерий.Дата = Объект.Дата;
	
	ПараметрыУказанияСерий.РегистрироватьСерии = Ложь;
	
	Возврат ПараметрыУказанияСерий;
	
КонецФункции

// Возвращает текст запроса для расчета статусов указания серий
//	Параметры:
//		ПараметрыУказанияСерий - Структура - состав полей задается в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаЗаполненияСтатусовУказанияСерий(ПараметрыУказанияСерий) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Количество,
	|	Товары.СтатусУказанияСерий,
	|	Товары.НомерСтроки
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	СУММА(Товары.Количество) КАК Количество,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ТоварыДляЗапроса
	|ИЗ
	|	Товары КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Серия,
	|	Товары.Номенклатура,
	|	Товары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Серии.Номенклатура,
	|	Серии.Характеристика,
	|	Серии.Количество
	|ПОМЕСТИТЬ Серии
	|ИЗ
	|	&Серии КАК Серии
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика,
	|	СУММА(ТаблицаСерий.Количество) КАК Количество
	|ПОМЕСТИТЬ СерииДляЗапроса
	|ИЗ
	|	Серии КАК ТаблицаСерий
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСерий.Номенклатура,
	|	ТаблицаСерий.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НомерСтроки КАК НомерСтроки,
	|	Товары.СтатусУказанияСерий КАК СтатусУказанияСерийСтарый,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям)
	|			ТОГДА ВЫБОР
	|					КОГДА Товары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий)
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|						ТОГДА 10
	|					ИНАЧЕ 9
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УправлениеПоFEFOОстаткамиСерий)
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|						ТОГДА 6
	|					ИНАЧЕ 5
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.СправочноеУказаниеСерий)
	|				И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПриОтраженииЗатратНаПроизводство
	|			ТОГДА ВЫБОР
	|					КОГДА ТоварыДляЗапроса.Количество = ЕСТЬNULL(СерииДляЗапроса.Количество, 0)
	|						ТОГДА 2
	|					ИНАЧЕ 1
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий
	|ПОМЕСТИТЬ Статусы
	|ИЗ
	|	Товары КАК Товары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыДляЗапроса КАК ТоварыДляЗапроса
	|			ЛЕВОЕ СОЕДИНЕНИЕ СерииДляЗапроса КАК СерииДляЗапроса
	|			ПО ТоварыДляЗапроса.Номенклатура = СерииДляЗапроса.Номенклатура
	|				И ТоварыДляЗапроса.Характеристика = СерииДляЗапроса.Характеристика
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|			ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|				И ТоварыДляЗапроса.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|		ПО Товары.Номенклатура = ТоварыДляЗапроса.Номенклатура
	|			И Товары.Характеристика = ТоварыДляЗапроса.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Статусы.НомерСтроки КАК НомерСтроки,
	|	Статусы.СтатусУказанияСерий КАК СтатусУказанияСерий
	|ИЗ
	|	Статусы КАК Статусы
	|ГДЕ
	|	Статусы.СтатусУказанияСерий <> Статусы.СтатусУказанияСерийСтарый
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса для заполнения рабочего места по распределению материалов и работ на себестоимость продукции
//
//	Возвращаемое значение:
//		Строка - текст запроса
//
Функция ТекстЗапросаЗаполнитьПроизводственныеЗатраты() Экспорт
	
	Возврат "
	|ВЫБРАТЬ
	|	ОстаткиОбороты.Организация,
	|	ОстаткиОбороты.Номенклатура,
	|	ОстаткиОбороты.Характеристика,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям)
	|			ТОГДА ОстаткиОбороты.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ОстаткиОбороты.Подразделение,
	|	СУММА(ОстаткиОбороты.НачальныйОстаток) КАК НачальныйОстаток,
	|	СУММА(ОстаткиОбороты.Поступило) КАК Поступило,
	|	СУММА(ОстаткиОбороты.Норматив) КАК Норматив,
	|	СУММА(ОстаткиОбороты.Отклонение) КАК Отклонение,
	|	СУММА(ОстаткиОбороты.КонечныйОстаток) КАК КонечныйОстаток,
	|	СУММА(ОстаткиОбороты.Реализовано) КАК Реализовано,
	|	СУММА(ОстаткиОбороты.Передано) КАК Передано,
	|	ОстаткиОбороты.Назначение
	|ПОМЕСТИТЬ ВТОстаткиОбороты
	|ИЗ
	|	(ВЫБРАТЬ
	|		НачальныйОстаток.Организация,
	|		НачальныйОстаток.Номенклатура,
	|		НачальныйОстаток.Характеристика,
	|		НачальныйОстаток.Серия,
	|		НачальныйОстаток.Подразделение,
	|		НачальныйОстаток.КоличествоОстаток КАК НачальныйОстаток,
	|		0 КАК Поступило,
	|		0 КАК Норматив,
	|		0 КАК Отклонение,
	|		0 КАК КонечныйОстаток,
	|		0 КАК Реализовано,
	|		0 КАК Передано,
	|		НачальныйОстаток.Назначение КАК Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(
	|				&НачалоПериода,
	|				Организация В (&Организация)
	|					И (Подразделение В (&Подразделение)
	|						ИЛИ &ВсеПодразделения)) КАК НачальныйОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КонечныйОстаток.Организация,
	|		КонечныйОстаток.Номенклатура,
	|		КонечныйОстаток.Характеристика,
	|		КонечныйОстаток.Серия,
	|		КонечныйОстаток.Подразделение,
	|		0,
	|		0,
	|		0,
	|		0,
	|		КонечныйОстаток.КоличествоОстаток,
	|		0,
	|		0,
	|		КонечныйОстаток.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве.Остатки(
	|				&ГраницаОкончаниеПериода,
	|				Организация В (&Организация)
	|					И (Подразделение В (&Подразделение)
	|						ИЛИ &ВсеПодразделения)) КАК КонечныйОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗатратыОбороты.Организация,
	|		ЗатратыОбороты.Номенклатура,
	|		ЗатратыОбороты.Характеристика,
	|		ЗатратыОбороты.Серия,
	|		ЗатратыОбороты.Подразделение,
	|		0,
	|		СУММА(ЗатратыОбороты.КоличествоПриход),
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЗатратыОбороты.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(
	|				&НачалоПериода,
	|				&ОкончаниеПериода,
	|				Регистратор,
	|				Организация В (&Организация)
	|					И (Подразделение В (&Подразделение)
	|						ИЛИ &ВсеПодразделения)) КАК ЗатратыОбороты
	|	ГДЕ
	|		(ЗатратыОбороты.КоличествоПриход > 0
	|				ИЛИ ЗатратыОбороты.Регистратор ССЫЛКА Документ.ВыпускПродукции
	|					И ЗатратыОбороты.КоличествоПриход < 0)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗатратыОбороты.Организация,
	|		ЗатратыОбороты.Номенклатура,
	|		ЗатратыОбороты.Характеристика,
	|		ЗатратыОбороты.Серия,
	|		ЗатратыОбороты.Подразделение,
	|		ЗатратыОбороты.Назначение
	|	
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗатратыОбороты.Организация,
	|		ЗатратыОбороты.Номенклатура,
	|		ЗатратыОбороты.Характеристика,
	|		ЗатратыОбороты.Серия,
	|		ЗатратыОбороты.Подразделение,
	|		0,
	|		0,
	|		СУММА(ЗатратыОбороты.Количество),
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЗатратыОбороты.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК ЗатратыОбороты
	|	ГДЕ
	|		ЗатратыОбороты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ЗатратыОбороты.Организация В (&Организация)
	|		И (ЗатратыОбороты.Подразделение В (&Подразделение) ИЛИ &ВсеПодразделения)
	|		И ЗатратыОбороты.Количество < 0
	|		И ЗатратыОбороты.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗатратыОбороты.Организация,
	|		ЗатратыОбороты.Номенклатура,
	|		ЗатратыОбороты.Характеристика,
	|		ЗатратыОбороты.Серия,
	|		ЗатратыОбороты.Подразделение,
	|		ЗатратыОбороты.Назначение
	//-- НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗатратыСписанияПоНормативам.Организация,
	|		ЗатратыСписанияПоНормативам.Номенклатура,
	|		ЗатратыСписанияПоНормативам.Характеристика,
	|		ЗатратыСписанияПоНормативам.Серия,
	|		ЗатратыСписанияПоНормативам.Подразделение,
	|		0,
	|		0,
	|		СУММА(ЗатратыСписанияПоНормативам.КоличествоРасход),
	|		0,
	|		0,
	|		0,
	|		0,
	|		ЗатратыСписанияПоНормативам.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве.Обороты(
	|				&НачалоПериода,
	|				&ОкончаниеПериода,
	|				Регистратор,
	|				Организация В (&Организация)
	|					И (Подразделение В (&Подразделение)
	|						ИЛИ &ВсеПодразделения)) КАК ЗатратыСписанияПоНормативам
	|	ГДЕ
	|		ЗатратыСписанияПоНормативам.Регистратор ССЫЛКА Документ.СписаниеЗатратНаВыпуск
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗатратыСписанияПоНормативам.Организация,
	|		ЗатратыСписанияПоНормативам.Номенклатура,
	|		ЗатратыСписанияПоНормативам.Характеристика,
	|		ЗатратыСписанияПоНормативам.Серия,
	|		ЗатратыСписанияПоНормативам.Подразделение,
	|		ЗатратыСписанияПоНормативам.Назначение
	|	
	//++ НЕ УТКА
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МЛ.Организация,
	|		ТЧ.Номенклатура,
	|		ТЧ.Характеристика,
	|		ТЧ.Серия,
	|		МЛ.Подразделение,
	|		0,
	|		0,
	|		СУММА(ТЧ.Количество),
	|		СУММА(ТЧ.КоличествоОтклонение),
	|		0,
	|		0,
	|		0,
	|		ТЧ.Назначение
	|	ИЗ
	|		Документ.МаршрутныйЛистПроизводства.МатериалыИУслуги КАК ТЧ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МЛ
	|			ПО ТЧ.Ссылка = МЛ.Ссылка
	|				И (МЛ.Организация В (&Организация))
	|				И (МЛ.Подразделение В (&Подразделение)
	|					ИЛИ &ВсеПодразделения)
	|				И (МЛ.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполняется), ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен))
	|					ИЛИ (МЛ.УправлениеМаршрутнымиЛистами = ЗНАЧЕНИЕ(Перечисление.УправлениеМаршрутнымиЛистами.ПооперационноеПланирование)
	|					ИЛИ МЛ.УправлениеМаршрутнымиЛистами = ЗНАЧЕНИЕ(Перечисление.УправлениеМаршрутнымиЛистами.РегистрацияОпераций))
	|					И МЛ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.КВыполнению))
	|				И (ТЧ.Количество + ТЧ.КоличествоОтклонение > 0)
	|				И (ТЧ.ДатаРасхода МЕЖДУ &НачалоПериода И &ОкончаниеПериода)
	|				И (МЛ.Проведен)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		МЛ.Организация,
	|		ТЧ.Номенклатура,
	|		ТЧ.Характеристика,
	|		ТЧ.Серия,
	|		МЛ.Подразделение,
	|		ТЧ.Назначение
	//-- НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Затраты.Организация,
	|		Затраты.Номенклатура,
	|		Затраты.Характеристика,
	|		Затраты.Серия,
	|		Затраты.Подразделение,
	|		0,
	|		СУММА(-Затраты.Количество),
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		Затраты.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|	ГДЕ
	|		Затраты.Организация В (&Организация)
	|		И (Затраты.Подразделение В (&Подразделение)
	|				ИЛИ &ВсеПодразделения)
	|		И Затраты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И Затраты.Регистратор ССЫЛКА Документ.ВозвратМатериаловИзПроизводства
	|		И Затраты.Количество > 0
	|		И Затраты.Активность
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Затраты.Организация,
	|		Затраты.Номенклатура,
	|		Затраты.Характеристика,
	|		Затраты.Серия,
	|		Затраты.Подразделение,
	|		Затраты.Назначение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Затраты.Организация,
	|		Затраты.Номенклатура,
	|		Затраты.Характеристика,
	|		Затраты.Серия,
	|		Затраты.Подразделение,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СУММА(Затраты.Количество),
	|		0,
	|		Затраты.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|	ГДЕ
	|		Затраты.Организация В (&Организация)
	|		И (Затраты.Подразделение В (&Подразделение)
	|				ИЛИ &ВсеПодразделения)
	|		И Затраты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Затраты.ЗаказНаПроизводство = НЕОПРЕДЕЛЕНО
	|		И Затраты.АналитикаУчетаПродукции = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|		И Затраты.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|		И Затраты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И НЕ Затраты.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
	|		И НЕ Затраты.Регистратор ССЫЛКА Документ.ВозвратМатериаловИзПроизводства
	|		И НЕ Затраты.Регистратор ССЫЛКА Документ.ПеремещениеМатериаловВПроизводстве
	|		И Затраты.Количество > 0
	|		И НЕ Затраты.Регистратор ССЫЛКА Документ.СписаниеЗатратНаВыпуск
	|		И Затраты.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|		И Затраты.Активность
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Затраты.Организация,
	|		Затраты.Номенклатура,
	|		Затраты.Характеристика,
	|		Затраты.Серия,
	|		Затраты.Подразделение,
	|		Затраты.Назначение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Затраты.Организация,
	|		Затраты.Номенклатура,
	|		Затраты.Характеристика,
	|		Затраты.Серия,
	|		Затраты.Подразделение,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		0,
	|		СУММА(Затраты.Количество),
	|		Затраты.Назначение
	|	ИЗ
	|		РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|	ГДЕ
	|		Затраты.Организация В (&Организация)
	|		И (Затраты.Подразделение В (&Подразделение)
	|				ИЛИ &ВсеПодразделения)
	|		И Затраты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Затраты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И Затраты.Регистратор ССЫЛКА Документ.ПеремещениеМатериаловВПроизводстве
	|		И Затраты.Количество > 0
	|		И Затраты.Активность
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Затраты.Организация,
	|		Затраты.Номенклатура,
	|		Затраты.Характеристика,
	|		Затраты.Серия,
	|		Затраты.Подразделение,
	|		Затраты.Назначение
	|	) КАК ОстаткиОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = ОстаткиОбороты.Подразделение)
	|			И (ПолитикиУчетаСерий.Ссылка = ОстаткиОбороты.Номенклатура.ВидНоменклатуры)
	|
	|ГДЕ
	|	ОстаткиОбороты.Подразделение ССЫЛКА Справочник.СтруктураПредприятия
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиОбороты.Организация,
	|	ОстаткиОбороты.Номенклатура,
	|	ОстаткиОбороты.Характеристика,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям)
	|			ТОГДА ОстаткиОбороты.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ОстаткиОбороты.Подразделение,
	|	ОстаткиОбороты.Назначение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДокументРаспределения.Организация КАК Организация,
	|	ДокументРаспределения.Номенклатура КАК Номенклатура,
	|	ДокументРаспределения.Характеристика КАК Характеристика,
	|	ДокументРаспределения.Серия КАК Серия,
	|	ДокументРаспределения.Подразделение КАК Подразделение,
	|	ДокументРаспределения.Ссылка КАК Документ,
	|	ДокументРаспределения.КоличествоФакт КАК ФактОстаток,
	|	ДокументРаспределения.КоличествоКРаспределениюПоПравилу КАК РаспределеноПоБазе,
	|	СУММА(ЕСТЬNULL(ТЧЭтапыГрафикаПроизводства.Количество, 0)) КАК РаспределеноНаПроизводство,
	|	СУММА(0) КАК СписаноНаРасходы,
	|	ДокументРаспределения.Назначение
	|ПОМЕСТИТЬ ВТДокументРаспределения
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДокументРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК ТЧЭтапыГрафикаПроизводства
	|		ПО ДокументРаспределения.Ссылка = ТЧЭтапыГрафикаПроизводства.Ссылка
	|ГДЕ
	|	ДокументРаспределения.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДокументРаспределения.Организация В (&Организация)
	|	И (ДокументРаспределения.Подразделение В (&Подразделение)
	|			ИЛИ &ВсеПодразделения)
	|	И ДокументРаспределения.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументРаспределения.Организация,
	|	ДокументРаспределения.Номенклатура,
	|	ДокументРаспределения.Характеристика,
	|	ДокументРаспределения.Серия,
	|	ДокументРаспределения.Подразделение,
	|	ДокументРаспределения.Ссылка,
	|	ДокументРаспределения.КоличествоФакт,
	|	ДокументРаспределения.КоличествоКРаспределениюПоПравилу,
	|	ДокументРаспределения.Назначение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРаспределения.Организация,
	|	ДокументРаспределения.Номенклатура,
	|	ДокументРаспределения.Характеристика,
	|	ДокументРаспределения.Серия,
	|	ДокументРаспределения.Подразделение,
	|	ДокументРаспределения.Ссылка,
	|	ДокументРаспределения.КоличествоФакт,
	|	ДокументРаспределения.КоличествоКРаспределениюПоПравилу,
	|	СУММА(ЕСТЬNULL(ТЧВыпускиБезРаспоряжения.Количество, 0)),
	|	СУММА(0),
	|	ДокументРаспределения.Назначение
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДокументРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ВыпускиБезРаспоряжения КАК ТЧВыпускиБезРаспоряжения
	|		ПО ДокументРаспределения.Ссылка = ТЧВыпускиБезРаспоряжения.Ссылка
	|ГДЕ
	|	ДокументРаспределения.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДокументРаспределения.Организация В (&Организация)
	|	И (ДокументРаспределения.Подразделение В (&Подразделение)
	|			ИЛИ &ВсеПодразделения)
	|	И ДокументРаспределения.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументРаспределения.Организация,
	|	ДокументРаспределения.Номенклатура,
	|	ДокументРаспределения.Характеристика,
	|	ДокументРаспределения.Серия,
	|	ДокументРаспределения.Подразделение,
	|	ДокументРаспределения.Ссылка,
	|	ДокументРаспределения.КоличествоФакт,
	|	ДокументРаспределения.КоличествоКРаспределениюПоПравилу,
	|	ДокументРаспределения.Назначение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРаспределения.Организация,
	|	ДокументРаспределения.Номенклатура,
	|	ДокументРаспределения.Характеристика,
	|	ДокументРаспределения.Серия,
	|	ДокументРаспределения.Подразделение,
	|	ДокументРаспределения.Ссылка,
	|	ДокументРаспределения.КоличествоФакт,
	|	ДокументРаспределения.КоличествоКРаспределениюПоПравилу,
	|	СУММА(0),
	|	СУММА(ЕСТЬNULL(ТЧПрочиеРасходы.Количество, 0)),
	|	ДокументРаспределения.Назначение
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДокументРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПрочиеРасходы КАК ТЧПрочиеРасходы
	|		ПО ДокументРаспределения.Ссылка = ТЧПрочиеРасходы.Ссылка
	|ГДЕ
	|	ДокументРаспределения.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ДокументРаспределения.Организация В (&Организация)
	|	И (ДокументРаспределения.Подразделение В (&Подразделение)
	|			ИЛИ &ВсеПодразделения)
	|	И ДокументРаспределения.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	ДокументРаспределения.Организация,
	|	ДокументРаспределения.Номенклатура,
	|	ДокументРаспределения.Характеристика,
	|	ДокументРаспределения.Серия,
	|	ДокументРаспределения.Подразделение,
	|	ДокументРаспределения.Ссылка,
	|	ДокументРаспределения.КоличествоФакт,
	|	ДокументРаспределения.КоличествоКРаспределениюПоПравилу,
	|	ДокументРаспределения.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДокументРаспределения.Организация КАК Организация,
	|	ВТДокументРаспределения.Номенклатура КАК Номенклатура,
	|	ВТДокументРаспределения.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям)
	|			ТОГДА ВТДокументРаспределения.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ВТДокументРаспределения.Подразделение КАК Подразделение,
	|	ВТДокументРаспределения.Документ,
	|	ВТДокументРаспределения.ФактОстаток,
	|	СУММА(ВТДокументРаспределения.РаспределеноНаПроизводство)
	|			   + ВТДокументРаспределения.РаспределеноПоБазе КАК РаспределеноНаПроизводство,
	|	СУММА(ВТДокументРаспределения.СписаноНаРасходы) КАК СписаноНаРасходы,
	|	ВТДокументРаспределения.Назначение
	|ПОМЕСТИТЬ ДокументРаспределения
	|ИЗ
	|	ВТДокументРаспределения КАК ВТДокументРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = ВТДокументРаспределения.Подразделение)
	|			И (ПолитикиУчетаСерий.Ссылка = ВТДокументРаспределения.Номенклатура.ВидНоменклатуры)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДокументРаспределения.Организация,
	|	ВТДокументРаспределения.Номенклатура,
	|	ВТДокументРаспределения.Характеристика,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям)
	|			ТОГДА ВТДокументРаспределения.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВТДокументРаспределения.Подразделение,
	|	ВТДокументРаспределения.Документ,
	|	ВТДокументРаспределения.ФактОстаток,
	|	ВТДокументРаспределения.РаспределеноПоБазе,
	|	ВТДокументРаспределения.Назначение
	|
	|ИМЕЮЩИЕ 
	|	СУММА(ВТДокументРаспределения.СписаноНаРасходы) + ВТДокументРаспределения.ФактОстаток + СУММА(ВТДокументРаспределения.РаспределеноНаПроизводство) <> 0
	|	ИЛИ ВТДокументРаспределения.РаспределеноПоБазе <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиОбороты.Организация КАК Организация,
	|	ОстаткиОбороты.Номенклатура КАК Номенклатура,
	|	ОстаткиОбороты.Характеристика КАК Характеристика,
	|	ОстаткиОбороты.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ОстаткиОбороты.Серия КАК Серия,
	|	ОстаткиОбороты.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(ДокументРаспределения.Документ, ЗНАЧЕНИЕ(Документ.РаспределениеПроизводственныхЗатрат.ПустаяСсылка)) КАК Документ,
	|	ОстаткиОбороты.НачальныйОстаток,
	|	ОстаткиОбороты.Поступило КАК Поступило,
	|	ОстаткиОбороты.Норматив КАК Норматив,
	|	ОстаткиОбороты.Отклонение КАК Отклонение,
	|	ОстаткиОбороты.Реализовано КАК Реализовано,
	|	ОстаткиОбороты.Передано КАК Передано,
	|	ОстаткиОбороты.КонечныйОстаток КАК КонечныйОстаток,
	|	ЕСТЬNULL(ДокументРаспределения.ФактОстаток, 0) КАК ФактОстаток,
	|	ЕСТЬNULL(ДокументРаспределения.СписаноНаРасходы, 0) КАК СписаноНаРасходы,
	|	ЕСТЬNULL(ДокументРаспределения.РаспределеноНаПроизводство, 0) КАК РаспределеноНаПроизводство,
	|	ОстаткиОбороты.Назначение
	|ПОМЕСТИТЬ ВТТаблицаДанных
	|ИЗ
	|	ВТОстаткиОбороты КАК ОстаткиОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДокументРаспределения КАК ДокументРаспределения
	|		ПО ОстаткиОбороты.Организация = ДокументРаспределения.Организация
	|			И ОстаткиОбороты.Номенклатура = ДокументРаспределения.Номенклатура
	|			И ОстаткиОбороты.Характеристика = ДокументРаспределения.Характеристика
	|			И ОстаткиОбороты.Серия = ДокументРаспределения.Серия
	|			И ОстаткиОбороты.Подразделение = ДокументРаспределения.Подразделение
	|			И ОстаткиОбороты.Назначение = ДокументРаспределения.Назначение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументРаспределения.Организация,
	|	ДокументРаспределения.Номенклатура,
	|	ДокументРаспределения.Характеристика,
	|	ДокументРаспределения.Номенклатура.ВидНоменклатуры,
	|	ДокументРаспределения.Серия,
	|	ДокументРаспределения.Подразделение,
	|	ДокументРаспределения.Документ,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ДокументРаспределения.ФактОстаток,
	|	ДокументРаспределения.СписаноНаРасходы,
	|	ДокументРаспределения.РаспределеноНаПроизводство,
	|	ДокументРаспределения.Назначение
	|ИЗ
	|	ДокументРаспределения КАК ДокументРаспределения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиОбороты КАК ОстаткиОбороты
	|		ПО ДокументРаспределения.Организация = ОстаткиОбороты.Организация
	|			И ДокументРаспределения.Номенклатура = ОстаткиОбороты.Номенклатура
	|			И ДокументРаспределения.Характеристика = ОстаткиОбороты.Характеристика
	|			И ДокументРаспределения.Серия = ОстаткиОбороты.Серия
	|			И ДокументРаспределения.Подразделение = ОстаткиОбороты.Подразделение
	|			И ДокументРаспределения.Назначение = ОстаткиОбороты.Назначение
	|ГДЕ
	|	ОстаткиОбороты.Номенклатура ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Материалы.Ссылка.Организация КАК Организация,
	|	Материалы.Номенклатура,
	|	Материалы.Характеристика,
	|	СУММА(Материалы.Количество) КАК Количество,
	|	Материалы.Подразделение,
	|	Материалы.Назначение,
	|	Материалы.Серия
	|ПОМЕСТИТЬ ВТЛимитыНЗП
	|ИЗ
	|	Документ.ИзделияИЗатратыНЗП.МатериалыИУслуги КАК Материалы
	|ГДЕ
	|	Материалы.Ссылка.Проведен
	|	И Материалы.Ссылка.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Материалы.Ссылка.Организация В (&Организация)
	|	И (Материалы.Подразделение В (&Подразделение)
	|			ИЛИ &ВсеПодразделения)
	|
	|СГРУППИРОВАТЬ ПО
	|	Материалы.Ссылка.Организация,
	|	Материалы.Номенклатура,
	|	Материалы.Характеристика,
	|	Материалы.Подразделение,
	|	Материалы.Назначение,
	|	Материалы.Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.Документ КАК Документ,
	|	ТаблицаДанных.Номенклатура КАК Номенклатура,
	|	СпрНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВЫБОР
	|		КОГДА СпрНоменклатура.ИспользованиеХарактеристик В (ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры),
	|															ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры),
	|															ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаДанных.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|					ТОГДА ЛОЖЬ
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ТаблицаДанных.Характеристика КАК Характеристика,
	|	ТаблицаДанных.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УчетСебестоимостиПоСериям)
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаДанных.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|						ТОГДА 14
	|					ИНАЧЕ 13
	|				КОНЕЦ
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УправлениеОстаткамиСерий)
	|			ТОГДА 9
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.ТипПолитики = ЗНАЧЕНИЕ(Перечисление.ТипыПолитикУказанияСерий.УправлениеПоFEFOОстаткамиСерий)
	|			ТОГДА 5
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СтатусУказанияСерий,
	|	ТаблицаДанных.Назначение,
	|	ТаблицаДанных.Организация КАК Организация,
	|	ТаблицаДанных.Подразделение КАК Подразделение,
	|	ТаблицаДанных.НачальныйОстаток КАК НачальныйОстаток,
	|	ТаблицаДанных.Поступило КАК Поступило,
	|	ТаблицаДанных.Норматив КАК Норматив,
	|	ТаблицаДанных.Отклонение КАК Отклонение,
	|	ТаблицаДанных.Реализовано КАК Реализовано,
	|	ТаблицаДанных.СписаноНаРасходы КАК СписаноНаРасходы,
	|	ТаблицаДанных.Передано КАК Передано,
	|	ТаблицаДанных.РаспределеноНаПроизводство КАК РаспределеноНаПроизводство,
	|	ТаблицаДанных.ФактОстаток КАК ФактОстаток,
	|	ТаблицаДанных.КонечныйОстаток - ТаблицаДанных.ФактОстаток КАК ВОбработке,
	|	ЕСТЬNULL(ВТЛимитыНЗП.Количество, 0) КАК ЛимитВОбработке,
	|	ЕСТЬNULL(ТаблицаДанных.Поступило + ТаблицаДанных.НачальныйОстаток - ТаблицаДанных.Норматив - ТаблицаДанных.Отклонение - ТаблицаДанных.ФактОстаток - ТаблицаДанных.Реализовано - ТаблицаДанных.Передано - ТаблицаДанных.СписаноНаРасходы - ТаблицаДанных.РаспределеноНаПроизводство, 0) КАК НеРаспределено
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	ВТТаблицаДанных КАК ТаблицаДанных
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ТаблицаДанных.Номенклатура = СпрНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|		ПО (ПолитикиУчетаСерий.Склад = ТаблицаДанных.Подразделение)
	|			И (ПолитикиУчетаСерий.Ссылка = ТаблицаДанных.ВидНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЛимитыНЗП КАК ВТЛимитыНЗП
	|		ПО ТаблицаДанных.Организация = ВТЛимитыНЗП.Организация
	|			И ТаблицаДанных.Номенклатура = ВТЛимитыНЗП.Номенклатура
	|			И ТаблицаДанных.Характеристика = ВТЛимитыНЗП.Характеристика
	|			И ТаблицаДанных.Серия = ВТЛимитыНЗП.Серия
	|			И ТаблицаДанных.Подразделение = ВТЛимитыНЗП.Подразделение
	|			И ТаблицаДанных.Назначение = ВТЛимитыНЗП.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.Документ,
	|	ТаблицаДанных.Номенклатура,
	|	ТаблицаДанных.ТипНоменклатуры,
	|	ТаблицаДанных.ХарактеристикиИспользуются,
	|	ТаблицаДанных.Характеристика,
	|	ТаблицаДанных.Серия,
	|	ТаблицаДанных.СтатусУказанияСерий,
	|	ТаблицаДанных.Назначение,
	|	ТаблицаДанных.Организация,
	|	ТаблицаДанных.Подразделение,
	|	ТаблицаДанных.НачальныйОстаток,
	|	ТаблицаДанных.Поступило,
	|	ТаблицаДанных.Норматив,
	|	ТаблицаДанных.Отклонение,
	|	ТаблицаДанных.Реализовано,
	|	ТаблицаДанных.СписаноНаРасходы,
	|	ТаблицаДанных.Передано,
	|	ТаблицаДанных.РаспределеноНаПроизводство,
	|	ТаблицаДанных.ФактОстаток,
	|	ТаблицаДанных.ВОбработке,
	|	ТаблицаДанных.ЛимитВОбработке,
	|	ТаблицаДанных.НеРаспределено,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.НеРаспределено < 0
	|			ТОГДА ТаблицаДанных.НеРаспределено
	|		ИНАЧЕ ТаблицаДанных.НеРаспределено - ТаблицаДанных.ЛимитВОбработке
	|	КОНЕЦ КАК КРаспределению,
	|	ВЫБОР
	|		КОГДА ТаблицаДанных.НеРаспределено < 0
	|				ИЛИ ТаблицаДанных.НеРаспределено - ТаблицаДанных.ЛимитВОбработке > 0
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|;
	|";
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);
	
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка                         КАК Ссылка,
	|	ДанныеДокумента.Дата                           КАК Дата,
	|	ДанныеДокумента.Организация                    КАК Организация,
	|	ДанныеДокумента.Подразделение                  КАК Склад,
	|	ДанныеДокумента.Номенклатура                   КАК Номенклатура,
	|	ДанныеДокумента.Характеристика                 КАК Характеристика,
	|	ДанныеДокумента.Серия                          КАК Серия,
	|	ДанныеДокумента.Назначение                     КАК Назначение,
	|	ДанныеДокумента.СтатусУказанияСерий            КАК СтатусУказанияСерий,
	|	ДанныеДокумента.Номенклатура.ТипНоменклатуры   КАК ТипНоменклатуры,
    |	ДанныеДокумента.НалоговоеНазначениеРасходов    КАК НалоговоеНазначениеРасходов,
	|	ДанныеДокумента.Количество                     КАК Количество,
	|	ДанныеДокумента.КоличествоФакт                 КАК КоличествоФакт
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период",               Реквизиты.Дата);
	Запрос.УстановитьПараметр("Организация",          Реквизиты.Организация);
	Запрос.УстановитьПараметр("Номенклатура",         Реквизиты.Номенклатура);
	Запрос.УстановитьПараметр("Характеристика",       Реквизиты.Характеристика);
	Запрос.УстановитьПараметр("Количество",           Реквизиты.Количество);
	Запрос.УстановитьПараметр("Назначение",           Реквизиты.Назначение);
	Запрос.УстановитьПараметр("Серия",                Реквизиты.Серия);
	Запрос.УстановитьПараметр("СтатусУказанияСерий",  Реквизиты.СтатусУказанияСерий);
	Запрос.УстановитьПараметр("ТипНоменклатуры",      Реквизиты.ТипНоменклатуры);
    Запрос.УстановитьПараметр("НалоговоеНазначениеРасходов", Реквизиты.НалоговоеНазначениеРасходов);
	Запрос.УстановитьПараметр("Склад",                Реквизиты.Склад);
	Запрос.УстановитьПараметр("Подразделение",        Реквизиты.Склад);

КонецПроцедуры

Процедура ИнициализироватьКлючиАналитикиНоменклатуры(Запрос)
	
	Если Запрос.Параметры.Свойство("КлючиАналитикиУчетаНоменклатурыИнициализированы") Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросАналитик = Новый Запрос;
	ЗапросАналитик.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаМатериала.Номенклатура   КАК Номенклатура,
	|	АналитикаМатериала.Характеристика КАК Характеристика,
	|	АналитикаМатериала.Склад          КАК Склад,
	|	АналитикаМатериала.Серия          КАК Серия
	|ИЗ
	|	(ВЫБРАТЬ
	|		&Номенклатура   КАК Номенклатура,
	|		&Характеристика КАК Характеристика,
	|		&Склад          КАК Склад,
	|		&Серия          КАК Серия
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Выпуски.Номенклатура,
	|		Выпуски.Характеристика,
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка),
	|		Выпуски.Серия
	|	ИЗ
	|		Документ.РаспределениеПроизводственныхЗатрат.ВыпускиБезРаспоряжения КАК Выпуски
	|	ГДЕ
	|		Выпуски.Ссылка = &Ссылка) КАК АналитикаМатериала
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|		ПО  АналитикаМатериала.Номенклатура   = Аналитика.Номенклатура
	|		  И АналитикаМатериала.Характеристика = Аналитика.Характеристика
	|		  И АналитикаМатериала.Склад          = Аналитика.Склад
	|		  И АналитикаМатериала.Серия          = Аналитика.Серия
	|ГДЕ
	|	Аналитика.Номенклатура ЕСТЬ NULL ";
	
	ЗапросАналитик.УстановитьПараметр("Номенклатура",   Запрос.Параметры.Номенклатура);
	ЗапросАналитик.УстановитьПараметр("Характеристика", Запрос.Параметры.Характеристика);
	ЗапросАналитик.УстановитьПараметр("Склад",          Запрос.Параметры.Склад);
	ЗапросАналитик.УстановитьПараметр("Серия",          Запрос.Параметры.Серия);
	ЗапросАналитик.УстановитьПараметр("Ссылка",         Запрос.Параметры.Ссылка);
	
	Выборка = ЗапросАналитик.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьКлючАналитики(Выборка);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("КлючиАналитикиУчетаНоменклатурыИнициализированы", Истина);
	
КонецПроцедуры

Процедура УстановитьПараметрЗапросаАналитикаУчетаНоменклатуры(Запрос)
	
	Если Запрос.Параметры.Свойство("АналитикаУчетаНоменклатуры") Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	
	АналитикаУчетаНоменклатуры = РегистрыСведений.АналитикаУчетаНоменклатуры.ЗначениеКлючаАналитики(Запрос.Параметры);
	Запрос.УстановитьПараметр("АналитикаУчетаНоменклатуры", АналитикаУчетаНоменклатуры);
	
КонецПроцедуры

Функция ТекстЗапросаТаблицаМатериалыИРаботыВПроизводстве(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "МатериалыИРаботыВПроизводстве";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ИнициализироватьКлючиАналитикиНоменклатуры(Запрос);
	УстановитьПараметрЗапросаАналитикаУчетаНоменклатуры(Запрос);
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаЭтапыСерии", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаЭтапыСерии(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаВыпускиСерии", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаВыпускиСерии(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	Если НЕ ПроведениеСервер.ЕстьТаблицаЗапроса("ВтТаблицаРасходыСерии", ТекстыЗапроса) Тогда
		ТекстЗапросаВтТаблицаРасходыСерии(Запрос, ТекстыЗапроса);
	КонецЕсли;
	
	ТекстЗапроса =
	// 1. Серии не используются или серии для себестоимости
	// 1.1 Расход на этапы графика производства
	"ВЫБРАТЬ
	|	0 КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Этапы.НомерСтроки,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Номенклатура КАК Номенклатура,
	|	&Характеристика КАК Характеристика,
	|	&Серия КАК Серия,
	|	&Назначение КАК Назначение,
	|	&АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Этапы.Количество,
	|	Этапы.СтатьяКалькуляции,
	|	Этапы.ЗаказНаПроизводство,
	|	Этапы.ЗаказНаПроизводство.НалоговоеНазначение КАК НалоговоеНазначение,
	|	Этапы.КодСтрокиПродукция,
	|	Этапы.Этап,
	|	Значение(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|	Значение(Справочник.РесурсныеСпецификации.ПустаяСсылка) КАК Спецификация,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК Этапы
	|ГДЕ
	|	Этапы.Ссылка = &Ссылка
	|	И &СтатусУказанияСерий Не В (5,6,9,10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 1.2 Расход на выпуски без распоряжений
	|ВЫБРАТЬ
	|	1,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	Выпуски.НомерСтроки,
	|	&Период,
	|	&Организация,
	|	&Подразделение КАК Подразделение,
	|	&Номенклатура КАК Номенклатура,
	|	&Характеристика КАК Характеристика,
	|	&Серия КАК Серия,
	|	&Назначение КАК Назначение,
	|	&АналитикаУчетаНоменклатуры,
	|	Выпуски.Количество,
	|	Выпуски.СтатьяКалькуляции,
	|	ЕСТЬNULL(ДокВыпуска.Ссылка, НЕОПРЕДЕЛЕНО),
	|	ЕСТЬNULL(ДокВыпуска.НалоговоеНазначение, НЕОПРЕДЕЛЕНО) КАК НалоговоеНазначение,
	|	Выпуски.КодСтроки,
	|	НЕОПРЕДЕЛЕНО,
	|	АналитикаПродукции.КлючАналитики,
	|	Выпуски.Спецификация,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ВыпускиБезРаспоряжения КАК Выпуски
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПродукции
	|		ПО Выпуски.Номенклатура = АналитикаПродукции.Номенклатура
	|			И Выпуски.Характеристика = АналитикаПродукции.Характеристика
	|			И АналитикаПродукции.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			И АналитикаПродукции.Серия = Выпуски.Серия
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции КАК ДокВыпуска
	|		ПО Выпуски.ДокументВыпуска = ДокВыпуска.Ссылка
	|
	|ГДЕ
	|	Выпуски.Ссылка = &Ссылка
	|	И &СтатусУказанияСерий Не В (5,6,9,10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 1.3 Расход на прочие расходы
	|ВЫБРАТЬ
	|	2,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ПрочиеРасходы.НомерСтроки,
	|	&Период,
	|	&Организация,
	|	&Подразделение КАК Подразделение,
	|	&Номенклатура КАК Номенклатура,
	|	&Характеристика КАК Характеристика,
	|	&Серия КАК Серия,
	|	&Назначение КАК Назначение,
	|	&АналитикаУчетаНоменклатуры,
	|	ПрочиеРасходы.Количество,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
    |   &НалоговоеНазначениеРасходов КАК НалоговоеНазначение,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ПрочиеРасходы.СтатьяРасходов,
	|	ПрочиеРасходы.АналитикаРасходов,
	|	&Подразделение КАК ПодразделениеПолучатель
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ПрочиеРасходы КАК ПрочиеРасходы
	|ГДЕ
	|	ПрочиеРасходы.Ссылка = &Ссылка
	|	И &СтатусУказанияСерий Не В (5,6,9,10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 2. Учет остатков по сериям
	// 2.1 Расход на этапы графика производства
	|ВЫБРАТЬ
	|	3,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	0,
	|	&Период,
	|	&Организация,
	|	&Подразделение КАК Подразделение,
	|	&Номенклатура КАК Номенклатура,
	|	&Характеристика КАК Характеристика,
	|	ТаблицаЭтапыСерии.Серия,
	|	&Назначение КАК Назначение,
	|	&АналитикаУчетаНоменклатуры,
	|	ТаблицаЭтапыСерии.Количество,
	|	ТаблицаЭтапыСерии.СтатьяКалькуляции,
	|	ТаблицаЭтапыСерии.ЗаказНаПроизводство,
	|	ТаблицаЭтапыСерии.ЗаказНаПроизводство.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТаблицаЭтапыСерии.КодСтрокиПродукция,
	|	ТаблицаЭтапыСерии.Этап,
	|	Значение(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаПродукции,
	|	Значение(Справочник.РесурсныеСпецификации.ПустаяСсылка) КАК Спецификация,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель
	|ИЗ
	|	ВтТаблицаЭтапыСерии КАК ТаблицаЭтапыСерии
	|ГДЕ
	|	&СтатусУказанияСерий В (5,6,9,10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 2.2 Расход на выпуски без распоряжений
	|ВЫБРАТЬ
	|	3,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	0,
	|	&Период,
	|	&Организация,
	|	&Подразделение КАК Подразделение,
	|	&Номенклатура КАК Номенклатура,
	|	&Характеристика КАК Характеристика,
	|	ТаблицаВыпускиСерии.Серия,
	|	&Назначение КАК Назначение,
	|	&АналитикаУчетаНоменклатуры,
	|	ТаблицаВыпускиСерии.Количество,
	|	ТаблицаВыпускиСерии.СтатьяКалькуляции,
	|	ТаблицаВыпускиСерии.ДокументВыпуска,
	|	ТаблицаВыпускиСерии.ДокументВыпуска.НалоговоеНазначение КАК НалоговоеНазначение,
	|	ТаблицаВыпускиСерии.КодСтроки,
	|	НЕОПРЕДЕЛЕНО,
	|	АналитикаПродукции.КлючАналитики,
	|	ТаблицаВыпускиСерии.Спецификация,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеПолучатель
	|ИЗ
	|	ВтТаблицаВыпускиСерии КАК ТаблицаВыпускиСерии
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаПродукции
	|		ПО ТаблицаВыпускиСерии.Номенклатура = АналитикаПродукции.Номенклатура
	|			И ТаблицаВыпускиСерии.Характеристика = АналитикаПродукции.Характеристика
	|			И ТаблицаВыпускиСерии.Серия = АналитикаПродукции.Серия
	|			И АналитикаПродукции.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|ГДЕ
	|	&СтатусУказанияСерий В (5,6,9,10)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// 2.3 Расход на прочие расходы
	|ВЫБРАТЬ
	|	2,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	0,
	|	&Период,
	|	&Организация,
	|	&Подразделение КАК Подразделение,
	|	&Номенклатура КАК Номенклатура,
	|	&Характеристика КАК Характеристика,
	|	ТаблицаРасходыСерии.Серия КАК Серия,
	|	&Назначение КАК Назначение,
	|	&АналитикаУчетаНоменклатуры,
	|	ТаблицаРасходыСерии.Количество,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
    |   &НалоговоеНазначениеРасходов КАК НалоговоеНазначение,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаРасходыСерии.СтатьяРасходов,
	|	ТаблицаРасходыСерии.АналитикаРасходов,
	|	&Подразделение КАК ПодразделениеПолучатель
	|ИЗ
	|	ВтТаблицаРасходыСерии КАК ТаблицаРасходыСерии
	|ГДЕ
	|	&СтатусУказанияСерий В (5,6,9,10)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаДвиженияСерийТоваров(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ДвиженияСерийТоваров";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	&Номенклатура        КАК Номенклатура,
	|	&Характеристика      КАК Характеристика,
	|	&Назначение          КАК Назначение,
	|	&Серия               КАК Серия,
	|	&Количество          КАК Количество,
	|	&Подразделение       КАК Отправитель,
	|	НЕОПРЕДЕЛЕНО         КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты) КАК СкладскаяОперация,
	|	&Ссылка              КАК Документ,
	|	&Период              КАК Период,
	|	ИСТИНА               КАК ЭтоСкладскоеДвижение
	|ГДЕ
	|	&Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Номенклатура           КАК Номенклатура,
	|	&Характеристика         КАК Характеристика,
	|	&Назначение             КАК Назначение,
	|	ТаблицаСерии.Серия      КАК Серия,
	|	ТаблицаСерии.Количество КАК Количество,
	|	&Подразделение          КАК Отправитель,
	|	НЕОПРЕДЕЛЕНО            КАК Получатель,
	|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты) КАК СкладскаяОперация,
	|	&Ссылка                 КАК Документ,
	|	&Период                 КАК Период,
	|	ИСТИНА                  КАК ЭтоСкладскоеДвижение
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.Серии КАК ТаблицаСерии
	|ГДЕ
	|	ТаблицаСерии.Ссылка = &Ссылка";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура УстановитьПараметрыЗапросаТаблицыСерии(Запрос)
	
	Если Запрос.Параметры.Свойство("ТаблицаЭтапыСерии") Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЭтапыСерии = Новый ТаблицаЗначений;
	
	Если НЕ ПолучитьФункциональнуюОпцию("УправлениеПредприятием") Тогда
		ТаблицаЭтапыСерии.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(10)));
	//++ НЕ УТ
	Иначе
		ТаблицаЭтапыСерии.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство"));
	//-- НЕ УТ
	КонецЕсли;
	
	ТаблицаЭтапыСерии.Колонки.Добавить("КодСтрокиПродукция",  Новый ОписаниеТипов("Число"));
	ТаблицаЭтапыСерии.Колонки.Добавить("Этап",                Новый ОписаниеТипов("СправочникСсылка.ЭтапыПроизводства"));
	ТаблицаЭтапыСерии.Колонки.Добавить("СтатьяКалькуляции",   Новый ОписаниеТипов("СправочникСсылка.СтатьиКалькуляции"));
	ТаблицаЭтапыСерии.Колонки.Добавить("Количество",          Новый ОписаниеТипов("Число"));
	ТаблицаЭтапыСерии.Колонки.Добавить("Серия",               Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	
	ТаблицаВыпускиСерии = Новый ТаблицаЗначений;
	ТаблицаВыпускиСерии.Колонки.Добавить("ДокументВыпуска",   Новый ОписаниеТипов("ДокументСсылка.ВыпускПродукции"));
	ТаблицаВыпускиСерии.Колонки.Добавить("КодСтроки",         Новый ОписаниеТипов("Число"));
	ТаблицаВыпускиСерии.Колонки.Добавить("СтатьяКалькуляции", Новый ОписаниеТипов("СправочникСсылка.СтатьиКалькуляции"));
	ТаблицаВыпускиСерии.Колонки.Добавить("Количество",        Новый ОписаниеТипов("Число"));
	ТаблицаВыпускиСерии.Колонки.Добавить("Спецификация",      Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	ТаблицаВыпускиСерии.Колонки.Добавить("Серия",             Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ТаблицаВыпускиСерии.Колонки.Добавить("Номенклатура",      Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаВыпускиСерии.Колонки.Добавить("Характеристика",    Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
    
	ТаблицаРасходыСерии = Новый ТаблицаЗначений;
	ТаблицаРасходыСерии.Колонки.Добавить("СтатьяРасходов",    Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.СтатьиРасходов"));
	ТаблицаРасходыСерии.Колонки.Добавить("АналитикаРасходов", Метаданные.ПланыВидовХарактеристик.СтатьиРасходов.Тип);
	ТаблицаРасходыСерии.Колонки.Добавить("Количество",        Новый ОписаниеТипов("Число"));
	ТаблицаРасходыСерии.Колонки.Добавить("Серия",             Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
    
	
	ЗапросСерии = Новый Запрос;
	ЗапросСерии.Текст = 
	"ВЫБРАТЬ
	|	Серии.Количество,
	|	Серии.Серия
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.Серии КАК Серии
	|ГДЕ
	|	Серии.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Этапы.ЗаказНаПроизводство,
	|	Этапы.КодСтрокиПродукция,
	|	Этапы.Этап,
	|	Этапы.СтатьяКалькуляции,
	|	Этапы.Количество
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК Этапы
	|ГДЕ
	|	Этапы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Выпуски.СтатьяКалькуляции,
	|	Выпуски.Количество,
	|	Выпуски.ДокументВыпуска,
	|	Выпуски.КодСтроки,
	|	Выпуски.Спецификация,
	|	Выпуски.Номенклатура,
	|	Выпуски.Характеристика
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ВыпускиБезРаспоряжения КАК Выпуски
	|ГДЕ
	|	Выпуски.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расходы.Количество,
	|	Расходы.СтатьяРасходов,
	|	Расходы.АналитикаРасходов
	|ИЗ
	|	Документ.РаспределениеПроизводственныхЗатрат.ПрочиеРасходы КАК Расходы
	|ГДЕ
	|	Расходы.Ссылка = &Ссылка";
	
	ЗапросСерии.УстановитьПараметр("Ссылка", Запрос.Параметры.Ссылка);
	
	Результат = ЗапросСерии.ВыполнитьПакет();
	
	Серии = Результат[0].Выгрузить();
	Коэффициенты = Серии.ВыгрузитьКолонку("Количество");
	
	ЭтапыГрафикаПроизводства = Результат[1].Выбрать();
	ВыпускиБезРаспоряжения   = Результат[2].Выбрать();
	ПрочиеРасходы            = Результат[3].Выбрать();
	
	Если Коэффициенты.Количество() > 0 Тогда
		
		Пока ЭтапыГрафикаПроизводства.Следующий() Цикл
			
			Распределение = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ЭтапыГрафикаПроизводства.Количество, Коэффициенты, 3);
			
			Индекс = 0;
			Для Каждого Строка Из Распределение Цикл
				НоваяСтрока = ТаблицаЭтапыСерии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭтапыГрафикаПроизводства);
				НоваяСтрока.Серия = Серии[Индекс].Серия;
				НоваяСтрока.Количество = Строка;
				Индекс = Индекс + 1;
			КонецЦикла;
			
		КонецЦикла;
		
		Пока ВыпускиБезРаспоряжения.Следующий() Цикл
			
			Распределение = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ВыпускиБезРаспоряжения.Количество, Коэффициенты, 3);
			
			Индекс = 0;
			Для Каждого Строка Из Распределение Цикл
				НоваяСтрока = ТаблицаВыпускиСерии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыпускиБезРаспоряжения);
				НоваяСтрока.Серия = Серии[Индекс].Серия;
				НоваяСтрока.Количество = Строка;
				Индекс = Индекс + 1;
			КонецЦикла;
			
		КонецЦикла;
		
		Пока ПрочиеРасходы.Следующий() Цикл
			
			Распределение = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(ПрочиеРасходы.Количество, Коэффициенты, 3);
			
			Индекс = 0;
			Для Каждого Строка Из Распределение Цикл
				НоваяСтрока = ТаблицаРасходыСерии.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ПрочиеРасходы);
				НоваяСтрока.Серия = Серии[Индекс].Серия;
				НоваяСтрока.Количество = Строка;
				Индекс = Индекс + 1;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТаблицаЭтапыСерии",   ТаблицаЭтапыСерии);
	Запрос.УстановитьПараметр("ТаблицаВыпускиСерии", ТаблицаВыпускиСерии);
	Запрос.УстановитьПараметр("ТаблицаРасходыСерии", ТаблицаРасходыСерии);
	
КонецПроцедуры

Функция ТекстЗапросаВтТаблицаЭтапыСерии(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаЭтапыСерии";
	
	УстановитьПараметрыЗапросаТаблицыСерии(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаЭтапыСерии.ЗаказНаПроизводство,
	|	ТаблицаЭтапыСерии.КодСтрокиПродукция,
	|	ТаблицаЭтапыСерии.Этап,
	|	ТаблицаЭтапыСерии.СтатьяКалькуляции,
	|	ТаблицаЭтапыСерии.Количество,
	|	ТаблицаЭтапыСерии.Серия
	|ПОМЕСТИТЬ ВтТаблицаЭтапыСерии
	|ИЗ
	|	&ТаблицаЭтапыСерии КАК ТаблицаЭтапыСерии";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаВыпускиСерии(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаВыпускиСерии";
	
	УстановитьПараметрыЗапросаТаблицыСерии(Запрос);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаВыпускиСерии.ДокументВыпуска,
	|	ТаблицаВыпускиСерии.КодСтроки,
	|	ТаблицаВыпускиСерии.Спецификация,
	|	ТаблицаВыпускиСерии.СтатьяКалькуляции,
	|	ТаблицаВыпускиСерии.Количество,
	|	ТаблицаВыпускиСерии.Номенклатура,
	|	ТаблицаВыпускиСерии.Характеристика,
	|	ТаблицаВыпускиСерии.Серия
	|ПОМЕСТИТЬ ВтТаблицаВыпускиСерии
	|ИЗ
	|	&ТаблицаВыпускиСерии КАК ТаблицаВыпускиСерии";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтТаблицаРасходыСерии(Запрос, ТекстыЗапроса)
	
	ИмяРегистра = "ВтТаблицаРасходыСерии";
	
	УстановитьПараметрыЗапросаТаблицыСерии(Запрос);
    
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаРасходыСерии.СтатьяРасходов,
	|	ТаблицаРасходыСерии.АналитикаРасходов,
	|	ТаблицаРасходыСерии.Количество,
	|	ТаблицаРасходыСерии.Серия
	|ПОМЕСТИТЬ ВтТаблицаРасходыСерии
	|ИЗ
	|	&ТаблицаРасходыСерии КАК ТаблицаРасходыСерии";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ПроведениеПоРеглУчетуУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
#Область ТекстСписаниеМатериаловНаРасходы // (Дт 9X :: Кт 23)
	ТекстСписаниеМатериаловНаРасходы = "
	|ВЫБРАТЬ //// Списание на расходы товаров из незавершенного производства (Дт 9X :: Кт 23)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Операция.Номенклатура КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК АналитикаУчетаКт,
	|	Операция.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание на расходы товаров из незавершенного производства"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПроизводственныхЗатрат КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставОС),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставНМА),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаПрочиеЦели))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|ГДЕ
	|	СтатьиСтроительства.Ссылка ЕСТЬ NULL
	|";
#КонецОбласти
	
#Область ТекстСписаниеМатериаловНаВнеоборотныеАктивы // (Дт 151 :: Кт 23)
	ТекстСписаниеМатериаловНаВнеоборотныеАктивы = "
	|ВЫБРАТЬ //// Списание на прочие активы (Дт 151 :: Кт 23)
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ЕСТЬNULL(Стоимости.Сумма, Строки.Сумма) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.АналитикаРасходов КАК СубконтоДт1, // Объект строительства
	|	Строки.СтатьяРасходов КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК АналитикаУчетаКт,
	|	Операция.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Строки.Номенклатура КАК СубконтоКт1,
	|	ЕСТЬNULL(Стоимости.ГруппаПродукции, Строки.ГруппаПродукции) КАК СубконтоКт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.МатериальныеЗатраты) КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Списание на прочие активы"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПроизводственныхЗатрат КАК Операция
	|	ПО
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставОС),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставНМА),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаПрочиеЦели))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ВтСтоимости КАК Стоимости
	|	ПО
	|		Строки.Ссылка = Стоимости.Ссылка
	|		И Строки.Номенклатура = Стоимости.Номенклатура
	|		И Строки.Склад = Стоимости.Склад
	|		И Строки.ГруппаФинансовогоУчета = Стоимости.ГруппаФинансовогоУчета
	|		И Строки.ТипЗапасов = Стоимости.ТипЗапасов
	|		И Строки.Контрагент = Стоимости.Контрагент
	|		И Строки.СтатьяРасходов = Стоимости.СтатьяРасходов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО
	|		Строки.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы)
	|
	|";
#КонецОбласти

#Область ТекстКорректировкиНДСПартий // (Дт 9Х :: Кт 6435)
	ТекстКорректировкиНДСПартий = "
	|ВЫБРАТЬ //// Корректировка НДС партий (Дт 9Х :: Кт 6435)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
    |	КорректировкиНДСПартий.Сумма КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Строки.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	Операция.Номенклатура КАК СубконтоДт1,
	|	Строки.СтатьяРасходов КАК СубконтоДт2,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт, 
	|
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	НЕОПРЕДЕЛЕНО КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УсловнаяПродажа) КАК СчетКт,
	|
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Условная продажа"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПроизводственныхЗатрат КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
    |
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ВтСтроки КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|		И Строки.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		И Строки.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеТоваровПоТребованию),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставОС),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаВСоставНМА),
	|										  ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаНаПрочиеЦели))
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ 
	|		КорректировкиНДСПартий КАК КорректировкиНДСПартий
	|	ПО 
	|		(Операция.Ссылка = КорректировкиНДСПартий.Ссылка)
	|ГДЕ
	|	КорректировкиНДСПартий.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.КомиссионныйТовар)
	|
	|";
#КонецОбласти


	Возврат ТекстСписаниеМатериаловНаРасходы
		+ " ОБЪЕДИНИТЬ ВСЕ " + ТекстСписаниеМатериаловНаВнеоборотныеАктивы
        + " ОБЪЕДИНИТЬ ВСЕ " + ТекстКорректировкиНДСПартий
		;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламетированном учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область Печать

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "АктИнвентаризацииМатериаловИПолуфабрикатов") Тогда
		
		ТабличныйДокумент = СформироватьАктИнвентаризацииМатериаловИПолуфабрикатов(ПараметрыПечати, ОбъектыПечати, ПараметрыВывода);
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
									КоллекцияПечатныхФорм, 
									"АктИнвентаризацииМатериаловИПолуфабрикатов",
									НСтр("ru='Акт инвентаризации материалов и полуфабрикатов в цехе';uk='Акт інвентаризації матеріалів і напівфабрикатів у цеху'"),
									ТабличныйДокумент,
									,						
									"Документ.РаспределениеПроизводственныхЗатрат.ПФ_MXL_АктИнвентаризацииМатериаловИПолуфабрикатов",
									,
									Истина // ЭтоМногоязычнаяПечатнаяФорма
									);
	КонецЕсли;
	
КонецПроцедуры

Функция СформироватьАктИнвентаризацииМатериаловИПолуфабрикатов(ПараметрыПечати, ОбъектыПечати, ПараметрыВывода)

	КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РаспределениеПроизводственныхЗатрат.ПФ_MXL_АктИнвентаризацииМатериаловИПолуфабрикатов", КодЯзыкаПечать);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(Затраты.Подразделение КАК Справочник.СтруктураПредприятия) КАК Подразделение,
	|	ВЫРАЗИТЬ(Затраты.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Затраты.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	Затраты.ВОбработке + Затраты.ФактОстаток КАК УчетОстаток,
	|	Затраты.ФактОстаток КАК ФактОстатокВЗапасе,
	|	Затраты.НеРаспределено КАК ФактОстатокНЗП,
	|	Затраты.КРаспределению КАК КРаспределению,
	|	Затраты.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ Затраты
	|ИЗ
	|	&Затраты КАК Затраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫРАЗИТЬ(&Организация КАК Справочник.Организации).НаименованиеСокращенное КАК ОрганизацияПредставление,
	|	Затраты.Подразделение КАК Подразделение,
	|	Затраты.Подразделение.Представление КАК ПодразделениеПредставление,
	|	Затраты.Номенклатура КАК Номенклатура,
	|	Затраты.Характеристика КАК Характеристика,
	|	Затраты.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Затраты.Номенклатура.Представление КАК НоменклатураПредставление,
	|	Затраты.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	Затраты.Номенклатура.Код КАК НоменклатураКод,
	|	Затраты.Номенклатура.Артикул КАК НоменклатураАртикул,
	|	Затраты.УчетОстаток КАК УчетОстаток,
	|	Затраты.ФактОстатокВЗапасе КАК ФактОстатокВЗапасе,
	|	Затраты.ФактОстатокНЗП КАК ФактОстатокНЗП,
	|	Затраты.ФактОстатокВЗапасе + Затраты.ФактОстатокНЗП КАК ФактОстатокВсего,
	|	Затраты.КРаспределению КАК КРаспределению,
	|	Затраты.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Затраты КАК Затраты
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ
	|	МАКСИМУМ(ОрганизацияПредставление),
	|	МАКСИМУМ(ПодразделениеПредставление)
	|ПО
	|	Подразделение";
	
	ТаблицаЗатраты = ПараметрыПечати.Затраты.Выгрузить();
	ТаблицаЗатраты.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	
	НомерСтроки = 1;
	Для каждого ДанныеСтроки Из ТаблицаЗатраты Цикл
		ДанныеСтроки.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("Затраты", ТаблицаЗатраты);
	Запрос.УстановитьПараметр("Организация", ПараметрыПечати.Организация);
	
	Результат = Запрос.Выполнить();
	ВыборкаПоПодразделению = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.ПолеСлева = 5;
	ТабличныйДокумент.ПолеСправа = 5;
	ТабличныйДокумент.РазмерКолонтитулаСверху = 0;
	ТабличныйДокумент.РазмерКолонтитулаСнизу = 0;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_АктИнвентаризацииМатериаловИПолуфабрикатов";
	
  	КолонкаКодов = ФормированиеПечатныхФорм.ИмяДополнительнойКолонки();
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы" + ?(ВыводитьКоды, "_Код", ""));
	ОбластьМакета = Макет.ПолучитьОбласть("Строка" + ?(ВыводитьКоды, "_Код", ""));
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("Подвал");
	
	Если ВыводитьКоды Тогда
		ОбластьШапкаТаблицы.Параметры.ИмяКолонкиКодов = КолонкаКодов;
	КонецЕсли; 
	
	ПервыйДокумент = Истина;
	
	Пока ВыборкаПоПодразделению.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ПараметрыЗаголовка = Новый Структура("ОрганизацияПредставление,ПодразделениеПредставление");
		ЗаполнитьЗначенияСвойств(ПараметрыЗаголовка, ВыборкаПоПодразделению);
		ПараметрыЗаголовка.Вставить("ДатаДокумента", Формат(КонецМесяца(ПараметрыПечати.Период), "ДЛФ=D"));
		
		ОбластьЗаголовок.Параметры.Заполнить(ПараметрыЗаголовка);
		
		ТабличныйДокумент.Вывести(ОбластьЗаголовок);
		
		СсылкаОбластьПечати = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru='Акт инвентаризации в подразделении %1';uk='Акт інвентаризації в підрозділі %1'",КодЯзыкаПечать),
									ВыборкаПоПодразделению.ПодразделениеПредставление);
								
		НомерСтраницы = 1;
		НомерСтроки = 0;
		
		// Создаем массив для проверки вывода
		МассивВыводимыхОбластей = Новый Массив;
		
		ДанныеПечати = ВыборкаПоПодразделению.Выбрать();
		Пока ДанныеПечати.Следующий() Цикл
			
			НомерСтроки = НомерСтроки + 1;
			
			ОбластьМакета.Параметры.Заполнить(ДанныеПечати);
			
			Если НомерСтроки = 1 Тогда
				
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
				
			Иначе
			
				МассивВыводимыхОбластей.Очистить();
				МассивВыводимыхОбластей.Добавить(ОбластьМакета);
				
				Если НомерСтроки <> 1 И Не ТабличныйДокумент.ПроверитьВывод(МассивВыводимыхОбластей) Тогда
					
					НомерСтраницы = НомерСтраницы + 1;
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					
					ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
					
				КонецЕсли;
				
			КонецЕсли;
			
			// Используем структуру для заполнения параметров, т.к. макет может изменяться пользователем
			ДополнительныеПараметрыОбластьМакета = Новый Структура;
			ДополнительныеПараметрыОбластьМакета.Вставить("НомерСтроки", НомерСтроки);
			Если ВыводитьКоды Тогда
				ДополнительныеПараметрыОбластьМакета.Вставить("Артикул", ДанныеПечати["Номенклатура" + КолонкаКодов]);
			КонецЕсли;
			
			НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
										ДанныеПечати.НоменклатураПредставление, 
										ДанныеПечати.ХарактеристикаПредставление);
											
			ДополнительныеПараметрыОбластьМакета.Вставить("НоменклатураПредставление", НоменклатураПредставление);
			ОбластьМакета.Параметры.Заполнить(ДополнительныеПараметрыОбластьМакета);
			
			ТабличныйДокумент.Вывести(ОбластьМакета);
					
		КонецЦикла;
		
		ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
		
		ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
		ТабличныйДокумент.Вывести(ОбластьПодписи);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, СсылкаОбластьПечати);
	
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

#КонецОбласти

#Область Отчеты

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#КонецОбласти

#КонецЕсли