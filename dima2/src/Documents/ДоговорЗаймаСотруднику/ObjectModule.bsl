#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЗарплатаКадры.ЗаполнитьНаборыПоОрганизацииИФизическимЛицам(ЭтотОбъект, Таблица, "Организация", "ФизическоеЛицо");
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// В режиме ввода начальных остатков при включенном обмене с бухгалтерией формируем движения по выдаче займа датой
	// документа.
	Если ЗаемЧастичноПогашен И Не ЗаймыСотрудникам.ДоступнаРегистрацияВыдачиЗаймаДокументомДоговорЗайма(Организация) Тогда 
		ДатаВыдачи = Дата;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПараметрыПолученияСотрудниковОрганизаций = КадровыйУчет.ПараметрыПолученияРабочихМестВОрганизацийПоСпискуФизическихЛиц();
	ПараметрыПолученияСотрудниковОрганизаций.Организация 				= Организация;
	ПараметрыПолученияСотрудниковОрганизаций.НачалоПериода				= ДатаПредоставления;
	ПараметрыПолученияСотрудниковОрганизаций.ОкончаниеПериода			= КонецМесяца(ДатаПредоставления);
	ПараметрыПолученияСотрудниковОрганизаций.РаботникиПоДоговорамГПХ 	= Неопределено;
	
	СписокФизическихЛиц = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо);
	
	КадровыйУчет.ПроверитьРаботающихФизическихЛиц(
		СписокФизическихЛиц,
		ПараметрыПолученияСотрудниковОрганизаций,
		Отказ,
		Новый Структура("ИмяПоляСотрудник, ИмяОбъекта", "ФизическоеЛицо", "Объект"));
	
	Если ЗаемЧастичноПогашен Тогда 
		ЗаймыСотрудникам.ПроверитьДатыДоговораПриВводеНачальныхОстатков(Организация, ДатаПогашения, ДатаВыдачи, ДатаПредоставления, Отказ);
	ИначеЕсли ЗаемПоДоговоруВыданПолностью Тогда
		ЗаймыСотрудникам.ПроверитьДатуВыдачиЗайма(ДатаВыдачи, ДатаПредоставления, "Объект.ДатаВыдачи", Отказ);
	КонецЕсли;
	
	Если Не ЗаемПоДоговоруВыданПолностью Или Не ЗаймыСотрудникам.ДоступнаРегистрацияВыдачиЗаймаДокументомДоговорЗайма(Организация) Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ФормаРасчетов");
	КонецЕсли;
	
	Если Не ЗаемЧастичноПогашен Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПогашеннаяСуммаЗайма");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПогашенныеПроценты");
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ДатаПогашения");
	ИначеЕсли ВидПлатежей = Перечисления.ВидыПлатежейПогашенияЗаймаСотруднику.ТолькоЗаем Или ПроцентнаяСтавка = 0 Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПогашенныеПроценты");
	ИначеЕсли ВидПлатежей = Перечисления.ВидыПлатежейПогашенияЗаймаСотруднику.ТолькоПроценты Тогда 
		ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(ПроверяемыеРеквизиты, "ПогашеннаяСуммаЗайма");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗаймыСотрудникам.ДанныеЗаполненияДокументаПоСотруднику(ДанныеЗаполнения));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);

	// Условия договора
	ЗаймыСотрудникам.ЗаписатьУсловияДоговораЗайма(Движения, Ссылка, ДатаПредоставления, Ссылка);
	
	// Если есть кассовые (банковские) документы - взаиморасчеты по займам регистрируются с их помощью.
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьВнешниеХозяйственныеОперацииЗарплатаКадры") Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗаемПоДоговоруВыданПолностью Тогда 
		Если СпособПредоставления = Перечисления.СпособыПредоставленияЗаймаСотруднику.Траншами Тогда
			// Если предоставляется траншами, то регистрируем только тот транш (транши), 
			// которые предоставляются сразу.
			ТраншиСразу = ТраншиЗайма.НайтиСтроки(Новый Структура("ДатаПредоставления", ДатаПредоставления));
			Для Каждого Транш Из ТраншиСразу Цикл
				ЗаймыСотрудникам.ЗарегистрироватьПредоставлениеЗайма(Движения, Ссылка, Транш.Сумма, ДатаВыдачи, Организация, ФизическоеЛицо, Отказ);
			КонецЦикла;
		Иначе
			ЗаймыСотрудникам.ЗарегистрироватьПредоставлениеЗайма(Движения, Ссылка, Сумма, ДатаВыдачи, Организация, ФизическоеЛицо, Отказ);
		КонецЕсли;	
	КонецЕсли;
	
	Если ЗаемЧастичноПогашен Тогда 
		ЗаймыСотрудникам.ЗарегистрироватьЧастичноПогашенныйЗаем(Движения, Ссылка, ПогашеннаяСуммаЗайма, ПогашенныеПроценты, ДатаПогашения, Организация, ФизическоеЛицо, Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
