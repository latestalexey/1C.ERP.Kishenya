
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИнициализироватьПараметрыЗагрузки();

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьПроводки(Команда)
	
	Модифицированность = Ложь;
	Если ЗагрузитьПроводкиСервер() Тогда
		Структура = Новый Структура("АдресПроводокВХранилище", АдресПроводокВХранилище);
		ОповеститьОВыборе(Структура);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗагрузитьПроводкиСервер()
	
	НомерСтроки = 2;
	ЗагрузкаСпискаДокументов = НЕ ПараметрыЗагрузки.ЗагрузкаВДокумент;
	ПоляЗагрузки = ПараметрыЗагрузки.Поля;
	СтрокаЗаполнена = СтрокаЗаполнена(ТабличныйДокумент, НомерСтроки);
	ЗагруженоБезОшибок = Истина;
	УчетнаяВалюта = МеждународнаяОтчетностьВызовСервера.УчетнаяВалюта();
	КэшКурсовВалют = РаботаСКурсамиВалютУТ.ИнициализироватьКэшКурсовВалют();
	УчетнаяВалюта.Вставить("КэшКурсов", КэшКурсовВалют);
	
	ОбъектДокумента = Неопределено;
	ДатаДокумента = Неопределено;
	ОрганизацияДокумента = Неопределено;
	Проводки = Неопределено;
	Если ПараметрыЗагрузки.ЗагрузкаВДокумент Тогда
		ДатаДокумента = ПараметрыЗагрузки.ДатаДокумента;
		ОрганизацияДокумента = ПараметрыЗагрузки.Организация;
		Проводки = РегистрыБухгалтерии.Международный.СоздатьНаборЗаписей().Выгрузить();
	КонецЕсли;
	
	// Подготовим набор записей
	НачатьТранзакцию();
	Пока СтрокаЗаполнена Цикл
		
		ДанныеПроводки = Неопределено;
		Попытка
			// Прочитаем данные строки графика
			ДанныеПроводки = Новый Структура();
			Для НомерКолонки = 1 По ПоляЗагрузки.Количество() Цикл
				
				Поле = ПоляЗагрузки[НомерКолонки-1];
				ТекстЯчейки = ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки);
				
				Если ПустаяСтрока(ТекстЯчейки) И Поле <> "ТипПроводки" Тогда
					Продолжить;
				КонецЕсли;
				
				Если Поле = "Период" Тогда
					ЗначениеПоля = ТекстВДату(ТекстЯчейки, НомерСтроки, Поле);
				ИначеЕсли Поле = "Организация" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(Справочники.Организации, ТекстЯчейки);
					
				ИначеЕсли Поле = "СчетДт" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(ПланыСчетов.Международный,ТекстЯчейки);
					
				ИначеЕсли Поле = "СубконтоДт1" Тогда
					ЗначениеПоля = НайтиЗначениеСубконто(ДанныеПроводки.СчетДт,1, ТекстЯчейки);
					
				ИначеЕсли Поле = "СубконтоДт2" Тогда
					ЗначениеПоля = НайтиЗначениеСубконто(ДанныеПроводки.СчетДт,2, ТекстЯчейки);
					
				ИначеЕсли Поле = "СубконтоДт3" Тогда
					ЗначениеПоля = НайтиЗначениеСубконто(ДанныеПроводки.СчетДт,3, ТекстЯчейки);
					
				ИначеЕсли Поле = "СчетКт" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(ПланыСчетов.Международный,ТекстЯчейки);
					
				ИначеЕсли Поле = "СубконтоКт1" Тогда
					ЗначениеПоля = НайтиЗначениеСубконто(ДанныеПроводки.СчетКт,1, ТекстЯчейки);
					
				ИначеЕсли Поле = "СубконтоКт2" Тогда
					ЗначениеПоля = НайтиЗначениеСубконто(ДанныеПроводки.СчетКт,2, ТекстЯчейки);
					
				ИначеЕсли Поле = "СубконтоКт3" Тогда
					ЗначениеПоля = НайтиЗначениеСубконто(ДанныеПроводки.СчетКт,3, ТекстЯчейки);
					
				ИначеЕсли Поле = "ВалютаДт" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(Справочники.Валюты, ТекстЯчейки);
					
				ИначеЕсли Поле = "ВалютаКт" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(Справочники.Валюты, ТекстЯчейки);
					
				ИначеЕсли Поле = "ПодразделениеДт" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(Справочники.СтруктураПредприятия, ТекстЯчейки);
					
				ИначеЕсли Поле = "ПодразделениеКт" Тогда
					ЗначениеПоля = НайтиЗначениеПоля(Справочники.СтруктураПредприятия, ТекстЯчейки);
					
				ИначеЕсли Поле = "ТипПроводки" Тогда
					Если НЕ ПустаяСтрока(ТекстЯчейки) Тогда
						ЗначениеПоля = Перечисления.ТипыПроводокМеждународныйУчет[ТекстЯчейки];
					КонецЕсли;
					
				ИначеЕсли Поле = "Содержание" Тогда
					ЗначениеПоля = ТекстЯчейки;
					
				Иначе
					ЗначениеПоля = ТекстВЧисло(ТекстЯчейки, НомерСтроки, Поле);
					
				КонецЕсли;
				ДанныеПроводки.Вставить(Поле,ЗначениеПоля);
				
			КонецЦикла;// по колонкам таблицы загрузки
			
		Исключение
			ШаблонТекста = НСтр("ru='Строка №%1 колонка ""%2"" - ошибка преобразования значения ""%3""
                                |Строка не загружена по причине'
                                |;uk='Рядок №%1 колонка ""%2"" - помилка перетворення значення ""%3""
                                |Рядок не завантажений через'") + ": ";
			Текст = СтрШаблон(ШаблонТекста, НомерСтроки, Поле, ТекстЯчейки);
			Текст = Текст + Символы.ПС + Символы.Таб + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
			ЗагруженоБезОшибок = Ложь;
		КонецПопытки;
		
		Если ЗначениеЗаполнено(ДанныеПроводки) Тогда
			
			Если ЗагрузкаСпискаДокументов Тогда
				
				Если ОрганизацияДокумента <> ДанныеПроводки.Организация Тогда
					Если ОбъектДокумента <> Неопределено Тогда
						ОбъектДокумента.Движения.Международный.Загрузить(Проводки);
						ОбъектДокумента.Записать();
					КонецЕсли;
					ОбъектДокумента = Документы.ОперацияМеждународный.СоздатьДокумент();
					ОбъектДокумента.Дата = ДанныеПроводки.Период;
					ОбъектДокумента.Организация = ДанныеПроводки.Организация;
					ОбъектДокумента.Записать();
					
					ДатаДокумента = ДанныеПроводки.Период;
					ОрганизацияДокумента = ДанныеПроводки.Организация;
					Проводки = ОбъектДокумента.Движения.Международный.Выгрузить();
				КонецЕсли;
				
				Если НЕ ОбъектДокумента.ВводитьПериодПоСтрокам И НачалоДня(ДатаДокумента) <> НачалоДня(ДанныеПроводки.Период) Тогда
					ОбъектДокумента.ВводитьПериодПоСтрокам = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
			НоваяПроводка = Проводки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяПроводка, ДанныеПроводки);
			ЗаполнитьСубконто(НоваяПроводка, ДанныеПроводки.СчетДт);
			ЗаполнитьСубконто(НоваяПроводка, ДанныеПроводки.СчетКт, Ложь);
			ЗаполнитьСуммы(НоваяПроводка, УчетнаяВалюта, ДатаДокумента);
			НоваяПроводка.Активность = Ложь;
			
		КонецЕсли;
		
		НомерСтроки = НомерСтроки + 1;
		
		Попытка
			СтрокаЗаполнена = СтрокаЗаполнена(ТабличныйДокумент, НомерСтроки);
		Исключение
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;// по строкам таблицы загрузки
	
	Если ЗагрузкаСпискаДокументов И ОбъектДокумента <> Неопределено Тогда
		ОбъектДокумента.Движения.Международный.Загрузить(Проводки);
		ОбъектДокумента.Записать();
	КонецЕсли;
	ЗафиксироватьТранзакцию();
	
	Если ПараметрыЗагрузки.ЗагрузкаВДокумент Тогда
		АдресПроводокВХранилище = ПоместитьВоВременноеХранилище(Проводки, УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат ЗагруженоБезОшибок;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьПараметрыЗагрузки()
	
	ПараметрыЗагрузки = Новый Структура("ЗагрузкаВДокумент, ДатаДокумента, Организация", Ложь);
	Если Параметры.Свойство("Организация") Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗагрузки, Параметры);
		ПараметрыЗагрузки.ЗагрузкаВДокумент = Истина;
	КонецЕсли;
	
	ПоляЗагрузки = Новый Массив;
	ПоляЗагрузки.Добавить("Период");
	Если НЕ ПараметрыЗагрузки.ЗагрузкаВДокумент Тогда
		ПоляЗагрузки.Добавить("Организация");
	КонецЕсли;
	ПоляЗагрузки.Добавить("СчетДт");
	ПоляЗагрузки.Добавить("СубконтоДт1");
	ПоляЗагрузки.Добавить("СубконтоДт2");
	ПоляЗагрузки.Добавить("СубконтоДт3");
	ПоляЗагрузки.Добавить("СчетКт");
	ПоляЗагрузки.Добавить("СубконтоКт1");
	ПоляЗагрузки.Добавить("СубконтоКт2");
	ПоляЗагрузки.Добавить("СубконтоКт3");
	ПоляЗагрузки.Добавить("ВалютаДт");
	ПоляЗагрузки.Добавить("ВалютнаяСуммаДт");
	ПоляЗагрузки.Добавить("ВалютаКт");
	ПоляЗагрузки.Добавить("ВалютнаяСуммаКт");
	ПоляЗагрузки.Добавить("ПодразделениеДт");
	ПоляЗагрузки.Добавить("ПодразделениеКт");
	ПоляЗагрузки.Добавить("Сумма");
	ПоляЗагрузки.Добавить("СуммаПредставления");
	ПоляЗагрузки.Добавить("ТипПроводки");
	ПоляЗагрузки.Добавить("Содержание");
	
	ПараметрыЗагрузки.Вставить("Поля",ПоляЗагрузки);
	
	// Инициализируем табличный документ
	ТабличныйДокумент.Очистить();
	
	МакетШаблона = Документы.ОперацияМеждународный.ПолучитьМакет("МакетЗагрузкиПроводок");
	ОбластьКолонки = МакетШаблона.ПолучитьОбласть("Проводки");
	ТабличныйДокумент.Присоединить(ОбластьКолонки);
	Если ПараметрыЗагрузки.ЗагрузкаВДокумент Тогда
		КолонкаОрганизация = ТабличныйДокумент.Область("C2");
		ТабличныйДокумент.УдалитьОбласть(КолонкаОрганизация,ТипСмещенияТабличногоДокумента.ПоГоризонтали);
	КонецЕсли;
	ТабличныйДокумент.ФиксацияСверху = 1;
	
КонецПроцедуры

#Область Прочее

&НаСервере
Функция ТекстВДату(Знач СтроковоеЗначение, НомерСтроки, ИмяКолонки, Разделитель = ".")
	
	Попытка
		Результат = Дата(СтроковоеЗначение)
	Исключение
		ТекстДаты = Лев(СокрЛП(СтроковоеЗначение),10);
		Поз = СтрНайти(ТекстДаты," ");
		Если Поз = 9 Тогда
			ТекстДаты = Лев(СокрЛП(СтроковоеЗначение),8);
		КонецЕсли;
		Цифры = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ТекстДаты,Разделитель);
		Попытка
			Результат = ?(СтрДлина(Цифры[2]) = 2, "20"+Цифры[2], Цифры[2]) 
						+ Формат(Цифры[1], "ЧЦ=2; ЧВН=;") 
						+ Формат(Цифры[0], "ЧЦ=2; ЧВН=;");
			Результат = Дата(Результат);
		Исключение
			СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение);
		КонецПопытки;
	КонецПопытки;
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ТекстВЧисло(СтроковоеЗначение, НомерСтроки, ИмяКолонки)
	
	Если ПустаяСтрока(СтроковоеЗначение) ИЛИ СтроковоеЗначение = Неопределено Тогда
		Возврат 0;
	КонецЕсли;
	
	Попытка
		Результат = Число(СтрЗаменить(СтроковоеЗначение," ",""));
	Исключение
		СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	КонецПопытки;
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСубконто(Проводка, Счет, ЭтоДебет = Истина)
	
	СторонаСчета = ?(ЭтоДебет, "Дт","Кт");
	Если ЗначениеЗаполнено(Счет) Тогда
		Для Сч = 1 По 3 Цикл
			СвойстваСчета = МеждународныйУчетСерверПовтИсп.ПолучитьСвойстваСчета(Счет);
			Проводка["ВидСубконто" + СторонаСчета + Строка(Сч)] = СвойстваСчета["ВидСубконто" + Строка(Сч)];
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСуммы(Проводка, УчетнаяВалюта, ДатаДокумента)
	
	Если Проводка.Сумма <> 0 И Проводка.СуммаПредставления <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДатаКурса = Проводка.Период;
	Если НЕ ЗначениеЗаполнено(ДатаКурса) Тогда
		ДатаКурса = ДатаДокумента;
	КонецЕсли;
	Валюта = Неопределено;
	ВалютнаяСумма = ВалютнаяСуммаПроводки(Проводка, Валюта);
	
	КурсФункцВалюты = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(УчетнаяВалюта.Функциональная, ДатаКурса, УчетнаяВалюта.КэшКурсов);
	КурсВалютыПред  = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(УчетнаяВалюта.Представления, ДатаКурса, УчетнаяВалюта.КэшКурсов);
	КурсВалюты      = РаботаСКурсамиВалютУТ.ПолучитьКурсВалютыИзКэша(Валюта, ДатаКурса, УчетнаяВалюта.КэшКурсов);
	
	Если Проводка.Сумма = 0 И КурсФункцВалюты <> 0 Тогда
		Проводка.Сумма = ВалютнаяСумма * КурсВалюты / КурсФункцВалюты;
		Если Проводка.Сумма = 0 И Проводка.СуммаПредставления <> 0 Тогда
			Проводка.Сумма = Проводка.СуммаПредставления * КурсВалютыПред / КурсФункцВалюты;
		КонецЕсли;
	КонецЕсли;
	
	Если Проводка.СуммаПредставления = 0 И КурсВалютыПред <> 0 Тогда
		Проводка.СуммаПредставления = Проводка.Сумма * КурсФункцВалюты / КурсВалютыПред;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВалютнаяСуммаПроводки(Проводка, Валюта)
	
	Если ЗначениеЗаполнено(Проводка.ВалютнаяСуммаДт) Тогда
		Валюта = Проводка.ВалютаДт;
		Возврат Проводка.ВалютнаяСуммаДт;
	ИначеЕсли ЗначениеЗаполнено(Проводка.ВалютнаяСуммаКт) Тогда
		Валюта = Проводка.ВалютаКт;
		Возврат Проводка.ВалютнаяСуммаКт;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция НайтиЗначениеСубконто(Счет,НомерСубконто, ТекстЗначения)
	
	Для Каждого ТипСубконто Из Счет.ВидыСубконто[НомерСубконто-1].ВидСубконто.ТипЗначения.Типы() Цикл
		
		ВидОбъекта = ОбщегоНазначения.ВидОбъектаПоТипу(ТипСубконто);
		ПолноеИмя = Метаданные.НайтиПоТипу(ТипСубконто).ПолноеИмя();
		ИмяОбъекта = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПолноеИмя,".")[1];
		
		Если ВидОбъекта = "Справочник" Тогда
			ЗначениеПоля = НайтиЗначениеПоля(Справочники[ИмяОбъекта], ТекстЗначения);
			
		ИначеЕсли ВидОбъекта = "ПланВидовХарактеристик" Тогда
			ЗначениеПоля = НайтиЗначениеПоля(ПланыВидовХарактеристик[ИмяОбъекта], ТекстЗначения);
			
		ИначеЕсли ВидОбъекта = "ПланСчетов" Тогда
			ЗначениеПоля = НайтиЗначениеПоля(ПланыСчетов[ИмяОбъекта], ТекстЗначения);
			
		ИначеЕсли ВидОбъекта = "Перечисление" Тогда
			ТекстЗначенияПеречисления = СтрЗаменить(ТРег(ТекстЗначения), " ", "");
			ЗначениеПоля = Перечисления[ИмяОбъекта][ТекстЗначенияПеречисления];
			
		Иначе
			ЗначениеПоля = Неопределено;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗначениеПоля) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ЗначениеПоля;
	
КонецФункции

&НаСервере
Функция НайтиЗначениеПоля(МенеджерОбъекта,ТекстЗначения)
	
	ЗначениеПоля = МенеджерОбъекта.НайтиПоНаименованию(ТекстЗначения);
	Если НЕ ЗначениеЗаполнено(ЗначениеПоля) Тогда
		ЗначениеПоля = МенеджерОбъекта.НайтиПоКоду(ТекстЗначения);
	КонецЕсли;
	Возврат ЗначениеПоля;
	
КонецФункции

&НаСервере
Функция ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки)
	
	АдресЯчейки = СтрШаблон("R%1C%2", Формат(НомерСтроки, "ЧН=0; ЧГ=0"), Строка(НомерКолонки));
	Возврат СокрЛП(ТабличныйДокумент.Область(АдресЯчейки).Текст);
	
КонецФункции

&НаСервере
Функция СтрокаЗаполнена(ТабличныйДокумент, НомерСтроки)
	
	Возврат НЕ (ПустаяСтрока(ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки("ВалютнаяСуммаДт")))
					И ПустаяСтрока(ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки("ВалютаДт")))
			)
			ИЛИ НЕ (ПустаяСтрока(ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки("ВалютнаяСуммаКт")))
						И ПустаяСтрока(ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки("ВалютаКт")))
				)
			ИЛИ НЕ ПустаяСтрока(ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки("Сумма")))
			ИЛИ НЕ ПустаяСтрока(ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки("СуммаПредставления")));
	
КонецФункции

&НаСервере
Процедура СообщитьОбОшибкеПреобразования(НомерСтроки, ИмяКолонки, СтроковоеЗначение)
	
	ШаблонТекста = НСтр("ru='Строка №%1 колонка ""%2"" - ошибка преобразования значения ""%3""
                        |Значение ячейки не загружено!'
                        |;uk='Рядок №%1 колонка ""%2"" - помилка перетворення значення ""%3""
                        |Значення комірки не завантажене!'");
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонТекста, НомерСтроки, ИмяКолонки, СтроковоеЗначение);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст);
	
КонецПроцедуры

&НаСервере
Функция НомерКолонки(ИмяПоля)
	
	Возврат ПараметрыЗагрузки.Поля.Найти(ИмяПоля)+1;
	
КонецФункции

#КонецОбласти

#КонецОбласти
