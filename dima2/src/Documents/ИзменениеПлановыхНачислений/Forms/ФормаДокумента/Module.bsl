
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	РасчетЗарплатыРасширенныйФормы.ДокументыПриСозданииНаСервере(ЭтаФорма);
	
	Если Не ЗначениеЗаполнено(Параметры.Ключ) Тогда
		
		ЗаполнитьПервоначальныеЗначения();
		ПрочитатьВремяРегистрации();
		
		Если Объект.ЭтоОтражениеИзмененияШтатногоРасписания Тогда
			ЗаполнитьНачисленияПоказателиСотрудников();
		КонецЕсли;
		
		ПриПолученииДанныхНаСервере();
		
	КонецЕсли;
	
	СвязиПараметровВыбораСотрудников = Новый Массив(Элементы.ПоказателиСотрудниковСотрудник.СвязиПараметровВыбора);
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") Тогда
		Связь = Новый СвязьПараметраВыбора("Отбор.ДолжностьПоШтатномуРасписанию", "Объект.ДолжностьПоШтатномуРасписанию", РежимИзмененияСвязанногоЗначения.НеИзменять);
	Иначе
		Связь = Новый СвязьПараметраВыбора("Отбор.Должность", "Объект.Должность", РежимИзмененияСвязанногоЗначения.НеИзменять);
	КонецЕсли;
	СвязиПараметровВыбораСотрудников.Добавить(Связь);
	
	Элементы.ПоказателиСотрудниковСотрудник.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбораСотрудников);
	
	ЭтотОбъект.ПодробныйРасчетФОТ = Ложь;
	
	// Обработчик подсистемы "Дополнительные отчеты и обработки".
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Обработчик подсистемы "ВерсионированиеОбъектов".
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.Печать
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ДатыЗапретаИзменения
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.ДатыЗапретаИзменения
	
	ДанныеСотрудниковВРеквизитФормы();
	
	ФОТСотрудниковВРеквизитФормы();

	ПриПолученииДанныхНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("ПроведениеДокументаИзменениеПлановыхНачислений");
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	РеквизитФормыВДанныеСотрудников(ТекущийОбъект);
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ДанныеСотрудниковВРеквизитФормы();
	
	ФОТСотрудниковВРеквизитФормы();

	УстановитьОтображениеНадписей();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененыНачисления" И Источник = ЭтаФорма Тогда
		ЗаполнитьДанныеСотрудникаИзВРеменногоХранилища(Параметр.АдресВХранилище);
	КонецЕсли;
	
	Если ИмяСобытия = "ИзмененыПоказателиДокумента" И Источник.ВладелецФормы = ЭтаФорма Тогда
		Если Параметр.Показатели.Количество() > 0 Тогда 
			ОбработатьИзменениеПоказателейНаСервере(Параметр.Показатели);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Элементы.Найти("ПоказателиСотрудников") <> Неопределено Тогда
		ТекущийОбъект = РеквизитФормыВЗначение("Объект");
		РеквизитФормыВДанныеСотрудников(ТекущийОбъект);
		Отказ = Отказ Или Не ТекущийОбъект.ПроверитьЗаполнение();
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Объект"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
    ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПоШтатномуРасписаниюПриИзменении(Элемент)
	ДолжностьПоШтатномуРасписаниюПриИзмененииНаСервере()
КонецПроцедуры
  
&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
    ЗарплатаКадрыРасширенныйКлиент.КлючевыеРеквизитыЗаполненияФормыОчиститьТаблицы(ЭтаФорма);
	ПодразделениеПриИзмененииНаСервере()
КонецПроцедуры

&НаКлиенте
Процедура ДолжностьПриИзменении(Элемент)
	ДолжностьПриИзмененииНаСервере()
КонецПроцедуры

&НаКлиенте
Процедура ДатаИзмененияПриИзменении(Элемент)
	ДатаИзмененияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьДокументыВведенныеПозже(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗарплатаКадрыРасширенныйКлиент.ОткрытьВведенныеНаДатуДокументы(ЭтотОбъект.ДокументыВведенныеПозже);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытьРанееВведенныеДокументы(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗарплатаКадрыРасширенныйКлиент.ОткрытьВведенныеНаДатуДокументы(ЭтотОбъект.РанееВведенныеДокументы);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ПоказателиСотрудников

&НаКлиенте
Процедура ПоказателиСотрудниковПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока 
		И Не Копирование Тогда
		
		Строка = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.ПоказателиСотрудников.ТекущаяСтрока);
		Если НЕ Строка = Неопределено Тогда
			Строка.ДатаИзменения = Объект.ДатаИзменения;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковПередНачаломИзменения(Элемент, Отказ)
	
	Строка = Объект.Сотрудники.НайтиПоИдентификатору(Элементы.ПоказателиСотрудников.ТекущаяСтрока);
	Если НЕ Строка = Неопределено Тогда
		МассивУдаляемыхСотрудников = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.Сотрудник);
		ЗафиксироватьУдаляемыхСотрудников(МассивУдаляемыхСотрудников);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковСотрудникПриИзменении(Элемент)
	ПоказателиСотрудниковСотрудникПриИзмененииНаСервере(Элементы.ПоказателиСотрудников.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковДатаИзмененияПриИзменении(Элемент)
	ПоказателиСотрудниковДатаИзмененияПриИзмененииНаСервере(Элементы.ПоказателиСотрудников.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ПоказателиСотрудниковФОТ"
		Или Поле.Имя = "ПоказателиСотрудниковПредставлениеНачислений" Тогда
		
		РедактироватьСоставНачислений = Поле.Имя = "ПоказателиСотрудниковПредставлениеНачислений";
		ОткрытьФормуРедактированияСоставаНачисленийИУдержаний(Элемент.ТекущиеДанные, Элемент.ТекущаяСтрока, РедактироватьСоставНачислений);
		
	ИначеЕсли СтрНайти(Поле.Имя, ПрефиксЭлементаПоказателиСотрудников()) > 0 Тогда
		
		ПутьКДанным = Прав(Поле.Имя, СтрДлина(Поле.Имя) - СтрДлина(ПрефиксЭлементаПоказателиСотрудников()));
		Если Элемент.ТекущиеДанные[ИмяРеквизитаТабличноеПредставление(ПутьКДанным)] Тогда
			СтандартнаяОбработка = Ложь;
			ОткрытьФормуРедактированияПлановогоПоказателяПоДокументамОснованиям(Элемент.ТекущиеДанные, ПутьКДанным);
		КонецЕсли; 
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковПередУдалением(Элемент, Отказ)
	
	МассивУдаляемыхСотрудников = Новый Массив;
	
	Для каждого ИдентификаторУдаляемойСтроки Из Элемент.ВыделенныеСтроки Цикл
		Строка = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторУдаляемойСтроки);
		Если НЕ Строка = Неопределено Тогда
			МассивУдаляемыхСотрудников.Добавить(Строка.Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	ЗафиксироватьУдаляемыхСотрудников(МассивУдаляемыхСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковПослеУдаления(Элемент)
	
	ПоказателиСотрудниковПослеУдаленияНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("ИзменениеЗначенийПоказателейВФормеДокументаИзменениеПлановыхНачислений");
	
	ОчиститьУдаляемыхСотрудников();
	Если ОтменаРедактирования Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент.Имя <> "ПоказателиСотрудниковСовокупнаяТарифнаяСтавка"
		И Элемент.ТекущийЭлемент.Имя <> "ПоказателиСотрудниковСотрудник" Тогда
		Строка = Элементы.ПоказателиСотрудников.ТекущиеДанные;
		Если НЕ Строка = Неопределено 
			И ЗначениеЗаполнено(Строка.Сотрудник) Тогда
			ПоказателиСотрудниковПриОкончанииРедактированияНаСервере(Элементы.ПоказателиСотрудников.ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказателиСотрудниковОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ПоказателиСотрудниковОбработкаВыбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказателиСотрудниковПоказательПриИзменении(Элемент)
	
	ПутьКДанным = Прав(Элемент.Имя, СтрДлина(Элемент.Имя) - СтрДлина(ПрефиксЭлементаПоказателиСотрудников()));
	ЗначениеПоказателя = Элементы.ПоказателиСотрудников.ТекущиеДанные[ПутьКДанным];
	ТаблицаЗначенийПоказателя = Элементы.ПоказателиСотрудников.ТекущиеДанные[ИмяРеквизитаТаблицаПоказателя(ПутьКДанным)];
	ТаблицаЗначенийПоказателя[0].Значение = ЗначениеПоказателя;;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ Начисления

&НаКлиенте
Процедура НачисленияНачислениеПриИзменении(Элемент)
	
	Строка = Элементы.Начисления.ТекущиеДанные;
	
	Если Строка = Неопределено 
		Или Не ЗначениеЗаполнено(Строка.Начисление) Тогда
		Строка.КраткоеНаименование = "";
		Строка.РеквизитДопУпорядочивания = 0;
		Возврат;
	КонецЕсли;
	
	ПредставлениеНачислений = ПредставлениеНачислений(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Строка.Начисление));
	
	ПредставлениеНачисления = ПредставлениеНачислений[Строка.Начисление];
	
	Строка.КраткоеНаименование = ПредставлениеНачисления.КраткоеНаименование;
	Строка.РеквизитДопУпорядочивания = ПредставлениеНачисления.РеквизитДопУпорядочивания;
	
	ОбновитьЗаголовокГруппыНачисления(ЭтотОбъект, "ГруппаФильтрНачисления");
	
КонецПроцедуры

&НаКлиенте
Процедура НачисленияПослеУдаления(Элемент)
	ОбновитьЗаголовокГруппыНачисления(ЭтотОбъект, "ГруппаФильтрНачисления");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
		ДополнительныеОтчетыИОбработкиКлиент.ПоказатьРезультатВыполненияКоманды(ЭтаФорма, РезультатВыполнения);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтаФорма, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура Заполнить(Команда)
	
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("ЗаполнениеДокументаИзменениеПлановыхНачислений");
	
	ОчиститьСообщения();
	ЗаполнитьНачисленияПоказателиСотрудников(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	ОценкаПроизводительностиКлиентСервер.НачатьЗамерВремени("ПодборСотрудникаВФормеДокументаИзменениеПлановыхНачислений");	
	
	ПараметрыОткрытия = Неопределено;
	КадровыйУчетРасширенныйКлиент.ДобавитьПараметрыОтбораПоФункциональнойОпцииВыполнятьРасчетЗарплатыПоПодразделениям(
		ЭтаФорма, ПараметрыОткрытия);
		
	ДобавитьОтборПоДолжностиДляПодбораСотрудников(ПараметрыОткрытия);
		
	КадровыйУчетКлиент.ВыбратьСотрудниковРаботающихВПериодеПоПараметрамОткрытияФормыСписка(
		Элементы.ПоказателиСотрудников,
		Объект.Организация, 
		Объект.Подразделение,
		Объект.ДатаИзменения, 
		Объект.ДатаОкончания,
		Истина, 
		АдресСпискаПодобранныхСотрудников(),
		ПараметрыОткрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНачислениеВсемСотрудникам(Команда)
	
	МассивСпособовРасчета = Новый Массив;
	МассивСпособовРасчета.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВыполненияНачислений.ЕжемесячноПриОкончательномРасчете"));
	МассивСпособовРасчета.Добавить(ПредопределенноеЗначение("Перечисление.СпособыВыполненияНачислений.ВЗаданныхМесяцахПриОкончательномРасчете"));

	Отбор = Новый Массив;
	Отбор.Добавить(Новый Структура("ЛевоеЗначение, ВидСравнения, ПравоеЗначение", "СпособВыполненияНачисления", ВидСравненияКомпоновкиДанных.ВСписке, МассивСпособовРасчета));

	ВыбратьНачисление("ДобавитьНачислениеВсемСотрудникамЗавершение", Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьНачислениеУВсехСотрудников(Команда)
	
	МассивВидовРасчета = Новый Массив;
	Для каждого СтрокаКоллекции Из Объект.НачисленияСотрудников Цикл
		МассивВидовРасчета.Добавить(СтрокаКоллекции.Начисление);
	КонецЦикла;
	
	Отбор = Новый Массив;
	Отбор.Добавить(Новый Структура("ЛевоеЗначение, ВидСравнения, ПравоеЗначение", "Ссылка", ВидСравненияКомпоновкиДанных.ВСписке, МассивВидовРасчета));

	ВыбратьНачисление("УдалитьНачислениеВсемСотрудникамЗавершение", Отбор);
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетФОТПодробно(Команда)
	РасчетФОТПодробноНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоказатели(Команда)
	
	МассивПоказателей = Новый Массив;
	
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
		Если Не ТипЗнч(КолонкаПоказатель.Значение) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда
			Продолжить;
		КонецЕсли;
		МассивПоказателей.Добавить(КолонкаПоказатель.Значение);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("МассивПоказателей", МассивПоказателей);
	ОткрытьФорму("ОбщаяФорма.ГрупповоеЗаполнениеПоказателейДокументов", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

&НаСервере
Процедура ЗаполнитьПервоначальныеЗначения()
	ЗначенияДляЗаполнения = Новый Структура;
	ЗначенияДляЗаполнения.Вставить("Ответственный", "Объект.Ответственный");
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Организация") 
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Организация) Тогда
		Объект.Организация = Параметры.ЗначенияЗаполнения.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		ЗначенияДляЗаполнения.Вставить("Организация", "Объект.Организация");
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ВыполнятьРасчетЗарплатыПоПодразделениям") Тогда
		ЗначенияДляЗаполнения.Вставить("Подразделение", "Объект.Подразделение");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаИзменения) Тогда
		ЗначенияДляЗаполнения.Вставить("ДатаСобытия", "Объект.ДатаИзменения");
	КонецЕсли;
	
	ЗарплатаКадры.ЗаполнитьПервоначальныеЗначенияВФорме(ЭтаФорма, ЗначенияДляЗаполнения);
КонецПроцедуры

#Область СервернаяЧастьОбработчиковСобытийЭлементовФормы

&НаСервере
Процедура ПоказателиСотрудниковПриОкончанииРедактированияНаСервере(ИдентификаторСтроки)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	ЭлементФильтра = ФильтрСотрудников.Добавить();
	ЭлементФильтра.Сотрудник = СтрокаСотрудника.Сотрудник;
	ЭлементФильтра.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);

	ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников);
	ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников);
	СтрокаСотрудника.ФОТ = ФОТСотрудников.Получить(СтрокаСотрудника.Сотрудник);
	
	НачисленияСотрудника = Объект.НачисленияСотрудников.НайтиСтроки(Новый Структура("Сотрудник", СтрокаСотрудника.Сотрудник));
	
	Запрос = ЗапросИзменяемыхНачислений(НачисленияСотрудника);
	НачисленияСотрудникаСОписанием = НачисленияСотрудниковСОписанием(Запрос);
	НачисленияСотрудникаВСтроку(СтрокаСотрудника, НачисленияСотрудникаСОписанием, ПредставлениеПустойКоллекцииНачислений());
	
	НачисленияСотрудников = ПлановыеНачисленияПоказателиСотрудников.НачисленияСотрудников.СкопироватьКолонки();
	ПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудников.ПоказателиСотрудников.СкопироватьКолонки();
	
	ЗаполнитьНачисленияПоказателиСотрудниковДляРасчетаСовокупнойСтавки(ФОТПлановыхНачисленийСотрудников, НачисленияСотрудников, ПоказателиСотрудников);	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(НачисленияСотрудников, ПоказателиСотрудников);

	УстановитьОтображениеНадписей();
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПоказателиСотрудниковСотрудникПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ЗавершитьУдалениеСотрудников();
	
	ТекущаяСтрока = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьВремяРегистрацииСтроки(Объект.Ссылка, ТекущаяСтрока);

	ЗаполнитьНачисленияПоказателиСотрудника(ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ПоказателиСотрудниковДатаИзмененияПриИзмененииНаСервере(ИдентификаторСтроки)
	
	ТекущаяСтрока = Объект.Сотрудники.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПрочитатьВремяРегистрацииСтроки(Объект.Ссылка, ТекущаяСтрока);

	ЗаполнитьНачисленияПоказателиСотрудника(ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ПоказателиСотрудниковОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	Для каждого Сотрудник Из ВыбранноеЗначение Цикл
		ЭлементФильтра = ФильтрСотрудников.Добавить();
		ЭлементФильтра.Сотрудник = Сотрудник;
		ЭлементФильтра.ДатаИзменения = ВремяРегистрации;
	КонецЦикла;
	
	ЗаполнитьНачисленияПоказателиСотрудников(Ложь, ФильтрСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере()
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ДолжностьПриИзмененииНаСервере()
	УстановитьДоступностьЭлементовФормы();
КонецПроцедуры

&НаСервере
Процедура ДолжностьПоШтатномуРасписаниюПриИзмененииНаСервере()
	Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
		
		РеквизитыПозиции = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДолжностьПоШтатномуРасписанию, "Подразделение,Должность");
		Объект.Подразделение = РеквизитыПозиции.Подразделение;
		Объект.Должность = РеквизитыПозиции.Должность;
		
	КонецЕсли;
	
	УстановитьДоступностьЭлементовФормы();	
КонецПроцедуры

&НаСервере
Процедура ПоказателиСотрудниковПослеУдаленияНаСервере()
	
	Если НЕ ЕстьУдаляемыеСотрудники() Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершитьУдалениеСотрудников();
	
	РеквизитФормыВДанныеСотрудников(Объект);
	
	ДанныеСотрудниковВРеквизитФормы();
	
	ФОТСотрудниковВРеквизитФормы();
	
	УстановитьОтображениеНадписей();
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ДатаИзмененияПриИзмененииНаСервере()
	
	ПрочитатьВремяРегистрации();
	
	УточнитьДолжностьПоШтатномуРасписанию();
	
	УстановитьОтображениеНадписей();
	
КонецПроцедуры

&НаСервере
Процедура РасчетФОТПодробноНаСервере()
	
	Пометка = ОбщегоНазначенияКлиентСервер.ЗначениеСвойстваЭлементаФормы(ЭтотОбъект.Элементы, "ПоказателиСотрудниковРасчетФОТПодробно", "Пометка");
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПоказателиСотрудниковРасчетФОТПодробно", "Пометка", Не Пометка);
	ЭтотОбъект.ПодробныйРасчетФОТ = Не Пометка;
	
	УстановитьВидимостьКолонокПодробногоРасчета();
	
	Если ЭтотОбъект.ПодробныйРасчетФОТ Тогда
		ЗаполнитьФОТСотрудниковВФорме();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПриПолученииДанныхНаСервере

&НаСервере
Процедура ПриПолученииДанныхНаСервере()
	
	ЗарплатаКадрыРасширенный.ОформлениеНесколькихДокументовНаОднуДатуДополнитьФорму(ЭтотОбъект);	
	
	ЗарплатаКадры.КлючевыеРеквизитыЗаполненияФормыЗаполнитьПредупреждения(ЭтаФорма);
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
	УстановитьВидимостьЭлементовФормы();
	УстановитьДоступностьЭлементовФормы();
	УстановитьОтображениеНадписей();
	
	ЗаполнитьПредставлениеНачислений();
	ОбновитьЗаголовокГруппыНачисления(ЭтотОбъект, "ГруппаФильтрНачисления");
	
	УстановитьДоступностьКомандыЗаполнитьПоказатели();
	
КонецПроцедуры
	
#КонецОбласти

#Область ДанныеСотрудниковВРеквизитФормы

&НаСервере
Процедура ДанныеСотрудниковВРеквизитФормы(ФильтрСотрудников = Неопределено)
	
	ПрочитатьВремяРегистрации();
	
	Если ФильтрСотрудников = Неопределено Тогда
		Запрос = ЗапросИзменяемыхНачислений();
	Иначе
		
		СтрокиНачислений = Новый Массив;
		Для каждого ОписаниеСотрудника Из ФильтрСотрудников Цикл
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СтрокиНачислений, Объект.НачисленияСотрудников.НайтиСтроки(Новый Структура("Сотрудник", ОписаниеСотрудника.Сотрудник)));
			
		КонецЦикла;
		
		Запрос = ЗапросИзменяемыхНачислений(СтрокиНачислений);
		
	КонецЕсли;
	
	НачисленияСотрудниковСОписанием = НачисленияСотрудниковСОписанием(Запрос);
	ИзменяемыеПоказатели			= ИзменяемыеПоказатели(Запрос);
	ДополнитьФормуИзменяемымиПоказателями(ИзменяемыеПоказатели);
	ОписаниеИзменяемыхПоказателей	= ОписаниеПоказателей(ИзменяемыеПоказатели);
	ПоказателиСотрудников			= ПоказателиСотрудников(Запрос);
	
	ДанныеСотрудниковВСтроки(НачисленияСотрудниковСОписанием, ПоказателиСотрудников, ОписаниеИзменяемыхПоказателей, ФильтрСотрудников);
	
КонецПроцедуры

&НаСервере
Процедура ФОТСотрудниковВРеквизитФормы(ФОТСотрудников = Неопределено)
	
	Если ФОТСотрудников = Неопределено Тогда
		
		ФОТСотрудников = ФОТСотрудников();	
		
	КонецЕсли;
	
	Отбор = Новый Структура("Сотрудник");
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		Отбор.Сотрудник = СтрокаСотрудника.Сотрудник;
		
		СтрокаСотрудника.ФОТ = ФОТСотрудников.Получить(СтрокаСотрудника.Сотрудник);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗапросИзменяемыхНачислений(НачисленияСотрудника = Неопределено, ФильтрСотрудников = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачисленияСотрудников.Сотрудник,
	|	НачисленияСотрудников.Действие,
	|	НачисленияСотрудников.Начисление КАК Начисление,
	|	НачисленияСотрудников.ДокументОснование КАК ДокументОснование,
	|	НачисленияСотрудников.Размер
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	&НачисленияСотрудников КАК НачисленияСотрудников";
	
	Если НачисленияСотрудника = Неопределено Тогда
		НачисленияСотрудников = Объект.НачисленияСотрудников.Выгрузить();
	Иначе
		НачисленияСотрудников = Объект.НачисленияСотрудников.Выгрузить(НачисленияСотрудника);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачисленияСотрудников", НачисленияСотрудников);
	
	Запрос.Выполнить();
	
	Возврат Запрос;
	
КонецФункции

&НаСервере
Функция НачисленияСотрудниковСОписанием(Запрос)
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТНачисления.Сотрудник КАК Сотрудник,
	|	ВТНачисления.Начисление,
	|	ВТНачисления.ДокументОснование,
	|	ВТНачисления.Размер,
	|	ВТНачисления.Действие,
	|	Начисления.Представление,
	|	Начисления.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания,
	|	НЕ Начисления.Рассчитывается КАК НачислениеФиксированнойСуммой,
	|	Начисления.Код
	|ИЗ
	|	ВТНачисления КАК ВТНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО ВТНачисления.Начисление = Начисления.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сотрудник,
	|	РеквизитДопУпорядочивания";
	
	НачисленияСотрудниковСОписанием = Запрос.Выполнить().Выгрузить();
	НачисленияСотрудниковСОписанием.Индексы.Добавить("Сотрудник");
	
	Возврат НачисленияСотрудниковСОписанием;
	
КонецФункции

&НаСервере
Функция ПоказателиСотрудников(Запрос)
	
	ПоказателиСотрудников = Новый ТаблицаЗначений;	
	ПоказателиСотрудников.Колонки.Добавить("Сотрудник");
	ПоказателиСотрудников.Колонки.Добавить("Показатель");
	ПоказателиСотрудников.Колонки.Добавить("ДокументОснование");
	ПоказателиСотрудников.Колонки.Добавить("Значение");
	Для каждого Строка Из Объект.ПоказателиСотрудников Цикл
		ЗаполнитьЗначенияСвойств(ПоказателиСотрудников.Добавить(), Строка);
	КонецЦикла;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВТНачисления.Сотрудник КАК Сотрудник,
	|	ВТНачисления.Начисление КАК Показатель,
	|	ВТНачисления.ДокументОснование КАК ДокументОснование,
	|	ВТНачисления.Размер КАК Значение
	|ИЗ
	|	ВТНачисления КАК ВТНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК Начисления
	|		ПО ВТНачисления.Начисление = Начисления.Ссылка
	|ГДЕ
	|	НЕ Начисления.Рассчитывается
	|	И НЕ ВТНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Отменить)";
	
	НачисленияФиксированнойСуммой = Запрос.Выполнить().Выгрузить();
	Для каждого Строка Из НачисленияФиксированнойСуммой Цикл
		ЗаполнитьЗначенияСвойств(ПоказателиСотрудников.Добавить(), Строка);
	КонецЦикла;
	
	ПоказателиСотрудников.Индексы.Добавить("Сотрудник");
	
	Возврат ПоказателиСотрудников;	
	
КонецФункции

&НаСервере
Функция ИзменяемыеПоказатели(Запрос)
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НачисленияДокумента.Начисление,
		|	НачисленияДокумента.Действие
		|ПОМЕСТИТЬ ВТНачисленияДокументаПредварительно
		|ИЗ
		|	&НачисленияДокумента КАК НачисленияДокумента
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачисленияДокумента.Начисление,
		|	НачисленияДокумента.Действие
		|ПОМЕСТИТЬ ВТНачисленияДокумента
		|ИЗ
		|	ВТНачисленияДокументаПредварительно КАК НачисленияДокумента
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Начисление,
		|	ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.ПустаяСсылка)
		|ИЗ
		|	ВТНачисления КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияДокументаПредварительно КАК НачисленияДокумента
		|		ПО Начисления.Начисление = НачисленияДокумента.Начисление
		|ГДЕ
		|	НачисленияДокумента.Начисление ЕСТЬ NULL 
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияПоказатели.Показатель,
		|	ПоказателиРасчетаЗарплаты.Идентификатор,
		|	ПоказателиРасчетаЗарплаты.КраткоеНаименование КАК Заголовок,
		|	ПоказателиРасчетаЗарплаты.РеквизитДопУпорядочивания КАК РеквизитДопУпорядочивания
		|ИЗ
		|	ВТНачисленияДокумента КАК НачисленияДокумента
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления.Показатели КАК НачисленияПоказатели
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПоказателиРасчетаЗарплаты КАК ПоказателиРасчетаЗарплаты
		|			ПО НачисленияПоказатели.Показатель = ПоказателиРасчетаЗарплаты.Ссылка
		|		ПО (НачисленияПоказатели.Ссылка = НачисленияДокумента.Начисление)
		|ГДЕ
		|	НачисленияПоказатели.ЗапрашиватьПриВводе
		|	И НачисленияДокумента.Действие <> ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Отменить)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Начисления.Начисление,
		|	NULL,
		|	ВЫБОР
		|		КОГДА ПВРНачисления.КраткоеНаименование = """"
		|			ТОГДА ПВРНачисления.Наименование
		|		ИНАЧЕ ПВРНачисления.КраткоеНаименование
		|	КОНЕЦ,
		|	ПВРНачисления.РеквизитДопУпорядочивания
		|ИЗ
		|	ВТНачисленияДокумента КАК Начисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.Начисления КАК ПВРНачисления
		|		ПО Начисления.Начисление = ПВРНачисления.Ссылка
		|ГДЕ
		|	НЕ ПВРНачисления.Рассчитывается
		|	И Начисления.Действие <> ЗНАЧЕНИЕ(Перечисление.ДействияСНачислениямиИУдержаниями.Отменить)
		|
		|УПОРЯДОЧИТЬ ПО
		|	РеквизитДопУпорядочивания";
	
	Запрос.УстановитьПараметр("НачисленияДокумента", Объект.Начисления.Выгрузить());
	ИзменяемыеПоказатели = Запрос.Выполнить().Выгрузить();
	ИзменяемыеПоказатели.Индексы.Добавить("Показатель"); 
	ЗаполнитьИдентификаторыНачисленийФиксированнойСуммой(ИзменяемыеПоказатели);
	
	Возврат ИзменяемыеПоказатели;
	
КонецФункции 

#Область ДополнитьФормуИзменяемымиПоказателями

&НаСервере
Процедура ДополнитьФормуИзменяемымиПоказателями(ИзменяемыеПоказатели)
	
	ДобавитьРеквизитыПоказателей(ИзменяемыеПоказатели);
	ДобавитьЭлементыПоказателей(ИзменяемыеПоказатели);
	
КонецПроцедуры

#Область ДобавитьРеквизитыПоказателей

&НаСервере
Процедура ДобавитьРеквизитыПоказателей(ИзменяемыеПоказатели)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавитьРеквизитыИзменяемыеПоказатели(ИзменяемыеПоказатели, ДобавляемыеРеквизиты);
	
	СуществующиеРеквизиты = МассивИменРеквизитовФормы();
	
	ЗарплатаКадры.ИзменитьРеквизитыФормы(ЭтаФорма, ДобавляемыеРеквизиты, СуществующиеРеквизиты);
	
	ЗаполнитьСоответствиеРеквизитовИПоказателей(ИзменяемыеПоказатели);

КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыИзменяемыеПоказатели(Показатели, ДобавляемыеРеквизиты)
	
	Для Каждого Показатель Из Показатели Цикл
		ДобавитьРеквизитыПоказателя(ДобавляемыеРеквизиты, Показатель.Идентификатор, Показатель.Заголовок);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСоответствиеРеквизитовИПоказателей(ТаблицаПоказателей)
	
	СтруктураКолонкиПоказателей = Новый Структура;
	Для Каждого СтрокаПоказателя Из ТаблицаПоказателей Цикл
		СтруктураКолонкиПоказателей.Вставить(СтрокаПоказателя.Идентификатор, СтрокаПоказателя.Показатель); 
	КонецЦикла;
	
	ЭтотОбъект.КолонкиПоказателей  = Новый ФиксированнаяСтруктура(СтруктураКолонкиПоказателей);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитыПоказателя(ДобавляемыеРеквизиты, ИмяРеквизита, Заголовок)
	
	Путь = ИмяТаблицыПоказатели();
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ИмяРеквизита, Справочники.ПоказателиРасчетаЗарплаты.ОписаниеТиповЗначенияПоказателяРасчетаЗарплаты(), Путь, Заголовок);
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ИмяРеквизитаПоказательИспользуется(ИмяРеквизита), Новый ОписаниеТипов("Булево"), Путь);
	
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ИмяРеквизитаПредставлениеТаблицы(ИмяРеквизита), Новый ОписаниеТипов("Строка"), Путь);
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ИмяРеквизитаТабличноеПредставление(ИмяРеквизита), Новый ОписаниеТипов("Булево"), Путь);
	ДобавитьРеквизитТаблицыЗначенийПоказателя(ДобавляемыеРеквизиты, ИмяРеквизита, Путь);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитТаблицыЗначенийПоказателя(ДобавляемыеРеквизиты, ИмяРеквизита, Путь)
	
	ТаблицаПоказателей = Новый РеквизитФормы(ИмяРеквизитаТаблицаПоказателя(ИмяРеквизита), Новый ОписаниеТипов("ТаблицаЗначений"), Путь);
	ДобавляемыеРеквизиты.Добавить(ТаблицаПоказателей);
	
	ПутьКТаблице = Путь + "." + ИмяРеквизитаТаблицаПоказателя(ИмяРеквизита);
	
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, "ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеНачисленияУдержания.Тип, ПутьКТаблице);
	ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, "Значение", Справочники.ПоказателиРасчетаЗарплаты.ОписаниеТиповЗначенияПоказателяРасчетаЗарплаты(), ПутьКТаблице);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьРеквизитПоказателя(ДобавляемыеРеквизиты, ПутьКДанным, ТипЗначения, Путь, Заголовок = Неопределено)
	НовыйПоказатель = Новый РеквизитФормы(ПутьКДанным, ТипЗначения, Путь, Заголовок);
	ДобавляемыеРеквизиты.Добавить(НовыйПоказатель);
КонецПроцедуры

&НаСервере
Функция МассивИменРеквизитовФормы()
	МассивИменРеквизитовФормы = Новый Массив;
	ЗарплатаКадры.ЗаполнитьМассивИменРеквизитовФормы(ЭтаФорма, МассивИменРеквизитовФормы, "Объект.Сотрудники");
	Возврат МассивИменРеквизитовФормы;
КонецФункции 

#КонецОбласти

#Область ДобавитьЭлементыПоказателей

&НаСервере
Процедура ДобавитьЭлементыПоказателей(ИзменяемыеПоказатели)
	
	УдалитьЭлементыИзменяемыхПоказателей();
	ДобавитьЭлементыИзменяемыеПоказатели(ИзменяемыеПоказатели);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементыИзменяемыхПоказателей()
	ГруппаПоказателиСотрудников = ГруппаПоказателиСотрудников();
	УдалитьУсловноеОформлениеПоказателей(ГруппаПоказателиСотрудников);
	ЗарплатаКадры.УдалитьПодчиненныеЭлементыГруппы(ЭтаФорма, ГруппаПоказателиСотрудников);
КонецПроцедуры

&НаСервере
Процедура УдалитьУсловноеОформлениеПоказателей(ГруппаПоказателиСотрудников)
	
	УдаляемыеЭлементы = Новый Массив;	
	
	ПредставлениеУникальногоИдентификатора = Строка(ЭтотОбъект.УникальныйИдентификатор);
	
	Для каждого ЭлементОформления Из ЭтотОбъект.УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = ПредставлениеУникальногоИдентификатора Тогда
			УдаляемыеЭлементы.Добавить(ЭлементОформления);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Элемент Из УдаляемыеЭлементы Цикл
		ЭтотОбъект.УсловноеОформление.Элементы.Удалить(Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементыИзменяемыеПоказатели(ТаблицаПоказателей)
	
	ГруппаПоказатели = ГруппаПоказателиСотрудников();
	
	Для Каждого СтрокаПоказателя Из ТаблицаПоказателей Цикл
		НовыйЭлемент = НовыйЭлементФормыИзменяемыйПоказатель(СтрокаПоказателя.Идентификатор, ГруппаПоказатели, СтрокаПоказателя.Заголовок);
		УстановитьФорматИзменяемогоПоказателя(НовыйЭлемент, СтрокаПоказателя.Показатель);
		УстановитьУсловноеОформлениеПоказателя(СтрокаПоказателя.Идентификатор);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НовыйЭлементФормыИзменяемыйПоказатель(ПутьКДанным, Группа, Заголовок)
	ЭлементПоказатель = ЭтаФорма.Элементы.Добавить(ПрефиксЭлементаПоказателиСотрудников() + ПутьКДанным, Тип("ПолеФормы"), Группа);
	ЭлементПоказатель.ПутьКДанным = "Объект.Сотрудники." + ПутьКДанным;
	ЭлементПоказатель.Заголовок = Заголовок;
	ЭлементПоказатель.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементПоказатель.РастягиватьПоГоризонтали = Истина;
	ЭлементПоказатель.Ширина = 10;
	ЭлементПоказатель.УстановитьДействие("ПриИзменении", "Подключаемый_ПоказателиСотрудниковПоказательПриИзменении");
	Возврат ЭлементПоказатель;	
КонецФункции

&НаСервере
Процедура УстановитьФорматИзменяемогоПоказателя(ЭлементПоказатель, Показатель)
	Если ТипЗнч(Показатель) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда
		ПоказательИнфо = ЗарплатаКадрыРасширенный.СведенияОПоказателеРасчетаЗарплаты(Показатель);
		ЭлементПоказатель.ОграничениеТипа = ПоказательИнфо.ТипПоказателя;
		ЭлементПоказатель.Формат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ЧДЦ=%1", ПоказательИнфо["Точность"]);
	Иначе
		ЭлементПоказатель.ОграничениеТипа = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 2));
		ЭлементПоказатель.Формат = "ЧДЦ=2";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПоказателя(ПутьКДанным)
	
	УстановитьУсловноеОформлениеПоказателяТолькоПросмотр(ПутьКДанным);
	УстановитьУсловноеОформлениеПоказателяТекст(ПутьКДанным);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПоказателяТолькоПросмотр(ПутьКДанным)
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	ЭлементУсловногоОформления.Представление = Строка(ЭтотОбъект.УникальныйИдентификатор);
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сотрудники." + ИмяРеквизитаПоказательИспользуется(ПутьКДанным));
	ЭлементОтбора.ПравоеЗначение = Ложь;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить(); 
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ПрефиксЭлементаПоказателиСотрудников() + ПутьКДанным);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПоказателяТекст(ПутьКДанным)
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("Объект.Сотрудники." + ИмяРеквизитаПредставлениеТаблицы(ПутьКДанным)));    	
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", Новый Шрифт(,,,,Истина));    	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылки);    	
	
	ЭлементУсловногоОформления.Представление = Строка(ЭтотОбъект.УникальныйИдентификатор);
	
	ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));	
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Сотрудники." + ИмяРеквизитаТабличноеПредставление(ПутьКДанным));
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить(); 
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ПрефиксЭлементаПоказателиСотрудников() + ПутьКДанным);		
	
КонецПроцедуры

&НаСервере
Функция ГруппаПоказателиСотрудников()
	Возврат ЭтаФорма.Элементы.Найти("ГруппаПоказателиСотрудников");
КонецФункции 

&НаСервере
Функция ИмяРеквизитаПоказательИспользуется(ИмяРеквизита)
	Возврат ИмяРеквизита + "Используется";
КонецФункции 

&НаСервере
Функция ИмяРеквизитаТаблицаПоказателя(ИмяРеквизита)
	Возврат ИмяРеквизита + "Таблица";
КонецФункции 

&НаСервере
Функция ИмяРеквизитаПредставлениеТаблицы(ИмяРеквизита)
	Возврат ИмяРеквизита + "ПредставлениеТаблицы";
КонецФункции 

&НаСервере
Функция ИмяРеквизитаТабличноеПредставление(ИмяРеквизита)
	Возврат ИмяРеквизита + "ТабличноеПредставление";
КонецФункции 

&НаСервере
Функция ИмяТаблицыПоказатели()
	Возврат "Объект.Сотрудники";
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Функция ПрефиксЭлементаПоказателиСотрудников()
	Возврат "ЗначенияПоказателейСотрудников";
КонецФункции 

#КонецОбласти

&НаСервере
Процедура ЗаполнитьИдентификаторыНачисленийФиксированнойСуммой(ИзменяемыеПоказатели)
	Для каждого Строка Из ИзменяемыеПоказатели Цикл
		Если Строка.Идентификатор = Null Тогда
			Строка.Идентификатор = "а" + СтрЗаменить(Строка(Строка.Показатель.УникальныйИдентификатор()), "-", "");
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
	
#КонецОбласти 

&НаСервере
Процедура ДанныеСотрудниковВСтроки(НачисленияСотрудниковСОписанием, ПоказателиСотрудников, ОписаниеИзменяемыхПоказателей, ФильтрСотрудников = Неопределено)
	
	Отбор = Новый Структура("Сотрудник");
	
	ПредставлениеПустойКоллекцииНачислений = ПредставлениеПустойКоллекцииНачислений();
	Если ФильтрСотрудников = Неопределено Тогда
		СтрокиСотрудников = Объект.Сотрудники;
	Иначе
		
		СтрокиСотрудников = Новый Массив;
		Для каждого ОписаниеСотрудника Из ФильтрСотрудников Цикл
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СтрокиСотрудников, Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ОписаниеСотрудника.Сотрудник)));
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаСотрудника Из СтрокиСотрудников Цикл
		
		Отбор.Сотрудник = СтрокаСотрудника.Сотрудник;
		
		ПоказателиСотрудника = ПоказателиСотрудников.НайтиСтроки(Отбор);
		НачисленияСотрудника = НачисленияСотрудниковСОписанием.НайтиСтроки(Отбор);
		
		ДанныеСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника, НачисленияСотрудника, ОписаниеИзменяемыхПоказателей, ПредставлениеПустойКоллекцииНачислений);
		
	КонецЦикла;
	
	Объект.ПоказателиСотрудников.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ДанныеСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника, НачисленияСотрудника, ОписаниеИзменяемыхПоказателей, ПредставлениеПустойКоллекцииНачислений)
	
	НачисленияСотрудникаВСтроку(СтрокаСотрудника, НачисленияСотрудника, ПредставлениеПустойКоллекцииНачислений);
	ПоказателиСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника, ОписаниеИзменяемыхПоказателей);
	
КонецПроцедуры

&НаСервере
Процедура НачисленияСотрудникаВСтроку(СтрокаСотрудника, НачисленияСотрудника, ПредставлениеПустойКоллекцииНачислений)
	
	Если НачисленияСотрудника.Количество() = 0 Тогда
		СтрокаСотрудника.ПредставлениеНачислений = ПредставлениеПустойКоллекцииНачислений;
		Возврат;
	КонецЕсли;
	
	ПредставлениеНачисленийСотрудника = "";
	ПредставлениеОтмененныхНачисленийСотрудника = "";
	ЕстьОтмененныеНачисления = Ложь;
	
	Для каждого СтрокаНачисления Из НачисленияСотрудника Цикл
		
		Если СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда
			
			ПредставлениеОтмененныхНачисленийСотрудника = ПредставлениеОтмененныхНачисленийСотрудника + СтрокаНачисления.Представление+ ", ";
			
			ЕстьОтмененныеНачисления = Истина;
			
		Иначе
			
			Если СтрокаНачисления.Размер = 0 Тогда
				ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + СтрокаНачисления.Представление+ ", ";
			Иначе
				ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + СтрокаНачисления.Представление + "=" + СтрокаНачисления.Размер + ", ";		
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПредставлениеНачисленийСотрудника = Лев(ПредставлениеНачисленийСотрудника, СтрДлина(ПредставлениеНачисленийСотрудника) - 2);
	Если ЕстьОтмененныеНачисления Тогда
		
		ПредставлениеОтмененныхНачисленийСотрудника = Лев(ПредставлениеОтмененныхНачисленийСотрудника, СтрДлина(ПредставлениеОтмененныхНачисленийСотрудника) - 2);
		
		Если Не ПустаяСтрока(ПредставлениеНачисленийСотрудника) Тогда
			ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + "; ";
		КонецЕсли;
		
		ПредставлениеНачисленийСотрудника = ПредставлениеНачисленийСотрудника + СтрШаблон(НСтр("ru='Отменены: %1';uk='Скасовані: %1'"), ПредставлениеОтмененныхНачисленийСотрудника); 
		
	КонецЕсли;
	
	СтрокаСотрудника.ПредставлениеНачислений = ПредставлениеНачисленийСотрудника;
	
КонецПроцедуры

&НаСервере
Процедура ПоказателиСотрудникаВСтроку(СтрокаСотрудника, ПоказателиСотрудника, ОписаниеИзменяемыхПоказателей)
	
	Для каждого ОписаниеПоказателя Из ОписаниеИзменяемыхПоказателей Цикл
		СтрокаСотрудника[ОписаниеПоказателя.Значение.ИмяРеквизитаТаблицаПоказателя].Очистить();
		СтрокаСотрудника[ОписаниеПоказателя.Значение.ИмяРеквизитаПредставлениеТаблицы] = "";
		СтрокаСотрудника[ОписаниеПоказателя.Значение.ИмяРеквизитаТабличноеПредставление] = Ложь;
	КонецЦикла;
	
	Для каждого ПоказательСотрудника Из ПоказателиСотрудника Цикл
		ОписаниеПоказателя = ОписаниеИзменяемыхПоказателей[ПоказательСотрудника.Показатель];
		Если Не ОписаниеПоказателя = Неопределено Тогда
			УстановитьЗначениеПоказателяВСтроке(СтрокаСотрудника, ОписаниеПоказателя, ПоказательСотрудника);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ОписаниеПоказателей(ИзменяемыеПоказатели)
	
	ИдентификаторыПоказателей = Новый Соответствие;
	
	Для каждого Строка Из ИзменяемыеПоказатели Цикл
		ОписаниеПоказателя = Новый Структура;
		ОписаниеПоказателя.Вставить("ИдентификаторПоказателя", Строка.Идентификатор);
		ОписаниеПоказателя.Вставить("ИмяРеквизитаПоказательИспользуется", ИмяРеквизитаПоказательИспользуется(Строка.Идентификатор));
		ОписаниеПоказателя.Вставить("ИмяРеквизитаПредставлениеТаблицы", ИмяРеквизитаПредставлениеТаблицы(Строка.Идентификатор));
		ОписаниеПоказателя.Вставить("ИмяРеквизитаТаблицаПоказателя", ИмяРеквизитаТаблицаПоказателя(Строка.Идентификатор));
		ОписаниеПоказателя.Вставить("ИмяРеквизитаТабличноеПредставление", ИмяРеквизитаТабличноеПредставление(Строка.Идентификатор));
		ИдентификаторыПоказателей.Вставить(Строка.Показатель, ОписаниеПоказателя);
	КонецЦикла;
	
	Возврат ИдентификаторыПоказателей;
	
КонецФункции

&НаСервере
Процедура УстановитьЗначениеПоказателяВСтроке(СтрокаСотрудника, ОписаниеПоказателя, ДанныеПоказателя)
	
	СтрокаСотрудника[ОписаниеПоказателя.ИдентификаторПоказателя] 				= ДанныеПоказателя.Значение;
	СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаПоказательИспользуется] 	= Истина;
	
	НоваяСтрокаПоказателя = СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаТаблицаПоказателя].Добавить();
	НоваяСтрокаПоказателя.ДокументОснование = ДанныеПоказателя.ДокументОснование;
	НоваяСтрокаПоказателя.Значение = ДанныеПоказателя.Значение;
	
	СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаТабличноеПредставление]	= СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаТаблицаПоказателя].Количество() > 1;
	
	Если ЗначениеЗаполнено(СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаПредставлениеТаблицы]) Тогда
		СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаПредставлениеТаблицы] = СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаПредставлениеТаблицы] + "; " + ДанныеПоказателя.Значение;
	Иначе
		СтрокаСотрудника[ОписаниеПоказателя.ИмяРеквизитаПредставлениеТаблицы] = Строка(ДанныеПоказателя.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеПустойКоллекцииНачислений()
	
	Возврат НСтр("ru='Ввести';uk='Ввести'");	
	
КонецФункции

#КонецОбласти

#Область РеквизитФормыВДанныеСотрудников

&НаСервере
Процедура РеквизитФормыВДанныеСотрудников(ОбъектПолучатель)
	
	Если ЕстьРедактируемыеПоказатели() Тогда
		РеквизитФормыВПоказателиСотрудников(ОбъектПолучатель); 
	Иначе	
		ПеренестиСотрудниковБезПоказателей(ОбъектПолучатель);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеренестиСотрудниковБезПоказателей(ОбъектПолучатель)
	Для каждого ПоказателиСотрудника Из Объект.Сотрудники Цикл
		Строка 				= ОбъектПолучатель.ПоказателиСотрудников.Добавить();
		Строка.Сотрудник 	= ПоказателиСотрудника.Сотрудник; 
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура РеквизитФормыВПоказателиСотрудников(ОбъектПолучатель)
	Для каждого ПоказателиСотрудника Из Объект.Сотрудники Цикл
		РеквизитФормыВПоказателиСотрудника(ОбъектПолучатель.ПоказателиСотрудников, ПоказателиСотрудника);
		РеквизитФормыВНачисленияСотрудника(ОбъектПолучатель.НачисленияСотрудников, ПоказателиСотрудника);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура РеквизитФормыВПоказателиСотрудника(ПоказателиСотрудников, ПоказателиСотрудника)
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
		Если ПоказательИспользуется(ПоказателиСотрудника, КолонкаПоказатель.Ключ) 
			И ТипЗнч(КолонкаПоказатель.Значение) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда	
			Для каждого СтрокаПоказателя Из ПоказателиСотрудника[ИмяРеквизитаТаблицаПоказателя(КолонкаПоказатель.Ключ)] Цикл
				Строка 						= ПоказателиСотрудников.Добавить();
				Строка.Сотрудник 			= ПоказателиСотрудника.Сотрудник;
				Строка.Показатель 			= КолонкаПоказатель.Значение;
				Строка.ДокументОснование 	= СтрокаПоказателя.ДокументОснование;
				Строка.Значение 			= СтрокаПоказателя.Значение;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура РеквизитФормыВНачисленияСотрудника(НачисленияСотрудников, ПоказателиСотрудника)
	Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
		Если ПоказательИспользуется(ПоказателиСотрудника, КолонкаПоказатель.Ключ) 
			И ТипЗнч(КолонкаПоказатель.Значение) = Тип("ПланВидовРасчетаСсылка.Начисления") Тогда
			Для каждого СтрокаПоказателя Из ПоказателиСотрудника[ИмяРеквизитаТаблицаПоказателя(КолонкаПоказатель.Ключ)] Цикл
				СтрокиНачисления = НачисленияСотрудников.НайтиСтроки(Новый Структура("Сотрудник,Начисление,ДокументОснование", ПоказателиСотрудника.Сотрудник, КолонкаПоказатель.Значение, СтрокаПоказателя.ДокументОснование));
				Если СтрокиНачисления.Количество() > 0 Тогда
					СтрокиНачисления[0].Размер = СтрокаПоказателя.Значение;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция ЕстьРедактируемыеПоказатели()
	Возврат НЕ КолонкиПоказателей = Неопределено И КолонкиПоказателей.Количество() > 0;
КонецФункции

&НаСервере
Функция ПоказательИспользуется(ПоказателиСотрудника, Показатель)
	ИмяРеквизитаПоказательИспользуется = ИмяРеквизитаПоказательИспользуется(Показатель);
	Возврат ПоказателиСотрудника[ИмяРеквизитаПоказательИспользуется];
КонецФункции

#КонецОбласти

#Область ЗаполнитьНачисленияПоказатели

&НаСервере
Процедура ЗаполнитьНачисленияПоказателиСотрудников(СообщатьПользователю = Ложь, ФильтрСотрудников = Неопределено)
	
	Если НЕ ФормаДокументаГотоваДляЗаполнения(СообщатьПользователю) Тогда
		Возврат;
	КонецЕсли;
	
	Если ФильтрСотрудников = Неопределено Тогда
		РеквизитФормыВДанныеСотрудников(Объект);
		ЗаполнитьДокумент(ФильтрСотрудников);
	Иначе
		
		Для каждого ОписаниеСотрудника Из ФильтрСотрудников Цикл
			
			СтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", ОписаниеСотрудника.Сотрудник));
			Если СтрокиСотрудника.Количество() > 0 Тогда
				
				УдалитьНачисленияПоСотруднику(ОписаниеСотрудника.Сотрудник);
				УдалитьПоказателиПоСотруднику(ОписаниеСотрудника.Сотрудник);
				
			КонецЕсли;
			
		КонецЦикла;
		
		НачисленияПоказателиСотрудников = Документы.ИзменениеПлановыхНачислений.НачисленияПоказателиСотрудниковПоОбъекту(
			Объект, ФильтрСотрудников);
		
		Документы.ИзменениеПлановыхНачислений.ЗаполнитьСотрудников(Объект, НачисленияПоказателиСотрудников.Сотрудники);
		Документы.ИзменениеПлановыхНачислений.ЗаполнитьНачисленияПоказатели(
			Объект, НачисленияПоказателиСотрудников.НачисленияСотрудников, НачисленияПоказателиСотрудников.ПоказателиСотрудников);
			
	КонецЕсли;
	
	ДанныеСотрудниковВРеквизитФормы(ФильтрСотрудников);
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	
	ФОТСотрудников = Неопределено;
	
	Если Объект.ЭтоОтражениеИзмененияШтатногоРасписания Тогда
		
		ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
		ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников);
		
		ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников);
		
		НачисленияСотрудников = ПлановыеНачисленияПоказателиСотрудников.НачисленияСотрудников.СкопироватьКолонки();
		ПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудников.ПоказателиСотрудников.СкопироватьКолонки();
		
		ЗаполнитьНачисленияПоказателиСотрудниковДляРасчетаСовокупнойСтавки(ФОТПлановыхНачисленийСотрудников, НачисленияСотрудников, ПоказателиСотрудников);	
		
	Иначе 
		
		НачисленияСотрудников = ПлановыеНачисленияПоказателиСотрудников.НачисленияСотрудников;
		ПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудников.ПоказателиСотрудников;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(НачисленияСотрудников, ПоказателиСотрудников);
	
	ФОТСотрудниковВРеквизитФормы(ФОТСотрудников);
	
	ЗаполнитьПредставлениеНачислений();
	ОбновитьЗаголовокГруппыНачисления(ЭтотОбъект, "ГруппаФильтрНачисления");
	
	УстановитьДоступностьКомандыЗаполнитьПоказатели();
	
	ЗарплатаКадрыКлиентСервер.КлючевыеРеквизитыЗаполненияФормыУстановитьОтображениеПредупреждения(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияПоказателиСотрудника(Строка)
	
	Если НЕ ЗначениеЗаполнено(Строка.Сотрудник) Тогда
		Возврат;
	КонецЕсли;
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	ЭлементФильтра = ФильтрСотрудников.Добавить();
	ЭлементФильтра.Сотрудник = Строка.Сотрудник;
	ЭлементФильтра.ДатаИзменения = Строка.ВремяРегистрации;
	
	ЗаполнитьНачисленияПоказателиСотрудников(Ложь, ФильтрСотрудников);
	
КонецПроцедуры

&НаСервере
Функция ФормаДокументаГотоваДляЗаполнения(СообщатьПользователю = Ложь)
	
	ФормаДокументаГотова = Истина;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда		
		ФормаДокументаГотова = Ложь;		
		Если СообщатьПользователю Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Необходимо указать организацию.';uk='Необхідно вказати організацію.'"), , "Организация");
		КонецЕсли;		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.ДатаИзменения) Тогда		
		ФормаДокументаГотова = Ложь;		
		Если СообщатьПользователю Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Необходимо указать дату изменения.';uk='Необхідно вказати дату зміни.'"), , "ДатаИзменения");
		КонецЕсли;		
	КонецЕсли;
	
	Возврат ФормаДокументаГотова;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДокумент(ФильтрСотрудников = Неопределено)	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьДокумент(ФильтрСотрудников);
	ЗначениеВРеквизитФормы(Документ, "Объект");
КонецПроцедуры

#КонецОбласти

#Область ФормаРедактированияСоставаНачисленийИУдержаний

&НаКлиенте
Процедура ОткрытьФормуРедактированияСоставаНачисленийИУдержаний(ТекущиеДанные, ТекущаяСтрока, РедактироватьСоставНачислений)
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Сотрудник) Тогда
		
		ПараметрыРедактирования = ПараметрыРедактированияСоставаНачисленийИУдержаний(ТекущаяСтрока, РедактироватьСоставНачислений);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("АдресВХранилище", ПоместитьВоВременноеХранилище(ПараметрыРедактирования, УникальныйИдентификатор));
		ПараметрыОткрытия.Вставить("ТолькоПросмотр", ТолькоПросмотр);
		
		ЗарплатаКадрыРасширенныйКлиент.ОткрытьФормуРедактированияСоставаНачисленийИУдержаний(ПараметрыОткрытия, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыРедактированияСоставаНачисленийИУдержаний(ТекущаяСтрока, РедактироватьСоставНачислений)
	
	СтрокаСотрудника = Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока);
	Сотрудник = СтрокаСотрудника.Сотрудник;
	
	ПараметрыРедактирования = ЗарплатаКадрыРасширенныйКлиентСервер.ПараметрыРедактированияСоставаНачисленийИУдержаний();
	
	ПараметрыРедактирования.ВладелецНачисленийИУдержаний = Сотрудник;
	ПараметрыРедактирования.ДатаРедактирования = СтрокаСотрудника.ВремяРегистрации;
	ПараметрыРедактирования.Организация = Объект.Организация;
	ПараметрыРедактирования.РежимРаботы = 3;
	
	ФильтрСотрудников = Документы.ИзменениеПлановыхНачислений.ПустойФильтрСотрудников();
	ЭлементФильтра = ФильтрСотрудников.Добавить();
	ЭлементФильтра.Сотрудник = СтрокаСотрудника.Сотрудник;
	ЭлементФильтра.ДатаИзменения = СтрокаСотрудника.ВремяРегистрации;

	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников);
	ФОТПлановыхНачисленийСИзменениямиДокумента = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ФОТПлановыхНачисленийСотрудникаСИзменениямиДокумента = ФОТПлановыхНачисленийСИзменениямиДокумента.Получить(СтрокаСотрудника.Сотрудник);	
	ПриведенныеНачисленияПоказатели = НачисленияПоказателиСотрудникаПриведенныеДляФормыРедактирования(ПлановыеНачисленияПоказателиСотрудников, ФОТПлановыхНачисленийСотрудникаСИзменениямиДокумента);
	
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.Используется = Истина;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.ИзменятьСоставВидовРасчета = РедактироватьСоставНачислений;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.ИзменятьЗначенияПоказателей = РедактироватьСоставНачислений;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.НомерТаблицы = 1;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.ПоказатьФОТ = НЕ РедактироватьСоставНачислений;
	
	РедактируемыеНачисления = Новый Массив;
	Для каждого Строка Из Объект.Начисления Цикл
		Если Строка.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда
			Продолжить;
		КонецЕсли;
		РедактируемыеНачисления.Добавить(Строка.Начисление);		
	КонецЦикла;
	
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.РедактируемыеНачисления = РедактируемыеНачисления;
	ПараметрыРедактирования.ОписаниеТаблицыНачислений.Таблица = ПриведенныеНачисленияПоказатели.Начисления;
	ПараметрыРедактирования.Показатели = ПриведенныеНачисленияПоказатели.Показатели;
	
	Возврат ПараметрыРедактирования;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеСотрудникаИзВРеменногоХранилища(АдресВХранилище)
	
	ДанныеИзХранилища = ПолучитьИзВременногоХранилища(АдресВХранилище);
	
	Если ДанныеИзХранилища <> Неопределено Тогда
		
		РеквизитФормыВДанныеСотрудников(Объект);
		
		ЗаполнитьНачисленияПоСотрудникуИзХранилища(ДанныеИзХранилища);
		
		ЗаполнитьПоказателиПоСотрудникуИзХранилища(ДанныеИзХранилища);
		
		ДанныеСотрудниковВРеквизитФормы();
		
		ФОТСотрудниковВРеквизитФормы();
		
		ЗаполнитьЗначенияСовокупныхСтавокПоСотрудникуИзХранилища(ДанныеИзХранилища);
		
		УстановитьДоступностьКомандыЗаполнитьПоказатели();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияПоСотрудникуИзХранилища(ДанныеИзХранилища)
	
	Сотрудник = ДанныеИзХранилища.ВладелецНачисленийИУдержаний;
	
	УдалитьНачисленияПоСотруднику(Сотрудник);	
	
	Отбор = Новый Структура;
	ФильтроватьНачисления = Объект.Начисления.Количество() > 0;
	Для каждого НачислениеСотрудника Из ДанныеИзХранилища.Начисления Цикл
		
		Отбор.Вставить("Начисление", НачислениеСотрудника.Начисление);
		
		Если ФильтроватьНачисления И Объект.Начисления.НайтиСтроки(Отбор).Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Объект.НачисленияСотрудников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, НачислениеСотрудника);
		НоваяСтрока.Сотрудник = Сотрудник;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоказателиПоСотрудникуИзХранилища(ДанныеИзХранилища)
	
	Сотрудник = ДанныеИзХранилища.ВладелецНачисленийИУдержаний;
	
	УдалитьПоказателиПоСотруднику(Сотрудник);
	Отбор = Новый Структура;
	Отбор.Вставить("Сотрудник", Сотрудник);
	
	Для каждого ОписаниеПоказателя Из ДанныеИзХранилища.Показатели Цикл
		
		ОписаниеНачисления = НайтиПоЗначениюПоляВКоллекцииСтруктур(ДанныеИзХранилища.Начисления, "ИдентификаторСтрокиВидаРасчета", ОписаниеПоказателя.ИдентификаторСтрокиВидаРасчета);
		
		Если Не ОписаниеНачисления = Неопределено 
			И ОписаниеНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда
			Продолжить;
		КонецЕсли;
		
		Отбор.Вставить("Показатель", ОписаниеПоказателя.Показатель);
		Отбор.Вставить("ДокументОснование", ОписаниеПоказателя.ДокументОснование);
		СтрокиПоказателя = Объект.ПоказателиСотрудников.НайтиСтроки(Отбор);
		
		Если СтрокиПоказателя.Количество() = 0 Тогда
			СтрокаПоказателя = Объект.ПоказателиСотрудников.Добавить();
			СтрокаПоказателя.Сотрудник = Сотрудник;
		Иначе
			СтрокаПоказателя = СтрокиПоказателя[0];
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтрокаПоказателя, ОписаниеПоказателя);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСовокупныхСтавокПоСотрудникуИзХранилища(ДанныеИзХранилища)
	
	ТаблицаНачислений = Новый ТаблицаЗначений();
	ТаблицаНачислений.Колонки.Добавить("Сотрудник");
	ТаблицаНачислений.Колонки.Добавить("Начисление");
	ТаблицаНачислений.Колонки.Добавить("Размер");
	ТаблицаНачислений.Колонки.Добавить("Действие");
	
	Для каждого СтрокаНачисления Из ДанныеИзХранилища.Начисления Цикл
		НоваяСтрока = ТаблицаНачислений.Добавить();
		НоваяСтрока.Сотрудник = ДанныеИзХранилища.ВладелецНачисленийИУдержаний;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления, "Начисление,Размер,Действие");
	КонецЦикла;
	
	ТаблицаПоказателей = Новый ТаблицаЗначений();
	ТаблицаПоказателей.Колонки.Добавить("Сотрудник");
	ТаблицаПоказателей.Колонки.Добавить("Показатель");
	ТаблицаПоказателей.Колонки.Добавить("Значение");
	
	Для каждого СтрокаНачисления Из ДанныеИзХранилища.Показатели Цикл
		НоваяСтрока = ТаблицаПоказателей.Добавить();
		НоваяСтрока.Сотрудник = ДанныеИзХранилища.ВладелецНачисленийИУдержаний;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНачисления, "Показатель,Значение");
	КонецЦикла;
	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(ТаблицаНачислений, ТаблицаПоказателей);
	
КонецПроцедуры

&НаСервере
Функция НайтиПоЗначениюПоляВКоллекцииСтруктур(Коллекция, Поле, Значение)
	
	НайденнаяСтруктура = Неопределено;
	
	Для каждого Структура Из Коллекция Цикл
		Если Структура.Свойство(Поле) 
			И Структура[Поле] = Значение Тогда
			НайденнаяСтруктура = Структура;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденнаяСтруктура;
	
КонецФункции

#КонецОбласти

#Область ФормаРедактированияПлановогоПоказателяПоДокументамОснованиям

&НаКлиенте
Процедура ОткрытьФормуРедактированияПлановогоПоказателяПоДокументамОснованиям(Строка, ИмяПоказателя) Экспорт
	
	ИмяРеквизитаТаблица = ИмяРеквизитаТаблицаПоказателя(ИмяПоказателя);
	
	ЗначенияПоказателя = Новый Массив;
	Для каждого ЗначениеПоказателя Из Строка[ИмяРеквизитаТаблица] Цикл
		ЗначенияПоказателя.Добавить(Новый ФиксированнаяСтруктура(Новый Структура("ДокументОснование,Значение", ЗначениеПоказателя.ДокументОснование, ЗначениеПоказателя.Значение)))
	КонецЦикла;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Сотрудник", Строка.Сотрудник);
	ПараметрыОткрытия.Вставить("Показатель", КолонкиПоказателей[ИмяПоказателя]);
	ПараметрыОткрытия.Вставить("ЗначенияПоказателя", Новый ФиксированныйМассив(ЗначенияПоказателя));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеРедактированияПлановогоПоказателяПоДокументамОснованиям", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.РедактированиеПлановогоПоказателяРасчетаЗарплатыСотрудникаПоДокументамОснованиям", ПараметрыОткрытия, ЭтотОбъект,,,, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеРедактированияПлановогоПоказателяПоДокументамОснованиям(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиСотрудника = Объект.Сотрудники.НайтиСтроки(Новый Структура("Сотрудник", Результат.Сотрудник));
	
	Если СтрокиСотрудника = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаСотрудника = СтрокиСотрудника[0];
	
	ИмяПоказателя = Неопределено;
	Для каждого ОписаниеПоказателя Из КолонкиПоказателей Цикл
		Если ОписаниеПоказателя.Значение = Результат.Показатель Тогда
			ИмяПоказателя = ОписаниеПоказателя.Ключ;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ИмяПоказателя = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаТаблица = ИмяРеквизитаТаблицаПоказателя(ИмяПоказателя);	
	ИмяРеквизитаПредставлениеТаблицы = ИмяРеквизитаПредставлениеТаблицы(ИмяПоказателя);
	
	Отбор = Новый Структура("ДокументОснование");
	Для каждого ЗначениеПоказателя Из Результат.ЗначенияПоказателя Цикл
		Отбор.ДокументОснование = ЗначениеПоказателя.ДокументОснование;
		СтрокиПоДокументуОснованию = СтрокаСотрудника[ИмяРеквизитаТаблица].НайтиСтроки(Отбор);
		Если СтрокиПоДокументуОснованию.Количество() > 0 Тогда
			СтрокиПоДокументуОснованию[0].Значение = ЗначениеПоказателя.Значение;
		КонецЕсли;
	КонецЦикла;
	
	ПредставлениеТаблицы = "";
	Для каждого ЗначениеПоказателя Из СтрокаСотрудника[ИмяРеквизитаТаблица] Цикл
	  ПредставлениеТаблицы = ПредставлениеТаблицы + ЗначениеПоказателя.Значение + "; ";
	КонецЦикла;
	
	СтрокаСотрудника[ИмяРеквизитаПредставлениеТаблицы] = Лев(ПредставлениеТаблицы, СтрДлина(ПредставлениеТаблицы) - 2);
	
	ПоказателиСотрудниковПриОкончанииРедактированияНаСервере(СтрокаСотрудника.ПолучитьИдентификатор());

КонецПроцедуры

#КонецОбласти

#Область ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента

&НаСервере
Функция ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента(ФильтрСотрудников = Неопределено)
	
	НачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудников(ФильтрСотрудников);
	
	ПрименитьИзмененияВДокументеКПлановымНачислениямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников);
	ПрименитьИзмененияВДокументеКПлановымПоказателямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников);
	
	Возврат НачисленияПоказателиСотрудников;
	
КонецФункции

&НаСервере
Функция ПлановыеНачисленияПоказателиСотрудников(ФильтрСотрудников = Неопределено)
			
	ПараметрыПолучения = ПараметрыПолученияНачисленийПоказателейСотрудников();
	Если Не ФильтрСотрудников = Неопределено Тогда
		ПараметрыПолучения.Вставить("ИспользоватьФильтрСотрудников", Истина);
		ПараметрыПолучения.Вставить("ФильтрСотрудников", ФильтрСотрудников);
	КонецЕсли;
                    	
	НачисленияПоказателиСотрудников = Документы.ИзменениеПлановыхНачислений.НачисленияПоказателиСотрудников(ПараметрыПолучения);
	
	Возврат НачисленияПоказателиСотрудников;
	
КонецФункции	   	

&НаСервере
Функция НачисленияПоказателиСотрудникаПриведенныеДляФормыРедактирования(ПлановыеНачисленияПоказателиСотрудника, ФОТПлановыхНачисленийСотрудника)
	
	МассивНачислений = Новый Массив;
	МассивПоказателей = Новый Массив;
	
	ИдентификаторСтрокиВидаРасчета = 1;
	
	ПостоянныеПоказателиНачислений = Новый Соответствие;
	
	// Добавление всех начислений сотрудника.
	Для Каждого СтрокаНачислений Из ПлановыеНачисленияПоказателиСотрудника.НачисленияСотрудников Цикл
		
		СтруктураНачисления = Новый Структура("Начисление,ДокументОснование,ИдентификаторСтрокиВидаРасчета,Размер,Действие");
		ЗаполнитьЗначенияСвойств(СтруктураНачисления, СтрокаНачислений);
		СтруктураНачисления.ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета;
		
		Отбор = Новый Структура("Начисление, ДокументОснование", СтрокаНачислений.Начисление, СтрокаНачислений.ДокументОснование);
		СтрокиПлановыхНачислений = ФОТПлановыхНачисленийСотрудника.Начисления.НайтиСтроки(Отбор);
		Если СтрокиПлановыхНачислений.Количество() > 0 Тогда 
			СтруктураНачисления.Размер = СтрокиПлановыхНачислений[0].Размер;
		КонецЕсли;
		МассивНачислений.Добавить(СтруктураНачисления);
		
		// Добавление показателей начислений.
		
		ПостоянныеПоказателиНачисления = ПостоянныеПоказателиНачислений.Получить(СтрокаНачислений.Начисление); 
		Если ПостоянныеПоказателиНачисления = Неопределено Тогда
			ПостоянныеПоказателиНачисления = ПостоянныеПоказателиНачисления(СтрокаНачислений.Начисление);
			ПостоянныеПоказателиНачислений.Вставить(СтрокаНачислений.Начисление, ПостоянныеПоказателиНачисления);	
		КонецЕсли;
		
		Для Каждого ПостоянныйПоказатель Из ПостоянныеПоказателиНачисления Цикл
			
			Отбор = Новый Структура("Показатель,ДокументОснование", ПостоянныйПоказатель, СтруктураНачисления.ДокументОснование);
			СтрокиПоказателей = ПлановыеНачисленияПоказателиСотрудника.ПоказателиСотрудников.НайтиСтроки(Отбор);
			Если СтрокиПоказателей.Количество() = 0 Тогда
				ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета + 1;
				Продолжить;
			КонецЕсли;
			
			СтруктураПоказателя = Новый Структура("Показатель,ДокументОснование,ИдентификаторСтрокиВидаРасчета,Значение");
			СтруктураПоказателя.Показатель = СтрокиПоказателей[0].Показатель;
			СтруктураПоказателя.ДокументОснование = СтрокиПоказателей[0].ДокументОснование;
			СтруктураПоказателя.Значение = СтрокиПоказателей[0].Значение;
			СтруктураПоказателя.ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета;
			
			МассивПоказателей.Добавить(СтруктураПоказателя);
			
		КонецЦикла;	  
		
		ИдентификаторСтрокиВидаРасчета = ИдентификаторСтрокиВидаРасчета + 1;
		
	КонецЦикла;
	
	Возврат Новый Структура("Начисления,Показатели", МассивНачислений, МассивПоказателей);
	
КонецФункции

&НаСервере
Функция ПараметрыПолученияНачисленийПоказателейСотрудников()
	
	ПараметрыПолучения = Документы.ИзменениеПлановыхНачислений.ПараметрыПолученияНачисленийПоказателейСотрудников();
	ПараметрыПолучения.Вставить("Ссылка", Объект.Ссылка);
	ПараметрыПолучения.Вставить("Организация", Объект.Организация);
	ПараметрыПолучения.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыПолучения.Вставить("ДатаИзменения", Объект.ДатаИзменения);
	ПараметрыПолучения.Вставить("ДатаОкончания", Объект.ДатаОкончания);
	
	Если Объект.ЭтоОтражениеИзмененияШтатногоРасписания Тогда
		ПараметрыПолучения.Вставить("ЭтоОтражениеИзмененияШтатногоРасписания", Истина);
		ПараметрыПолучения.Вставить("ИзменениеШтатногоРасписания", Объект.ИзменениеШтатногоРасписания);
		ПараметрыПолучения.Вставить("ИдентификаторСтрокиПозиции", Объект.ИдентификаторСтрокиПозиции);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьШтатноеРасписание") 
		Или Объект.ЭтоОтражениеИзмененияШтатногоРасписания Тогда
		ПараметрыПолучения.Вставить("ПолеДолжность", "ДолжностьПоШтатномуРасписанию");
		ПараметрыПолучения.Вставить("ДолжностьПоШтатномуРасписанию", Объект.ДолжностьПоШтатномуРасписанию);
	Иначе
		ПараметрыПолучения.Вставить("ПолеДолжность", "Должность");
		ПараметрыПолучения.Вставить("Должность", Объект.Должность);
	КонецЕсли;
	
	Возврат ПараметрыПолучения;	
	
КонецФункции

&НаСервере
Процедура ПрименитьИзмененияВДокументеКПлановымНачислениямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников = Неопределено)
	
	НачисленияПоказателиСотрудников.НачисленияСотрудников.Индексы.Добавить("Сотрудник, Начисление, ДокументОснование");
		
	Отбор = Новый Структура("Сотрудник, Начисление, ДокументОснование");
	Для каждого Строка Из Объект.НачисленияСотрудников Цикл
		Если ФильтрСотрудников <> Неопределено 
			И ФильтрСотрудников.Найти(Строка.Сотрудник) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Отбор.Сотрудник = Строка.Сотрудник;
		Отбор.Начисление = Строка.Начисление;
		Отбор.ДокументОснование = Строка.ДокументОснование;
		СтрокиНачисления = НачисленияПоказателиСотрудников.НачисленияСотрудников.НайтиСтроки(Отбор);
		Если СтрокиНачисления.Количество() > 0 Тогда
			СтрокиНачисления[0].Размер = Строка.Размер; 
			СтрокиНачисления[0].Действие = Строка.Действие; 
		Иначе 
			СтрокаНачисления = НачисленияПоказателиСотрудников.НачисленияСотрудников.Добавить();
			СтрокаНачисления.Сотрудник = Строка.Сотрудник;
			СтрокаНачисления.Начисление = Строка.Начисление;
			СтрокаНачисления.ДокументОснование = Строка.ДокументОснование;
			СтрокаНачисления.Размер = Строка.Размер; 
			СтрокаНачисления.Действие = Строка.Действие; 
		КонецЕсли;
	КонецЦикла;	
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		Если ФильтрСотрудников <> Неопределено 
			И ФильтрСотрудников.Найти(СтрокаСотрудника.Сотрудник) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		РеквизитФормыВНачисленияСотрудника(НачисленияПоказателиСотрудников.НачисленияСотрудников, СтрокаСотрудника);	
	КонецЦикла;	
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьИзмененияВДокументеКПлановымПоказателямСотрудников(НачисленияПоказателиСотрудников, ФильтрСотрудников = Неопределено)
		
	ПоказателиСотрудниковВДокументе = Объект.ПоказателиСотрудников.Выгрузить(Новый Массив);
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		Если ФильтрСотрудников <> Неопределено 
			И ФильтрСотрудников.Найти(СтрокаСотрудника.Сотрудник) = Неопределено Тогда  
			Продолжить;
		КонецЕсли;
		РеквизитФормыВПоказателиСотрудника(ПоказателиСотрудниковВДокументе, СтрокаСотрудника); 
	КонецЦикла;
	
	НачисленияПоказателиСотрудников.ПоказателиСотрудников.Индексы.Добавить("Сотрудник,Показатель,ДокументОснование");
	
	Отбор = Новый Структура("Сотрудник, Показатель, ДокументОснование");
	
	Для каждого Строка Из ПоказателиСотрудниковВДокументе Цикл
		Отбор.Сотрудник = Строка.Сотрудник;
		Отбор.Показатель = Строка.Показатель;
		Отбор.ДокументОснование = Строка.ДокументОснование;
		СтрокиПоказателей = НачисленияПоказателиСотрудников.ПоказателиСотрудников.НайтиСтроки(Отбор);
		Если СтрокиПоказателей.Количество() > 0 Тогда
			СтрокиПоказателей[0].Значение = Строка.Значение; 
		Иначе 
			СтрокиПоказателей = НачисленияПоказателиСотрудников.ПоказателиСотрудников.Добавить();
			СтрокиПоказателей.Сотрудник = Строка.Сотрудник;
			СтрокиПоказателей.Показатель = Строка.Показатель;
			СтрокиПоказателей.ДокументОснование = Строка.ДокументОснование;
			СтрокиПоказателей.Значение = Строка.Значение; 
		КонецЕсли;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область ДобавитьУдалитьНачислениеВсемСотрудникам

#Область ДобавитьНачислениеВсемСотрудникам

&НаКлиенте
Процедура ДобавитьНачислениеВсемСотрудникамЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли;
	
	ДобавитьНачислениеВсемСотрудникамНаСервере(РезультатЗакрытия);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНачислениеВсемСотрудникамНаСервере(РезультатЗакрытия)
	
	РеквизитФормыВДанныеСотрудников(Объект);
	
	ПредставлениеНачислений = ПредставлениеНачислений(РезультатЗакрытия.ДобавленныеВидыРасчета);
	
	УстановленФильтрНачислений = Объект.Начисления.Количество() > 0;
	СотрудникиЗаполнены = Объект.Сотрудники.Количество() > 0;
	
	Для каждого Начисление Из РезультатЗакрытия.ДобавленныеВидыРасчета Цикл
		
		Если СотрудникиЗаполнены Тогда
			ДобавитьНачислениеСотрудникам(Начисление);
		КонецЕсли;
		
		Если УстановленФильтрНачислений
			Или Не СотрудникиЗаполнены Тогда
			ДобавитьНачислениеВФильтр(Начисление, ПредставлениеНачислений);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьЗаголовокГруппыНачисления(ЭтотОбъект, "ГруппаФильтрНачисления");

	ДанныеСотрудниковВРеквизитФормы();
	
	ФОТСотрудниковВРеквизитФормы();
	
	УстановитьДоступностьКомандыЗаполнитьПоказатели();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНачислениеВФильтр(Начисление, ПредставлениеНачислений)
		
	Отбор = Новый Структура("Начисление");
	Отбор.Начисление = Начисление;
	СтрокиНачисления = Объект.Начисления.НайтиСтроки(Отбор);
	
	Если СтрокиНачисления.Количество() > 0  Тогда
		СтрокаНачисления = СтрокиНачисления[0];
		СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Утвердить;
	Иначе
		СтрокаНачисления = Объект.Начисления.Добавить();
		
		ПредставлениеНачисления = ПредставлениеНачислений[Начисление];
		СтрокаНачисления.Начисление = Начисление;
		СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Утвердить;
		СтрокаНачисления.КраткоеНаименование = ПредставлениеНачисления.КраткоеНаименование;
		СтрокаНачисления.РеквизитДопУпорядочивания = ПредставлениеНачисления.РеквизитДопУпорядочивания;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНачислениеСотрудникам(Начисление)
	
	ОписаниеНачисления = ОписаниеНачисления(Начисление);
	
	Для каждого Строка Из Объект.Сотрудники Цикл
		ДобавитьНачислениеСотруднику(Строка.Сотрудник, ОписаниеНачисления);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНачислениеСотруднику(Сотрудник, ОписаниеНачисления)
	
	Если НачислениеНазначеноРанее(Сотрудник, ОписаниеНачисления.Начисление) Тогда
		Возврат;
	КонецЕсли;
	
	НовоеНачисление = Объект.НачисленияСотрудников.Добавить();
	НовоеНачисление.Сотрудник 	= Сотрудник;
	НовоеНачисление.Начисление 	= ОписаниеНачисления.Начисление;
	НовоеНачисление.Действие 	= Перечисления.ДействияСНачислениямиИУдержаниями.Утвердить;
	
	Для каждого Показатель Из ОписаниеНачисления.ПостоянныеПоказатели Цикл
		НовыйПоказатель = Объект.ПоказателиСотрудников.Добавить();
		НовыйПоказатель.Сотрудник 	= Сотрудник;
		НовыйПоказатель.Показатель 	= Показатель;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НачислениеНазначеноРанее(Сотрудник, Начисление) 
	
	ОтборПоСотрудникуНачислению = Новый Структура("Сотрудник,Начисление");
	ОтборПоСотрудникуНачислению.Вставить("Начисление", Начисление);
	ОтборПоСотрудникуНачислению.Вставить("Сотрудник", Сотрудник);
	
	Возврат НЕ Объект.НачисленияСотрудников.НайтиСтроки(ОтборПоСотрудникуНачислению).Количество() = 0;
	
КонецФункции

&НаСервере
Функция ОписаниеНачисления(Начисление) 
	
	ОписаниеНачисления = Новый Структура;
	ОписаниеНачисления.Вставить("Начисление", Начисление);
	ОписаниеНачисления.Вставить("ПостоянныеПоказатели", ПостоянныеПоказателиНачисления(Начисление));
	
	Возврат ОписаниеНачисления; 
	
КонецФункции

&НаСервере
Функция ПостоянныеПоказателиНачисления(Начисление) 
	
	ПостоянныеПоказатели = Новый Массив;
	
	ИнфоОВидеРасчета = ЗарплатаКадрыРасширенныйПовтИсп.ПолучитьИнформациюОВидеРасчета(Начисление);
	Для Каждого СтрокаПоказателя Из ИнфоОВидеРасчета.Показатели Цикл
		Если ЭтоПостоянныйПоказатель(СтрокаПоказателя) Тогда
			ПостоянныеПоказатели.Добавить(СтрокаПоказателя.Показатель);
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ПостоянныеПоказатели;
	
КонецФункции

&НаСервере
Функция ЭтоПостоянныйПоказатель(СтрокаПоказателя) 
	
	ЭтоПостоянныйПоказатель = Истина;
	
	Если СтрокаПоказателя.ЗапрашиватьПриВводе Тогда
		ПоказательИнфо = ЗарплатаКадрыРасширенныйПовтИсп.СведенияОПоказателеРасчетаЗарплаты(СтрокаПоказателя.Показатель);
		Если ПоказательИнфо.СпособПримененияЗначений <> Перечисления.СпособыПримененияЗначенийПоказателейРасчетаЗарплаты.Постоянное
			Или ПоказательИнфо.ЗначениеРассчитываетсяАвтоматически Тогда
			ЭтоПостоянныйПоказатель = Ложь;	
		КонецЕсли;
	Иначе
		ЭтоПостоянныйПоказатель = Ложь;	
	КонецЕсли;

	Возврат ЭтоПостоянныйПоказатель;
	
КонецФункции
	
#КонецОбласти

#Область УдалитьНачислениеВсемСотрудникам

&НаКлиенте
Процедура УдалитьНачислениеВсемСотрудникамЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьНачислениеВсемСотрудникамНаСервере(РезультатЗакрытия);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьНачислениеВсемСотрудникамНаСервере(РезультатЗакрытия)
	
	РеквизитФормыВДанныеСотрудников(Объект);

	УдаляемыеСтроки = Новый Массив;
	
	ПредставлениеНачислений = ПредставлениеНачислений(РезультатЗакрытия.ДобавленныеВидыРасчета);
	Отбор = Новый Структура("Начисление");

	Для каждого Начисление Из РезультатЗакрытия.ДобавленныеВидыРасчета Цикл
		Отбор.Начисление = Начисление;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УдаляемыеСтроки, Объект.НачисленияСотрудников.НайтиСтроки(Отбор));
		
		СтрокиНачисления = Объект.Начисления.НайтиСтроки(Отбор);
		
		Если СтрокиНачисления.Количество() > 0  Тогда
			СтрокаНачисления = СтрокиНачисления[0];
			СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить;
		Иначе
			СтрокаНачисления = Объект.Начисления.Добавить();
			
			ПредставлениеНачисления = ПредставлениеНачислений[Начисление];
			СтрокаНачисления.Начисление = Начисление;
			СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить;
			СтрокаНачисления.КраткоеНаименование = ПредставлениеНачисления.КраткоеНаименование;
			СтрокаНачисления.РеквизитДопУпорядочивания = ПредставлениеНачисления.РеквизитДопУпорядочивания;
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
		Если УдаляемаяСтрока.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Утвердить Тогда
			Объект.НачисленияСотрудников.Удалить(УдаляемаяСтрока);
		Иначе
			УдаляемаяСтрока.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить;
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьЗаголовокГруппыНачисления(ЭтотОбъект, "ГруппаФильтрНачисления");

	ДанныеСотрудниковВРеквизитФормы();
	
	ФОТСотрудниковВРеквизитФормы();
	
	УстановитьДоступностьКомандыЗаполнитьПоказатели();
	
КонецПроцедуры
	
#КонецОбласти

&НаКлиенте
Процедура ВыбратьНачисление(ОбработчикВыбора, Отбор = Неопределено)
	
	ОписаниеОповещения = Новый ОписаниеОповещения(ОбработчикВыбора, ЭтотОбъект);
	ПараметрыФормы = Новый Структура("МассивВидовРасчета, УсловияОтбора", Новый Массив, Отбор);
	ОткрытьФорму("ОбщаяФорма.ПодборВидовРасчета", ПараметрыФормы, , , , , ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ФОТ

&НаСервере
Функция ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников)
	
	РассчитываемыеОбъекты = Новый Соответствие;	
	Сотрудники = Новый Соответствие;
	
	Отбор = Новый Структура;
	
	ВремяРегистрацииСотрудников = ВремяРегистрацииСотрудников();
	Для Каждого СтрокаСотрудника Из ПлановыеНачисленияПоказателиСотрудников.Сотрудники Цикл
		
		Отбор.Вставить("Сотрудник", СтрокаСотрудника.Сотрудник);
		
		ОписаниеСотрудника = Новый Структура;
		ОписаниеСотрудника.Вставить("Организация", Объект.Организация);
		ОписаниеСотрудника.Вставить("ДатаРасчета", ВремяРегистрацииСотрудников[СтрокаСотрудника.Сотрудник]);	
		ОписаниеСотрудника.Вставить("Начисления", РасчетЗарплатыРасширенный.ПустаяТаблицаДанныеНачисленийДляРасчетаФОТ());
		ОписаниеСотрудника.Вставить("Показатели", РасчетЗарплатыРасширенный.ПустаяТаблицаДанныеПоказателейДляРасчетаФОТ());
		
		СтрокиПоСотруднику = ПлановыеНачисленияПоказателиСотрудников.НачисленияСотрудников.НайтиСтроки(Отбор);
		Для Каждого СтрокаНачисления Из СтрокиПоСотруднику Цикл	
			
			Если СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда
				Продолжить;
			КонецЕсли;
			
			ДанныеНачисления = ОписаниеСотрудника.Начисления.Добавить();
			ДанныеНачисления.Начисление = СтрокаНачисления.Начисление;
			ДанныеНачисления.ДокументОснование = СтрокаНачисления.ДокументОснование;
			ДанныеНачисления.Размер = СтрокаНачисления.Размер;
			
		КонецЦикла;
		
		СтрокиПоСотруднику = ПлановыеНачисленияПоказателиСотрудников.ПоказателиСотрудников.НайтиСтроки(Отбор);
		Для Каждого СтрокаПоказателя Из СтрокиПоСотруднику Цикл	
			ДанныеПоказателя = ОписаниеСотрудника.Показатели.Добавить();
			ДанныеПоказателя.Показатель = СтрокаПоказателя.Показатель;
			ДанныеПоказателя.ДокументОснование = СтрокаПоказателя.ДокументОснование;
			ДанныеПоказателя.Значение = СтрокаПоказателя.Значение;
		КонецЦикла;
		
		Сотрудники.Вставить(СтрокаСотрудника.Сотрудник, ОписаниеСотрудника);
		
	КонецЦикла;
	
	РассчитываемыеОбъекты.Вставить(Объект.Ссылка, Сотрудники);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РасчетЗарплатыРасширенный.РассчитатьФОТСотрудников(РассчитываемыеОбъекты, Объект.Организация, ВремяРегистрации);
	
	УстановитьПривилегированныйРежим(Ложь);			
	
	Возврат РассчитываемыеОбъекты.Получить(Объект.Ссылка);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников)
	
	Отбор = Новый Структура;
	
	Для Каждого ОписаниеСотрудника Из ФОТПлановыхНачисленийСотрудников Цикл
		
		Отбор.Очистить();
		Отбор.Вставить("Сотрудник", ОписаниеСотрудника.Ключ);
		
		Для Каждого ОписаниеНачисления Из ОписаниеСотрудника.Значение.Начисления Цикл
			
			Отбор.Вставить("Начисление", ОписаниеНачисления.Начисление);
			Отбор.Вставить("ДокументОснование", ОписаниеНачисления.ДокументОснование);
			
			СтрокиНачисления = Объект.НачисленияСотрудников.НайтиСтроки(Отбор);
			
			Для Каждого СтрокаНачисления Из СтрокиНачисления Цикл
				
				Если СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаНачисления.Размер = ОписаниеНачисления.Размер;
				
			КонецЦикла;
			
		КонецЦикла;		
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПрименитьИзмененияВДокументеКФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников)
	
	Отбор = Новый Структура;
	
	Для Каждого ОписаниеСотрудника Из ФОТПлановыхНачисленийСотрудников Цикл
		
		Отбор.Очистить();
		Отбор.Вставить("Сотрудник", ОписаниеСотрудника.Ключ);
		
		Для Каждого ОписаниеНачисления Из ОписаниеСотрудника.Значение.Начисления Цикл
			
			Отбор.Вставить("Начисление", ОписаниеНачисления.Начисление);
			Отбор.Вставить("ДокументОснование", ОписаниеНачисления.ДокументОснование);
			
			СтрокиНачисления = Объект.НачисленияСотрудников.НайтиСтроки(Отбор);
			Если СтрокиНачисления.Количество() > 0 Тогда
				ОписаниеНачисления.Размер = СтрокиНачисления[0].Размер;					
			КонецЕсли;
			
		КонецЦикла;		
		
	КонецЦикла;
	
КонецФункции

&НаСервере
Функция ФОТСотрудников()
	
	ФОТСотрудников = Новый Соответствие;
	
	Если ЭтотОбъект.ПодробныйРасчетФОТ Тогда
		Если Объект.Начисления.Количество() > 0 Тогда
			
			ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента();
			
			ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
			ПрименитьИзмененияВДокументеКФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников);
			ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников);	
		Иначе
			ФОТСотрудников = ФОТСотрудниковПоДаннымДокумента();	
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФОТСотрудников;	
	
КонецФункции

&НаСервере
Функция ФОТСотрудниковПоОписаниюНачислений(ФОТНачисленийСотрудников)
	
	ФОТСотрудников = Новый Соответствие;
	
	Для Каждого ОписаниеСотрудника Из ФОТНачисленийСотрудников Цикл
		
		ФОТСотрудника = 0;
		
		Для Каждого ОписаниеНачисления Из ОписаниеСотрудника.Значение.Начисления Цикл
			
			ФОТСотрудника = ФОТСотрудника + ОписаниеНачисления.Размер;
			
		КонецЦикла;
		
		ФОТСотрудников.Вставить(ОписаниеСотрудника.Ключ, ФОТСотрудника);	
		
	КонецЦикла;
	
	Возврат ФОТСотрудников;
	
КонецФункции

&НаСервере
Функция ФОТСотрудниковПоДаннымДокумента()
	
	ФОТСотрудников = Новый Соответствие;
	
	Отбор = Новый Структура("Сотрудник");
	
	Для Каждого ОписаниеСотрудника Из Объект.Сотрудники Цикл
		
		ФОТСотрудника = 0;
		
		Отбор.Сотрудник = ОписаниеСотрудника.Сотрудник;
		
		НачисленияСотрудника = Объект.НачисленияСотрудников.НайтиСтроки(Отбор);
		
		Для Каждого ОписаниеНачисления Из НачисленияСотрудника Цикл
			
			Если ОписаниеНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда
				Продолжить;
			КонецЕсли;
			
			ФОТСотрудника = ФОТСотрудника + ОписаниеНачисления.Размер;
			
		КонецЦикла;
		
		ФОТСотрудников.Вставить(ОписаниеСотрудника.Сотрудник, ФОТСотрудника);	
		
	КонецЦикла;
	
	Возврат ФОТСотрудников;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФОТСотрудниковВФорме()	
	
	ФОТСотрудников = ФОТСотрудников();	
	
	Для каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		СтрокаСотрудника.ФОТ = ФОТСотрудников.Получить(СтрокаСотрудника.Сотрудник);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СовокупнаяТарифнаяСтавка

&НаСервере
Процедура ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(НачисленияСотрудников, ПоказателиСотрудников)
	
	// Удаление текущих данных
	МассивСотрудников = ОбщегоНазначения.ВыгрузитьКолонку(НачисленияСотрудников, "Сотрудник", Истина);
	Для Каждого Сотрудник Из МассивСотрудников Цикл 
		Отбор = Новый Структура("Сотрудник", Сотрудник);
		НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда  
			НайденныеСтроки[0].СовокупнаяТарифнаяСтавка = 0;
			НайденныеСтроки[0].ВидТарифнойСтавки = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	// Заполнение документа результатами расчета.
	ЗначенияСовокупныхТарифныхСтавок = ЗначенияСовокупныхТарифныхСтавокСотрудников(НачисленияСотрудников, ПоказателиСотрудников);
	
	Для Каждого ДанныеСотрудника Из ЗначенияСовокупныхТарифныхСтавок Цикл 
		Отбор = Новый Структура("Сотрудник", ДанныеСотрудника.Сотрудник);
		НайденныеСтроки = Объект.Сотрудники.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() > 0 Тогда  
			НайденныеСтроки[0].СовокупнаяТарифнаяСтавка = ДанныеСотрудника.СовокупнаяТарифнаяСтавка;
			НайденныеСтроки[0].ВидТарифнойСтавки = ДанныеСотрудника.ВидТарифнойСтавки;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНачисленияПоказателиСотрудниковДляРасчетаСовокупнойСтавки(ФОТПлановыхНачисленийСотрудников, НачисленияСотрудников, ПоказателиСотрудников) 
	
	Для Каждого КлючИЗначение Из ФОТПлановыхНачисленийСотрудников Цикл 
		
		Сотрудник = КлючИЗначение.Ключ;
		НачисленияПоказателиСотрудника = КлючИЗначение.Значение;
		
		Для Каждого СтрокаНачисления Из НачисленияПоказателиСотрудника.Начисления Цикл 
			НоваяСтрокаНачисления = НачисленияСотрудников.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаНачисления, СтрокаНачисления);
			НоваяСтрокаНачисления.Сотрудник = Сотрудник;
		КонецЦикла;
		
		Для Каждого СтрокаПоказателя Из НачисленияПоказателиСотрудника.Показатели Цикл 
			НоваяСтрокаПоказателя = ПоказателиСотрудников.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПоказателя, СтрокаПоказателя);
			НоваяСтрокаПоказателя.Сотрудник = Сотрудник;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗначенияСовокупныхТарифныхСтавокСотрудников(НачисленияСотрудников, ПоказателиСотрудников)  
	
	ИсходныеДанные = ЗарплатаКадрыРасширенный.ИсходныеДанныеРасчетаСовокупныхТарифныхСтавок();
	
	РассчитываемыеСотрудники = Новый Соответствие;
	
	НомерСтроки = 1;
	
	ВремяРегистрацииСотрудников = ВремяРегистрацииСотрудников();
	
	Для Каждого СтрокаНачисления Из НачисленияСотрудников Цикл
		
		Если СтрокаНачисления.Действие = Перечисления.ДействияСНачислениямиИУдержаниями.Отменить Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ИсходныеДанные.Добавить();
		НоваяСтрока.ДатаСобытия = ВремяРегистрацииСотрудников[СтрокаНачисления.Сотрудник];
		НоваяСтрока.Сотрудник = СтрокаНачисления.Сотрудник;
		НоваяСтрока.Начисление = СтрокаНачисления.Начисление;
		НоваяСтрока.РазмерФОТ = СтрокаНачисления.Размер;
		НоваяСтрока.НомерСтроки = НомерСтроки;
		
		НомерСтроки = НомерСтроки + 1;
		
		РассчитываемыеСотрудники.Вставить(СтрокаНачисления.Сотрудник, Истина);
		
	КонецЦикла;
	
	ТаблицаПоказателей = ЗарплатаКадрыРасширенный.ПоказателиРасчетаСовокупныхТарифныхСтавок();
	Для каждого СтрокаПоказателя Из ПоказателиСотрудников Цикл
		НоваяСтрокаПоказателя = ТаблицаПоказателей.Добавить();
		НоваяСтрокаПоказателя.Период = ВремяРегистрацииСотрудников[СтрокаНачисления.Сотрудник];
		НоваяСтрокаПоказателя.Сотрудник = СтрокаПоказателя.Сотрудник;
		НоваяСтрокаПоказателя.Показатель = СтрокаПоказателя.Показатель;
		НоваяСтрокаПоказателя.Значение = СтрокаПоказателя.Значение;
	КонецЦикла;
	
	ЗначенияСовокупныхТарифныхСтавок = ЗарплатаКадрыРасширенный.ЗначенияСовокупныхТарифныхСтавок(ИсходныеДанные, Неопределено, ТаблицаПоказателей);
	
	Возврат ЗначенияСовокупныхТарифныхСтавок.Выгрузить();
	
КонецФункции

#КонецОбласти

#Область УдалитьНачисленияПоказателиСотрудника

&НаСервере
Функция УдалитьНачисленияПоСотруднику(Сотрудник)
	
	ОтборПоСотруднику = Новый Структура("Сотрудник", Сотрудник);
	
	СтрокиДляУдаления = Объект.НачисленияСотрудников.НайтиСтроки(ОтборПоСотруднику);
	Для Каждого СтрокаДляУдаления Из СтрокиДляУдаления Цикл
		Объект.НачисленияСотрудников.Удалить(СтрокаДляУдаления);
	КонецЦикла;

КонецФункции 

&НаСервере
Функция УдалитьПоказателиПоСотруднику(Сотрудник)
	
	ОтборПоСотруднику = Новый Структура("Сотрудник", Сотрудник);
	
	СтрокиДляУдаления = Объект.ПоказателиСотрудников.НайтиСтроки(ОтборПоСотруднику);
	Для Каждого СтрокаДляУдаления Из СтрокиДляУдаления Цикл
		Объект.ПоказателиСотрудников.Удалить(СтрокаДляУдаления);
	КонецЦикла;

КонецФункции 

&НаСервере
Функция ЕстьУдаляемыеСотрудники()
	Возврат НЕ (УдаляемыеСотрудники = Неопределено Или УдаляемыеСотрудники.Количество() = 0);
КонецФункции

&НаСервере
Процедура ЗавершитьУдалениеСотрудников()
	
	Если НЕ ЕстьУдаляемыеСотрудники() Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Сотрудник Из УдаляемыеСотрудники Цикл
		УдалитьНачисленияПоСотруднику(Сотрудник);
		УдалитьПоказателиПоСотруднику(Сотрудник);
	КонецЦикла;
	
	ОчиститьУдаляемыхСотрудников();
	
КонецПроцедуры

&НаСервере
Процедура ЗафиксироватьУдаляемыхСотрудников(МассивУдаляемыхСотрудников)
	УдаляемыеСотрудники = Новый ФиксированныйМассив(МассивУдаляемыхСотрудников);	
КонецПроцедуры

&НаСервере
Процедура ОчиститьУдаляемыхСотрудников()
	УдаляемыеСотрудники = Неопределено;
КонецПроцедуры

#КонецОбласти

#Область КлючевыеРеквизитыЗаполненияФормы

// Функция возвращает описание таблиц формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыТаблицыОчищаемыеПриИзменении() Экспорт
	Массив = Новый Массив;
	Массив.Добавить("Объект.Сотрудники");
	Массив.Добавить("Объект.НачисленияСотрудников");
	Массив.Добавить("Объект.ПоказателиСотрудников");
	Возврат Массив
КонецФункции 

// Функция возвращает массив реквизитов формы подключенных к механизму ключевых реквизитов формы.
&НаСервере
Функция КлючевыеРеквизитыЗаполненияФормыОписаниеКлючевыхРеквизитов() Экспорт
	Массив = Новый Массив;
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Организация",	НСтр("ru='организации';uk='організації'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Подразделение", НСтр("ru='подразделения';uk='підрозділи'")));
	Массив.Добавить(Новый Структура("ЭлементФормы, Представление", "Подразделение", НСтр("ru='подразделения';uk='підрозділи'")));
	Возврат Массив
КонецФункции

#КонецОбласти

&НаСервере
Процедура УстановитьДоступностьКомандыЗаполнитьПоказатели()
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоказателиСотрудниковЗаполнитьПоказатели", "Доступность", ЕстьРедактируемыеПоказатели());
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьИсториюИзмененияШтатногоРасписания") Тогда
		ОснованиеЗаполнено = ЗначениеЗаполнено(Объект.ИзменениеШтатногоРасписания);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИзменениеШтатногоРасписания", "Видимость", ОснованиеЗаполнено);
	КонецЕсли;
	
	УстановитьВидимостьКолонокПодробногоРасчета();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовФормы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Организация",
		"ТолькоПросмотр",
		Объект.ЭтоОтражениеИзмененияШтатногоРасписания);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"Подразделение",
		"ТолькоПросмотр",
		Объект.ЭтоОтражениеИзмененияШтатногоРасписания);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДолжностьПоШтатномуРасписанию",
		"ТолькоПросмотр",
		Объект.ЭтоОтражениеИзмененияШтатногоРасписания);
	
	ТолькоПросмотрДатыИзменения = Объект.ЭтоОтражениеИзмененияШтатногоРасписания И ПолучитьФункциональнуюОпциюФормы("ИспользоватьИсториюИзмененияШтатногоРасписания");
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ДатаИзменения",
		"ТолькоПросмотр",
		ТолькоПросмотрДатыИзменения);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ПоказателиСотрудниковДатаИзменения",
		"ТолькоПросмотр",
		ТолькоПросмотрДатыИзменения);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКолонокПодробногоРасчета()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПоказателиСотрудниковСовокупнаяТарифнаяСтавка", "Видимость", ЭтотОбъект.ПодробныйРасчетФОТ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПоказателиСотрудниковФОТ", "Видимость", ЭтотОбъект.ПодробныйРасчетФОТ);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, "ПоказателиСотрудниковПредставлениеНачислений", "Видимость", ЭтотОбъект.ПодробныйРасчетФОТ);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеНадписей()
	
	УстановитьПривилегированныйРежим(Истина);
	МассивСотрудников = Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник");
	ЗарплатаКадрыРасширенный.УстановитьТекстНадписиОДокументахВведенныхНаДату(ЭтотОбъект, ВремяРегистрации, 
		МассивСотрудников, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеНачислений()
	
	Начисления = Объект.Начисления.Выгрузить(,"Начисление").ВыгрузитьКолонку("Начисление");
	ПредставлениеНачислений = ПредставлениеНачислений(Начисления);
	
	Для каждого Строка Из  Объект.Начисления Цикл
		
		ПредставлениеНачисления = ПредставлениеНачислений[Строка.Начисление];
		
		Строка.КраткоеНаименование = ПредставлениеНачисления.КраткоеНаименование;
		Строка.РеквизитДопУпорядочивания = ПредставлениеНачисления.РеквизитДопУпорядочивания;
		
	КонецЦикла;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеНачислений(Начисления)
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъектов(Начисления, "КраткоеНаименование,РеквизитДопУпорядочивания");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЗаголовокГруппыНачисления(Форма, ИмяГруппы)
	
	Если Форма.Объект.Начисления.Количество() > 0 Тогда
		
		МассивСтрок = Новый Массив;
		
		Для каждого Строка Из Форма.Объект.Начисления Цикл
			
			Если МассивСтрок.Количество() = 0 Тогда
				МассивСтрок.Добавить(Строка);
				Продолжить;
			КонецЕсли;
			
			ДобавленаСтрока = Ложь;
			
			Для Индекс = 0 По МассивСтрок.ВГраница() Цикл
				Если МассивСтрок[Индекс].РеквизитДопУпорядочивания > Строка.РеквизитДопУпорядочивания Тогда
					МассивСтрок.Вставить(Индекс, Строка);
					ДобавленаСтрока = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если Не ДобавленаСтрока Тогда
				МассивСтрок.Добавить(Строка);
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаголовокНачислений =  НСтр("ru='Изменить';uk='Змінити'") + " ";
		
		Для каждого Строка Из МассивСтрок Цикл
			ЗаголовокНачислений = ЗаголовокНачислений + ?(ЗначениеЗаполнено(Строка.КраткоеНаименование), Строка.КраткоеНаименование, Строка.Начисление) + ", ";
		КонецЦикла;
		
		ЗаголовокНачислений = Лев(ЗаголовокНачислений, СтрДлина(ЗаголовокНачислений) - 2);
		
	Иначе
		ЗаголовокНачислений = НСтр("ru='Отбор начислений не установлен';uk='Відбір нарахувань не встановлено'");
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Форма.Элементы, ИмяГруппы, "Заголовок", ЗаголовокНачислений);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьВремяРегистрации()
	
	ДатыИзменений = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Сотрудники, "ДатаИзменения", Истина);
	ДатыИзменений.Добавить(Объект.ДатаИзменения);
	
	ВременаРегистрации = ЗарплатаКадрыРасширенный.ЗначенияВремениРегистрацииДокумента(Объект.Ссылка, ДатыИзменений);
	
	Для каждого Строка Из Объект.Сотрудники Цикл
		Строка.ВремяРегистрации = ВременаРегистрации.Получить(Строка.ДатаИзменения);
	КонецЦикла;
	
	ВремяРегистрации = ВременаРегистрации.Получить(Объект.ДатаИзменения);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьВремяРегистрацииСтроки(Ссылка, Строка)
	Строка.ВремяРегистрации = ЗарплатаКадрыРасширенный.ВремяРегистрацииДокумента(Ссылка, Строка.ДатаИзменения);
КонецПроцедуры

&НаСервере
Функция ВремяРегистрацииСотрудников()
	
	ВремяРегистрацииСотрудников = Новый Соответствие;
	
	Для каждого Строка Из Объект.Сотрудники Цикл
		ВремяРегистрацииСотрудников.Вставить(Строка.Сотрудник, Строка.ВремяРегистрации);
	КонецЦикла;	
	
	Возврат ВремяРегистрацииСотрудников;
	
КонецФункции

&НаСервере
Функция АдресСпискаПодобранныхСотрудников()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.Сотрудники.Выгрузить(,"Сотрудник").ВыгрузитьКолонку("Сотрудник"), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ДобавитьОтборПоДолжностиДляПодбораСотрудников(ПараметрыОткрытия)
	
	ОтборПоДолжности = Ложь;
	
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьШтатноеРасписание") Тогда
		Если ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
			
			ОтборПоДолжности = Истина;
			ИмяОтбора = "ДолжностьПоШтатномуРасписанию";
			ЗначениеОтбора = Объект.ДолжностьПоШтатномуРасписанию;
			
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(Объект.Должность) Тогда
			
			ОтборПоДолжности = Истина;
			ИмяОтбора = "Должность";
			ЗначениеОтбора = Объект.Должность;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ОтборПоДолжности Тогда
		
		Если ПараметрыОткрытия = Неопределено Тогда
			ПараметрыОткрытия = Новый Структура;
		КонецЕсли; 
		
		Если Не ПараметрыОткрытия.Свойство("Отбор") Тогда
			ПараметрыОткрытия.Вставить("Отбор", Новый Структура);
		КонецЕсли; 
		
		ПараметрыОткрытия.Отбор.Вставить(ИмяОтбора, ЗначениеОтбора);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УточнитьДолжностьПоШтатномуРасписанию()
	
	Если ПолучитьФункциональнуюОпциюФормы("ИспользоватьШтатноеРасписание")
		И ЗначениеЗаполнено(Объект.ДолжностьПоШтатномуРасписанию) Тогда
		
		Если ДолжностьПоШтатномуРасписаниюНеУтверждена() Тогда
			Объект.ДолжностьПоШтатномуРасписанию = Справочники.ШтатноеРасписание.ПустаяСсылка();
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДолжностьПоШтатномуРасписаниюНеУтверждена()
	
	Возврат Объект.ДатаИзменения < ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДолжностьПоШтатномуРасписанию, "ДатаУтверждения");
	
КонецФункции

&НаСервере
Процедура ОбработатьИзменениеПоказателейНаСервере(ЗначенияПоказателей)
	
	Для Каждого СтрокаСотрудника Из Объект.Сотрудники Цикл
		
		Для каждого КолонкаПоказатель Из КолонкиПоказателей Цикл
			ЗначениеПоказателя = ЗначенияПоказателей[КолонкаПоказатель.Значение];
			Если ЗначениеПоказателя = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ПоказательИспользуется(СтрокаСотрудника, КолонкаПоказатель.Ключ) 
				И ТипЗнч(КолонкаПоказатель.Значение) = Тип("СправочникСсылка.ПоказателиРасчетаЗарплаты") Тогда	
				СтрокаСотрудника[КолонкаПоказатель.Ключ] = ЗначениеПоказателя;
				Для каждого ПоказательСотрудника Из СтрокаСотрудника[ИмяРеквизитаТаблицаПоказателя(КолонкаПоказатель.Ключ)] Цикл
					Если Не ЗначениеЗаполнено(ПоказательСотрудника.ДокументОснование) Тогда
						ПоказательСотрудника.Значение = ЗначениеПоказателя;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ПлановыеНачисленияПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудниковСИзменениямиДокумента();
	ФОТПлановыхНачисленийСотрудников = ФОТПлановыхНачисленийСотрудниковСИзменениямиДокумента(ПлановыеНачисленияПоказателиСотрудников);
	ЗаполнитьФОТНачисленийСотрудников(ФОТПлановыхНачисленийСотрудников);
	ФОТСотрудников = ФОТСотрудниковПоОписаниюНачислений(ФОТПлановыхНачисленийСотрудников);
	
	НачисленияСотрудников = ПлановыеНачисленияПоказателиСотрудников.НачисленияСотрудников.СкопироватьКолонки();
	ПоказателиСотрудников = ПлановыеНачисленияПоказателиСотрудников.ПоказателиСотрудников.СкопироватьКолонки();
	
	ЗаполнитьНачисленияПоказателиСотрудниковДляРасчетаСовокупнойСтавки(ФОТПлановыхНачисленийСотрудников, НачисленияСотрудников, ПоказателиСотрудников);	
	ЗаполнитьЗначенияСовокупныхТарифныхСтавокСотрудников(НачисленияСотрудников, ПоказателиСотрудников);	
	
	РеквизитФормыВДанныеСотрудников(Объект);
	ДанныеСотрудниковВРеквизитФормы();
	
	ФОТСотрудниковВРеквизитФормы(ФОТСотрудников);
	
	ЗаполнитьПредставлениеНачислений();
	
	УстановитьДоступностьКомандыЗаполнитьПоказатели();
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти
