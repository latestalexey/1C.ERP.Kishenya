#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПереоценкаНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ПереоценкаНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ПереоценкаНМА);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Переоценка НМА';uk='Переоцінка нематеріального активу'"));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	ТекстыЗапроса = Новый СписокЗначений;
	УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры, ПрочиеРасходы(ТекстыЗапроса));
	УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры, ПартииПрочихРасходов(ТекстыЗапроса));
	
	УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	
	ПереоценкаНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Ложь, Ложь, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов
	|ИЗ
	|	Документ.ПереоценкаНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДополнительныеСвойства);
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ГраницаМесяцОкончание", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	
КонецПроцедуры

Процедура ВременнаяТаблицаПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаПоДокументу";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втТаблицаПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.Ссылка КАК Ссылка,
	|	втТаблицаПоДокументу.НематериальныйАктив КАК ОбъектУчета,
	|	втТаблицаПоДокументу.Организация КАК Организация,
	|	втТаблицаПоДокументу.Подразделение КАК Подразделение,
	|	ЕСТЬNULL(втТаблицаПоДокументу.НематериальныйАктив.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	втТаблицаПоДокументу.Стоимость КАК Стоимость
	|ПОМЕСТИТЬ втТаблицаПоДокументу
	|ИЗ
	|	Документ.ПереоценкаНМА КАК втТаблицаПоДокументу
	|ГДЕ
	|	втТаблицаПоДокументу.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	втТаблицаПоДокументу.НематериальныйАктив,
	|	Организация,
	|	Подразделение"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаАмортизацияПоДокументу(ТекстыЗапроса)
	
	ИмяТаблицы = "втАмортизацияПоДокументу";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ВременнаяТаблицаНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втАмортизацияПоДокументу
	|"+
	"ВЫБРАТЬ
	|	втНачисленнаяАмортизация.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(втНачисленнаяАмортизация.СуммаБУ) КАК Сумма
	|ПОМЕСТИТЬ втАмортизацияПоДокументу
	|ИЗ
	|	втНачисленнаяАмортизация КАК втНачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	втНачисленнаяАмортизация.ОбъектУчета"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаУчета";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаУчета
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета КАК ОбъектУчета,
	|	СчетаБухгалтерскогоУчета.СчетУчета,
	|	СчетаБухгалтерскогоУчета.СчетНачисленияАмортизации КАК СчетАмортизации
	|ПОМЕСТИТЬ втСчетаУчета
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				&Период,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втТаблицаПоДокументу КАК Т)) КАК СчетаБухгалтерскогоУчета
	|		ПО втТаблицаПоДокументу.ОбъектУчета = СчетаБухгалтерскогоУчета.НематериальныйАктив
	|			"+";";;
		
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаУчетаСгруппированные";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаУчетаСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчета.ОбъектУчета КАК ОбъектУчета,
	|	втСчетаУчета.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втСчетаУчетаСгруппированные
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|	"+";";;
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаАмортизацииСгруппированные";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаАмортизацииСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчета.ОбъектУчета КАК ОбъектУчета,
	|	втСчетаУчета.СчетАмортизации КАК Счет
	|ПОМЕСТИТЬ втСчетаАмортизацииСгруппированные
	|ИЗ
	|	втСчетаУчета КАК втСчетаУчета
	|	"+";";;
	
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаСгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "втСчетаСгруппированные";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втСчетаСгруппированные
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаУчетаСгруппированные.Счет КАК Счет
	|ПОМЕСТИТЬ втСчетаСгруппированные
	|ИЗ
	|	втСчетаУчетаСгруппированные КАК втСчетаУчетаСгруппированные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	втСчетаАмортизацииСгруппированные.Счет
	|ИЗ
	|	втСчетаАмортизацииСгруппированные КАК втСчетаАмортизацииСгруппированные"+";";;
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиПоСчетам";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаСчетаСгруппированные(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиПоСчетам
	|"+
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ОбъектУчета,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	СУММА(ХозрасчетныйОстатки.СуммаОстаток) КАК Сумма
	|ПОМЕСТИТЬ втОстаткиПоСчетам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ГраницаМесяцОкончание,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Счет
	|				ИЗ
	|					втСчетаСгруппированные КАК Т),
	|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы),
	|			(Организация, Подразделение, Субконто1, НаправлениеДеятельности) В
	|				(ВЫБРАТЬ
	|					Т.Организация,
	|					Т.Подразделение,
	|					Т.ОбъектУчета,
	|					Т.НаправлениеДеятельности
	|				ИЗ
	|					втТаблицаПоДокументу КАК Т)) КАК ХозрасчетныйОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.Счет"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиСтоимости(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиСтоимости";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаСчетаУчетаСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаСчетаАмортизацииСгруппированные(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиСтоимости
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ВосстановительнаяСтоимость.Сумма, 0)) КАК ВосстановительнаяСтоимость,
	|	СУММА(ЕСТЬNULL(-НакопленнаяАмортизация.Сумма, 0)) 	 КАК НакопленнаяАмортизация
	|ПОМЕСТИТЬ втОстаткиСтоимости
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаУчета КАК втСчетаУчета
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втСчетаУчета.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК ВосстановительнаяСтоимость
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ВосстановительнаяСтоимость.ОбъектУчета
	|			И (втСчетаУчета.СчетУчета = ВосстановительнаяСтоимость.Счет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК НакопленнаяАмортизация
	|		ПО втТаблицаПоДокументу.ОбъектУчета = НакопленнаяАмортизация.ОбъектУчета
	|			И (втСчетаУчета.СчетАмортизации = НакопленнаяАмортизация.Счет)
	|
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаПоДокументу.ОбъектУчета"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПредыдущаяПереоценкаОбъектовСгруппированная(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущаяПереоценкаОбъектовСгруппированная";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	
	Текст =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втПредыдущаяПереоценкаОбъектовСгруппированная
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиПриход, 0)) КАК СуммаДооценки,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиРасход, 0)) КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаОбъектовСгруппированная
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереоценкаНМАБухгалтерскийУчет.Обороты(
	|				,
	|				&ГраницаМесяцОкончание,
	|				Регистратор,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втТаблицаПоДокументу КАК Т)) КАК ПереоценкаНМАБухгалтерскийУчетОбороты
	|		ПО втТаблицаПоДокументу.ОбъектУчета = ПереоценкаНМАБухгалтерскийУчетОбороты.НематериальныйАктив
	|			И втТаблицаПоДокументу.Организация = ПереоценкаНМАБухгалтерскийУчетОбороты.Организация
	|			И (ПереоценкаНМАБухгалтерскийУчетОбороты.Регистратор <> &Ссылка)
	|СГРУППИРОВАТЬ ПО
	|	втТаблицаПоДокументу.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета" + ";";
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПредыдущаяПереоценкаОбъектов(ТекстыЗапроса)
	
	ИмяТаблицы = "втПредыдущаяПереоценкаОбъектов";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПредыдущаяПереоценкаОбъектовСгруппированная(ТекстыЗапроса);
	
	Текст =
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втПредыдущаяПереоценкаОбъектов
	|"+
	"ВЫБРАТЬ
	|	втПредыдущаяПереоценкаОбъектовСгруппированная.ОбъектУчета,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки > втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки
	|			ТОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки - втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки > втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки
	|			ТОГДА втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаУценки - втПредыдущаяПереоценкаОбъектовСгруппированная.СуммаДооценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаОбъектов
	|ИЗ
	|	втПредыдущаяПереоценкаОбъектовСгруппированная КАК втПредыдущаяПереоценкаОбъектовСгруппированная" + ";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса)
	
	ИмяТаблицы = "втДанныеДляПроведения";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиСтоимости(ТекстыЗапроса);
	ВременнаяТаблицаАмортизацияПоДокументу(ТекстыЗапроса);
	ВременнаяТаблицаПредыдущаяПереоценкаОбъектов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втДанныеДляПроведения
	|"+
	"ВЫБРАТЬ
	|	втТаблицаПоДокументу.Ссылка,
	|	втТаблицаПоДокументу.ОбъектУчета,
	|	втТаблицаПоДокументу.Организация КАК Организация,
	|	втТаблицаПоДокументу.Подразделение КАК Подразделение,
	|	втТаблицаПоДокументу.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) КАК ВосстановительнаяСтоимость,
	|	ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0) КАК НакопленнаяАмортизация,
	|	втТаблицаПоДокументу.Стоимость КАК НоваяСтоимость,
	|	ВЫБОР
	|		КОГДА втОстаткиСтоимости.ОбъектУчета ЕСТЬ NULL 
	|				ИЛИ втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|					< (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0))
	|			ТОГДА 0
	|		ИНАЧЕ втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|				- (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0))
	|	КОНЕЦ КАК ОстаточнаяСтоимостьСУчетомАмортизации,
	|	ВЫБОР
	|		КОГДА втТаблицаПоДокументу.Стоимость > 
	|				(втОстаткиСтоимости.ВосстановительнаяСтоимость - ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) - ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) 
	|			ТОГДА  втТаблицаПоДокументу.Стоимость 
	|					- (втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|						- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) 
	|							- ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втТаблицаПоДокументу.Стоимость < 
	|								(втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|									- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) 
	|												- ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) 
	|			ТОГДА  втОстаткиСтоимости.ВосстановительнаяСтоимость 
	|						- ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) 
	|							-ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0) 
	|								- втТаблицаПоДокументу.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) = 0
	|				ИЛИ втТаблицаПоДокументу.Стоимость < (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))
	|					ИЛИ ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) = 0
	|		ТОГДА 0
	|		ИНАЧЕ - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) + ВЫРАЗИТЬ(((ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) * втТаблицаПоДокументу.Стоимость / (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))) КАК ЧИСЛО(15, 2)) 
	|	КОНЕЦ КАК СуммаДооценкиАмортизации,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) = 0
	|				ИЛИ втТаблицаПоДокументу.Стоимость > (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))
	|					ИЛИ ЕСТЬNULL(втОстаткиСтоимости.ВосстановительнаяСтоимость, 0) - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) = 0
	|		ТОГДА 0
	|		ИНАЧЕ (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) - ВЫРАЗИТЬ(((ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)) * втТаблицаПоДокументу.Стоимость / (втОстаткиСтоимости.ВосстановительнаяСтоимость - (ЕСТЬNULL(втОстаткиСтоимости.НакопленнаяАмортизация, 0) + ЕСТЬNULL(втАмортизацияПоДокументу.Сумма, 0)))) КАК ЧИСЛО(15, 2)) 
	|	КОНЕЦ КАК СуммаУценкиАмортизации,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаОбъектов.СуммаДооценки, 0) КАК СуммаПрошлойДооценки,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаОбъектов.СуммаУценки, 0) КАК СуммаПрошлойУценки
	|ПОМЕСТИТЬ втДанныеДляПроведения
	|ИЗ
	|	втТаблицаПоДокументу КАК втТаблицаПоДокументу
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиСтоимости КАК втОстаткиСтоимости
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втОстаткиСтоимости.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияПоДокументу КАК втАмортизацияПоДокументу
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втАмортизацияПоДокументу.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредыдущаяПереоценкаОбъектов КАК втПредыдущаяПереоценкаОбъектов
	|		ПО втТаблицаПоДокументу.ОбъектУчета = втПредыдущаяПереоценкаОбъектов.ОбъектУчета"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ПервоначальныеСведенияНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	втДанныеДляПроведения.ОбъектУчета КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.ПервоначальнаяСтоимость 
	|		+ втДанныеДляПроведения.СуммаДооценки
	|			- втДанныеДляПроведения.СуммаУценки 
	|				+ втДанныеДляПроведения.СуммаДооценкиАмортизации 
	|					- втДанныеДляПроведения.СуммаУценкиАмортизации КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость
	|	
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Регистратор<>&Ссылка
	|		И НематериальныйАктив = &НематериальныйАктив
	|	) КАК ПервоначальныеСведенияНМАБухгалтерскийУчет
	|	ПО втДанныеДляПроведения.ОбъектУчета = ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПереоценкаНМАБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	
	ИмяРегистра = "ПереоценкаНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПереоценкаНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	втДанныеДляПроведения.ОбъектУчета КАК НематериальныйАктив,
	|	втДанныеДляПроведения.СуммаДооценки КАК СуммаПереоценки
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Ссылка,
	|	&Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	&Организация,
	|	втДанныеДляПроведения.ОбъектУчета,
	|	втДанныеДляПроведения.СуммаУценки
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценки  > 0"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Функция ПрочиеРасходы(ТекстыЗапроса)
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ // Уценка восстановительной стоимости
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&НалоговоеНазначениеОрганизации КАК НалоговоеНазначение,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	втДанныеДляПроведения.СуммаУценки - втДанныеДляПроведения.СуммаПрошлойДооценки КАК СуммаРегл,
	|	0 КАК СуммаРеглБезНДС,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|	ПО &СтатьяРасходов = ПВХСтатьиРасходов.Ссылка
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаУценки > втДанныеДляПроведения.СуммаПрошлойДооценки

		|	";

	
КонецФункции

Функция ПартииПрочихРасходов(ТекстыЗапроса)
	
	ВременнаяТаблицаДанныеДляПроведения(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ // Уценка восстановительной стоимости
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	втДанныеДляПроведения.СуммаУценки - втДанныеДляПроведения.СуммаПрошлойДооценки КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	втДанныеДляПроведения.СуммаУценки - втДанныеДляПроведения.СуммаПрошлойДооценки КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|	ПО &СтатьяРасходов = ПВХСтатьиРасходов.Ссылка
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаУценки > втДанныеДляПроведения.СуммаПрошлойДооценки
	|	И ПВХСтатьиРасходов.ВариантРаспределенияРасходов В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Дооценка накопленной амортизации из доли уценки
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * (втДанныеДляПроведения.СуммаДооценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * (втДанныеДляПроведения.СуммаДооценки/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|	ПО &СтатьяРасходов = ПВХСтатьиРасходов.Ссылка
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценки > 0
	|	И втДанныеДляПроведения.СуммаДооценки < втДанныеДляПроведения.СуммаПрошлойУценки
	|	И ПВХСтатьиРасходов.ВариантРаспределенияРасходов В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Дооценка накопленной амортизации из уценки
	|	&Дата КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Сумма,
	|	0 КАК СуммаБезНДС,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * ((втДанныеДляПроведения.СуммаПрошлойУценки)/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	ВЫБОР КОГДА втДанныеДляПроведения.ВосстановительнаяСтоимость = 0
	|		ТОГДА 0
	|		ИНАЧЕ (втДанныеДляПроведения.НакопленнаяАмортизация) * ((втДанныеДляПроведения.СуммаПрошлойУценки)/втДанныеДляПроведения.ВосстановительнаяСтоимость)
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК ПВХСтатьиРасходов
	|	ПО &СтатьяРасходов = ПВХСтатьиРасходов.Ссылка
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценки > 0
	|	И втДанныеДляПроведения.ВосстановительнаяСтоимость > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценки > 0
	|	И втДанныеДляПроведения.СуммаДооценки >= втДанныеДляПроведения.СуммаПрошлойУценки
	|	И ПВХСтатьиРасходов.ВариантРаспределенияРасходов В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	)";
	
КонецФункции

#КонецОбласти

#Область ПроведениеРегламентированныйУчет

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	Возврат
	УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизацииУКР("ПереоценкаНМА")
		+ УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетамУКР()
		+ ВТДанныеДокумента()
		+ ВТПредыдущаяПереоценкаОбъектов()
		+ ВТДанныеДляПроведения()
		;
		
КонецФункции

Функция ВТДанныеДокумента()
	
	Возврат
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втТаблицаОбъектов
	|"+
	"ВЫБРАТЬ
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Дата,
	|	Операция.Организация КАК Организация,
	|	Операция.Подразделение КАК Подразделение,
	|	Операция.НематериальныйАктив КАК ОбъектУчета,
	|	ЕСТЬNULL(Операция.НематериальныйАктив.НаправлениеДеятельности, ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)) КАК НаправлениеДеятельности,
	|	Операция.Стоимость КАК Стоимость,
	|	Операция.СтатьяДоходов,
	|	Операция.АналитикаДоходов,
	|	Операция.СтатьяРасходов,
	|	Операция.АналитикаРасходов
	|ПОМЕСТИТЬ втДанныеДокументаПодготовка
	|ИЗ
	|	Документ.ПереоценкаНМА КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеДокументаПодготовка.Ссылка,
	|	втДанныеДокументаПодготовка.Дата,
	|	втДанныеДокументаПодготовка.Организация,
	|	втДанныеДокументаПодготовка.Подразделение,
	|	втДанныеДокументаПодготовка.ОбъектУчета КАК ОбъектУчета,
	|	втДанныеДокументаПодготовка.НаправлениеДеятельности,
	|	втДанныеДокументаПодготовка.Стоимость,
	|	втДанныеДокументаПодготовка.СтатьяДоходов,
	|	втДанныеДокументаПодготовка.АналитикаДоходов,
	|	втДанныеДокументаПодготовка.СтатьяРасходов,
	|	втДанныеДокументаПодготовка.АналитикаРасходов,
	|	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчетаДооценокНМА КАК СчетДооценки,
	|	ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НалоговоеНазначение КАК НалоговоеНазначение
	|ПОМЕСТИТЬ втДанныеДокумента
	|ИЗ
	|	втДанныеДокументаПодготовка КАК втДанныеДокументаПодготовка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				&Дата,
	|				(НематериальныйАктив, Организация) В
	|					(ВЫБРАТЬ
	|						Т.ОбъектУчета,
	|						Т.Организация
	|					ИЗ
	|						втДанныеДокументаПодготовка КАК Т)) КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
	|		ПО втДанныеДокументаПодготовка.Организация = СчетаБухгалтерскогоУчетаНМАСрезПоследних.Организация
	|			И втДанныеДокументаПодготовка.ОбъектУчета = СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|				&Дата,
	|				(НематериальныйАктив, Организация) В
	|					(ВЫБРАТЬ
	|						Т.ОбъектУчета,
	|						Т.Организация
	|					ИЗ
	|						втДанныеДокументаПодготовка КАК Т)) КАК ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних
	|		ПО втДанныеДокументаПодготовка.ОбъектУчета = ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.НематериальныйАктив
	|			И втДанныеДокументаПодготовка.Организация = ПервоначальныеСведенияНМАНалоговыйУчетСрезПоследних.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета" + ";";
КонецФункции

Функция ВТПредыдущаяПереоценкаОбъектов()
	
	Возврат
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втПредыдущаяПереоценкаОбъектов
	|"+
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаБухгалтерскогоУчета.СчетУчета КАК Счет
	|ПОМЕСТИТЬ втВсеСчетаУчетаСтоимости
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА КАК СчетаБухгалтерскогоУчета
	|		ПО втОбъектыДокумента.ОбъектУчета = СчетаБухгалтерскогоУчета.НематериальныйАктив
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъектыДокумента.ОбъектУчета КАК ОбъектУчета,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиПриход, 0)) КАК СуммаДооценки,
	|	СУММА(ЕСТЬNULL(ПереоценкаНМАБухгалтерскийУчетОбороты.СуммаПереоценкиРасход, 0)) КАК СуммаУценки
	|ПОМЕСТИТЬ  втПредыдущаяПереоценкаСтоимостиСгруппированная
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереоценкаНМАБухгалтерскийУчет.Обороты(
	|				,
	|				&ГраницаМесяцОкончание,
	|				Регистратор,
	|				(Организация, НематериальныйАктив) В
	|					(ВЫБРАТЬ
	|						Т.Организация,
	|						Т.ОбъектУчета
	|					ИЗ
	|						втОбъектыДокумента КАК Т)) КАК ПереоценкаНМАБухгалтерскийУчетОбороты
	|		ПО втОбъектыДокумента.ОбъектУчета = ПереоценкаНМАБухгалтерскийУчетОбороты.НематериальныйАктив
	|			И втОбъектыДокумента.Организация = ПереоценкаНМАБухгалтерскийУчетОбороты.Организация
	|			И (ПереоценкаНМАБухгалтерскийУчетОбороты.Регистратор <> &Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	втОбъектыДокумента.ОбъектУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втПредыдущаяПереоценкаСтоимостиСгруппированная.ОбъектУчета,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки > втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки
	|			ТОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки - втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДооценки,
	|	ВЫБОР
	|		КОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки > втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки
	|			ТОГДА втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаУценки - втПредыдущаяПереоценкаСтоимостиСгруппированная.СуммаДооценки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаУценки
	|ПОМЕСТИТЬ втПредыдущаяПереоценкаСтоимости
	|ИЗ
	|	втПредыдущаяПереоценкаСтоимостиСгруппированная КАК втПредыдущаяПереоценкаСтоимостиСгруппированная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетаБухгалтерскогоУчетаНМА.СчетНачисленияАмортизации КАК Счет
	|ПОМЕСТИТЬ втВсеСчетаУчетаАмортизации
	|ИЗ
	|	втОбъектыДокумента КАК втОбъектыДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА КАК СчетаБухгалтерскогоУчетаНМА
	|		ПО втОбъектыДокумента.ОбъектУчета = СчетаБухгалтерскогоУчетаНМА.НематериальныйАктив
	|
	|" + ";";
	
КонецФункции

Функция ВТДанныеДляПроведения()
	
	Возврат
	"////////////////////////////////////////////////////////////////////////////////////////////////////
	|//Временная таблица втДанныеДляПроведения
	|"+
	"ВЫБРАТЬ
	|	втДанныеДокумента.Ссылка КАК Ссылка,
	|	втДанныеДокумента.Дата КАК Дата,
	|	втДанныеДокумента.ОбъектУчета,
	|	втДанныеДокумента.Организация КАК Организация,
	|	втДанныеДокумента.Подразделение КАК Подразделение,
	|	втДанныеДокумента.СчетДооценки КАК СчетДооценки,
	|	втДанныеДокумента.СтатьяДоходов КАК СтатьяДоходов,
	|	втДанныеДокумента.АналитикаДоходов КАК АналитикаДоходов,
	|	втДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	втДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	втДанныеДокумента.НалоговоеНазначение КАК НалоговоеНазначение,
	|	втСчетаОтражения.СчетУчета КАК СчетУчета,
	|	втСчетаОтражения.СчетНачисленияАмортизации КАК СчетАмортизации,
	|	втДанныеДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ВосстановительнаяСтоимость,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК НакопленнаяАмортизация,
	|	втДанныеДокумента.Стоимость КАК НоваяСтоимость,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL
	|				ИЛИ втДанныеДокумента.Стоимость < (ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|			ТОГДА 0
	|		ИНАЧЕ втДанныеДокумента.Стоимость -  (ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|	КОНЕЦ КАК СуммаДооценкиСтоимости,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL
	|				ИЛИ втДанныеДокумента.Стоимость > (ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))  
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) - втДанныеДокумента.Стоимость
	|	КОНЕЦ КАК СуммаУценкиСтоимости,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL
	|				ИЛИ втДанныеДокумента.Стоимость < (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|				ИЛИ (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) = 0
	|			ТОГДА 0
	|		ИНАЧЕ - втОстаткиПоСчетам.НакопленнаяАмортизацияБУ + ВЫРАЗИТЬ(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ*втДанныеДокумента.Стоимость / (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаДооценкиАмортизации,
	|	ВЫБОР
	|		КОГДА втОстаткиПоСчетам.ОбъектУчета ЕСТЬ NULL
	|				ИЛИ втДанныеДокумента.Стоимость > (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0))
	|				ИЛИ  (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) = 0
	|			ТОГДА 0
	|		ИНАЧЕ втОстаткиПоСчетам.НакопленнаяАмортизацияБУ - ВЫРАЗИТЬ(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ*втДанныеДокумента.Стоимость / (втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ - ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК СуммаУценкиАмортизации,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаСтоимости.СуммаДооценки, 0) КАК СуммаПрошлойДооценкиСтоимости,
	|	ЕСТЬNULL(втПредыдущаяПереоценкаСтоимости.СуммаУценки, 0) КАК СуммаПрошлойУценкиСтоимости
	|ПОМЕСТИТЬ втДанныеДляПроведения
	|ИЗ
	|	втДанныеДокумента КАК втДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК втСчетаОтражения
	|		ПО втДанныеДокумента.ОбъектУчета = втСчетаОтражения.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО втДанныеДокумента.ОбъектУчета = втОстаткиПоСчетам.ОбъектУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПредыдущаяПереоценкаСтоимости КАК втПредыдущаяПереоценкаСтоимости
	|		ПО втДанныеДокумента.ОбъектУчета = втПредыдущаяПереоценкаСтоимости.ОбъектУчета
	|		" + ";";
	
	
КонецФункции

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	Разделитель = Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
	
	Возврат УчетНМА.ТекстОтраженияВРеглУчетеНачисленнойАмортизацииУКР()
	
	+ Разделитель + ДооценкаВосстановительнойСтоимостиУКР()
	+ Разделитель + ДооценкаВосстановительнойСтоимостиИзДолиУценкиУКР()
	+ Разделитель + ДооценкаВосстановительнойСтоимостиИзУценкиУКР()
	+ Разделитель + ДооценкаНакопленнойАмортизацииУКР()
	+ Разделитель + УценкаВосстановительнойСтоимостиУКР()
	+ Разделитель + УценкаВосстановительнойСтоимостиИзДолиДооценкиУКР()
	+ Разделитель + УценкаВосстановительнойСтоимостиИзДооценкиУКР()
	+ Разделитель + УценкаНакопленнойАмортизацииУКР()
      ;
	
КонецФункции

Функция ДооценкаВосстановительнойСтоимостиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка восстановительной стоимости (Дт СчетУчета :: Кт СчетДооценки)
	|ВЫБРАТЬ 
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости - втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт
	|	
	|	втДанныеДляПроведения.СчетДооценки КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Переоценка НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаДооценкиСтоимости > втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости
	|";
КонецФункции

Функция ДооценкаВосстановительнойСтоимостиИзДолиУценкиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка восстановительной стоимости из уценки (Дт СчетУчета :: Кт 746(742))
	|ВЫБРАТЬ 
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	втДанныеДляПроведения.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК СубконтоКт1,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт2,
	|	втДанныеДляПроведения.АналитикаДоходов КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Переоценка НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаДооценкиСтоимости < втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости
	|";
КонецФункции

Функция ДооценкаВосстановительнойСтоимостиИзУценкиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|//  Дооценка восстановительной стоимости из предыдущей уценки (Дт СчетУчета :: Кт 746(742))
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт,
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Доходы) КАК ВидСчетаКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК АналитикаУчетаКт,
	|	втДанныеДляПроведения.Подразделение КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт
	|	
	|	НЕОПРЕДЕЛЕНО КАК СчетКт,
	|	втДанныеДляПроведения.СтатьяДоходов КАК СубконтоКт1,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт2,
	|	втДанныеДляПроведения.АналитикаДоходов КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Переоценка НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаДооценкиСтоимости >= втДанныеДляПроведения.СуммаПрошлойУценкиСтоимости
	|";
КонецФункции

Функция ДооценкаНакопленнойАмортизацииУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Дооценка накопленной амортизации (Дт СчетУчета :: Кт СчетАмортизации)
	|ВЫБРАТЬ 
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаДооценкиАмортизации  КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|
	|	втДанныеДляПроведения.СчетУчета КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|	
	|	втДанныеДляПроведения.СчетАмортизации КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Дооценка накопленной амортизации"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаДооценкиАмортизации > 0
	|";
КонецФункции

Функция УценкаВосстановительнойСтоимостиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка восстановительной стоимости (Дт 975 (972) :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	втДанныеДляПроведения.СуммаУценкиСтоимости - втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	втДанныеДляПроведения.СтатьяРасходов КАК АналитикаУчетаДт,
	|	втДанныеДляПроведения.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	втДанныеДляПроведения.СтатьяРасходов КАК СубконтоДт1,
	|	втДанныеДляПроведения.АналитикаРасходов КАК СубконтоДт2,
	|   ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Переоценка НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО втДанныеДляПроведения.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаУценкиСтоимости > втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости
	|";
КонецФункции

Функция УценкаВосстановительнойСтоимостиИзДолиДооценкиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка восстановительной стоимости из дооценки (Дт СчетДооценки :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаУценкиСтоимости КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|	
	|	втДанныеДляПроведения.СчетДооценки КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Переоценка НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаУценкиСтоимости < втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости
	|";
КонецФункции

Функция УценкаВосстановительнойСтоимостиИзДооценкиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка восстановительной стоимости из предыдущей дооценки (Дт СчетДооценки :: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|	
	|	втДанныеДляПроведения.СчетДооценки КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Переоценка НМА"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости > 0
	|	И втДанныеДляПроведения.СуммаУценкиСтоимости >= втДанныеДляПроведения.СуммаПрошлойДооценкиСтоимости
	|";
КонецФункции

Функция УценкаНакопленнойАмортизацииУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Уценка накопленной амортизации (Дт СчетАмортизации :: Кт СчетУчета)
	
	|ВЫБРАТЬ
	|	
	|	втДанныеДляПроведения.Ссылка КАК Ссылка,
	|	втДанныеДляПроведения.Дата КАК Период,
	|	втДанныеДляПроведения.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втДанныеДляПроведения.СуммаУценкиАмортизации КАК Сумма,
	|	
	|	// Дт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеДт,
	|	втДанныеДляПроведения.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеДт, 
	|	
	|	втДанныеДляПроведения.СчетАмортизации КАК СчетДт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	// Кт ///////////////////////////////////////////////////////////////////////////////////////////
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втДанныеДляПроведения.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	втДанныеДляПроведения.НалоговоеНазначение КАК НалоговоеНазначениеКт, 
	|	
	|	втДанныеДляПроведения.СчетУчета КАК СчетКт,
	|	втДанныеДляПроведения.ОбъектУчета КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Уценка накопленной амортизации"" КАК Содержание
	|ИЗ
	|	втДанныеДляПроведения КАК втДанныеДляПроведения
	|	
	|ГДЕ
	|	втДанныеДляПроведения.СуммаУценкиАмортизации > 0
	|";
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетНМА.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
