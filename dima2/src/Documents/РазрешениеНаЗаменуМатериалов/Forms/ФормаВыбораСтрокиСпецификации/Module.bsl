
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Спецификация = Неопределено;
	
	Параметры.Свойство("Спецификация", Спецификация);
	
	Если Не ЗначениеЗаполнено(Спецификация) Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(МатериалыИУслугиСпецификации, "Спецификация", Спецификация);

	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыИУслугиСпецификации

&НаКлиенте
Процедура МатериалыИУслугиСпецификацииВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.МатериалыИУслугиСпецификации.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ЗначениеВыбора = Новый Структура("КлючСвязиСпецификация, Номенклатура, Характеристика, Количество, КоличествоУпаковок, Упаковка");
		
		ЗаполнитьЗначенияСвойств(ЗначениеВыбора, ТекущиеДанные);
		ЗначениеВыбора.Вставить("ПрименениеМатериала", Строка(ТекущиеДанные.НомерСтроки) + ": " + ТекущиеДанные.ПрименениеМатериала);
		ЗначениеВыбора.Вставить("ОшибкаСоответствияСтрокеСпецификации", Ложь);
		
		СтруктураВыбора = Новый Структура();
		СтруктураВыбора.Вставить("ВыполняемаяОперация", "ВыборСтрокиСпецификации");
		СтруктураВыбора.Вставить("ЗначениеВыбора", ЗначениеВыбора);
		
		ОповеститьОВыборе(СтруктураВыбора);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

