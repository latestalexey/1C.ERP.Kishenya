#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоПродукции Тогда
		ЗаголовокГруппы = НСтр("ru='Продукция';uk='Продукція'");
		Заголовок = НСтр("ru='Выбор продукции';uk='Вибір продукції'");
	ИначеЕсли Параметры.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоСпецификациям Тогда
		ЗаголовокГруппы = НСтр("ru='Спецификация продукции';uk='Специфікація продукції'");
		Заголовок = НСтр("ru='Выбор спецификации продукции';uk='Вибір специфікації продукції'");
	ИначеЕсли Параметры.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.ПоЗаказамНаПроизводство Тогда
		ЗаголовокГруппы = НСтр("ru='Заказ (этап, спецификация)';uk='Замовлення (етап, специфікація)'");
		Заголовок = НСтр("ru='Выбор заказа';uk='Вибір замовлення'");
	КонецЕсли; 
	
	Элементы.СписокГруппГруппаЗатрат.Заголовок = ЗаголовокГруппы;
	
	Если Параметры.ПоЗаказам Тогда
		
		ДанныеЗаказа = ПолучитьИзВременногоХранилища(Параметры.АдресХранилищаДанныхЗаказа);
		Для каждого СтрокаУслуга Из ДанныеЗаказа.Услуги Цикл
			СтрокаГруппа = СписокГрупп.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаГруппа, СтрокаУслуга);
		КонецЦикла;
		
	Иначе
		
		ОтборПоДокументуПоступления = Неопределено;
		Параметры.Свойство("ОтборПоДокументуПоступления", ОтборПоДокументуПоступления);
		
		Для каждого СтрокаУслуга Из Параметры.Услуги Цикл
			Если ОтборПоДокументуПоступления = Неопределено
				ИЛИ СтрокаУслуга.ДокументПоступления = ОтборПоДокументуПоступления Тогда
				СтрокаГруппа = СписокГрупп.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаГруппа, СтрокаУслуга);
			КонецЕсли; 
		КонецЦикла;
		
		Если Параметры.ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВЗаказеПереработчику.БезГруппировки Тогда
			Элементы.СписокГруппГруппаЗатрат.Видимость = Ложь;
			Заголовок = НСтр("ru='Выбор поступления';uk='Вибір надходження'")
		КонецЕсли; 
		
	КонецЕсли; 
	
	Элементы.СписокГруппДокументПоступления.Видимость = НЕ Параметры.ПоЗаказам;
	
	Для каждого СтрокаГруппа Из СписокГрупп Цикл
		Если Параметры.НомерГруппыЗатрат = СтрокаГруппа.НомерГруппыЗатрат Тогда
			Элементы.СписокГрупп.ТекущаяСтрока = СтрокаГруппа.ПолучитьИдентификатор();
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура СписокГруппВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборГруппы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьГруппу(Команда)
	
	ОбработатьВыборГруппы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборГруппы()

	ТекущиеДанные = Элементы.СписокГрупп.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("ГруппаЗатрат", ТекущиеДанные.ГруппаЗатрат);
	ПараметрыВыбора.Вставить("НомерГруппыЗатрат", ТекущиеДанные.НомерГруппыЗатрат);
	ПараметрыВыбора.Вставить("Спецификация", ТекущиеДанные.Спецификация);
	ПараметрыВыбора.Вставить("ДокументПоступления", ТекущиеДанные.ДокументПоступления);

	Закрыть(ПараметрыВыбора);

КонецПроцедуры

#КонецОбласти
