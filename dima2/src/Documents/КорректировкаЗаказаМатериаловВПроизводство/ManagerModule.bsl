#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	Документы.РасходныйОрдерНаТовары.ДобавитьКомандуСоздатьНаОснованииРасходныйОрдерНаТовары(КомандыСоздатьНаОсновании);

КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.КорректировкаЗаказаМатериаловВПроизводство) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.КорректировкаЗаказаМатериаловВПроизводство.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.КорректировкаЗаказаМатериаловВПроизводство);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьКорректировкиЗаказаМатериаловВПроизводство,ИспользоватьПроизводство";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры


// Заполняет документ "Корректировка заказа материалов в производство" для отражения операций изменения обеспечения.
//
//  Параметры:
//   Объект - ДокументОбъект.КорректировкаЗаказаМатериаловВПроизводство - Заполняемый документ.
//   Распоряжение - ДокументСсылка.ЗаказНаПроизводство - Распоряжение, корректировку которого отражает заполняемый документ
//   Подразделение - СправочникСсылка.СтруктураПредприятия - Подразделение распоряжения.
//   ТаблицаИзмененийПоКодамСтрок - ТаблицаЗначений - Таблица с полями:
//                                                    КодСтроки - код строки корректируемого распоряжения в регистре "Заказы материалов с учетом корректировок".
//                                                    ВариантОбеспечения - новый вариант обеспечения.
//                                                    ДатаОтгрузки - новая дата потребности.
//                                                    Количество - новое количество.
//
Процедура ЗаполнитьНаОснованииИзменений(Объект, Распоряжение, Подразделение, ТаблицаИзмененийПоКодамСтрок) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Таблица",      ТаблицаИзмененийПоКодамСтрок);
	Запрос.УстановитьПараметр("Распоряжение", Распоряжение);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.КодСтроки          КАК КодСтроки,
		|	Таблица.Склад              КАК Склад,
		|	Таблица.ВариантОбеспечения КАК ВариантОбеспечения,
		|	Таблица.ДатаОтгрузки       КАК ДатаОтгрузки,
		|	Таблица.Количество         КАК Количество
		|
		|ПОМЕСТИТЬ ВтКодыСтрок
		|ИЗ
		|	&Таблица КАК Таблица
		|;
		|
		|///////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР КОГДА Таблица.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|				РегистрЗаказы.ДатаПотребности
		|			ИНАЧЕ
		|				Таблица.ДатаОтгрузки
		|		КОНЕЦ                           КАК ДатаПотребности,
		|	РегистрЗаказы.ДатаПотребности       КАК ДатаПотребностиИсходный,
		|
		|	ТаблицаМатериалыЗаказа.ЗаказатьНаСклад  КАК ЗаказатьНаСклад,
		|	ТаблицаМатериалыЗаказа.ЗаказатьНаСклад КАК ЗаказатьНаСкладИсходный,
		
		|	Таблица.Количество         КАК Количество,
		|	Таблица.Количество
		|		/ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоУпаковок,
		|
		|	РегистрЗаказы.Номенклатура   КАК Номенклатура,
		|	РегистрЗаказы.Характеристика КАК Характеристика,
		|	РегистрЗаказы.Назначение     КАК Назначение,
		
		|	РегистрЗаказы.Отменено       КАК Отменено,
		|	РегистрЗаказы.Отменено       КАК ОтмененоИсходный,
		
		|	РегистрЗаказы.Серия          КАК Серия,
		|	РегистрЗаказы.Серия          КАК СерияИсходный,
		
		|	Таблица.Склад                КАК Склад,
		|	РегистрЗаказы.Склад          КАК СкладИсходный,
		
		|	РегистрЗаказы.Упаковка       КАК Упаковка,
		|	РегистрЗаказы.Упаковка       КАК УпаковкаИсходный,
		|
		|	Таблица.ВариантОбеспечения          КАК ВариантОбеспечения,
		|	РегистрЗаказы.ВариантОбеспечения    КАК ВариантОбеспеченияИсходный,
		
		|	РегистрЗаказы.КодСтроки             КАК КодСтрокиИсходный,
		|	РегистрЗаказы.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
		|
		|	ТаблицаМатериалыЗаказа.СтатусУказанияСерийОтправитель КАК СтатусУказанияСерийОтправитель,
		|	ТаблицаМатериалыЗаказа.СтатусУказанияСерийПолучатель  КАК СтатусУказанияСерийПолучатель,
		
		|	ТаблицаМатериалыЗаказа.СтатусУказанияСерий            КАК СтатусУказанияСерий
		
		|
		|ИЗ
		|	ВтКодыСтрок КАК Таблица
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыМатериаловСУчетомКорректировок.Обороты(,,,
		|			(Распоряжение, КодСтроки) В(
		|				ВЫБРАТЬ
		|					&Распоряжение КАК Распоряжение,
		|					Фильтр.КодСтроки КАК КодСтроки
		|				ИЗ
		|					ВтКодыСтрок КАК Фильтр)) КАК РегистрЗаказы
		|		ПО Таблица.КодСтроки = РегистрЗаказы.КодСтроки
		|			И РегистрЗаказы.Распоряжение = &Распоряжение
		|		
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыЗаказа
		|		ПО ТаблицаМатериалыЗаказа.Ссылка    = РегистрЗаказы.Распоряжение
		|		 И ТаблицаМатериалыЗаказа.КодСтроки = РегистрЗаказы.КодСтрокиРаспоряжения
		|		
		|ГДЕ
		|	Таблица.Склад <> РегистрЗаказы.Склад
		|		ИЛИ Таблица.ВариантОбеспечения <> РегистрЗаказы.ВариантОбеспечения
		|		ИЛИ ВЫБОР КОГДА Таблица.ДатаОтгрузки = ДАТАВРЕМЯ(1, 1, 1) ТОГДА
		|						РегистрЗаказы.ДатаПотребности
		|					ИНАЧЕ Таблица.ДатаОтгрузки
		|			КОНЕЦ <> РегистрЗаказы.ДатаПотребности";
		
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"РегистрЗаказы.Упаковка",
		"РегистрЗаказы.Номенклатура"));
		
	Таблица = Запрос.Выполнить().Выгрузить();
	Объект.Распоряжение  = Распоряжение;
	Объект.Подразделение = Подразделение;
	Объект.Организация   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Распоряжение, "Организация");
	Объект.Дата          = ТекущаяДатаСеанса();
	Объект.Ответственный = Пользователи.ТекущийПользователь();
	
	Объект.МатериалыИУслуги.Загрузить(Таблица);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции

Функция АдаптированныйТекстЗапросаДвиженийПоРегистру(ИмяРегистра) Экспорт
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый СписокЗначений;
	ПолноеИмяДокумента = "Документ.КорректировкаЗаказаМатериаловВПроизводство"; 
	
	ПереопределениеРасчетаПараметров = Новый Структура;

	Если ИмяРегистра = "СвободныеОстатки" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "ОбеспечениеЗаказов" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаТовары";
		
	ИначеЕсли ИмяРегистра = "ТоварыКОтгрузке" Тогда
		
		ТекстЗапроса = ТекстЗапросаТаблицаТоварыКОтгрузке(Запрос, ТекстыЗапроса, ИмяРегистра);
		СинонимТаблицыДокумента = "ТаблицаМатериалыИУслуги";
		
	Иначе
		ТекстИсключения = НСтр("ru='В документе %ПолноеИмяДокумента% не реализована адаптация текста запроса формирования движений по регистру %ИмяРегистра%.';uk='У документі %ПолноеИмяДокумента% не реалізована адаптація тексту запиту формування рухів по регістру %ИмяРегистра%.'");
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ПолноеИмяДокумента%", ПолноеИмяДокумента);
		ТекстИсключения = СтрЗаменить(ТекстИсключения, "%ИмяРегистра%", ИмяРегистра);
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Результат = ОбновлениеИнформационнойБазыУТ.РезультатАдаптацииЗапроса();
	Результат.ТекстЗапроса = ОбновлениеИнформационнойБазыУТ.АдаптироватьЗапросМеханизмаПроведения(ТекстЗапроса,
																								ПолноеИмяДокумента,
																								СинонимТаблицыДокумента,
																								ПереопределениеРасчетаПараметров );

	Возврат Результат;
																				
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт

	// Создание запроса инициализации движений и заполенение его параметров.
	Запрос = Новый Запрос;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка);

	// Формирование текста запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстЗапросаТаблицаГрафикОтгрузкиТоваров(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаОбеспечениеЗаказовРаботами(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаТоварыКОтгрузке(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаЗаказыМатериаловВПроизводство(Запрос, ТекстыЗапроса, Регистры);
	ТекстЗапросаТаблицаЗаказыМатериаловСУчетомКорректировок(Запрос, ТекстыЗапроса, Регистры);

	// Исполнение запроса и выгрузка полученных таблиц для движений.
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Истина);

КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)

	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);

	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДанныеШапки.Дата           КАК Период,
		|	ДанныеШапки.Организация    КАК Организация,
		|	ДанныеШапки.Подразделение  КАК Подразделение,
		|	ДанныеШапки.Распоряжение   КАК Распоряжение
		|ИЗ
		|	Документ.КорректировкаЗаказаМатериаловВПроизводство КАК ДанныеШапки
		|ГДЕ
		|	ДанныеШапки.Ссылка = &Ссылка";
	
	Реквизиты = Запрос.Выполнить().Выбрать();
	Реквизиты.Следующий();
	
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("Подразделение", Реквизиты.Подразделение);
	Запрос.УстановитьПараметр("Распоряжение", Реквизиты.Распоряжение);

КонецПроцедуры

Функция ТекстЗапросаТаблицаСвободныеОстатки(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "СвободныеОстатки";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	//Сторно движений заказа
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаМатериалы.СкладИсходный         КАК Склад,
	|	ТаблицаМатериалы.Номенклатура          КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика        КАК Характеристика,
	|	-ТаблицаМатериалы.Количество           КАК ВРезервеСоСклада,
	|	0                                      КАК ВРезервеПодЗаказ,
	|	0                                      КАК ВНаличии
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.КодСтрокиИсходный <> 0
	|	И ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаМатериалы.СкладИсходный         КАК Склад,
	|	ТаблицаМатериалы.Номенклатура          КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика        КАК Характеристика,
	|	0                                      КАК ВРезервеСоСклада,
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно) ТОГДА
	|			-ТаблицаМатериалы.Количество
	|		КОНЕЦ                              КАК ВРезервеПодЗаказ,
	|
	|	-ТаблицаМатериалы.Количество           КАК ВНаличии
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.КодСтрокиИсходный <> 0
	|	И ТаблицаМатериалы.ВариантОбеспеченияИсходный В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Движения по текущему документу
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаМатериалы.Склад                 КАК Склад,
	|	ТаблицаМатериалы.Номенклатура          КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика        КАК Характеристика,
	|	ТаблицаМатериалы.Количество            КАК ВРезервеСоСклада,
	|	0                                      КАК ВРезервеПодЗаказ,
	|	0                                      КАК ВНаличии
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Период                                КАК Период,
	|	ТаблицаМатериалы.Склад                 КАК Склад,
	|	ТаблицаМатериалы.Номенклатура          КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика        КАК Характеристика,
	|	0                                      КАК ВРезервеСоСклада,
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно) ТОГДА
	|			ТаблицаМатериалы.Количество
	|		КОНЕЦ                              КАК ВРезервеПодЗаказ,
	|
	|	ТаблицаМатериалы.Количество            КАК ВНаличии
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.ВариантОбеспечения В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказов(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ОбеспечениеЗаказов";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	//Сторно движений заказа
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	&Период                                   КАК Период,
	|	ТаблицаМатериалы.СкладИсходный            КАК Склад,
	|	ТаблицаМатериалы.Номенклатура             КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика           КАК Характеристика,
	|	ТаблицаМатериалы.НазначениеИсходный       КАК Назначение,
	|	-ТаблицаМатериалы.Количество              КАК Потребность,
	|	-ТаблицаМатериалы.Количество              КАК КЗаказу,
	|	0                                         КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.КодСтрокиИсходный <> 0
	|	И ТаблицаМатериалы.ВариантОбеспеченияИсходный В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	&Период                                   КАК Период,
	|	ТаблицаМатериалы.СкладИсходный            КАК Склад,
	|	ТаблицаМатериалы.Номенклатура             КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика           КАК Характеристика,
	|	ТаблицаМатериалы.НазначениеИсходный       КАК Назначение,
	|	-ТаблицаМатериалы.Количество              КАК Потребность,
	|	0                                         КАК КЗаказу,
	|	-ТаблицаМатериалы.Количество              КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.КодСтрокиИсходный <> 0
	|	И ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Движения по текущему документу
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)    КАК ВидДвижения,
	|	&Период                                   КАК Период,
	|	ТаблицаМатериалы.Склад                    КАК Склад,
	|	ТаблицаМатериалы.Номенклатура             КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика           КАК Характеристика,
	|	ТаблицаМатериалы.Назначение               КАК Назначение,
	|	ТаблицаМатериалы.Количество               КАК Потребность,
	|	ТаблицаМатериалы.Количество               КАК КЗаказу,
	|	0                                         КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.ВариантОбеспечения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)    КАК ВидДвижения,
	|	&Период                                   КАК Период,
	|	ТаблицаМатериалы.Склад                    КАК Склад,
	|	ТаблицаМатериалы.Номенклатура             КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика           КАК Характеристика,
	|	ТаблицаМатериалы.Назначение               КАК Назначение,
	|	ТаблицаМатериалы.Количество               КАК Потребность,
	|	0                                         КАК КЗаказу,
	|	ТаблицаМатериалы.Количество               КАК НаличиеПодЗаказ
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаОбеспечениеЗаказовРаботами(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ОбеспечениеЗаказовРаботами";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 

	ТекстЗапроса = 
	//Сторно движений заказа
	"ВЫБРАТЬ
	|	ТабличнаяЧасть.ДатаПотребности                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                 КАК ВидДвижения,
	|	&Подразделение                                         КАК Подразделение,
	|	ТабличнаяЧасть.Номенклатура                            КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                          КАК Характеристика,
	|	ТабличнаяЧасть.НазначениеИсходный                      КАК Назначение,
	|	-ТабличнаяЧасть.Количество                             КАК КОбеспечению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТабличнаяЧасть
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И ТабличнаяЧасть.КодСтрокиИсходный <> 0
	|	И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТабличнаяЧасть.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Движения по текущему документу
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.ДатаПотребности                         КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)                 КАК ВидДвижения,
	|	&Подразделение                                         КАК Подразделение,
	|	ТабличнаяЧасть.Номенклатура                            КАК Номенклатура,
	|	ТабличнаяЧасть.Характеристика                          КАК Характеристика,
	|	ТабличнаяЧасть.Назначение                              КАК Назначение,
	|	ТабличнаяЧасть.Количество                              КАК КОбеспечению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТабличнаяЧасть
	|
	|ГДЕ
	|	ТабличнаяЧасть.Ссылка = &Ссылка
	|	И ТабличнаяЧасть.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И ТабличнаяЧасть.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно)";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаТоварыКОтгрузке(Запрос, ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ТоварыКОтгрузке";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	//Сторно движений заказа
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаМатериалыИУслуги.СкладИсходный   КАК Склад,
	|	&Подразделение                          КАК Получатель,
	|	&Распоряжение                           КАК ДокументОтгрузки,
	|	ТаблицаМатериалыИУслуги.Номенклатура    КАК Номенклатура,
	|	ТаблицаМатериалыИУслуги.Характеристика  КАК Характеристика,
	|	ТаблицаМатериалыИУслуги.СерияИсходный   КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	-ТаблицаМатериалыИУслуги.Количество     КАК ВРезерве,
	|	0                                       КАК КОтгрузке,
	|	0                                       КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|ГДЕ
	|	ТаблицаМатериалыИУслуги.Ссылка = &Ссылка
	|	И ТаблицаМатериалыИУслуги.КодСтрокиИсходный <> 0
	|	И ТаблицаМатериалыИУслуги.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
	|	И ТаблицаМатериалыИУслуги.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаМатериалыИУслуги.СкладИсходный   КАК Склад,
	|	&Подразделение                          КАК Получатель,
	|	&Распоряжение                           КАК ДокументОтгрузки,
	|	ТаблицаМатериалыИУслуги.Номенклатура    КАК Номенклатура,
	|	ТаблицаМатериалыИУслуги.Характеристика  КАК Характеристика,
	|	ТаблицаМатериалыИУслуги.СерияИсходный   КАК Серия,
	|	ВЫБОР КОГДА ТаблицаМатериалыИУслуги.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|			И ЕСТЬNULL(ТаблицаМатериалыИУслуги.НазначениеИсходный.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|		ТОГДА ТаблицаМатериалыИУслуги.НазначениеИсходный
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	0                                       КАК ВРезерве,
	|	-ТаблицаМатериалыИУслуги.Количество     КАК КОтгрузке,
	|	-ТаблицаМатериалыИУслуги.Количество     КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|ГДЕ
	|	ТаблицаМатериалыИУслуги.Ссылка = &Ссылка
	|	И ТаблицаМатериалыИУслуги.КодСтрокиИсходный <> 0
	|	И ТаблицаМатериалыИУслуги.ВариантОбеспеченияИсходный В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|	И ТаблицаМатериалыИУслуги.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Движения по текущему документу
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаМатериалыИУслуги.Склад           КАК Склад,
	|	&Подразделение                          КАК Получатель,
	|	&Распоряжение                           КАК ДокументОтгрузки,
	|	ТаблицаМатериалыИУслуги.Номенклатура    КАК Номенклатура,
	|	ТаблицаМатериалыИУслуги.Характеристика  КАК Характеристика,
	|	ТаблицаМатериалыИУслуги.Серия           КАК Серия,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	ТаблицаМатериалыИУслуги.Количество      КАК ВРезерве,
	|	0                                       КАК КОтгрузке,
	|	0                                       КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|ГДЕ
	|	ТаблицаМатериалыИУслуги.Ссылка = &Ссылка
	|	И ТаблицаМатериалыИУслуги.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада)
	|	И ТаблицаМатериалыИУслуги.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)  КАК ВидДвижения,
	|	&Период                                 КАК Период,
	|	ТаблицаМатериалыИУслуги.Склад           КАК Склад,
	|	&Подразделение                          КАК Получатель,
	|	&Распоряжение                           КАК ДокументОтгрузки,
	|	ТаблицаМатериалыИУслуги.Номенклатура    КАК Номенклатура,
	|	ТаблицаМатериалыИУслуги.Характеристика  КАК Характеристика,
	|	ТаблицаМатериалыИУслуги.Серия           КАК Серия,
	|	ВЫБОР КОГДА ТаблицаМатериалыИУслуги.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно)
	|			И ЕСТЬNULL(ТаблицаМатериалыИУслуги.Назначение.ДвиженияПоСкладскимРегистрам, ЛОЖЬ)
	|		ТОГДА ТаблицаМатериалыИУслуги.Назначение
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	0                                       КАК ВРезерве,
	|	ТаблицаМатериалыИУслуги.Количество      КАК КОтгрузке,
	|	ТаблицаМатериалыИУслуги.Количество      КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалыИУслуги
	|ГДЕ
	|	ТаблицаМатериалыИУслуги.Ссылка = &Ссылка
	|	И ТаблицаМатериалыИУслуги.ВариантОбеспечения В (
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|	И ТаблицаМатериалыИУслуги.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗаказыМатериаловВПроизводство(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ЗаказыМатериаловВПроизводство";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	// Отмена заказа
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Распоряжение КАК Распоряжение,
	|	&Подразделение КАК Подразделение,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика КАК Характеристика,
	|	ТаблицаМатериалы.СерияИсходный КАК Серия,
	|	ТаблицаМатериалы.СкладИсходный КАК Склад,
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|		ТОГДА ТаблицаМатериалы.НазначениеИсходный
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Назначение,
	|	ТаблицаМатериалы.КодСтрокиИсходный КАК КодСтроки,
	|	-ТаблицаМатериалы.Количество КАК Заказано,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада))
	|			ТОГДА -ТаблицаМатериалы.Количество
	|	КОНЕЦ КАК ВРезерве,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|			ТОГДА -ТаблицаМатериалы.Количество
	|	КОНЕЦ КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.ЗаказатьНаСкладИсходный
	|	И ТаблицаМатериалы.КодСтрокиИсходный <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Период КАК Период,
	|	&Распоряжение КАК Распоряжение,
	|	&Подразделение КАК Подразделение,
	|	ТаблицаМатериалы.Номенклатура КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика КАК Характеристика,
	|	ТаблицаМатериалы.Серия КАК Серия,
	|	ТаблицаМатериалы.Склад КАК Склад,
	|	ВЫБОР 
	|		КОГДА ТаблицаМатериалы.ВариантОбеспечения В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|		 ТОГДА ТаблицаМатериалы.Назначение
	|	ИНАЧЕ
	|		НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Назначение,
	|	ТаблицаМатериалы.КодСтроки КАК КодСтроки,
	|	ТаблицаМатериалы.Количество КАК Заказано,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ВариантОбеспечения В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада))
	|			ТОГДА ТаблицаМатериалы.Количество
	|	КОНЕЦ КАК ВРезерве,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ВариантОбеспечения В (
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить),
	|						ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|			ТОГДА ТаблицаМатериалы.Количество
	|	КОНЕЦ КАК КОформлению
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.ЗаказатьНаСклад";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаЗаказыМатериаловСУчетомКорректировок(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ЗаказыМатериаловСУчетомКорректировок";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	//Сторно движений
	|ВЫБРАТЬ
	|	ТаблицаМатериалы.НомерСтроки                   КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)                   КАК Период,
	|	&Организация                                   КАК Организация,
	|	&Подразделение                                 КАК Подразделение,
	|	&Распоряжение                                  КАК Распоряжение,
	|	ТаблицаМатериалы.КодСтрокиРаспоряжения         КАК КодСтрокиРаспоряжения,
	|	ТаблицаМатериалы.ВариантОбеспеченияИсходный    КАК ВариантОбеспечения,
	|	ТаблицаМатериалы.ДатаПотребностиИсходный       КАК ДатаПотребности,
	|	ТаблицаМатериалы.КодСтрокиИсходный             КАК КодСтроки,
	|	ТаблицаМатериалы.ОтмененоИсходный              КАК Отменено,
	|	ТаблицаМатериалы.СерияИсходный                 КАК Серия,
	|	ТаблицаМатериалы.СкладИсходный                 КАК Склад,
	|	ТаблицаМатериалы.УпаковкаИсходный              КАК Упаковка,
	|	ТаблицаМатериалы.Номенклатура                  КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика                КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|			ТОГДА ТаблицаМатериалы.НазначениеИсходный
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПустаяСсылка)
	|	КОНЕЦ                                          КАК Назначение,
	|	-ТаблицаМатериалы.Количество                   КАК Количество,
	|	-ТаблицаМатериалы.КоличествоУпаковок           КАК КоличествоУпаковок
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|	И ТаблицаМатериалы.КодСтрокиИсходный <> 0
	|	И НЕ ТаблицаМатериалы.КорректировкаНовойСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Движения по текущему документу
	|ВЫБРАТЬ
	|	ТаблицаМатериалы.НомерСтроки                   КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(&Период, ДЕНЬ)                   КАК Период,
	|	&Организация                                   КАК Организация,
	|	&Подразделение                                 КАК Подразделение,
	|	&Распоряжение                                  КАК Распоряжение,
	|	ТаблицаМатериалы.КодСтрокиРаспоряжения         КАК КодСтрокиРаспоряжения,
	|	ТаблицаМатериалы.ВариантОбеспечения            КАК ВариантОбеспечения,
	|	ТаблицаМатериалы.ДатаПотребности               КАК ДатаПотребности,
	|	ТаблицаМатериалы.КодСтроки                     КАК КодСтроки,
	|	ТаблицаМатериалы.Отменено                      КАК Отменено,
	|	ТаблицаМатериалы.Серия                         КАК Серия,
	|	ТаблицаМатериалы.Склад                         КАК Склад,
	|	ТаблицаМатериалы.Упаковка                      КАК Упаковка,
	|	ТаблицаМатериалы.Номенклатура                  КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика                КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ТаблицаМатериалы.ВариантОбеспечения В (
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно), 
	|					ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ОтгрузитьОбособленно))
	|			ТОГДА ТаблицаМатериалы.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ПустаяСсылка)
	|	КОНЕЦ                                          КАК Назначение,
	|	ТаблицаМатериалы.Количество                    КАК Количество,
	|	ТаблицаМатериалы.КоличествоУпаковок            КАК КоличествоУпаковок
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаГрафикОтгрузкиТоваров(Запрос, ТекстыЗапроса, Регистры)

	ИмяРегистра = "ГрафикОтгрузкиТоваров";
	
	Если НЕ ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 
	
	ТекстЗапроса = "
	//Сторно движений заказа
	|ВЫБРАТЬ
	|	ТаблицаМатериалы.НомерСтроки               КАК НомерСтроки,
	|	ТаблицаМатериалы.ДатаПотребностиИсходный   КАК Период,
	|	ТаблицаМатериалы.ДатаПотребностиИсходный   КАК ДатаОтгрузки,
	|
	|	ТаблицаМатериалы.Номенклатура              КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика            КАК Характеристика,
	|	ТаблицаМатериалы.СкладИсходный             КАК Склад,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно) ТОГДА
	|			ТаблицаМатериалы.НазначениеИсходный
	|		КОНЕЦ                                  КАК Назначение,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов) ТОГДА
	|			-ТаблицаМатериалы.Количество
	|		КОНЕЦ                                  КАК КоличествоИзЗаказов,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно) ТОГДА
	|			-ТаблицаМатериалы.Количество
	|		КОНЕЦ                                  КАК КоличествоПодЗаказ,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспеченияИсходный = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется) ТОГДА
	|			-ТаблицаМатериалы.Количество
	|		КОНЕЦ                                  КАК КоличествоНеобеспечено,
	|
	|	&Распоряжение                              КАК Распоряжение
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	И ТаблицаМатериалы.ВариантОбеспеченияИсходный В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	//Движения по текущему документу
	|ВЫБРАТЬ
	|	ТаблицаМатериалы.НомерСтроки               КАК НомерСтроки,
	|	ТаблицаМатериалы.ДатаПотребности           КАК Период,
	|	ТаблицаМатериалы.ДатаПотребности           КАК ДатаОтгрузки,
	|
	|	ТаблицаМатериалы.Номенклатура              КАК Номенклатура,
	|	ТаблицаМатериалы.Характеристика            КАК Характеристика,
	|	ТаблицаМатериалы.Склад                     КАК Склад,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно) ТОГДА
	|			ТаблицаМатериалы.Назначение
	|		КОНЕЦ                                  КАК Назначение,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов) ТОГДА
	|			ТаблицаМатериалы.Количество
	|		КОНЕЦ                                  КАК КоличествоИзЗаказов,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно) ТОГДА
	|			ТаблицаМатериалы.Количество
	|		КОНЕЦ                                  КАК КоличествоПодЗаказ,
	|
	|	ВЫБОР КОГДА ТаблицаМатериалы.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется) ТОГДА
	|			ТаблицаМатериалы.Количество
	|		КОНЕЦ                                  КАК КоличествоНеобеспечено,
	|
	|	&Распоряжение                              КАК Распоряжение
	|ИЗ
	|	Документ.КорректировкаЗаказаМатериаловВПроизводство.МатериалыИУслуги КАК ТаблицаМатериалы
	|
	|ГДЕ
	|	ТаблицаМатериалы.Ссылка = &Ссылка
	|
	|	И ТаблицаМатериалы.Номенклатура.ТипНоменклатуры В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	И ТаблицаМатериалы.ВариантОбеспечения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Требуется),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.ИзЗаказов),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Обособленно))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
		
	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы


#КонецОбласти

#КонецОбласти

#КонецЕсли
