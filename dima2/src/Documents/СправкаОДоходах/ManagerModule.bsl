
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//////////////////////////////////////////////////////////////////
/// Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Список перечислений

	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СправкаОДоходах";
	КомандаПечати.Представление = НСтр("ru='Справка о доходах';uk='Довідка про доходи'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Список перечислений в Microsoft Word
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СправкаОДоходах";
	КомандаПечати.Представление = НСтр("ru='Справка о доходах в Microsoft Word';uk='Довідка про доходи в Microsoft Word'");
	КомандаПечати.ФорматСохранения = ТипФайлаТабличногоДокумента.DOCX;
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
	// Реестр документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор  = "Реестр";
	КомандаПечати.Представление  = НСтр("ru='Реестр документов';uk='Реєстр документів'");
	КомандаПечати.ЗаголовокФормы = НСтр("ru='Реестр документов ""Справка о доходах""';uk='Реєстр документів ""Довідка про доходи""'");
	КомандаПечати.Обработчик     = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечатиРеестраДокументов";
	КомандаПечати.СписокФорм     = "ФормаСписка";
	КомандаПечати.Порядок        = 100;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если МассивОбъектов[0].ВидСправки = Перечисления.ВидыСправокОДоходах.Произвольная Тогда
		КоллекцияПечатныхФорм[0].ИмяМакета = "МакетПроизвольная"; 
		КоллекцияПечатныхФорм[0].ИмяВРЕГ = "МАКЕТПРОИЗВОЛЬНАЯ";
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПроизвольная") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МакетПроизвольная", НСтр("ru='Справка о доходах';uk='Довідка про доходи'"), ПечатьПроизвольная(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),,,,Истина);
		КонецЕсли;
	ИначеЕсли МассивОбъектов[0].ВидСправки = Перечисления.ВидыСправокОДоходах.ДоходыИНалоги Тогда
		КоллекцияПечатныхФорм[0].ИмяМакета = "МакетДоходыИНалоги"; 
		КоллекцияПечатныхФорм[0].ИмяВРЕГ = "МАКЕТДОХОДЫИНАЛОГИ";
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетДоходыИНалоги") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МакетДоходыИНалоги", НСтр("ru='Справка о доходах';uk='Довідка про доходи'"), ПечатьДОходыИНалоги(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),,,,Истина);
		КонецЕсли;
	ИначеЕсли МассивОбъектов[0].ВидСправки = Перечисления.ВидыСправокОДоходах.Субсидия2015 Тогда
		КоллекцияПечатныхФорм[0].ИмяМакета = "МакетСубсидия2015"; 
		КоллекцияПечатныхФорм[0].ИмяВРЕГ = "МАКЕТСУБСИДИЯ2015";
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетСубсидия2015") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МакетСубсидия2015", НСтр("ru='Справка о доходах';uk='Довідка про доходи'"), ПечатьСубсидия2015(МассивОбъектов, ОбъектыПечати, ПараметрыВывода));
		КонецЕсли;	
	ИначеЕсли МассивОбъектов[0].ВидСправки = Перечисления.ВидыСправокОДоходах.Соцстрах2015 Тогда
		КоллекцияПечатныхФорм[0].ИмяМакета = "МакетСоцстрах2015"; 
		КоллекцияПечатныхФорм[0].ИмяВРЕГ = "МАКЕТСОЦСТРАХ2015";
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетСоцстрах2015") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МакетСоцстрах2015", НСтр("ru='Справка о доходах';uk='Довідка про доходи'"), ПечатьСоцстрах2015(МассивОбъектов, ОбъектыПечати, ПараметрыВывода));
		КонецЕсли;	
	ИначеЕсли МассивОбъектов[0].ВидСправки = Перечисления.ВидыСправокОДоходах.Пенсия2015 Тогда
		КоллекцияПечатныхФорм[0].ИмяМакета = "МакетПенсия2015"; 
		КоллекцияПечатныхФорм[0].ИмяВРЕГ = "МАКЕТПЕНСИЯ2015";
		Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "МакетПенсия2015") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "МакетПенсия2015", НСтр("ru='Справка о доходах';uk='Довідка про доходи'"), ПечатьПенсия2015(МассивОбъектов, ОбъектыПечати, ПараметрыВывода));
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПечатьПроизвольная(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;   
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОДоходах_МакетПроизвольная";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СправкаОДоходах.ПФ_MXL_МакетПроизвольная", ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм);
	
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Организация = Ссылка.Организация;
		Сотрудник = Ссылка.Сотрудник;
		Дата = Ссылка.Дата;
		Номер = Ссылка.Номер;
		НазначениеСправки = Ссылка.НазначениеСправки;
		Доходы = Ссылка.Доходы;
		
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
				
		СписокСотрудников = Новый СписокЗначений;
		СписокСотрудников.Добавить(Сотрудник);
		СтрокаКадровыеДанные = "ДатаПриема, ДатаУвольнения, Должность";
		ДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокСотрудников, СтрокаКадровыеДанные, Дата);
		
		Если НЕ ДанныеСотрудника[0].ДатаУвольнения = Дата('00010101') Тогда
			Дата = ДанныеСотрудника[0].ДатаУвольнения;	
		КонецЕсли;	
		
		СписокФизическихЛиц = Новый СписокЗначений;
		СписокФизическихЛиц.Добавить(Сотрудник.ФизическоеЛицо);
		
		ТаблицаКадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина, 
		СписокФизическихЛиц, 
		"Фамилия, Имя, Отчество,", 
		Дата);
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);

		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МИНИМУМ(Доходы.Период) КАК НачалоПериодаДоходов,
		|	МАКСИМУМ(КОНЕЦПЕРИОДА(Доходы.Период,МЕСЯЦ)) КАК ОкончаниеПериодаДоходов
		|ИЗ
		|	Документ.СправкаОДоходах.Доходы КАК Доходы
		|ГДЕ
		|	Доходы.Ссылка = &Ссылка";
		
		ЗапросПериодДоходов = Новый Запрос(ТекстЗапроса);
		ЗапросПериодДоходов.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатПериодДоходов = ЗапросПериодДоходов.Выполнить().Выбрать();
		Если РезультатПериодДоходов.Следующий() Тогда
			НачалоПериодаДоходов = ?(РезультатПериодДоходов.НачалоПериодаДоходов = NULL, Ссылка.ДатаНач, РезультатПериодДоходов.НачалоПериодаДоходов);
			ОкончаниеПериодаДоходов = ?(РезультатПериодДоходов.ОкончаниеПериодаДоходов = NULL, Ссылка.ДатаКон, РезультатПериодДоходов.ОкончаниеПериодаДоходов);
		Иначе
			НачалоПериодаДоходов = Ссылка.ДатаНач;
			ОкончаниеПериодаДоходов = Ссылка.ДатаКон;
		КонецЕсли;	
		
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакетаШапка.Параметры.Физлицо = ТаблицаКадровыеДанныеФизическихЛиц[0].Фамилия +" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Имя+" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Отчество;
		
		Если Сотрудник.ФизическоеЛицо.Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
			ОбластьМакетаШапка.Параметры.он                     =НСтр("ru='она';uk='вона'",КодЯзыкаПечать);
			ТекстРаботал = НСтр("ru='работала';uk='працювала'",КодЯзыкаПечать);
		Иначе
			ОбластьМакетаШапка.Параметры.он                     =НСтр("ru='он';uk='він'",КодЯзыкаПечать);
			ТекстРаботал = НСтр("ru='работал';uk='працював'",КодЯзыкаПечать);
		КонецЕсли;	
		
		ТекстДоговор = "";
		
		Если ДанныеСотрудника[0].ДатаУвольнения = Дата('00010101') Тогда
			ТекстРаботает = НСтр("ru='работает в ';uk='працює в '",КодЯзыкаПечать) + Организация.НаименованиеПолное +НСтр("ru=' с ';uk=' з '",КодЯзыкаПечать) +
			Формат(ДанныеСотрудника[0].ДатаПриема, "ДЛФ=Д")+НСтр("ru='г.';uk='р.'",КодЯзыкаПечать) + ТекстДоговор + НСтр("ru='. В настоящий момент занимает должность ';uk='. На даний момент займає посаду '",КодЯзыкаПечать) + ДанныеСотрудника[0].Должность;
		Иначе
			ТекстРаботает = ТекстРаботал + " в  "+Организация.НаименованиеПолное +НСтр("ru=' с ';uk=' з '",КодЯзыкаПечать) +
			Формат(ДанныеСотрудника[0].ДатаПриема, "ДЛФ=Д")+ НСтр("ru='г.';uk='р.'",КодЯзыкаПечать) + " по " + Формат(ДанныеСотрудника[0].ДатаУвольнения, "ДЛФ=Д")+ НСтр("ru='г.';uk='р.'",КодЯзыкаПечать)  + ТекстДоговор + НСтр("ru=' на должности ';uk=' на посаді '",КодЯзыкаПечать) + ДанныеСотрудника[0].Должность;
		КонецЕсли;
		ОбластьМакетаШапка.Параметры.Номер						= Номер;
		
		ОбластьМакетаШапка.Параметры.Дата						= Формат(Дата, "ДЛФ=Д");
		
		ОбластьМакетаШапка.Параметры.ИдентификационныйКод		= Сотрудник.ФизическоеЛицо.КодПоДРФО;
		ОбластьМакетаШапка.Параметры.ТекстРаботает				= ТекстРаботает;
		
		ОбластьМакетаШапка.Параметры.СуммаДоход	= Формат(Доходы.Итог("СовокупныйДоход"),	"ЧЦ=10; ЧДЦ=2");
		
		ОбластьМакетаШапка.Параметры.ДатаНач		= Формат(МАКС(ДанныеСотрудника[0].ДатаПриема,НачалоПериодаДоходов), "ДЛФ=Д");
		Если ДанныеСотрудника[0].ДатаУвольнения = Дата('00010101') Тогда
			ОбластьМакетаШапка.Параметры.ДатаКон			= Формат(ОкончаниеПериодаДоходов, "ДЛФ=Д"); 
		Иначе
			ОбластьМакетаШапка.Параметры.ДатаКон			= Формат(?(НЕ ЗначениеЗаполнено(Формат(ДанныеСотрудника[0].ДатаУвольнения, "ДЛФ=Д")),ОкончаниеПериодаДоходов,МИН(ДанныеСотрудника[0].ДатаУвольнения,ОкончаниеПериодаДоходов)), "ДЛФ=Д");
		КонецЕсли;	

		
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаПериод"); // строка периода
		Для Каждого СтрокаТЧ Из Доходы Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаТЧ);
			ОбластьМакета.Параметры.Период			= Формат(СтрокаТЧ.Период, "Л="+КодЯзыкаПечать+"; ДФ='ММММ ` гггг'");
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;			
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаИтого");
		ОбластьМакета.Параметры.СовокупныйДоход	= Формат(Доходы.Итог("СовокупныйДоход"),	"ЧЦ=10; ЧДЦ=2");
		ОбластьМакета.Параметры.Удержания		= Формат(Доходы.Итог("Удержания"),			"ЧЦ=10; ЧДЦ=2");
		ОбластьМакета.Параметры.СуммаКВыплате	= Формат(Доходы.Итог("СуммаКВыплате"),		"ЧЦ=10; ЧДЦ=2");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Дно");
		ОбластьМакета.Параметры.НазначениеСправки	= НазначениеСправки;
		ОбластьМакета.Параметры.ГлавныйБухгалтер	= ОтветственныеЛица.ГлавныйБухгалтер;
		ОбластьМакета.Параметры.Директор			= ОтветственныеЛица.Руководитель;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции 	

Функция ПечатьДоходыИНалоги(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;   
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОДоходах_МакетДоходыИНалоги";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СправкаОДоходах.ПФ_MXL_МакетДоходыИНалоги", ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм);
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Организация = Ссылка.Организация;
		Сотрудник = Ссылка.Сотрудник;
		Дата = Ссылка.Дата;
		Номер = Ссылка.Номер;
		НазначениеСправки = Ссылка.НазначениеСправки;
		Доходы = Ссылка.Доходы;
		
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		СписокСотрудников = Новый СписокЗначений;
		СписокСотрудников.Добавить(Сотрудник);
		СтрокаКадровыеДанные = "ДатаПриема, ДатаУвольнения, Должность";
		ДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокСотрудников, СтрокаКадровыеДанные, Дата);
		
		Если НЕ ДанныеСотрудника[0].ДатаУвольнения = Дата('00010101') Тогда
			Дата = ДанныеСотрудника[0].ДатаУвольнения;	
		КонецЕсли;	
		
		СписокФизическихЛиц = Новый СписокЗначений;
		СписокФизическихЛиц.Добавить(Сотрудник.ФизическоеЛицо);
		
		ТаблицаКадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина, 
		СписокФизическихЛиц, 
		"Фамилия, Имя, Отчество,", 
		Дата);

		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МИНИМУМ(Доходы.Период) КАК НачалоПериодаДоходов,
		|	МАКСИМУМ(КОНЕЦПЕРИОДА(Доходы.Период,МЕСЯЦ)) КАК ОкончаниеПериодаДоходов
		|ИЗ
		|	Документ.СправкаОДоходах.Доходы КАК Доходы
		|ГДЕ
		|	Доходы.Ссылка = &Ссылка";
		
		ЗапросПериодДоходов = Новый Запрос(ТекстЗапроса);
		ЗапросПериодДоходов.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатПериодДоходов = ЗапросПериодДоходов.Выполнить().Выбрать();
		Если РезультатПериодДоходов.Следующий() Тогда
			НачалоПериодаДоходов = ?(РезультатПериодДоходов.НачалоПериодаДоходов = NULL, Ссылка.ДатаНач, РезультатПериодДоходов.НачалоПериодаДоходов);
			ОкончаниеПериодаДоходов = ?(РезультатПериодДоходов.ОкончаниеПериодаДоходов = NULL, Ссылка.ДатаКон, РезультатПериодДоходов.ОкончаниеПериодаДоходов);
		Иначе
			НачалоПериодаДоходов = Ссылка.ДатаНач;
			ОкончаниеПериодаДоходов = Ссылка.ДатаКон;
		КонецЕсли;
		
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакетаШапка.Параметры.Физлицо = ТаблицаКадровыеДанныеФизическихЛиц[0].Фамилия +" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Имя+" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Отчество;
		
		Если  Сотрудник.ФизическоеЛицо.Пол = Перечисления.ПолФизическогоЛица.Женский Тогда
			ОбластьМакетаШапка.Параметры.он                     =НСтр("ru='она';uk='вона'",КодЯзыкаПечать);
			ТекстРаботал = НСтр("ru='работала';uk='працювала'",КодЯзыкаПечать);
		Иначе
			ОбластьМакетаШапка.Параметры.он                     =НСтр("ru='он';uk='він'",КодЯзыкаПечать);
			ТекстРаботал = НСтр("ru='работал';uk='працював'",КодЯзыкаПечать);
		КонецЕсли;	
		
		ТекстДоговор = "";
		
		Если ДанныеСотрудника[0].ДатаУвольнения = Дата('00010101') Тогда
			ТекстРаботает = НСтр("ru='работает в ';uk='працює в '",КодЯзыкаПечать) + Организация.НаименованиеПолное +НСтр("ru=' с ';uk=' з '",КодЯзыкаПечать) +
			Формат(ДанныеСотрудника[0].ДатаПриема, "ДЛФ=Д")+НСтр("ru='г.';uk='р.'",КодЯзыкаПечать) + ТекстДоговор + НСтр("ru='. В настоящий момент занимает должность ';uk='. На даний момент займає посаду '",КодЯзыкаПечать) + ДанныеСотрудника[0].Должность;
		Иначе
			ТекстРаботает = ТекстРаботал + " в  "+Организация.НаименованиеПолное +НСтр("ru=' с ';uk=' з '",КодЯзыкаПечать) +
			Формат(ДанныеСотрудника[0].ДатаПриема, "ДЛФ=Д")+ НСтр("ru='г.';uk='р.'",КодЯзыкаПечать) + " по " + Формат(ДанныеСотрудника[0].ДатаУвольнения, "ДЛФ=Д")+ НСтр("ru='г.';uk='р.'",КодЯзыкаПечать)  + ТекстДоговор + НСтр("ru=' на должности ';uk=' на посаді '",КодЯзыкаПечать) + ДанныеСотрудника[0].Должность;
		КонецЕсли;
		ОбластьМакетаШапка.Параметры.Номер						= Номер;
		
		ОбластьМакетаШапка.Параметры.Дата						= Формат(Дата, "ДЛФ=Д");
		
		ОбластьМакетаШапка.Параметры.ИдентификационныйКод		= Сотрудник.ФизическоеЛицо.КодПоДРФО;
		ОбластьМакетаШапка.Параметры.ТекстРаботает				= ТекстРаботает;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаПериод"); 
		
		Для Каждого СтрокаТЧ Из Доходы Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаТЧ);
			ОбластьМакета.Параметры.Период			= Формат(СтрокаТЧ.Период, "Л="+КодЯзыкаПечать+"; ДФ='ММММ ` гггг'");
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;		
		
		
		ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
		ТаблицаДоходы = Доходы.Выгрузить();
		
		ОбластьМакета.Параметры.ДатаНач			= Формат(МАКС(ДанныеСотрудника[0].ДатаПриема,НачалоПериодаДоходов), "ДЛФ=Д");
		Если ДанныеСотрудника[0].ДатаУвольнения = Дата('00010101') Тогда
			ОбластьМакета.Параметры.ДатаКон			= Формат(ОкончаниеПериодаДоходов, "ДЛФ=Д"); 
		Иначе
			ОбластьМакета.Параметры.ДатаКон			= Формат(?(НЕ ЗначениеЗаполнено(Формат(ДанныеСотрудника[0].ДатаУвольнения, "ДЛФ=Д")),ОкончаниеПериодаДоходов,МИН(ДанныеСотрудника[0].ДатаУвольнения,ОкончаниеПериодаДоходов)), "ДЛФ=Д");
		КонецЕсли;	
		
		ОбластьМакета.Параметры.ДоходНДФЛИтог = ТаблицаДоходы.Итог("ДоходНДФЛ");
		ОбластьМакета.Параметры.НДФЛИтог = ТаблицаДоходы.Итог("НДФЛ");
		ОбластьМакета.Параметры.ВыплаченоИтог = ТаблицаДоходы.Итог("Выплачено");
		ОбластьМакета.Параметры.ДоходВоенныйСборИтог = ТаблицаДоходы.Итог("ДоходВоенныйСбор");
		ОбластьМакета.Параметры.ВоенныйСборИтог = ТаблицаДоходы.Итог("ВоенныйСбор");
		ОбластьМакета.Параметры.ДоходЗаВычетомНДФЛИтог = ТаблицаДоходы.Итог("ДоходЗаВычетомНДФЛ");

		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Дно");
		ОбластьМакета.Параметры.НазначениеСправки	= НазначениеСправки;
		ОбластьМакета.Параметры.ГлавныйБухгалтер	= ОтветственныеЛица.ГлавныйБухгалтер;
		ОбластьМакета.Параметры.Директор			= ОтветственныеЛица.Руководитель;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатьПроизвольная	

Функция ПечатьСубсидия2015(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;   
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОДоходах_МакетСубсидия2015";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СправкаОДоходах.ПФ_MXL_UK_МакетСубсидия2015");
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Организация = Ссылка.Организация;
		Сотрудник = Ссылка.Сотрудник;
		Дата = Ссылка.Дата;
		Номер = Ссылка.Номер;
		Доходы = Ссылка.Доходы;
		
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		СписокСотрудников = Новый СписокЗначений;
		СписокСотрудников.Добавить(Сотрудник);
		СтрокаКадровыеДанные = "Должность, ВидЗанятости";
		ДанныеСотрудника = КадровыйУчет.КадровыеДанныеСотрудников(Истина, СписокСотрудников, СтрокаКадровыеДанные, Дата);
		
		СписокФизическихЛиц = Новый СписокЗначений;
		СписокФизическихЛиц.Добавить(Сотрудник.ФизическоеЛицо);
		
		ТаблицаКадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина, 
		СписокФизическихЛиц, 
		"Фамилия, Имя, Отчество, КодПоДРФО", 
		Дата);
		
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакетаШапка.Параметры.Физлицо = ТаблицаКадровыеДанныеФизическихЛиц[0].Фамилия +" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Имя+" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Отчество;
		ОбластьМакетаШапка.Параметры.Должность = ДанныеСотрудника[0].Должность;
		Если ДанныеСотрудника[0].ВидЗанятости = Перечисления.ВидыЗанятости.ОсновноеМестоРаботы Тогда
			ОбластьМакетаШапка.Параметры.ВидЗанятости = "основне";
		Иначе
			ОбластьМакетаШапка.Параметры.ВидЗанятости = "за сумісництвом";
		КонецЕсли;	
		
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);

		ОбластьМакетаШапка.Параметры.ФЛП = ?(Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо,СведенияОбОрганизации.ПолноеНаименование,"");
		ОбластьМакетаШапка.Параметры.Организация = ?(Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо,СведенияОбОрганизации.ПолноеНаименование,"");
		ОбластьМакетаШапка.Параметры.Организация_ФЛП = СведенияОбОрганизации.ПолноеНаименование;
		ОбластьМакетаШапка.Параметры.Адрес = СведенияОбОрганизации.ЮридическийАдрес;
		ОбластьМакетаШапка.Параметры.Телефон = СведенияОбОрганизации.Телефоны;
		ОбластьМакетаШапка.Параметры.ЕДРПОУ = ?(Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо,СведенияОбОрганизации.КодПоЕДРПОУ,СведенияОбОрганизации.КодПоДРФО);
		
		ОбластьМакетаШапка.Параметры.ДРФО			= ТаблицаКадровыеДанныеФизическихЛиц[0].КодПоДРФО;
			
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		Для каждого СтрокаТЧ Из Доходы Цикл
			ОбластьМакета.Параметры.Заполнить(СтрокаТЧ);
			ОбластьМакета.Параметры.Период			= Формат(СтрокаТЧ.Период,	"ДФ=""ММММ ` гггг""; Л=""uk""");
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;		
		
		//Итоги
		ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
		ОбластьМакета.Параметры.НачисленоЗП	= Доходы.Итог("НачисленоЗП");
		ОбластьМакета.Параметры.НачисленоПрочее	= Доходы.Итог("НачисленоПрочее");
		ОбластьМакета.Параметры.Алименты	= Доходы.Итог("Алименты");
		ОбластьМакета.Параметры.НДФЛ	= Доходы.Итог("НДФЛ");
		ОбластьМакета.Параметры.НДФЛПрочее	= Доходы.Итог("НДФЛПрочее");
	    ТабличныйДокумент.Вывести(ОбластьМакета);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Дно");
		Результат = Доходы.Итог("НачисленоЗП")+Доходы.Итог("НачисленоПрочее")-Доходы.Итог("Алименты")-Доходы.Итог("НДФЛ")-Доходы.Итог("НДФЛПрочее");
		Если Доходы.Количество() > 0 Тогда 
			ОбластьМакета.Параметры.ПериодС = Формат(Доходы[0].Период,	"ДФ=""ММММ гггг""; Л=""uk""");
			ОбластьМакета.Параметры.ПериодПо = Формат(СтрокаТЧ.Период,	"ДФ=""ММММ гггг""; Л=""uk""");
		КонецЕсли;	
		ОбластьМакета.Параметры.ДолжностьРуководителя	= ОтветственныеЛица.РуководительДолжность;
		ОбластьМакета.Параметры.Дата	= Дата;
		ОбластьМакета.Параметры.Номер	= Номер;
		ОбластьМакета.Параметры.Результат	= РаботаСКурсамиВалют.СформироватьСуммуПрописью(Результат, Константы.ВалютаРегламентированногоУчета.Получить(),, "uk");
		ОбластьМакета.Параметры.ГлавныйБухгалтер	= ОтветственныеЛица.ГлавныйБухгалтер;
		ОбластьМакета.Параметры.Руководитель			= ОтветственныеЛица.Руководитель;

		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции 	

Функция ПечатьСоцстрах2015(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;   
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОДоходах_МакетСоцстрах2015";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СправкаОДоходах.ПФ_MXL_UK_МакетСоцстрах2015");
	
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Организация = Ссылка.Организация;
		Сотрудник = Ссылка.Сотрудник;
		Дата = Ссылка.Дата;
		Номер = Ссылка.Номер;
		Доходы = Ссылка.Доходы;
		
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		СписокФизическихЛиц = Новый СписокЗначений;
		СписокФизическихЛиц.Добавить(Сотрудник.ФизическоеЛицо);
		
		ТаблицаКадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина, 
		СписокФизическихЛиц, 
		"Фамилия, Имя, Отчество, КодПоДРФО", 
		Дата);
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакетаШапка.Параметры.Физлицо = ТаблицаКадровыеДанныеФизическихЛиц[0].Фамилия +" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Имя+" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Отчество;
	
		СведенияОбОрганизации = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(Организация, Дата);
		ОбластьМакетаШапка.Параметры.Организация = СведенияОбОрганизации.ПолноеНаименование;
	
		ОбластьМакетаШапка.Параметры.Номер			= Номер;
		ОбластьМакетаШапка.Параметры.Дата			= Формат(Дата, "ДЛФ=Д");
		ОбластьМакетаШапка.Параметры.ДРФО			= ТаблицаКадровыеДанныеФизическихЛиц[0].КодПоДРФО;
			
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Строка");
		
		Для каждого СтрокаТЧ Из Доходы Цикл
			ОбластьМакета.Параметры.Период			= Формат(СтрокаТЧ.Период,	"ДФ=""ММММ ` гггг""; Л=""uk""");
			ОбластьМакета.Параметры.КалендарныеДни	= СтрокаТЧ.КалендарныеДни;
			ОбластьМакета.Параметры.РезультатВсего	= СтрокаТЧ.РезультатВсего;
			ОбластьМакета.Параметры.РезультатВзносы	= СтрокаТЧ.РезультатВзносы;
			ОбластьМакета.Параметры.СтавкаВзносов	= СтрокаТЧ.СтавкаВзносов;
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;	
		
		ОбластьМакета = Макет.ПолучитьОбласть("Итоги");
		ОбластьМакета.Параметры.КалендарныеДни	= Доходы.Итог("КалендарныеДни");
		ОбластьМакета.Параметры.РезультатВсего	= Доходы.Итог("РезультатВсего");
		ОбластьМакета.Параметры.РезультатВзносы	= Доходы.Итог("РезультатВзносы");
		ТабличныйДокумент.Вывести(ОбластьМакета);
	
		ОбластьМакета = Макет.ПолучитьОбласть("Дно");
		
		Неявки = СписокНеявокСоцстрах2015(Ссылка);

		Инд =1;
		Для Каждого Неявка из Неявки Цикл
			ОбластьМакета.Параметры["Причина"+Строка(Инд)] = Неявка.Наименование;
			Инд = Инд + 1;
			Если Инд > 4 Тогда
				Прервать;
			КонецЕсли;	
		КонецЦикла;	

	
		ОбластьМакета.Параметры.ГлавныйБухгалтер	= ОтветственныеЛица.ГлавныйБухгалтер;
		ОбластьМакета.Параметры.Руководитель			= ОтветственныеЛица.Руководитель;

		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;

КонецФункции 	

Функция ПечатьПенсия2015(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;   
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СправкаОДоходах_МакетПенсия2015";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СправкаОДоходах.ПФ_MXL_UK_МакетПенсия2015");
	
	
	Для Каждого Ссылка Из МассивОбъектов Цикл
		
		Организация = Ссылка.Организация;
		Сотрудник = Ссылка.Сотрудник;
		Дата = Ссылка.Дата;
		Номер = Ссылка.Номер;
		Доходы = Ссылка.Доходы;
		
		ТабличныйДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
		
		СписокФизическихЛиц = Новый СписокЗначений;
		СписокФизическихЛиц.Добавить(Сотрудник.ФизическоеЛицо);
		
		ТаблицаКадровыеДанныеФизическихЛиц = КадровыйУчет.КадровыеДанныеФизическихЛиц(
		Истина, 
		СписокФизическихЛиц, 
		"Фамилия, Имя, Отчество, КодПоДРФО", 
		Дата);
		ОтветственныеЛица = ОтветственныеЛицаБП.ОтветственныеЛица(Организация, Дата);
		
		КодЯзыкаПечать = ПараметрыВывода.КодЯзыкаДляМногоязычныхПечатныхФорм;
		Макет.КодЯзыкаМакета = КодЯзыкаПечать;
		
		ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакетаШапка.Параметры.Физлицо = ТаблицаКадровыеДанныеФизическихЛиц[0].Фамилия +" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Имя+" "+ТаблицаКадровыеДанныеФизическихЛиц[0].Отчество;
		Результат = Доходы.Итог("РезультатВсего");
		ОбластьМакетаШапка.Параметры.Результат	= РаботаСКурсамиВалют.СформироватьСуммуПрописью(Результат, Константы.ВалютаРегламентированногоУчета.Получить(),, "uk");
		
		ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Таблица|ПерваяКолонка");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		Год = 0;
		Итог = 0;
		ОбластьКолонка = Макет.ПолучитьОбласть("Таблица|Колонка");
		ТЧДоходы = Доходы.Выгрузить();
		ТЧДоходы.Сортировать("Период");
		Для каждого СтрокаТЧ Из ТЧДоходы Цикл
			Если Год(СтрокаТЧ.Период) <> Год Тогда
				Если Год > 0 Тогда
					ОбластьКолонка.Параметры.Итог = Итог;
					ТабличныйДокумент.Присоединить(ОбластьКолонка);
				КонецЕсли;
				Год = Год(СтрокаТЧ.Период);
				Итог = 0;
				ОбластьКолонка = Макет.ПолучитьОбласть("Таблица|Колонка");
				ОбластьКолонка.Параметры.Год = Строка(Год)+" р.";
				ОбластьКолонка.Параметры.Январь = 0;
				ОбластьКолонка.Параметры.Февраль = 0;
				ОбластьКолонка.Параметры.Март = 0;
				ОбластьКолонка.Параметры.Апрель = 0;
				ОбластьКолонка.Параметры.Май = 0;
				ОбластьКолонка.Параметры.Июнь = 0;
				ОбластьКолонка.Параметры.Июль = 0;
				ОбластьКолонка.Параметры.Август = 0;
				ОбластьКолонка.Параметры.Сентябрь = 0;
				ОбластьКолонка.Параметры.Октябрь = 0;
				ОбластьКолонка.Параметры.Ноябрь = 0;
				ОбластьКолонка.Параметры.Декабрь = 0;
			КонецЕсли;	
			Если Месяц(СтрокаТЧ.Период) = 1 Тогда
				ОбластьКолонка.Параметры.Январь = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 2 Тогда
				ОбластьКолонка.Параметры.Февраль = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 3 Тогда
				ОбластьКолонка.Параметры.Март = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 4 Тогда
				ОбластьКолонка.Параметры.Апрель = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 5 Тогда
				ОбластьКолонка.Параметры.Май = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 6 Тогда
				ОбластьКолонка.Параметры.Июнь = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 7 Тогда
				ОбластьКолонка.Параметры.Июль = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 8 Тогда
				ОбластьКолонка.Параметры.Август = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 9 Тогда
				ОбластьКолонка.Параметры.Сентябрь = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 10 Тогда
				ОбластьКолонка.Параметры.Октябрь = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 11 Тогда
				ОбластьКолонка.Параметры.Ноябрь = СтрокаТЧ.РезультатВсего;
			ИначеЕсли Месяц(СтрокаТЧ.Период) = 12 Тогда
				ОбластьКолонка.Параметры.Декабрь = СтрокаТЧ.РезультатВсего;
			КонецЕсли;	
			Итог = Итог + СтрокаТЧ.РезультатВсего;	
		КонецЦикла;
		
		ОбластьКолонка.Параметры.Итог = Итог;
		ТабличныйДокумент.Присоединить(ОбластьКолонка);
		
		ОбластьМакета = Макет.ПолучитьОбласть("Дно");
		
		ОбластьМакета.Параметры.ГлавныйБухгалтер	= ОтветственныеЛица.ГлавныйБухгалтер;
		ОбластьМакета.Параметры.Руководитель			= ОтветственныеЛица.Руководитель;
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции 	

Функция СписокНеявокСоцстрах2015(Ссылка)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
     |	ДанныеУчетаВремени.ВидБолезниУходаЗаДетьми.Наименование КАК Наименование,
	 |	ДанныеУчетаВремени.ВидБолезниУходаЗаДетьми.КатегорияНачисленияИлиНеоплаченногоВремени КАК Категория
	 |ИЗ
	 |	РегистрСведений.ДанныеОВремениДляРасчетаСреднегоФСС КАК ДанныеУчетаВремени
	 |ГДЕ
	 |	ДанныеУчетаВремени.ФизическоеЛицо = &парамФизическоеЛицо
     |	И ДанныеУчетаВремени.Месяц МЕЖДУ &парамДатаНач И &парамДатаКон
     |	И ДанныеУчетаВремени.ГоловнаяОрганизация = &парамОрганизация
     |";
						 
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("парамОрганизация", Ссылка.Организация);
	Запрос.УстановитьПараметр("парамФизическоеЛицо", Ссылка.Сотрудник.ФизическоеЛицо);
	Запрос.УстановитьПараметр("парамДатаНач", Ссылка.ДатаНач);
	Запрос.УстановитьПараметр("парамДатаКон", Ссылка.ДатаКон);
	
	Неявки = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Неявка из Неявки Цикл
		Если Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛиста
		 ИЛИ Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоЛистаЗаСчетРаботодателя
		 ИЛИ Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоНесчастныйСлучайНаПроизводстве
		 ИЛИ Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОплатаБольничногоПрофзаболевание
		 ИЛИ Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.БолезньБезОплаты Тогда
			 Неявка.Наименование = "Тимчасова непрацездатність";
		 ИначеЕсли Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодам
			   ИЛИ Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ОтпускПоБеременностиИРодамБезОплаты Тогда
			Неявка.Наименование = "Відпустка у зв’язку з вагітністю та пологами";
		 ИначеЕсли Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоТрехЛет Тогда
			Неявка.Наименование = "Відпустка по догляду за дитиною до досягнення нею трирічного віку";
		 ИначеЕсли Неявка.Категория = Перечисления.КатегорииНачисленийИНеоплаченногоВремени.ПособиеПоУходуЗаРебенкомДоПолутораЛет Тогда
			Неявка.Наименование = "Відпустка по догляду за дитиною до досягнення нею шестирічного віку";
		 Иначе
			Неявка.Наименование = "Відпустка без збереження заробітної плати";
		КонецЕсли;	 
	КонецЦикла;	
	
	Неявки.Свернуть("Наименование");
	
	Возврат Неявки;
	
КонецФункции




#КонецЕсли