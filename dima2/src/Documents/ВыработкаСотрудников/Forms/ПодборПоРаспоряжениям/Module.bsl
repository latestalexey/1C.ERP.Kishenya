
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	АдресВХранилище = Параметры.АдресВХранилище;
	Организация = Параметры.Организация;
	Подразделение = Параметры.Подразделение;
	Бригада = Параметры.Бригада;
	ВидНаряда = Параметры.ВидНаряда;
	Ссылка = Параметры.Ссылка;
	
	ПодборТоваровКлиентСервер.СформироватьЗаголовокФормыПодбора(Заголовок, Ссылка);
	
	ПараметрыОтбора = ХранилищеНастроекДанныхФорм.Загрузить("ПодборПоРаспоряжениямВБригадныеНаряды", "ПараметрыОтбора");
	Если ЗначениеЗаполнено(ПараметрыОтбора) И ВидНаряда = Перечисления.ВидыБригадныхНарядов.Производство Тогда
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ПараметрыОтбора);
	КонецЕсли;
	
	//++ НЕ УТКА
	
	ОпределитьУправлениеМаршрутнымиЛистами();
	
	ЗаполнитьТаблицу();
	
	//-- НЕ УТКА
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборРабочийЦентрПриИзменении(Элемент)
	
	//++ НЕ УТКА
	ОтборРабочийЦентрПриИзмененииНаСервере();
	//-- НЕ УТКА
	
	Возврат; // в КА обработчик пустой
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокументВыполнить()
	
	ПоместитьТаблицуВХранилище(ЭтаФорма.ВладелецФормы.УникальныйИдентификатор);
	ВыбранноеЗначение = Новый Структура("АдресВХранилище", АдресВХранилище);
	
	ОповеститьОВыборе(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСтрокиВыполнить()
	
	ОтметитьСтроки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьСтрокиВыполнить()
	
	ОтметитьСтроки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВыделенныеСтроки(Команда)
	
	МассивСтрок = Элементы.Таблица.ВыделенныеСтроки;
	ОтметитьСтроки(Истина, МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсключитьВыделенныеСтроки(Команда)
	
	МассивСтрок = Элементы.Таблица.ВыделенныеСтроки;
	ОтметитьСтроки(Ложь, МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаСпособОтражения.Имя);


	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидНаряда");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыБригадныхНарядов.Производство;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаСтатьяКалькуляции.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ВидНаряда");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВидыБригадныхНарядов.Ремонт;

	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

КонецПроцедуры

//++ НЕ УТКА

&НаСервере
Процедура ОтборРабочийЦентрПриИзмененииНаСервере()
	
	ПараметрыОтбора = Новый Структура("ОтборРабочийЦентр");
	ЗаполнитьЗначенияСвойств(ПараметрыОтбора, ЭтаФорма);
	ХранилищеНастроекДанныхФорм.Сохранить("ПодборПоРаспоряжениямВБригадныеНаряды", "ПараметрыОтбора", ПараметрыОтбора);
	
	ЗаполнитьТаблицу();
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьУправлениеМаршрутнымиЛистами()

	Если НЕ Подразделение.Пустая() Тогда
		ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение);
		УправлениеМаршрутнымиЛистами = ПараметрыПодразделения.УправлениеМаршрутнымиЛистами;
	Иначе
		УправлениеМаршрутнымиЛистами = Перечисления.УправлениеМаршрутнымиЛистами.ПустаяСсылка();
		ОтборРабочийЦентр = Справочники.РабочиеЦентры.ПустаяСсылка();
	КонецЕсли;

	ВидимостьОтбораПоРЦ = (НЕ Подразделение.Пустая() И ВидНаряда = Перечисления.ВидыБригадныхНарядов.Производство);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтборРабочийЦентр", "Видимость", ВидимостьОтбораПоРЦ);
	
КонецПроцедуры
//-- НЕ УТКА

&НаСервере
Процедура ПоместитьТаблицуВХранилище(Идентификатор)
	
	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		
		Если Не СтрокаТаблицы.Выбран Тогда
			МассивУдаляемыхСтрок.Добавить(СтрокаТаблицы);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		Таблица.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	АдресВХранилище = ПоместитьВоВременноеХранилище(Таблица.Выгрузить(), Идентификатор);
	
КонецПроцедуры

//++ НЕ УТКА
&НаСервере
Процедура ЗаполнитьТаблицу()
	
	Если ОтборРабочийЦентр.Пустая() Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТрудозатратыКОформлению.ВидРабот КАК ВидРабот,
		|	ТрудозатратыКОформлению.КоличествоОстаток КАК Количество,
		|	ТрудозатратыКОформлению.Распоряжение КАК Распоряжение,
		|	ТрудозатратыКОформлению.КодСтрокиРаспоряжения
		|ПОМЕСТИТЬ ВТПроизводственные
		|ИЗ
		|	РегистрНакопления.ТрудозатратыКОформлению.Остатки(
		|			,
		|			ВЫБОР
		|					КОГДА Распоряжение ССЫЛКА Документ.МаршрутныйЛистПроизводства
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Производство)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт)
		|				КОНЕЦ = &ВидНаряда
		|				И Бригада = &Бригада
		|				И Организация = &Организация) КАК ТрудозатратыКОформлению
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТрудозатратыКОформлению.ВидРабот,
		|	ТрудозатратыКОформлению.Количество,
		|	ТрудозатратыКОформлению.Распоряжение,
		|	ТрудозатратыКОформлению.КодСтрокиРаспоряжения
		|ИЗ
		|	РегистрНакопления.ТрудозатратыКОформлению КАК ТрудозатратыКОформлению
		|ГДЕ
		|	ТрудозатратыКОформлению.Регистратор = &Ссылка
		|	И ТрудозатратыКОформлению.Бригада = &Бригада
		|	И ТрудозатратыКОформлению.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТПроизводственные.ВидРабот КАК ВидРабот,
		|	СУММА(ВТПроизводственные.Количество) КАК Количество,
		|	ВТПроизводственные.Распоряжение КАК Распоряжение,
		|	ВТПроизводственные.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
		|	МаршрутныйЛистПроизводстваТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Неопределено КАК СпособОтраженияЗарплаты
		|ПОМЕСТИТЬ Производственные
		|ИЗ
		|	ВТПроизводственные КАК ВТПроизводственные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Трудозатраты КАК МаршрутныйЛистПроизводстваТрудозатраты
		|		ПО ВТПроизводственные.Распоряжение = МаршрутныйЛистПроизводстваТрудозатраты.Ссылка
		|			И ВТПроизводственные.КодСтрокиРаспоряжения = МаршрутныйЛистПроизводстваТрудозатраты.КодСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	МаршрутныйЛистПроизводстваТрудозатраты.СтатьяКалькуляции,
		|	ВТПроизводственные.Распоряжение,
		|	ВТПроизводственные.ВидРабот,
		|	ВТПроизводственные.КодСтрокиРаспоряжения
		|;";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	РабочиеЦентры.ВидРабочегоЦентра КАК ВидРабочегоЦентра
		|ПОМЕСТИТЬ РабочиеЦентры
		|ИЗ
		|	Справочник.РабочиеЦентры КАК РабочиеЦентры
		|ГДЕ
		|	РабочиеЦентры.Ссылка В ИЕРАРХИИ(&РабочийЦентр)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТрудозатратыКОформлению.ВидРабот КАК ВидРабот,
		|	ТрудозатратыКОформлению.КоличествоОстаток КАК Количество,
		|	ТрудозатратыКОформлению.Распоряжение КАК Распоряжение,
		|	ТрудозатратыКОформлению.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения
		|ПОМЕСТИТЬ ВТПроизводственные
		|ИЗ
		|	РегистрНакопления.ТрудозатратыКОформлению.Остатки(
		|			,
		|			ВЫБОР
		|					КОГДА Распоряжение ССЫЛКА Документ.МаршрутныйЛистПроизводства
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Производство)
		|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт)
		|				КОНЕЦ = &ВидНаряда
		|				И Бригада = &Бригада
		|				И Организация = &Организация) КАК ТрудозатратыКОформлению
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТрудозатратыКОформлению.ВидРабот,
		|	ТрудозатратыКОформлению.Количество,
		|	ТрудозатратыКОформлению.Распоряжение,
		|	ТрудозатратыКОформлению.КодСтрокиРаспоряжения
		|ИЗ
		|	РегистрНакопления.ТрудозатратыКОформлению КАК ТрудозатратыКОформлению
		|ГДЕ
		|	ТрудозатратыКОформлению.Регистратор = &Ссылка
		|	И ТрудозатратыКОформлению.Бригада = &Бригада
		|	И ТрудозатратыКОформлению.Организация = &Организация
		|;";
		
		Если УправлениеМаршрутнымиЛистами = Перечисления.УправлениеМаршрутнымиЛистами.МетодикаББВ Тогда
			
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВТПроизводственные.ВидРабот КАК ВидРабот,
			|	СУММА(ВТПроизводственные.Количество) КАК Количество,
			|	ВТПроизводственные.Распоряжение КАК Распоряжение,
			|	ВТПроизводственные.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения
			|ПОМЕСТИТЬ ВТОтбор
			|ИЗ
			|	ВТПроизводственные КАК ВТПроизводственные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВидыРабочихЦентров КАК МаршрутныйЛистПроизводстваВидыРабочихЦентров
			|		ПО ВТПроизводственные.Распоряжение = МаршрутныйЛистПроизводстваВидыРабочихЦентров.Ссылка
			|			И (МаршрутныйЛистПроизводстваВидыРабочихЦентров.ВидРабочегоЦентра В
			|				(ВЫБРАТЬ
			|					Т.ВидРабочегоЦентра
			|				ИЗ
			|					РабочиеЦентры КАК Т))
			|
			|СГРУППИРОВАТЬ ПО
			|	ВТПроизводственные.ВидРабот,
			|	ВТПроизводственные.Распоряжение,
			|	ВТПроизводственные.КодСтрокиРаспоряжения
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВТОтбор.ВидРабот КАК ВидРабот,
			|	ВТОтбор.Количество КАК Количество,
			|	ВТОтбор.Распоряжение КАК Распоряжение,
			|	ВТОтбор.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
			|	МаршрутныйЛистПроизводстваТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	Неопределено КАК СпособОтраженияЗарплаты
			|ПОМЕСТИТЬ Производственные
			|ИЗ
			|	ВТОтбор КАК ВТОтбор
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Трудозатраты КАК МаршрутныйЛистПроизводстваТрудозатраты
			|		ПО ВТОтбор.Распоряжение = МаршрутныйЛистПроизводстваТрудозатраты.Ссылка
			|			И ВТОтбор.КодСтрокиРаспоряжения = МаршрутныйЛистПроизводстваТрудозатраты.КодСтроки
			|;";
			
		Иначе
			
			ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВТПроизводственные.ВидРабот КАК ВидРабот,
			|	СУММА(ВТПроизводственные.Количество) КАК Количество,
			|	ВТПроизводственные.Распоряжение КАК Распоряжение,
			|	ВТПроизводственные.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
			|	МаршрутныйЛистПроизводстваТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
			|	Неопределено КАК СпособОтраженияЗарплаты
			|ПОМЕСТИТЬ Производственные
			|ИЗ
			|	ВТПроизводственные КАК ВТПроизводственные
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Трудозатраты КАК МаршрутныйЛистПроизводстваТрудозатраты
			|		ПО (МаршрутныйЛистПроизводстваТрудозатраты.Ссылка = ВТПроизводственные.Распоряжение)
			|			И (МаршрутныйЛистПроизводстваТрудозатраты.КодСтроки = ВТПроизводственные.КодСтрокиРаспоряжения)
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.Операции КАК МаршрутныйЛистПроизводстваОперации
			|		ПО (МаршрутныйЛистПроизводстваОперации.Ссылка = МаршрутныйЛистПроизводстваТрудозатраты.Ссылка)
			|			И (МаршрутныйЛистПроизводстваОперации.КлючСвязи = МаршрутныйЛистПроизводстваТрудозатраты.КлючСвязиОперации)
			|			И (МаршрутныйЛистПроизводстваОперации.ВидРабочегоЦентра В
			|				(ВЫБРАТЬ
			|					Т.ВидРабочегоЦентра
			|				ИЗ
			|					РабочиеЦентры КАК Т))
			|ГДЕ
			|	ВЫБОР
			|			КОГДА МаршрутныйЛистПроизводстваОперации.СтатусВыполнения <> ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.ПустаяСсылка)
			|				ТОГДА МаршрутныйЛистПроизводстваОперации.СтатусВыполнения
			|			КОГДА МаршрутныйЛистПроизводстваОперации.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Выполнен)
			|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.Завершено)
			|			КОГДА МаршрутныйЛистПроизводстваОперации.Ссылка.СостояниеРасписания = ЗНАЧЕНИЕ(Перечисление.СостоянияРасписанияРабочихЦентров.НеСформировано)
			|					ИЛИ МаршрутныйЛистПроизводстваОперации.Ссылка.СостояниеРасписания = ЗНАЧЕНИЕ(Перечисление.СостоянияРасписанияРабочихЦентров.НеАктуально)
			|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.НеЗапланировано)
			|			КОГДА МаршрутныйЛистПроизводстваОперации.Окончание <= &ОтметкаНаДату
			|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.Завершено)
			|			КОГДА МаршрутныйЛистПроизводстваОперации.Начало > &ОтметкаНаДату
			|				ТОГДА ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.НеНачато)
			|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.Начато)
			|		КОНЕЦ = ЗНАЧЕНИЕ(Перечисление.СтатусыВыполненияОпераций.Завершено)
			|
			|СГРУППИРОВАТЬ ПО
			|	МаршрутныйЛистПроизводстваТрудозатраты.СтатьяКалькуляции,
			|	ВТПроизводственные.КодСтрокиРаспоряжения,
			|	ВТПроизводственные.ВидРабот,
			|	ВТПроизводственные.Распоряжение
			|;";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	Производственные.Распоряжение КАК Распоряжение,
	|	Производственные.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
	|	Производственные.ВидРабот КАК ВидРабот,
	|	Производственные.Количество КАК Количество,
	|	Производственные.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Производственные.СпособОтраженияЗарплаты КАК СпособОтраженияЗарплаты
	|ПОМЕСТИТЬ ВТОстатки
	|ИЗ
	|	Производственные КАК Производственные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТрудозатратыКОформлениюОстатки.Распоряжение,
	|	ТрудозатратыКОформлениюОстатки.КодСтрокиРаспоряжения,
	|	ТрудозатратыКОформлениюОстатки.ВидРабот,
	|	ТрудозатратыКОформлениюОстатки.КоличествоОстаток,
	|	"""",
	|	ЗаказНаРемонтРемонты.СпособОтраженияЗарплаты
	|ИЗ
	|	РегистрНакопления.ТрудозатратыКОформлению.Остатки(
	|			,
	|			Бригада = &Бригада
	|				И Организация = &Организация) КАК ТрудозатратыКОформлениюОстатки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаРемонт.Трудозатраты КАК ЗаказНаРемонтТрудозатраты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаРемонт.Ремонты КАК ЗаказНаРемонтРемонты
	|			ПО ЗаказНаРемонтТрудозатраты.КодРемонта = ЗаказНаРемонтРемонты.КодРемонта
	|				И ЗаказНаРемонтТрудозатраты.Ссылка = ЗаказНаРемонтРемонты.Ссылка
	|		ПО ТрудозатратыКОформлениюОстатки.Распоряжение = ЗаказНаРемонтТрудозатраты.Ссылка
	|			И ТрудозатратыКОформлениюОстатки.КодСтрокиРаспоряжения = ЗаказНаРемонтТрудозатраты.КодСтроки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ТрудозатратыКОформлениюОстатки.Распоряжение ССЫЛКА Документ.МаршрутныйЛистПроизводства
	|				ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Производство)
	|			ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыБригадныхНарядов.Ремонт)
	|		КОНЕЦ = &ВидНаряда
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТрудозатратыКОформлению.Распоряжение,
	|	ТрудозатратыКОформлению.КодСтрокиРаспоряжения,
	|	ТрудозатратыКОформлению.ВидРабот,
	|	ТрудозатратыКОформлению.Количество,
	|	"""",
	|	ЗаказНаРемонтРемонты.СпособОтраженияЗарплаты
	|ИЗ
	|	РегистрНакопления.ТрудозатратыКОформлению КАК ТрудозатратыКОформлению
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаРемонт.Трудозатраты КАК ЗаказНаРемонтТрудозатраты
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказНаРемонт.Ремонты КАК ЗаказНаРемонтРемонты
	|			ПО ЗаказНаРемонтТрудозатраты.КодРемонта = ЗаказНаРемонтРемонты.КодРемонта
	|				И ЗаказНаРемонтТрудозатраты.Ссылка = ЗаказНаРемонтРемонты.Ссылка
	|		ПО ТрудозатратыКОформлению.Распоряжение = ЗаказНаРемонтТрудозатраты.Ссылка
	|			И ТрудозатратыКОформлению.КодСтрокиРаспоряжения = ЗаказНаРемонтТрудозатраты.КодСтроки
	|ГДЕ
	|	ТрудозатратыКОформлению.Регистратор = &Ссылка
	|	И ТрудозатратыКОформлению.Бригада = &Бригада
	|	И ТрудозатратыКОформлению.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.ВидРабот КАК ВидРабот,
	|	ТаблицаДокумента.Распоряжение КАК Распоряжение,
	|	ТаблицаДокумента.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения
	|ПОМЕСТИТЬ ВТТаблицаДокумента
	|ИЗ
	|	&ТаблицаДокумента КАК ТаблицаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОстатки.Распоряжение КАК Распоряжение,
	|	ВТОстатки.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
	|	ВТОстатки.ВидРабот КАК ВидРабот,
	|	СУММА(ВТОстатки.Количество) КАК Количество,
	|	ВТОстатки.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТОстатки.СпособОтраженияЗарплаты КАК СпособОтраженияЗарплаты
	|ПОМЕСТИТЬ ВТОстаткиИтог
	|ИЗ
	|	ВТОстатки КАК ВТОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТОстатки.Распоряжение,
	|	ВТОстатки.ВидРабот,
	|	ВТОстатки.СпособОтраженияЗарплаты,
	|	ВТОстатки.СтатьяКалькуляции,
	|	ВТОстатки.КодСтрокиРаспоряжения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТОстаткиИтог.Распоряжение КАК Распоряжение,
	|	ВТОстаткиИтог.КодСтрокиРаспоряжения КАК КодСтрокиРаспоряжения,
	|	ВТОстаткиИтог.ВидРабот КАК ВидРабот,
	|	ВТОстаткиИтог.Количество КАК Количество,
	|	ВТОстаткиИтог.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	ВТОстаткиИтог.СпособОтраженияЗарплаты КАК СпособОтраженияЗарплаты,
	|	ВЫБОР
	|		КОГДА ВТТаблицаДокумента.КодСтрокиРаспоряжения ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Выбран,
	|	ЕСТЬNULL(Расценки.Расценка, 0) КАК Расценка,
	|	ЕСТЬNULL(Расценки.Расценка, 0) * ВТОстаткиИтог.Количество КАК Сумма
	|ИЗ
	|	ВТОстаткиИтог КАК ВТОстаткиИтог
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТаблицаДокумента КАК ВТТаблицаДокумента
	|		ПО (ВТТаблицаДокумента.ВидРабот = ВТОстаткиИтог.ВидРабот)
	|			И (ВТТаблицаДокумента.Распоряжение = ВТОстаткиИтог.Распоряжение)
	|			И (ВТТаблицаДокумента.КодСтрокиРаспоряжения = ВТОстаткиИтог.КодСтрокиРаспоряжения)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(&Период, ) КАК Расценки
	|		ПО ВТОстаткиИтог.ВидРабот = Расценки.ВидРабот";
	
	ТаблицаДокумента = ПолучитьИзВременногоХранилища(АдресВХранилище);
	ТаблицаДокумента.Свернуть("ВидРабот, Распоряжение, КодСтрокиРаспоряжения");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ТаблицаДокумента", ТаблицаДокумента);
	Запрос.УстановитьПараметр("Бригада", Бригада);
	Запрос.УстановитьПараметр("ВидНаряда", ВидНаряда);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Период", КонецДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("РабочийЦентр", ОтборРабочийЦентр);
	Запрос.УстановитьПараметр("ОтметкаНаДату", ТекущаяДатаСеанса());
	
	Таблица.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Таблица.Сортировать("ВидРабот, Распоряжение");
	
КонецПроцедуры
//-- НЕ УТКА

&НаКлиенте
Процедура ОтметитьСтроки(Значение, МассивСтрок = Неопределено)
	
	Если МассивСтрок = Неопределено Тогда
		
		Для Каждого СтрокаТаблицы Из Таблица Цикл
			СтрокаТаблицы.Выбран = Значение;
		КонецЦикла;
		
	Иначе
		
		Для Каждого НомерСтроки Из МассивСтрок Цикл
			СтрокаТаблицы = Таблица.НайтиПоИдентификатору(НомерСтроки);
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.Выбран = Значение;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
