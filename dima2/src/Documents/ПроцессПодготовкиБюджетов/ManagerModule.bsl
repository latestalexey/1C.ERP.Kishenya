

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.ПроцессПодготовкиБюджетов) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.ПроцессПодготовкиБюджетов.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.ПроцессПодготовкиБюджетов);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	Возврат; //В дальнейшем будет добавлен код команд

КонецПроцедуры

Функция НайтиСтроки(СтрокиДерева, СтруктураПоиска) Экспорт
	
	Для Каждого Строка из СтрокиДерева Цикл
		Подходит = Истина;
		Для Каждого КлючИЗначение из СтруктураПоиска Цикл
			Если Строка[КлючИЗначение.Ключ] <> КлючИЗначение.Значение Тогда
				Подходит = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Подходит Тогда
			Возврат Строка.ПолучитьИдентификатор();
		КонецЕсли;
		
		Результат = НайтиСтроки(Строка.ПолучитьЭлементы(), СтруктураПоиска);
		Если Результат <> Неопределено Тогда
			Возврат Результат;
		КонецЕсли;
	КонецЦикла;
	
КонецФункции

Функция ЗаполнитьСтатусыЗадачДерева(ДокументОбъект, ДеревоШаговБюджетногоПроцесса) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	БюджетнаяЗадача.ЭтапПодготовкиБюджетов,
		|	БюджетнаяЗадача.ПовторяемыйЭтапПодготовкиБюджетов,
		|	БюджетнаяЗадача.Выполнена,
		|	БюджетнаяЗадача.ПометкаУдаления,
		|	БюджетнаяЗадача.Период
		|ПОМЕСТИТЬ ВременнаяТаблица
		|ИЗ
		|	Задача.БюджетнаяЗадача КАК БюджетнаяЗадача
		|ГДЕ
		|	БюджетнаяЗадача.ПроцессПодготовкиБюджетов = &ПроцессПодготовкиБюджетов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблица.ЭтапПодготовкиБюджетов,
		|	ВременнаяТаблица.ПовторяемыйЭтапПодготовкиБюджетов,
		|	ВременнаяТаблица.Выполнена,
		|	ВременнаяТаблица.Период
		|ИЗ
		|	ВременнаяТаблица КАК ВременнаяТаблица
		|ГДЕ
		|	НЕ ВременнаяТаблица.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ПроцессПодготовкиБюджетов", ДокументОбъект.Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура("ЭтапПодготовкиБюджетов, ПовторяемыйЭтапПодготовкиБюджетов, Период", 
												ВыборкаДетальныеЗаписи.ЭтапПодготовкиБюджетов, 
												ВыборкаДетальныеЗаписи.ПовторяемыйЭтапПодготовкиБюджетов,
												ВыборкаДетальныеЗаписи.Период);
		
		Если ТипЗнч(ДеревоШаговБюджетногоПроцесса) = Тип("ДанныеФормыДерево") Тогда
			НайденнаяСтрока = НайтиСтроки(ДеревоШаговБюджетногоПроцесса.ПолучитьЭлементы(), СтруктураПоиска);
			СтрокаОбработки = ДеревоШаговБюджетногоПроцесса.НайтиПоИдентификатору(НайденнаяСтрока);
		Иначе
			НайденнаяСтрока = ДеревоШаговБюджетногоПроцесса.Строки.НайтиСтроки(СтруктураПоиска)[0];
		КонецЕсли;
		
		Если ВыборкаДетальныеЗаписи.Выполнена Тогда
			СтрокаОбработки.ИндексСтатуса = 2;
		Иначе
			СтрокаОбработки.ИндексСтатуса = 1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

Функция ВДеревеЭтаповВсеЗадачиВыполнены(ДеревоШаговБюджетногоПроцесса) Экспорт
	
	Если ТипЗнч(ДеревоШаговБюджетногоПроцесса) = Тип("ДанныеФормыДерево") Тогда
		ЭлементыДерева = ДеревоШаговБюджетногоПроцесса.ПолучитьЭлементы();
		Возврат НайтиСтроки(ЭлементыДерева, Новый Структура("НеВыполняется, ИндексСтатуса", Ложь, 2)) <> Неопределено
				И НайтиСтроки(ЭлементыДерева, Новый Структура("НеВыполняется, ИндексСтатуса", Ложь, 0)) = Неопределено
				И НайтиСтроки(ЭлементыДерева, Новый Структура("НеВыполняется, ИндексСтатуса", Ложь, 1)) = Неопределено
	Иначе
		ЭлементыДерева = ДеревоШаговБюджетногоПроцесса.Строки;
		Возврат ЭлементыДерева.НайтиСтроки(Новый Структура("НеВыполняется, ИндексСтатуса", Ложь, 2), Истина).Количество()
			И ЭлементыДерева.НайтиСтроки(Новый Структура("НеВыполняется, ИндексСтатуса", Ложь, 0), Истина).Количество() = 0
			И ЭлементыДерева.НайтиСтроки(Новый Структура("НеВыполняется, ИндексСтатуса", Ложь, 1), Истина).Количество() = 0
	КонецЕсли;
		
КонецФункции

Функция ОбойтиЗаполнитьДатыГруппДерева(НовыеСтроки, СтрокиДерева, НачалоПериода, КонецПериода, 
										ПериодичностьРодителя, НастройкиРасчета)
	
	Для Каждого СтрокаДерева из СтрокиДерева Цикл
		ПериодЗадачи = НачалоПериода;
		Пока ПериодЗадачи < КонецПериода Цикл
			Если СтрокаДерева.ЭтоГруппа Тогда
				НоваяСтрока = НовыеСтроки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДерева);
				НоваяСтрока.Наименование = СтрокаДерева.ЭтапПодготовкиБюджетовНаименование;
				Если Не ЗначениеЗаполнено(НоваяСтрока.Наименование) Тогда
					НоваяСтрока.Наименование = Строка(СтрокаДерева.ЭтапПодготовкиБюджетов);
				КонецЕсли;
				НоваяСтрока.Период = ПериодЗадачи;
				Если СтрокаДерева.Периодичность <> ПериодичностьРодителя Тогда
					НоваяСтрока.ПредставлениеПериода = БюджетированиеКлиентСервер.ПредставлениеПериодаПоДате(ПериодЗадачи, СтрокаДерева.Периодичность);
				КонецЕсли;
				НоваяСтрока.ИндексКартинки = 1;
				НоваяСтрока.ИндексСтатуса = -1;
				КонецПериодаЗадачи = БюджетированиеКлиентСервер.ДатаКонцаПериода(ПериодЗадачи, СтрокаДерева.Периодичность);
				ОбойтиЗаполнитьДатыГруппДерева(НоваяСтрока.Строки, СтрокаДерева.Строки, ПериодЗадачи, КонецПериодаЗадачи, СтрокаДерева.Периодичность, НастройкиРасчета);
			Иначе
				НоваяСтрока = НовыеСтроки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДерева);
				НоваяСтрока.Наименование = СтрокаДерева.ЭтапПодготовкиБюджетовНаименование;
				НоваяСтрока.Период = ПериодЗадачи;
				Если Не ЗначениеЗаполнено(НоваяСтрока.Родитель) Тогда
					НоваяСтрока.ПредставлениеПериода = БюджетированиеКлиентСервер.ПредставлениеПериодаПоДате(ПериодЗадачи, СтрокаДерева.Периодичность);
				Иначе
					НоваяСтрока.ПредставлениеПериода = "";
				КонецЕсли;
				
				СтрокиНастроек = НастройкиРасчета.НайтиСтроки(Новый Структура("Период, ЭтапПодготовкиБюджетов, ПовторяемыйЭтапПодготовкиБюджетов", 
												НоваяСтрока.Период, НоваяСтрока.ЭтапПодготовкиБюджетов, НоваяСтрока.ПовторяемыйЭтапПодготовкиБюджетов));
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиНастроек[0], "Ответственный, Длительность, ТипДлительности, НеВыполняется, ВыполнятьАвтоматически");
				
			КонецЕсли;
			ПериодЗадачи = БюджетированиеКлиентСервер.ДобавитьИнтервал(ПериодЗадачи, СтрокаДерева.Периодичность, 1);
		КонецЦикла;
	КонецЦикла;
	
КонецФункции

Процедура СкопироватьДеревоПовторяемыхШагов(СтрокиПриемник, СтрокиИсточник, ЭтапПодготовкиБюджетов)
	
	Для Каждого СтрокаИсточник из СтрокиИсточник Цикл
		
		Если СтрокаИсточник.ПовторяемыйЭтапПодготовкиБюджетовДействие = Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ПовторГруппыЭтапов Тогда
			Продолжить;
		КонецЕсли;
		
		Приемник = СтрокиПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(Приемник, СтрокаИсточник);
		Приемник.ЭтапПодготовкиБюджетов = ЭтапПодготовкиБюджетов;
		Если СтрокаИсточник.ЭтоГруппа Тогда
			Приемник.ЭтапПодготовкиБюджетовНаименование = Строка(СтрокаИсточник.ПовторяемыйЭтапПодготовкиБюджетов);
		Иначе
			Приемник.ЭтапПодготовкиБюджетовНаименование = СтрокаИсточник.ПовторяемыйЭтапПодготовкиБюджетовНаименование;
			Приемник.ЭтапПодготовкиБюджетовНастройкаДействия = СтрокаИсточник.ПовторяемыйЭтапПодготовкиБюджетовНастройкаДействия;
			Приемник.ЭтапПодготовкиБюджетовДействие = СтрокаИсточник.ПовторяемыйЭтапПодготовкиБюджетовДействие;
		КонецЕсли;
		
		СкопироватьДеревоПовторяемыхШагов(Приемник.Строки, СтрокаИсточник.Строки, ЭтапПодготовкиБюджетов);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ИерархияЭтаповПроцессаПоДокументу(Знач НастройкиИерархии) Экспорт
	
	МассивШагов = НастройкиИерархии.ВыгрузитьКолонку("ЭтапПодготовкиБюджетов");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивШагов, НастройкиИерархии.ВыгрузитьКолонку("ПовторяемыйЭтапПодготовкиБюджетов"));
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивШагов, "ЭтоГруппа");
	
	МассивШагов = Новый Массив;
	МассивИерархия = Новый Массив;
	МассивШаговПовторяемые = Новый Массив;
	МассивИерархияПовторяемые = Новый Массив;
	
	НастройкиИерархии.Колонки.Добавить("ЭтоГруппа");
	Для Каждого СтрокаТаблицы из НастройкиИерархии Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ПовторяемыйЭтапПодготовкиБюджетов) Тогда
			СтрокаТаблицы.ЭтоГруппа = ЗначенияРеквизитов[СтрокаТаблицы.ЭтапПодготовкиБюджетов].ЭтоГруппа;
			МассивИерархия.Добавить(СтрокаТаблицы);
		Иначе
			СтрокаТаблицы.ЭтоГруппа = ЗначенияРеквизитов[СтрокаТаблицы.ПовторяемыйЭтапПодготовкиБюджетов].ЭтоГруппа;
			МассивИерархияПовторяемые.Добавить(СтрокаТаблицы);
		КонецЕсли;
		Если Не СтрокаТаблицы.ЭтоГруппа Тогда
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ПовторяемыйЭтапПодготовкиБюджетов) Тогда
				МассивШагов.Добавить(СтрокаТаблицы);
			Иначе
				МассивШаговПовторяемые.Добавить(СтрокаТаблицы);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ШагиБезПовторяемых = НастройкиИерархии.Скопировать(МассивШагов);
	ИерархияБезПовторяемых = НастройкиИерархии.Скопировать(МассивИерархия);
	ШагиПовторяемые = НастройкиИерархии.Скопировать(МассивШаговПовторяемые);
	ИерархияПовторяемые = НастройкиИерархии.Скопировать(МассивИерархияПовторяемые);
	
	ВнешнийНабор = Новый Структура("Шаги, Иерархия", ШагиБезПовторяемых, ИерархияБезПовторяемых);
	
	СКД = Документы.ПроцессПодготовкиБюджетов.ПолучитьМакет("ДеревоШагов");
	Компоновщик = ФинансоваяОтчетностьСервер.КомпоновщикСхемы(СКД);
	
	ДеревоШагов = ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКД(СКД, Компоновщик, ВнешнийНабор, Истина);
	ДеревоШагов.Строки.Сортировать("Код", Истина);
	ДеревоШагов.Колонки.Добавить("ПовторяемыйЭтапПодготовкиБюджетов", Новый ОписаниеТипов("СправочникСсылка.ЭтапыПодготовкиБюджетов"));
	
	СтруктураПоиска = Новый Структура("ЭтапПодготовкиБюджетовДействие", Перечисления.ТипыДействийЭтаповПодготовкиБюджетов.ПовторГруппыЭтапов);
	ПовторяемыеГруппы = ДеревоШагов.Строки.НайтиСтроки(СтруктураПоиска, Истина);
	
	СКД = Документы.ПроцессПодготовкиБюджетов.ПолучитьМакет("ДеревоПовторяемыхШагов");
	Компоновщик = ФинансоваяОтчетностьСервер.КомпоновщикСхемы(СКД);
	
	Для Каждого СтрокаПовторяемыхГрупп из ПовторяемыеГруппы Цикл
		
		СтрокаПовторяемыхГрупп.ЭтоГруппа = Истина;
		НастройкиДействия = СтрокаПовторяемыхГрупп.ЭтапПодготовкиБюджетовНастройкаДействия.Получить();
		СтрокаДействия = НастройкиДействия.Найти("ГруппаШагов", "Имя");
		СтрокаПовторяемыхГрупп.Периодичность = СтрокаДействия.Значение.Периодичность;
		
		ВнешнийНабор = Новый Структура("Шаги, Иерархия", 
									ШагиПовторяемые.Скопировать(Новый Структура("ЭтапПодготовкиБюджетов", СтрокаПовторяемыхГрупп.ЭтапПодготовкиБюджетов)), 
									ИерархияПовторяемые.Скопировать(Новый Структура("ЭтапПодготовкиБюджетов", СтрокаПовторяемыхГрупп.ЭтапПодготовкиБюджетов)));
		
		ФинансоваяОтчетностьСервер.УстановитьПараметрКомпоновки(Компоновщик, "ПовторяемыйШаг", СтрокаПовторяемыхГрупп.ЭтапПодготовкиБюджетов);
		ДеревоПовторяемыхШагов = ФинансоваяОтчетностьСервер.ВыгрузитьРезультатСКД(СКД, Компоновщик, ВнешнийНабор, Истина);
		ДеревоПовторяемыхШагов.Строки.Сортировать("Код", Истина);
		СкопироватьДеревоПовторяемыхШагов(СтрокаПовторяемыхГрупп.Строки, 
					ДеревоПовторяемыхШагов.Строки, СтрокаПовторяемыхГрупп.ЭтапПодготовкиБюджетов);
		
	КонецЦикла;
		
	Возврат ДеревоШагов;
	
КонецФункции

Функция ДеревоЭтаповПроцессаПоДокументу(ДокументОбъект) Экспорт
	
	ТаблицаРеквизитов = ДокументОбъект.НастройкиИерархии.Выгрузить();
	ДеревоШагов = ИерархияЭтаповПроцессаПоДокументу(ТаблицаРеквизитов);
	
	НовоеДерево = Новый ДеревоЗначений;
	Для Каждого Колонка из ДеревоШагов.Колонки Цикл
		НовоеДерево.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	НовоеДерево.Колонки.Добавить("Ответственный");
	НовоеДерево.Колонки.Добавить("Длительность");
	НовоеДерево.Колонки.Добавить("ТипДлительности");
	НовоеДерево.Колонки.Добавить("НеВыполняется");
	НовоеДерево.Колонки.Добавить("Период");
	НовоеДерево.Колонки.Добавить("ПредставлениеПериода");
	НовоеДерево.Колонки.Добавить("Наименование");
	НовоеДерево.Колонки.Добавить("ИндексКартинки", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	НовоеДерево.Колонки.Добавить("ИндексСтатуса", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	НовоеДерево.Колонки.Добавить("ВыполнятьАвтоматически");
	
	ТаблицаНастроек = ДокументОбъект.НастройкиРасчета.Выгрузить();
	ТаблицаНастроек.Индексы.Добавить("Период, ЭтапПодготовкиБюджетов");
	
	ОбойтиЗаполнитьДатыГруппДерева(НовоеДерево.Строки, ДеревоШагов.Строки, 
			ДокументОбъект.НачалоПериода, ДокументОбъект.КонецПериода, ДокументОбъект.Периодичность, ТаблицаНастроек);
	
	Возврат НовоеДерево;
	
КонецФункции

#КонецОбласти

#КонецЕсли