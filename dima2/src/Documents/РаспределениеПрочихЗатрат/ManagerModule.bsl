#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
// 
// Параметры:
//   КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт



КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт

	 
	Если ПравоДоступа("Добавление", Метаданные.Документы.РаспределениеПрочихЗатрат) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.РаспределениеПрочихЗатрат.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.РаспределениеПрочихЗатрат);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		КомандаСоздатьНаОсновании.ФункциональныеОпции = "ИспользоватьПроизводство,ИспользоватьУчетПрочихДоходовРасходов";
	

		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;

	Возврат Неопределено;
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

// Функция возвращает список статей, по которым необходимо выполнить распределение.
// Параметры;
//		Период - Дата - Период выборки.
//		СписокОрганизаций - Массив - Список организаций по которым анализируются статьи.
//		СписокПодразделений - Массив - Список подразделений по которым анализируются статьи.
//		Состояние - Перечисление.СостоянияРаспределенияРасходов - Фильтр по состоянию.
//		ТолькоПостоянныеРазницы - Булево - Отбор только постоянных разниц.
//	Возвращаемое значение:
//		ТаблицаЗначений - Таблица статей к распределению.
Функция СтатьиКРаспределению(Период, СписокОрганизаций, СписокПодразделений, Состояние, ТолькоПостоянныеРазницы = Ложь) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДляРаспределения();
	Запрос.УстановитьПараметр("НачалоПериода", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ОкончаниеПериода", КонецМесяца(Период));
	Запрос.УстановитьПараметр("ГраницаДатаОкончания", Новый Граница(КонецМесяца(Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СписокПодразделений", СписокПодразделений);
	Запрос.УстановитьПараметр("СписокОрганизаций", СписокОрганизаций);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", Ложь);
	Запрос.УстановитьПараметр("ПоВсемПодразделениям", Ложь);
	Запрос.УстановитьПараметр("ФильтрПоСостоянию", Состояние);
	Запрос.УстановитьПараметр("ТолькоПостоянныеРазницыПрошлыхПериодов", ТолькоПостоянныеРазницы);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Функция возвращает все поля статьи расходов.
// Параметры:
//	СтатьяРасходов - ПланВидовХарактеристик.СтатьиРасходов - Статья, у которой необходимо получить поля.
// Возвращаемое значение:
//	ВыборкаИзРезультатаЗапроса - Поля статьи расходов.
Функция ПоляСтатьиРасходов(Знач СтатьяРасходов) Экспорт

	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	СтатьиРасходов.СпособРаспределенияНаПроизводственныеЗатраты КАК ВариантРаспределения,
	|	СтатьиРасходов.ПравилоРаспределенияПоЭтапамПроизводства КАК ПравилоРаспределенияПоЭтапам,
	|	СтатьиРасходов.ПравилоРаспределенияПоПодразделениям КАК ПравилоРаспределенияПоПодразделениям,
	|	СтатьиРасходов.СтатьяКалькуляции КАК СтатьяКалькуляции
	|ИЗ
	|	ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|ГДЕ
	|	СтатьиРасходов.Ссылка = &СтатьяРасходов
	|");

	Запрос.УстановитьПараметр("СтатьяРасходов", СтатьяРасходов);

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка;
КонецФункции

// Метод формирует документ "Распределение прочих затрат" по заданным условиям.
// Параметры:
//	 ПараметрыЗаполнения - Структура - Параметры заполнения документа.
// Возвращаемое значение:
//	Структура - Содержит сведения о документе и состоянии распределения.
Функция СформироватьДокумент(ПараметрыЗаполнения) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Распределение прочих затрат';uk='Розподіл інших витрат'"));
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗаполнения.Документ) Тогда
		Док = Документы.РаспределениеПрочихЗатрат.СоздатьДокумент();
	Иначе
		Док = ПараметрыЗаполнения.Документ.ПолучитьОбъект();
		Док.ПоБазе.Очистить();
		Док.Вручную.Очистить();
		Док.ВыпускиБезРаспоряжения.Очистить();
		Док.Списание.Очистить();
		Док.ОтборПоМатериалам.Очистить();
		Док.ОтборПоВидамРабот.Очистить();
		Док.ОтборПоГруппамПродукции.Очистить();
		Док.ОтборПоПодразделениям.Очистить();
	КонецЕсли;
	Док.Заполнить(ПараметрыЗаполнения);
	Если НЕ(ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу
				ИЛИ ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамВручнуюПоВсемПодразделениям)
			И ПараметрыЗаполнения.СтатьяКалькуляции = Справочники.СтатьиКалькуляции.ПустаяСсылка() Тогда
		Возврат "Нет данных для заполнения статьи калькуляции в документе распределения";
	КонецЕсли;
	Если ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямИЭтапамПоПравилам
		ИЛИ ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуВДанномПодразделении
		ИЛИ ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоЭтапамПоПравилуПоВсемПодразделениям Тогда
		Док.ДоляСтоимостиПоПравилам = 100;
	КонецЕсли;
	Если ПараметрыЗаполнения.ВариантРаспределения = Перечисления.СпособыРаспределенияСтатейРасходов.ПоПодразделениямВручнуюПоЭтапамПоПравилу
		И ПараметрыЗаполнения.СтатьяКалькуляции <> Справочники.СтатьиКалькуляции.ПустаяСсылка()
		И ПараметрыЗаполнения.Подразделение <> Справочники.СтруктураПредприятия.ПустаяСсылка() Тогда
		НоваяСтрока = Док.ПоБазе.Добавить();
		НоваяСтрока.СтатьяКалькуляции = ПараметрыЗаполнения.СтатьяКалькуляции;
		НоваяСтрока.Подразделение = ПараметрыЗаполнения.Подразделение;
		НоваяСтрока.ДоляСтоимости = 100;
	КонецЕсли;

	Попытка
		Док.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ЗаписьЖурналаРегистрации(Док.Метаданные().Имя, 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	ПараметрыПередачи = Новый Структура();
	Если Док.ПоБазе.Итог("ДоляСтоимости") + Док.ДоляСтоимостиПоПравилам > 0 Тогда
		ПараметрыПередачи.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе);
	Иначе
		ПараметрыПередачи.Вставить("Состояние", Перечисления.СостоянияРаспределенияРасходов.ТребуетсяНастройкаРаспределения);
	КонецЕсли;
	ПараметрыПередачи.Вставить("Документ", Док.Ссылка);
		
	Возврат ПараметрыПередачи;

КонецФункции

// Функция возвращает текст запроса для получения данных о прочих расходах.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаПоступилоПрочихРасходов() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПрочиеРасходыОбороты.Организация КАК Организация,
	|	ПрочиеРасходыОбороты.Подразделение КАК Подразделение,
	|	ПрочиеРасходыОбороты.СтатьяРасходов КАК СтатьяРасходов,
	|	ПрочиеРасходыОбороты.АналитикаРасходов КАК АналитикаРасходов,
	|	ПрочиеРасходыОбороты.НалоговоеНазначение КАК НалоговоеНазначение,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.Сумма) КАК СуммаУпрПриход,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.СуммаБезНДС) КАК СуммаУпрБезНДСПриход,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.СуммаРегл) КАК СуммаРеглПриход,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.СуммаРеглБезНДС) КАК СуммаРеглБезНДСПриход,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.НДСРегл) КАК НДСРеглПриход,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.ПостояннаяРазница) КАК ПостояннаяРазницаПриход,
	|	СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|			* ПрочиеРасходыОбороты.ВременнаяРазница) КАК ВременнаяРазницаПриход
	|ПОМЕСТИТЬ ВТПриход
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходыОбороты
	|ГДЕ
	|	ПрочиеРасходыОбороты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И ПрочиеРасходыОбороты.Активность
	|	И ПрочиеРасходыОбороты.СтатьяРасходов.ВариантРаспределенияРасходов = 
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|	И (ПрочиеРасходыОбороты.Организация В (&СписокОрганизаций) ИЛИ &ПоВсемОрганизациям)
	|	И (ПрочиеРасходыОбороты.Подразделение В (&СписокПодразделений) ИЛИ &ПоВсемПодразделениям)
	|	И НЕ (ПрочиеРасходыОбороты.Регистратор ССЫЛКА Документ.РаспределениеПрочихЗатрат
	|		ИЛИ ПрочиеРасходыОбороты.Регистратор ССЫЛКА Документ.РасчетСебестоимостиТоваров
	|		ИЛИ ПрочиеРасходыОбороты.РасчетСебестоимости
	|		ИЛИ
	|			ВЫБОР
	|				КОГДА ПрочиеРасходыОбороты.Регистратор ССЫЛКА Документ.РегламентнаяОперация
	|				ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПрочиеРасходыОбороты.Организация,
	|	ПрочиеРасходыОбороты.Подразделение,
	|	ПрочиеРасходыОбороты.СтатьяРасходов,
	|	ПрочиеРасходыОбороты.НалоговоеНазначение,
	|	ПрочиеРасходыОбороты.АналитикаРасходов
	|
	|ИМЕЮЩИЕ
	|
	|	НЕ (СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.Сумма) = 0
	|		И СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.СуммаБезНДС) = 0
	|		И СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.СуммаРегл) = 0
	|		И СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.СуммаРеглБезНДС) = 0
	|		И СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.НДСРегл) = 0
	|		И СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.ПостояннаяРазница) = 0
	|		И СУММА(ВЫБОР КОГДА ПрочиеРасходыОбороты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА -1 ИНАЧЕ 1 КОНЕЦ
	|				* ПрочиеРасходыОбороты.ВременнаяРазница) = 0)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ПрочиеРасходыОстатки.Организация,
	|	ПрочиеРасходыОстатки.Подразделение,
	|	ПрочиеРасходыОстатки.СтатьяРасходов,
	|	ПрочиеРасходыОстатки.АналитикаРасходов,
	|	ПрочиеРасходыОстатки.НалоговоеНазначение,
	|	ПрочиеРасходыОстатки.СуммаОстаток,
	|	ПрочиеРасходыОстатки.СуммаБезНДСОстаток,
	|	ПрочиеРасходыОстатки.СуммаРеглОстаток,
	|	ПрочиеРасходыОстатки.СуммаРеглБезНДСОстаток,
	|	ПрочиеРасходыОстатки.НДСРеглОстаток,
	|	ПрочиеРасходыОстатки.ПостояннаяРазницаОстаток,
	|	ПрочиеРасходыОстатки.ВременнаяРазницаОстаток
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|			&НачалоПериода,
	|			(Организация В (&СписокОрганизаций) ИЛИ &ПоВсемОрганизациям)
	|			И (Подразделение В (&СписокПодразделений) ИЛИ &ПоВсемПодразделениям)
	|			И СтатьяРасходов.ВариантРаспределенияРасходов = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			) КАК ПрочиеРасходыОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СебестоимостьТоваров.Организация,
	|	СебестоимостьТоваров.Подразделение,
	|	СебестоимостьТоваров.СтатьяРасходовСписания,
	|	СебестоимостьТоваров.АналитикаРасходов,
	|	СебестоимостьТоваров.КорНалоговоеНазначение,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО (СебестоимостьТоваров.СтатьяРасходовСписания = СтатьиРасходов.Ссылка
	|			И СтатьиРасходов.ВариантРаспределенияРасходов = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			)
	|ГДЕ
	|	СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И СебестоимостьТоваров.Активность
	|	И (СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ВнутреннееПотреблениеТоваров
	|		ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ВыпускПродукции
	|		ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
	|		ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.ПрочееОприходованиеТоваров
	|		ИЛИ СебестоимостьТоваров.Регистратор ССЫЛКА Документ.СписаниеНедостачТоваров)
	|	И (СебестоимостьТоваров.Организация В (&СписокОрганизаций) ИЛИ &ПоВсемОрганизациям)
	|	И (СебестоимостьТоваров.Подразделение В (&СписокПодразделений) ИЛИ &ПоВсемПодразделениям)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Затраты.Организация,
	|	Затраты.Подразделение,
	|	Затраты.СтатьяРасходов,
	|	Затраты.АналитикаРасходов,
	|	Затраты.НалоговоеНазначение,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	РегистрНакопления.МатериалыИРаботыВПроизводстве КАК Затраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|		ПО (Затраты.СтатьяРасходов = СтатьиРасходов.Ссылка
	|			И СтатьиРасходов.ВариантРаспределенияРасходов = 
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|			)
	|ГДЕ
	|	Затраты.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И Затраты.Активность
	|	И Затраты.Регистратор ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат
	|	И (Затраты.Организация В (&СписокОрганизаций) ИЛИ &ПоВсемОрганизациям)
	|	И (Затраты.Подразделение В (&СписокПодразделений) ИЛИ &ПоВсемПодразделениям)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТПриход.Организация,
	|	ВТПриход.Подразделение,
	|	ВТПриход.СтатьяРасходов,
	|	ВТПриход.АналитикаРасходов,
	|	ВТПриход.НалоговоеНазначение,
	|	СУММА(ВТПриход.СуммаУпрПриход) КАК ПоступилоУпр,
	|	СУММА(ВТПриход.СуммаУпрБезНДСПриход) КАК ПоступилоУпрБезНДС,
	|	СУММА(ВТПриход.СуммаРеглПриход) КАК ПоступилоРегл,
	|	СУММА(ВТПриход.СуммаРеглБезНДСПриход) КАК ПоступилоРеглБезНДС,
	|	СУММА(ВТПриход.НДСРеглПриход) КАК ПоступилоНДСРегл,
	|	СУММА(ВТПриход.ПостояннаяРазницаПриход) КАК ПоступилоПостояннаяРазница,
	|	СУММА(ВТПриход.ВременнаяРазницаПриход) КАК ПоступилоВременнаяРазница
	|ПОМЕСТИТЬ ВТПоступило
	|ИЗ
	|	ВТПриход КАК ВТПриход
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТПриход.Организация,
	|	ВТПриход.Подразделение,
	|	ВТПриход.СтатьяРасходов,
	|	ВТПриход.НалоговоеНазначение,
	|	ВТПриход.АналитикаРасходов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает текст запроса для получения данных для рабочего места по распределению расходов.
//
// Возвращаемое значение:
//	Строка - Текст запроса.
//
Функция ТекстЗапросаДанныеДляРаспределения() Экспорт
	
	// Распределяемые документы.
	ТекстЗапроса = ТекстЗапросаПоступилоПрочихРасходов() + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таб.Регистратор
	|ПОМЕСТИТЬ ВТРегистраторыРаспределения
	|ИЗ (
	|	ВЫБРАТЬ
	|		ДД.Регистратор КАК Регистратор,
	|		0 КАК ЕстьСтрокиВТЧПоБазе,
	|		0 КАК ЕстьСтрокиВТЧВручную,
	|		0 КАК ЕстьСтрокиВТЧВыпускиБезРаспоряжения,
	|		0 КАК ЕстьСтрокиВТЧСписание
	|	ИЗ
	|		РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК ДД
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК Док
	|		ПО ДД.Регистратор = Док.Ссылка
	|		И Док.Подразделение В (&СписокПодразделений)
	|
	|	ГДЕ
	|		ДД.Период МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И ДД.Организация В (&СписокОрганизаций)
	|		И ДД.Активность
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Док.Ссылка,
	|		СУММА(ЕСТЬNULL(ТЧПоБазе.НомерСтроки, 0)) КАК ЕстьСтрокиВТЧПоБазе,
	|		СУММА(ЕСТЬNULL(ТЧВручную.НомерСтроки, 0)) КАК ЕстьСтрокиВТЧВручную,
	|		СУММА(ЕСТЬNULL(ТЧВыпускиБезРаспоряжения.НомерСтроки, 0)) КАК ЕстьСтрокиВТЧВыпускиБезРаспоряжения,
	|		СУММА(ТЧСписание.НомерСтроки) КАК ЕстьСтрокиВТЧСписание
	|	ИЗ
	|		Документ.РаспределениеПрочихЗатрат КАК Док
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ПоБазе КАК ТЧПоБазе
	|		ПО Док.Ссылка = ТЧПоБазе.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Вручную КАК ТЧВручную
	|		ПО Док.Ссылка = ТЧВручную.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ВыпускиБезРаспоряжения КАК ТЧВыпускиБезРаспоряжения
	|		ПО Док.Ссылка = ТЧВыпускиБезРаспоряжения.Ссылка
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Списание КАК ТЧСписание
	|		ПО Док.Ссылка = ТЧСписание.Ссылка
	|	ГДЕ
	|		Док.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|		И Док.Организация В (&СписокОрганизаций)
	|		И Док.Подразделение В (&СписокПодразделений)
	|		И Док.Проведен
	|
	|	СГРУППИРОВАТЬ ПО
	|		Док.Ссылка
	|
	|	ИМЕЮЩИЕ
	|		СУММА(ЕСТЬNULL(ТЧПоБазе.НомерСтроки, 0)) = 0
	|		И СУММА(ЕСТЬNULL(ТЧВручную.НомерСтроки, 0)) = 0
	|		И СУММА(ЕСТЬNULL(ТЧВыпускиБезРаспоряжения.НомерСтроки, 0)) = 0
	|		И СУММА(ТЧСписание.НомерСтроки) > 0
	|
	|	) КАК Таб
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспределениеПрочихЗатрат.Ссылка КАК Документ,
	|	ВЫБОР
	|		КОГДА ВТРегистраторыРаспределения.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьДвиженияРаспределения,
	|	РаспределениеПрочихЗатрат.Организация КАК Организация,
	|	РаспределениеПрочихЗатрат.Подразделение КАК Подразделение,
	|	РаспределениеПрочихЗатрат.СтатьяРасходов КАК СтатьяРасходов,
	|	РаспределениеПрочихЗатрат.АналитикаРасходов КАК АналитикаРасходов,
	|	СУММА(ЕСТЬNULL(РаспределениеПрочихЗатратВручную.ДоляСтоимости, 0) 
	|			   + ЕСТЬNULL(РаспределениеПрочихЗатратПоБазе.ДоляСтоимости, 0)
	|			   + ЕСТЬNULL(РаспределениеПрочихЗатратВыпускиБезРаспоряжения.ДоляСтоимости, 0)
	|			   + ЕСТЬNULL(РаспределениеПрочихЗатратСписание.ДоляСтоимости, 0)
	|			   ) + РаспределениеПрочихЗатрат.ДоляСтоимостиПоПравилам КАК СуммаДолейСтоимости
	|ПОМЕСТИТЬ ВТДокументы
	|ИЗ
	|	Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Вручную КАК РаспределениеПрочихЗатратВручную
	|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратВручную.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ПоБазе КАК РаспределениеПрочихЗатратПоБазе
	|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратПоБазе.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.ВыпускиБезРаспоряжения КАК РаспределениеПрочихЗатратВыпускиБезРаспоряжения
	|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратВыпускиБезРаспоряжения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат.Списание КАК РаспределениеПрочихЗатратСписание
	|		ПО РаспределениеПрочихЗатрат.Ссылка = РаспределениеПрочихЗатратСписание.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРегистраторыРаспределения КАК ВТРегистраторыРаспределения
	|		ПО РаспределениеПрочихЗатрат.Ссылка = ВТРегистраторыРаспределения.Регистратор
	|ГДЕ
	|	РаспределениеПрочихЗатрат.Дата МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|	И РаспределениеПрочихЗатрат.Организация В (&СписокОрганизаций)
	|	И РаспределениеПрочихЗатрат.Подразделение В (&СписокПодразделений)
	|	И РаспределениеПрочихЗатрат.Проведен
	|	И РаспределениеПрочихЗатрат.СтатьяРасходов.ВариантРаспределенияРасходов =
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспределениеПрочихЗатрат.Ссылка,
	|	РаспределениеПрочихЗатрат.Организация,
	|	РаспределениеПрочихЗатрат.Подразделение,
	|	РаспределениеПрочихЗатрат.СтатьяРасходов,
	|	РаспределениеПрочихЗатрат.АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА ВТРегистраторыРаспределения.Регистратор ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	// Данные для таблиц к распределению.
	ТекстЗапроса = ТекстЗапроса + "
	|ВЫБРАТЬ
	|	НеРаспределенные.Организация,
	|	НеРаспределенные.Подразделение,
	|	НеРаспределенные.СтатьяРасходов,
	|	НеРаспределенные.АналитикаРасходов
	|ПОМЕСТИТЬ НеРаспределенные
	|ИЗ
	|	РегистрНакопления.ПрочиеРасходы.Остатки(
	|		&ГраницаДатаОкончания,
	|		СтатьяРасходов.ВариантРаспределенияРасходов = 
	|			ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты)
	|		И Организация В (&СписокОрганизаций)
	|		И Подразделение В (&СписокПодразделений)
	|	) КАК НеРаспределенные
	|ГДЕ
	|	НеРаспределенные.СуммаОстаток <> 0
	|	ИЛИ НеРаспределенные.СуммаБезНДСОстаток <> 0
	|	ИЛИ НеРаспределенные.СуммаРеглОстаток <> 0
	|	ИЛИ НеРаспределенные.СуммаРеглБезНДСОстаток <> 0
	|	ИЛИ НеРаспределенные.НДСРеглОстаток <> 0
	|
	|;
	|
	|ВЫБРАТЬ
	|	ВТПоступило.Организация КАК Организация,
	|	ВТПоступило.Подразделение КАК Подразделение,
	|	ВТПоступило.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТПоступило.АналитикаРасходов КАК АналитикаРасходов,
	|	ВТДокументы.Документ КАК Документ,
	|	ВТПоступило.ПоступилоУпр КАК ПоступилоУпр,
	|	ВТПоступило.ПоступилоУпрБезНДС КАК ПоступилоУпрБезНДС,
	|	ВТПоступило.ПоступилоРегл КАК ПоступилоРегл,
	|	ВТПоступило.ПоступилоРеглБезНДС КАК ПоступилоРеглБезНДС,
	|	ВТПоступило.ПоступилоНДСРегл КАК ПоступилоНДСРегл,
	|	ВТПоступило.ПоступилоПостояннаяРазница КАК ПоступилоПостояннаяРазница,
	|	ВТПоступило.ПоступилоВременнаяРазница КАК ПоступилоВременнаяРазница,
	|	ВЫБОР
	// если не настроено распределение долей и есть сумма к распределению то требуется настройка
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) = 0 И ВТПоступило.ПоступилоУпр <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ТребуетсяНастройкаРаспределения)
	// если распределение долей есть, документ распределения сделал движения, но есть остаток к распределеню, то есть ошибка
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) > 0 И ЕСТЬNULL(ВТДокументы.ЕстьДвиженияРаспределения, ЛОЖЬ) И НЕ НеРаспределенные.СтатьяРасходов ЕСТЬ NULL
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
	// если распределение долей есть, документ распределения не сделал движения и сумма к распределению не нулевая, то расходы готовы к распределению
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) > 0 И НЕ ЕСТЬNULL(ВТДокументы.ЕстьДвиженияРаспределения, ЛОЖЬ) И ВТПоступило.ПоступилоУпр <> 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ГотовоКРаспределениюПоБазе)
	// если распределение долей есть, документ распределения сделал движения и сумма к распределению нулевая, то расходы распределены
	|		КОГДА ЕСТЬNULL(ВТДокументы.СуммаДолейСтоимости, 0) > 0 И ЕСТЬNULL(ВТДокументы.ЕстьДвиженияРаспределения, ЛОЖЬ) И НеРаспределенные.СтатьяРасходов ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.Распределено)
	// не классифицированный случай
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ОшибкаРаспределения)
	|	КОНЕЦ КАК Состояние
	|ПОМЕСТИТЬ ВТДанныеДляРаспределения
	|ИЗ
	|	ВТПоступило КАК ВТПоступило
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДокументы КАК ВТДокументы
	|		ПО	ВТПоступило.СтатьяРасходов = ВТДокументы.СтатьяРасходов
	|			И ВТПоступило.АналитикаРасходов = ВТДокументы.АналитикаРасходов
	|			И ВТПоступило.Подразделение		= ВТДокументы.Подразделение
	|			И ВТПоступило.Организация		= ВТДокументы.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ НеРаспределенные КАК НеРаспределенные
	|		ПО	ВТПоступило.СтатьяРасходов = НеРаспределенные.СтатьяРасходов
	|			И ВТПоступило.АналитикаРасходов = НеРаспределенные.АналитикаРасходов
	|			И ВТПоступило.Подразделение		= НеРаспределенные.Подразделение
	|			И ВТПоступило.Организация		= НеРаспределенные.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанныеДляЗагрузки.Организация КАК Организация,
	|	ВТДанныеДляЗагрузки.Подразделение КАК Подразделение,
	|	ВТДанныеДляЗагрузки.СтатьяРасходов КАК СтатьяРасходов,
	|	ВТДанныеДляЗагрузки.АналитикаРасходов КАК АналитикаРасходов,
	|	ВТДанныеДляЗагрузки.Документ,
	|	ВТДанныеДляЗагрузки.Состояние
	|ИЗ
	|	ВТДанныеДляРаспределения КАК ВТДанныеДляЗагрузки
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &ФильтрПоСостоянию = ЗНАЧЕНИЕ(Перечисление.СостоянияРаспределенияРасходов.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|			ИНАЧЕ ВТДанныеДляЗагрузки.Состояние = &ФильтрПоСостоянию
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация,
	|	Подразделение,
	|	СтатьяРасходов,
	|	АналитикаРасходов
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция возвращает статью калькуляции для документа распределения по данным правила распределения по этапам.
// и распределяемой статьи расходов.
//
// Возвращаемое значение:
//	СтатьяКалькуляции - Справочник.СтатьиКалькуляции - Ссылка на статью калькуляции.
//
Функция ПолучитьСтатьюКалькуляцииДляДокумента(СтатьяРасходов, ПравилоРаспределенияПоЭтапам) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	Правило.СтатьяКалькуляции КАК СтатьяКалькуляции
		|ИЗ
		|	Справочник.ПравилаРаспределенияРасходов КАК Правило
		|ГДЕ
		|	Правило.Ссылка = &Правило
		|	И Правило.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Статья.СтатьяКалькуляции
		|ИЗ
		|	ПланВидовХарактеристик.СтатьиРасходов КАК Статья
		|ГДЕ
		|	Статья.Ссылка = &Статья
		|	И Статья.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		|");
	
	Запрос.УстановитьПараметр("Правило", ПравилоРаспределенияПоЭтапам);
	Запрос.УстановитьПараметр("Статья", СтатьяРасходов);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.СтатьяКалькуляции;
	
КонецФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	Возврат;
КонецПроцедуры

#Область ПроведениеПоРеглУчетуУКР

// Функция возвращает текст запроса для отражения документа в регламентированном учете.
//
// Возвращаемое значение:
//	Строка - Текст запроса
//                                                                               
Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
#Область ТекстРаспределениеРасходов // (Дт 23, 91, 92 :: Кт 91, 92)
	ТекстРаспределениеРасходов = "
	|ВЫБРАТЬ //// Распределение расходов (Дт 23 :: Кт 91, 92)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	Строки.Сумма КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка) КАК АналитикаУчетаДт,
	|	Строки.МестоУчетаДт КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Строки.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	Строки.СтатьяРасходов КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт2,
	|	Строки.ГруппаПродукции КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Операция.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Операция.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Распределение расходов"" КАК Содержание
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПрочихЗатрат КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		ПрочиеРасходыНЗП КАК Строки
	|	ПО
	|		Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияРасходов КАК Счета
	|	ПО 
	|		Счета.Организация = Операция.Организация
	|		И Счета.Подразделение = Операция.Подразделение
	|		И Счета.СтатьяРасходов = Операция.СтатьяРасходов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиРасходов
	|	ПО
	|		СтатьиРасходов.Ссылка = Операция.СтатьяРасходов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ 
	|		РегистрСведений.ПорядокОтраженияРасходов КАК КорСчета
	|	ПО 
	|		КорСчета.Организация = Операция.Организация
	|		И КорСчета.Подразделение = Строки.Подразделение
	|		И КорСчета.СтатьяРасходов = ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|	
	|ГДЕ
	|	Строки.Сумма <> 0
	|	И ((ВЫБОР 
	|			КОГДА ЕСТЬNULL(Счета.СчетУчета, СтатьиРасходов.СчетУчета) <> ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка)
	|				ТОГДА ЕСТЬNULL(Счета.СчетУчета, СтатьиРасходов.СчетУчета)
	|			ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОбщепроизводственныеРасходы)
	|		КОНЕЦ) <> ЕСТЬNULL(КорСчета.СчетУчета, ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ОсновноеПроизводство))
	|	    ИЛИ Операция.Подразделение <> Строки.Подразделение
	|	    ИЛИ (&АналитическийУчетПоГруппамПродукции И Строки.ГруппаПродукции <> ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ //// Распределение расходов (Дт 91, 92 :: Кт 91, 92)
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|
	|	ПрочиеРасходы.СуммаРегл КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	ПрочиеРасходы.СтатьяРасходов КАК АналитикаУчетаДт,
	|	ПрочиеРасходы.Подразделение КАК МестоУчетаДт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	ПрочиеРасходы.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеДт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|
	|	ПрочиеРасходы.СтатьяРасходов КАК СубконтоДт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	0 КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаКт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаКт,
	|	Операция.Подразделение КАК МестоУчетаКт,
	|
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиКт,
	|	НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,
	|
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетКт,
	|
	|	Операция.СтатьяРасходов КАК СубконтоКт1,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗатратРегл.Прочее) КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	0 КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Распределение расходов"" КАК Содержание
	|
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.РаспределениеПрочихЗатрат КАК Операция
	|	ПО	
	|		ДокументыКОтражению.Ссылка = Операция.Ссылка
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ПрочиеРасходы КАК ПрочиеРасходы
	|	ПО Операция.Ссылка = ПрочиеРасходы.ДокументДвижения
	|	   И ПрочиеРасходы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	   И ПрочиеРасходы.СуммаРегл <> 0
	|";
#КонецОбласти 	

	Возврат ТекстРаспределениеРасходов;
	
КонецФункции

// Функция возвращает текст запроса дополнительных временных таблиц, 
// необходимых для отражения в регламетированном учете
//
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Движения.ДокументДвижения КАК Ссылка,
	|	ЕСТЬNULL(ЗаказПереработчику.Партнер, Движения.Подразделение) КАК МестоУчетаДт,
	|	Движения.Подразделение КАК Подразделение,
	|	Движения.СтатьяРасходов КАК СтатьяРасходов,
	|	Движения.ГруппаПродукции КАК ГруппаПродукции,
	|	СУММА(Движения.СтоимостьРегл) КАК Сумма
	|ПОМЕСТИТЬ ПрочиеРасходыНЗП
	|ИЗ
	|	ДокументыКОтражению КАК ДокументыКОтражению
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрНакопления.ПрочиеРасходыНезавершенногоПроизводства КАК Движения
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ЗаказПереработчику
	|				ПО ЗаказПереработчику.Ссылка = Движения.ЗаказНаПроизводство
	|	ПО
	|		ДокументыКОтражению.Ссылка = Движения.ДокументДвижения
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ЗаказПереработчику.Партнер, Движения.Подразделение),
	|	Движения.ДокументДвижения,
	|	Движения.Подразделение,
	|	Движения.СтатьяРасходов,
	|	Движения.ГруппаПродукции
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	ВходящиеДанные.Вставить(Метаданные.Документы.РаспределениеПрочихЗатрат);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.МатериалыИРаботыВПроизводстве);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.ПрочиеРасходыНезавершенногоПроизводства);
	ВходящиеДанные.Вставить(Метаданные.РегистрыНакопления.СебестоимостьТоваров);
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
