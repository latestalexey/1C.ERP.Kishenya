#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("Бригада") Тогда
			
			Если ЗначениеЗаполнено(ДанныеЗаполнения.Бригада) Тогда
				Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеЗаполнения.Бригада, "Подразделение");
				ДанныеЗаполнения.Вставить("Подразделение", Реквизиты.Подразделение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьРеквизитыПоУмолчанию();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПроверитьДублированиеСтрок() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРеквизитыПоУмолчанию()
	
	Ответственный = Пользователи.ТекущийПользователь();
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

Функция ПроверитьДублированиеСтрок(Отказ = Ложь)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Сотрудник КАК Сотрудник,
	|	Сотрудники.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&Сотрудники КАК Сотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТСотрудники.Сотрудник КАК Сотрудник,
	|	МАКСИМУМ(ВТСотрудники.НомерСтроки) КАК НомерСтроки,
	|	МИНИМУМ(ВТСотрудники.НомерСтроки) КАК НомерСтроки1
	|ИЗ
	|	ВТСотрудники КАК ВТСотрудники
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТСотрудники.Сотрудник
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ВТСотрудники.Сотрудник) > 1";
	
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники.Выгрузить(, "Сотрудник, НомерСтроки"));
	
	Результат = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = Результат.Выбрать();
	
	Если Не Результат.Пустой() Тогда
		
		Отказ = Истина;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
											НСтр("ru='Сотрудник уже содержится в строке %1';uk='Співробітник вже міститься в рядку %1'"),
											ВыборкаДетальныеЗаписи.НомерСтроки1);
											
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Сотрудники[" + (ВыборкаДетальныеЗаписи.НомерСтроки -1) + "].Сотрудник", "Объект");
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

#КонецОбласти

#КонецЕсли