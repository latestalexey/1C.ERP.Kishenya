
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
//
// Параметры:
// 		КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	//++ НЕ УТКА
	Документы.СписаниеНМАМеждународныйУчет.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании);
	//-- НЕ УТКА
	Возврат;// в КА пустой метод
	
КонецПроцедуры

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.СписаниеНМА) Тогда
		КомандаСоздатьНаОсновании = КомандыСоздатьНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Идентификатор = Метаданные.Документы.СписаниеНМА.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ВводНаОсновании.ПредставлениеОбъекта(Метаданные.Документы.СписаниеНМА);
		КомандаСоздатьНаОсновании.ПроверкаПроведенияПередСозданиемНаОсновании = Истина;
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт

	ИсточникиДанных = Новый Соответствие;

	Возврат ИсточникиДанных; 

КонецФункции


Процедура ИнициализироватьДанныеДокументаУКР(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Списание НМА';uk='Списання НМА'"));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализацииУКР(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	ТекстыЗапроса = Новый СписокЗначений;
	УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры, ПрочиеРасходыУКР(ТекстыЗапроса));
	УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры, ПартииПрочихРасходовУКР(ТекстыЗапроса));
	УчетОСВызовСервера.ПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
	УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАБухгалтерскийУчетУКР(ТекстыЗапроса, Регистры);
	ПервоначальныеСведенияНМАНалоговыйУчетУКР(ТекстыЗапроса, Регистры);
	СостоянияНМАОрганизацийУКР(ТекстыЗапроса, Регистры);
		
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Ложь, Ложь, Истина);
	
КонецПроцедуры


 
Процедура ЗаполнитьПараметрыИнициализацииУКР(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.Организация КАК Организация,
	|	ДанныеДокумента.Подразделение КАК Подразделение,
	|	ДанныеДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ДанныеДокумента.ЧастичнаяЛиквидация КАК ЧастичнаяЛиквидация,
	|	ДанныеДокумента.СтатьяРасходов КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов КАК АналитикаРасходов,
	|	ДанныеДокумента.СтатьяРасходов.ВариантРаспределенияРасходов КАК СтатьяРасходовВариантРаспределенияРасходов
	|ИЗ
	|	Документ.СписаниеНМА КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДополнительныеСвойства);
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("КонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("СписаниеОстаточнойСтоимости", Истина);
	Запрос.УстановитьПараметр("ДатаСписанияОстаточнойСтоиости", Реквизиты.Период);
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	
КонецПроцедуры

Процедура ВременнаяТаблицаСгруппированнаяАмортизация(ТекстыЗапроса)
	
	ИмяТаблицы = "втАмортизацияПоОбъектам";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ВременнаяТаблицаНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица сгруппированная амортизация
	|"+
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ОбъектУчета КАК НМА,
	|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ,
	|	СУММА(НачисленнаяАмортизация.СуммаПР) КАК СуммаПР,
	|	СУММА(НачисленнаяАмортизация.СуммаВР) КАК СуммаВР
	|ПОМЕСТИТЬ втАмортизацияПоОбъектам
	|ИЗ
	|	втНачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяАмортизация.ОбъектУчета
	|" + ";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаДанныхУчета(ТекстыЗапроса)
	
	ИмяТаблицы = "втТаблицаДанныхУчета";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСгруппированнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица по данным учета
	|"+
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.НематериальныйАктив КАК НематериальныйАктив,
	|	ТаблицаДокумента.СуммаСписанияБУ КАК СуммаСписанияБУ,
	|	ТаблицаДокумента.СуммаСписанияНУ КАК СуммаСписанияНУ,
	|	0 КАК СуммаСписанияПР,
	|	0 КАК СуммаСписанияВР,
	|	ЕСТЬNULL(Амортизация.СуммаБУ,0) КАК СуммаАмортизацииБУ,
	|	ЕСТЬNULL(Амортизация.СуммаНУ,0) КАК СуммаАмортизацииНУ,
	|	0 КАК СуммаАмортизацииПР,
	|	0 КАК СуммаАмортизацииВР,
	|	СчетаОтражения.СчетУчета КАК СчетУчета,
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетАмортизации
	|ПОМЕСТИТЬ втТаблицаДанныхУчета
	|ИЗ
	|	Документ.СписаниеНМА КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
	|				&Период,
	|				Регистратор <> &Ссылка
	|				И Организация = &Организация
	|				И НематериальныйАктив = &НематериальныйАктив) КАК СчетаОтражения
	|		ПО ТаблицаДокумента.НематериальныйАктив = СчетаОтражения.НематериальныйАктив
	|		ЛЕВОЕ СОЕДИНЕНИЕ втАмортизацияПоОбъектам КАК Амортизация
	|		ПО ТаблицаДокумента.НематериальныйАктив = Амортизация.НМА
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|" + ";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаНМА";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаДанныхУчета(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаНМА
	|"+
	"ВЫБРАТЬ
	|	втТаблицаДанныхУчета.НематериальныйАктив,
	|	
	|	втТаблицаДанныхУчета.СуммаСписанияНУ КАК СуммаСписанияНУ,
	|	
	|	втТаблицаДанныхУчета.СуммаСписанияБУ -
	|	ВЫБОР КОГДА ЕСТЬNULL(ДанныеСчетУчета.СуммаОстатокДт,0)=0
	|	ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(ДанныеСчетАмортизации.СуммаОстатокКт,0) + ЕСТЬNULL(втТаблицаДанныхУчета.СуммаАмортизацииБУ,0)) * ЕСТЬNULL(втТаблицаДанныхУчета.СуммаСписанияБУ/ДанныеСчетУчета.СуммаОстатокДт,0) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК РасходБУ,
	|	
	|	втТаблицаДанныхУчета.СуммаСписанияНУ -
	|	ВЫБОР КОГДА ЕСТЬNULL(ДанныеСчетУчета.СуммаНУОстатокДт,0)=0
	|	ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(ДанныеСчетАмортизации.СуммаНУОстатокКт,0) + ЕСТЬNULL(втТаблицаДанныхУчета.СуммаАмортизацииНУ,0)) * ЕСТЬNULL(втТаблицаДанныхУчета.СуммаСписанияНУ/ДанныеСчетУчета.СуммаНУОстатокДт,0) КАК ЧИСЛО(15,2))
	|	КОНЕЦ КАК РасходНУ,
	|	
	|	0 КАК РасходПР,
	|	
	|	ЕСТЬNULL(ДанныеСчетУчета.СуммаОстатокДт,0)
	|	- ЕСТЬNULL(ДанныеСчетАмортизации.СуммаОстатокКт,0)
	|	- ЕСТЬNULL(втТаблицаДанныхУчета.СуммаАмортизацииБУ,0) КАК ОстаточнаяСтоимостьБУ,
	|	ЕСТЬNULL(ДанныеСчетУчета.СуммаНУОстатокДт,0)
	|	- ЕСТЬNULL(ДанныеСчетАмортизации.СуммаНУОстатокКт,0)
	|	- ЕСТЬNULL(втТаблицаДанныхУчета.СуммаАмортизацииНУ,0) КАК ОстаточнаяСтоимостьНУ,
	|	0 КАК ОстаточнаяСтоимостьПР,
	|	0 КАК ОстаточнаяСтоимостьВР
	|	
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	втТаблицаДанныхУчета КАК втТаблицаДанныхУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&КонецМесяца,
	|				Счет В (ВЫБРАТЬ Т.СчетУчета ИЗ втТаблицаДанныхУчета КАК Т),,
	|				Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И Субконто1 = &НематериальныйАктив
	|				) КАК ДанныеСчетУчета
	|		ПО втТаблицаДанныхУчета.НематериальныйАктив = ДанныеСчетУчета.Субконто1
	|			И втТаблицаДанныхУчета.СчетУчета = ДанныеСчетУчета.Счет
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
	|				&КонецМесяца,
	|				Счет В (ВЫБРАТЬ Т.СчетАмортизации ИЗ втТаблицаДанныхУчета КАК Т),,
	|				Организация = &Организация
	|					И Подразделение = &Подразделение
	|					И Субконто1 = &НематериальныйАктив
	|				) КАК ДанныеСчетАмортизации
	|		ПО втТаблицаДанныхУчета.НематериальныйАктив = ДанныеСчетАмортизации.Субконто1
	|			И втТаблицаДанныхУчета.СчетАмортизации = ДанныеСчетАмортизации.Счет
	|"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры



 
Процедура ПервоначальныеСведенияНМАБухгалтерскийУчетУКР(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАБухгалтерскийУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаНМА.ОстаточнаяСтоимостьБУ - ТаблицаНМА.РасходБУ КАК ПервоначальнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.НачислятьАмортизацию КАК НачислятьАмортизацию,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СпособНачисленияАмортизации КАК СпособНачисленияАмортизации,
	 |	ПервоначальныеСведенияНМАБухгалтерскийУчет.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.СрокПолезногоИспользования
	|	- РАЗНОСТЬДАТ(ЕСТЬNULL(ПервоначальныеСведенияНМАБухгалтерскийУчет.Период, &Период), &Период, МЕСЯЦ) КАК СрокПолезногоИспользования,
	|	ПервоначальныеСведенияНМАБухгалтерскийУчет.ОбъемПродукцииРаботДляВычисленияАмортизации
	|	- ЕСТЬNULL(ВыработкаНМА.КоличествоОборот, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Регистратор<>&Ссылка
	|		И НематериальныйАктив = &НематериальныйАктив
	|	) КАК ПервоначальныеСведенияНМАБухгалтерскийУчет
	|	ПО ТаблицаНМА.НематериальныйАктив = ПервоначальныеСведенияНМАБухгалтерскийУчет.НематериальныйАктив
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыработкаНМА.Обороты(
	|		,
	|		&Период,
	|		,
	|		Организация = &Организация И НематериальныйАктив = &НематериальныйАктив
	|	) КАК ВыработкаНМА
	|	ПО ТаблицаНМА.НематериальныйАктив = ВыработкаНМА.НематериальныйАктив
	|	
	|ГДЕ
	|	&ЧастичнаяЛиквидация"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры


Процедура ПервоначальныеСведенияНМАНалоговыйУчетУКР(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПервоначальныеСведенияНМАНалоговыйУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПервоначальныеСведенияНМАНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ПервоначальныеСведенияНМАНалоговыйУчет.СпособНачисленияАмортизацииНУ    КАК СпособНачисленияАмортизацииНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.НалоговаяГруппаОС    			КАК НалоговаяГруппаОС,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.НалоговоеНазначение			    КАК НалоговоеНазначение,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.НачислятьАмортизацию 			КАК НачислятьАмортизацию,
	|	&Организация КАК Организация,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.ПервоначальнаяСтоимостьНУ - ТаблицаНМА.СуммаСписанияНУ КАК ПервоначальнаяСтоимостьНУ,
	|	ПервоначальныеСведенияНМАНалоговыйУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования
	|	
	|ИЗ
	|	ТаблицаНМА КАК ТаблицаНМА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАНалоговыйУчет.СрезПоследних(
	|		&Период,
	|		Организация = &Организация
	|		И Регистратор<>&Ссылка
	|		И НематериальныйАктив = &НематериальныйАктив
	|	) КАК ПервоначальныеСведенияНМАНалоговыйУчет
	|	ПО ТаблицаНМА.НематериальныйАктив = ПервоначальныеСведенияНМАНалоговыйУчет.НематериальныйАктив
	|	
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|"+";";
	
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

 
Процедура СостоянияНМАОрганизацийУКР(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СостоянияНМАОрганизаций";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СостоянияНМАОрганизаций
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	&НематериальныйАктив КАК НематериальныйАктив,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийНМА.Списан) КАК Состояние
	|	
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры


Функция ПрочиеРасходыУКР(ТекстыЗапроса)
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	
	|	0 КАК Сумма,
	|	
	|	0 КАК СуммаБезНДС,
	|	
	|	ОстаточнаяСтоимостьНМА.ОстаточнаяСтоимостьБУ КАК СуммаРегл,
	|	0 КАК СуммаРеглБезНДС,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|	И НЕ &СтатьяРасходовВариантРаспределенияРасходов В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	
	|	0 КАК Сумма,
	|	
	|	0 КАК СуммаБезНДС,
	|	
	|	ОстаточнаяСтоимостьНМА.ОстаточнаяСтоимостьБУ КАК СуммаРегл,
	|	0 КАК СуммаРеглБезНДС,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	
	|	0 КАК Сумма,
	|	
	|	0 КАК СуммаБезНДС,
	|	
	|	ОстаточнаяСтоимостьНМА.РасходБУ КАК СуммаРегл,
	|	0 КАК СуммаРеглБезНДС,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|	И НЕ &СтатьяРасходовВариантРаспределенияРасходов В (
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаВнеоборотныеАктивы),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначение,
	|	&Подразделение КАК Подразделение,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	
	|	0 КАК Сумма,
	|	
	|	0 КАК СуммаБезНДС,
	|	
	|	ОстаточнаяСтоимостьНМА.РасходБУ КАК СуммаРегл,
	|	0 КАК СуммаРеглБезНДС,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница,
	|	
	|	Неопределено КАК ХозяйственнаяОперация,
	|	Неопределено КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	ЛОЖЬ
	|";
		
КонецФункции


Функция ПартииПрочихРасходовУКР(ТекстыЗапроса)
	
	ВременнаяТаблицаНематериальныхАктивов(ТекстыЗапроса);
	
	Возврат
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	ОстаточнаяСтоимостьНМА.ОстаточнаяСтоимостьБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	НЕ &ЧастичнаяЛиквидация
	|	И &СтатьяРасходовВариантРаспределенияРасходов В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	ОстаточнаяСтоимостьНМА.ОстаточнаяСтоимостьБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	ОстаточнаяСтоимостьНМА.ОстаточнаяСтоимостьБУ КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	ЛОЖЬ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	ОстаточнаяСтоимостьНМА.РасходБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	&ЧастичнаяЛиквидация
	|	И &СтатьяРасходовВариантРаспределенияРасходов В (ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьТоваров))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Период КАК Период,
	|	&Ссылка КАК Регистратор,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	
	|	&Организация КАК Организация,
	|	&Подразделение КАК Подразделение,
	|	&Ссылка КАК ДокументПоступленияРасходов,
	|	&СтатьяРасходов КАК СтатьяРасходов,
	|	&АналитикаРасходов КАК АналитикаРасходов,
	|	НЕОПРЕДЕЛЕНО КАК АналитикаУчетаПартий,
	|	
	|	0 КАК Стоимость,
	|	0 КАК СтоимостьБезНДС,
	|	ОстаточнаяСтоимостьНМА.РасходБУ КАК СтоимостьРегл,
	|	0 КАК НДСРегл,
	|	0 КАК ПостояннаяРазница,
	|	0 КАК ВременнаяРазница
	|ИЗ
	|	ТаблицаНМА КАК ОстаточнаяСтоимостьНМА
	|ГДЕ
	|	ЛОЖЬ
	|";
	
КонецФункции

#КонецОбласти

#Область ПроведениеРегламентированныйУчет
	
Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	Возврат УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизацииУКР("СписаниеНМА")
	+ УчетНМА.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетамУКР()
	+ ТекстЗапросаВТСуммыЧастичнойЛиквидацииУКР()
	;
		
КонецФункции

Функция ТекстЗапросаВТСуммыЧастичнойЛиквидацииУКР()
	
	Возврат
	"ВЫБРАТЬ
	|	Операция.НематериальныйАктив КАК ОбъектУчета,
	|
	|	Операция.СуммаСписанияБУ - ВЫРАЗИТЬ(Операция.СуммаСписанияБУ КАК ЧИСЛО(15,2)) КАК СуммаБУ,
	|	Операция.СуммаСписанияНУ - ВЫРАЗИТЬ(Операция.СуммаСписанияНУ КАК ЧИСЛО(15,2)) КАК СуммаНУ,
	|	0 КАК СуммаПР
	|	
	|ПОМЕСТИТЬ втВостановительныеСтоимостиЧастичнойЛиквидации
	|ИЗ
	|	Документ.СписаниеНМА КАК Операция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Операция.НематериальныйАктив = втОстаткиПоСчетам.ОбъектУчета
	|
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|;
	|
	|ВЫБРАТЬ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.ОбъектУчета КАК ОбъектУчета,
	|	
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ КАК ВосстановительнаяСтоимостьБУ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ КАК ВосстановительнаяСтоимостьНУ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПР КАК ВосстановительнаяСтоимостьПР, //Тут = 0 из временной таблицы
	|	
	|	
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) * втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ / ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(15,2)) КОНЕЦ КАК НакопленнаяАмортизацияБУ,
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) = 0 ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0)) * втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ / ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК ЧИСЛО(15,2)) КОНЕЦ КАК НакопленнаяАмортизацияНУ,
	|	0 КАК НакопленнаяАмортизацияПР,
	|
	|	
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ
	|	-
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) = 0 ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0)) * втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаБУ / ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК ЧИСЛО(15,2)) КОНЕЦ КАК ОстаточнаяСтоимостьБУ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ
	|	-
	|	ВЫБОР КОГДА ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) = 0 ТОГДА 0
	|	ИНАЧЕ ВЫРАЗИТЬ((ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0)) * втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаНУ / ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0) КАК ЧИСЛО(15,2)) КОНЕЦ КАК ОстаточнаяСтоимостьНУ,
	|	втВостановительныеСтоимостиЧастичнойЛиквидации.СуммаПР
	|	-
	|	0 КАК ОстаточнаяСтоимостьПР
	|	
	|ПОМЕСТИТЬ втСуммыЧастичнойЛиквидации
	|ИЗ
	|	втВостановительныеСтоимостиЧастичнойЛиквидации КАК втВостановительныеСтоимостиЧастичнойЛиквидации
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО втВостановительныеСтоимостиЧастичнойЛиквидации.ОбъектУчета = втОстаткиПоСчетам.ОбъектУчета" + ";";
	
КонецФункции

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	Разделитель = Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
	
	Возврат УчетНМА.ТекстОтраженияВРеглУчетеНачисленнойАмортизацииУКР()
	+ Разделитель + СписаниеНакопленнойАмортизацииУКР()
	+ Разделитель + СписаниеСтоимостиУКР()
	+ Разделитель + ЧастичнаяЛиквидацияНакопленнойАмортизацииУКР()
	+ Разделитель + ЧастичнаяЛиквидацияСтоимостиНМАУКР()
	;
	
КонецФункции

Функция СписаниеНакопленнойАмортизацииУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Списание накопленной амортизации НМА (Дт СчетНачисленияАмортизации:: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Операция.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Списание начисленной амортизации НМА"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеНМА КАК Операция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Операция.НематериальныйАктив = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Операция.НематериальныйАктив = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция СписаниеСтоимостиУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Списание остаточной стоимости НМА на расходы (Дт СчетРасходов:: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьБУ, 0) КАК Сумма,
	|
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.ОстаточнаяСтоимостьНУ, 0) КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Списание остаточной стоимости НМА на расходы"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеНМА КАК Операция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Операция.НематериальныйАктив = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Операция.НематериальныйАктив = втОстаткиПоСчетам.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Операция.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И НЕ Операция.ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияНакопленнойАмортизацииУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация накопленной амортизации НМА (Дт СчетНачисленияАмортизации:: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияБУ КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	Операция.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.НакопленнаяАмортизацияНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация накопленной амортизации"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеНМА КАК Операция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|	ПО Операция.НематериальныйАктив = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Операция.НематериальныйАктив = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|";
	
КонецФункции

Функция ЧастичнаяЛиквидацияСтоимостиНМАУКР()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////
	|// Частичная ликвидация остаточной стоимости НМА на расходы (Дт СчетРасходов:: Кт СчетУчета)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьБУ КАК Сумма,
	|	
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСчетовРеглУчета.Расходы) КАК ВидСчетаДт,
	|	Операция.СтатьяРасходов КАК АналитикаУчетаДт,
	|	Операция.Подразделение КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	Операция.Подразделение КАК ПодразделениеДт,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельностиДт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.ПустаяСсылка) КАК СчетДт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУ КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	Операция.Подразделение КАК ПодразделениеКт,
	|	Операция.НематериальныйАктив.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	
	|	Операция.НематериальныйАктив КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	втСуммыЧастичнойЛиквидации.ОстаточнаяСтоимостьНУ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	
	|	""Частичная ликвидация стоимости"" КАК Содержание
	|ИЗ
	|	Документ.СписаниеНМА КАК Операция
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|	ПО Операция.НематериальныйАктив = СчетаОтражения.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСуммыЧастичнойЛиквидации КАК втСуммыЧастичнойЛиквидации
	|	ПО Операция.НематериальныйАктив = втСуммыЧастичнойЛиквидации.ОбъектУчета
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК СтатьиСтроительства
	|	ПО Операция.СтатьяРасходов = СтатьиСтроительства.Ссылка
	|		И СтатьиСтроительства.ВариантРаспределенияРасходов = ЗНАЧЕНИЕ(Перечисление.ВариантыРаспределенияРасходов.НаПрочиеАктивы)
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.ЧастичнаяЛиквидация
	|";
	
КонецФункции

#КонецОбласти

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "НА3";
	КомандаПечати.Представление = НСтр("ru='Форма НА-3';uk='Форма НА-3'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБПКлиент.ВыполнитьКомандуПечати";
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "НА3") Тогда
		ИмяМакета = "";
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "НА3", НСтр("ru='Форма НА-3';uk='Форма НА-3'"),
			ПечатьНА3(МассивОбъектов, ОбъектыПечати, ПараметрыВывода),
			,
			,
			,
			Ложь // ЭтоМногоязычнаяПечатнаяФорма
		);
	КонецЕсли;
	
КонецПроцедуры


// Функция формирует табличный документ с типовой печатной формой НА-3
//
// Возвращаемое значение:
//  Табличный документ - печатная форма
//
Функция ПечатьНА3(МассивОбъектов, ОбъектыПечати, ПараметрыВывода)
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТабДокумент   = Новый ТабличныйДокумент();
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СписаниеНМА_НА3";
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_UK_НА3");
	
	ПервыйДокумент = Истина;
	
	Для Каждого Ссылка Из МассивОбъектов Цикл	
		
		Если Не ПервыйДокумент Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		// Запомним номер строки, с которой начали выводить текущий документ.
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
	
	
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("Ссылка"				,Ссылка);
		Запрос.УстановитьПараметр("Период"				,Ссылка.МоментВремени());
		Запрос.УстановитьПараметр("Организация"			,Ссылка.Организация);
		Запрос.УстановитьПараметр("НематериальныйАктив" ,Ссылка.НематериальныйАктив);
		Запрос.Текст = 
		 "ВЫБРАТЬ
		 |	СписаниеНМА.Номер КАК НомерДок,
		 |	СписаниеНМА.Дата КАК ДатаДок,
		 |	ВЫРАЗИТЬ(СписаниеНМА.Организация.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеПолноеОрганизации,
		 |	СписаниеНМА.Организация.КодПоЕДРПОУ КАК КодПоЕДРПОУ,
		 |	ВЫРАЗИТЬ(СписаниеНМА.НематериальныйАктив.НаименованиеПолное КАК СТРОКА(1000)) КАК НаименованиеПолное,
		 |	СписаниеНМА.НематериальныйАктив.ПрочиеСведения КАК ПрочиеСведения,
		 |	МестонахождениеНМАБухгалтерскийУчетСрезПоследних.МОЛ,
		 |	МестонахождениеНМАБухгалтерскийУчетСрезПоследних.Местонахождение,
		 |	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимость,
		 |	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокИспользования,
		 |	СчетаБухгалтерскогоУчетаНМАСрезПоследних.СчетУчета КАК СчетУчетаБУ,
		 |	ЕСТЬNULL(ДанныеСчетУчета.СуммаОстатокДт, 0) - ЕСТЬNULL(ДанныеСчетаАмортизации.СуммаОборотКт, 0) КАК ОстаточнаяСтоимость,
		 |	ДанныеСчетаРасходов.Счет КАК СчетСписанияБУ,
		 |	ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.ПервоначальнаяСтоимость КАК ПервоначальнаяСтоимость
		 |ИЗ
		 |	Документ.СписаниеНМА КАК СписаниеНМА
		 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеНМАБухгалтерскийУчет.СрезПоследних(
		 |				&Период,
		 |				НематериальныйАктив = &НематериальныйАктив
		 |					И Организация = &Организация) КАК МестонахождениеНМАБухгалтерскийУчетСрезПоследних
		 |		ПО СписаниеНМА.НематериальныйАктив = МестонахождениеНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
		 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияНМАБухгалтерскийУчет.СрезПоследних(
		 |				&Период,
		 |				НематериальныйАктив = &НематериальныйАктив
		 |					И Организация = &Организация) КАК ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних
		 |		ПО СписаниеНМА.НематериальныйАктив = ПервоначальныеСведенияНМАБухгалтерскийУчетСрезПоследних.НематериальныйАктив
		 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(
		 |				&Период,
		 |				НематериальныйАктив = &НематериальныйАктив
		 |					И Организация = &Организация) КАК СчетаБухгалтерскогоУчетаНМАСрезПоследних
		 |		ПО СписаниеНМА.НематериальныйАктив = СчетаБухгалтерскогоУчетаНМАСрезПоследних.НематериальныйАктив
		 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ОстаткиИОбороты(
		 |				,
		 |				&Период,
		 |				,
		 |				,
		 |				Счет В
		 |					(ВЫБРАТЬ
		 |						Т.СчетНачисленияАмортизации
		 |					ИЗ
		 |						РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&Период, НематериальныйАктив = &НематериальныйАктив
		 |							И Организация = &Организация) КАК Т),
		 |				,
		 |				(Организация, Подразделение, Субконто1) В
		 |					(ВЫБРАТЬ
		 |						СписаниеНМА.Организация,
		 |						СписаниеНМА.Подразделение,
		 |						СписаниеНМА.НематериальныйАктив
		 |					ИЗ
		 |						Документ.СписаниеНМА КАК СписаниеНМА
		 |					ГДЕ
		 |						СписаниеНМА.Ссылка = &Ссылка)) КАК ДанныеСчетаАмортизации
		 |		ПО СписаниеНМА.НематериальныйАктив = ДанныеСчетаАмортизации.Субконто1
		 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Остатки(
		 |				&Период,
		 |				Счет В
		 |					(ВЫБРАТЬ
		 |						Т.СчетУчета
		 |					ИЗ
		 |						РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&Период, НематериальныйАктив = &НематериальныйАктив
		 |							И Организация = &Организация) КАК Т),
		 |				,
		 |				) КАК ДанныеСчетУчета
		 |		ПО СписаниеНМА.НематериальныйАктив = ДанныеСчетУчета.Субконто1
		 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.Обороты(
		 |				,
		 |				&Период,
		 |				,
		 |				,
		 |				,
		 |				(Организация, Подразделение, Субконто1) В
		 |					(ВЫБРАТЬ
		 |						СписаниеНМА.Организация,
		 |						СписаниеНМА.Подразделение,
		 |						СписаниеНМА.СтатьяРасходов
		 |					ИЗ
		 |						Документ.СписаниеНМА КАК СписаниеНМА
		 |					ГДЕ
		 |						СписаниеНМА.Ссылка = &Ссылка),
		 |				КорСчет В
		 |					(ВЫБРАТЬ
		 |						Т.СчетУчета
		 |					ИЗ
		 |						РегистрСведений.СчетаБухгалтерскогоУчетаНМА.СрезПоследних(&Период, НематериальныйАктив = &НематериальныйАктив
		 |							И Организация = &Организация) КАК Т),
		 |				) КАК ДанныеСчетаРасходов
		 |		ПО СписаниеНМА.НематериальныйАктив = ДанныеСчетаРасходов.КорСубконто1
		 |ГДЕ
		 |	СписаниеНМА.Ссылка = &Ссылка";
		Результат = Запрос.Выполнить();
		ВыборкаПоНМА = Результат.Выбрать();
		
		ВыборкаПоКомиссии = ОбщегоНазначенияБПВызовСервера.ПолучитьСведенияОКомиссии(Ссылка);
		ОтветственныеЛицаОрганизации = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Ссылка.Организация,Ссылка.Дата);
		
		Пока ВыборкаПоНМА.Следующий() Цикл
			
			ОбластьМакета = Макет.ПолучитьОбласть("НА3");
			Параметры     = ОбластьМакета.Параметры;
			Параметры.Заполнить(ВыборкаПоКомиссии);
			Параметры.Заполнить(ВыборкаПоНМА);
			
			ОсновныеСотрудникиФизическихЛицМОЛ = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(ВыборкаПоНМА.МОЛ, Истина, Ссылка.Организация, Ссылка.Дата);
			Если ЗначениеЗаполнено(ОсновныеСотрудникиФизическихЛицМОЛ) Тогда
				Для каждого Строка Из ОсновныеСотрудникиФизическихЛицМОЛ Цикл
				    ДанныеФизЛицаПолучил = КадровыйУчет.КадровыеДанныеСотрудников(Истина, Строка.Сотрудник, "Должность", Ссылка.Дата);			
				КонецЦикла;
				Для каждого СтрокаДанныеПолучил Из ДанныеФизЛицаПолучил Цикл			
					ОбластьМакета.Параметры.МОЛДолжность = СтрокаДанныеПолучил.Должность;
					ОбластьМакета.Параметры.МОЛФИО 			= СтрокаДанныеПолучил.ФизическоеЛицо;			
				КонецЦикла;
			КонецЕсли;
 	
			Параметры.ФИОРук      = ОтветственныеЛицаОрганизации[Метаданные.Перечисления.ОтветственныеЛицаОрганизаций.ЗначенияПеречисления.Руководитель.Имя + "Наименование"];
			Параметры.ФИОБух      = ОтветственныеЛицаОрганизации[Метаданные.Перечисления.ОтветственныеЛицаОрганизаций.ЗначенияПеречисления.ГлавныйБухгалтер.Имя + "Наименование"];
			
			ТабДокумент.Вывести(ОбластьМакета);
			НеНачало = Истина;
			
		КонецЦикла;
		
		// В табличном документе зададим имя области, в которую был 
		// выведен объект. Нужно для возможности печати покомплектно.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, 
			НомерСтрокиНачало, ОбъектыПечати, Ссылка);
		
	КонецЦикла;	
		
	Возврат ТабДокумент;

КонецФункции // ПечатьНА3()

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетНМА.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
