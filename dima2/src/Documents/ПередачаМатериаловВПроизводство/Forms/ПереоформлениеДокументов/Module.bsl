#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Склад = Параметры.Склад;
	Заказ = Параметры.Заказы[0];
	
	СписокЗаказов.ЗагрузитьЗначения(Параметры.Заказы);
	ТаблицаНакладных = НакладныеПоЗаказам(Параметры.Заказы);
	ОформленныеНакладные.Загрузить(ТаблицаНакладных);
	
	Элементы.ЗаказТаблица.Видимость = Параметры.Заказы.Количество() > 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если (ИмяСобытия = "Запись_ПередачаМатериаловВПроизводство"
		ИЛИ ИмяСобытия = "Запись_ЗаказМатериаловВПроизводство"
		ИЛИ ИмяСобытия = "Запись_ЗаказНаПроизводство") Тогда
		
		Элементы.ТоварыНакладной.Обновить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОформленныеНакладные

&НаКлиенте
Процедура ОформленныеНакладныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	УстановитьПараметрыСписков();
	
	ДанныеНакладная = Элементы.ОформленныеНакладные.ТекущиеДанные;
	ПоказатьЗначение(, ДанныеНакладная.Накладная);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформленныеНакладныеПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("УстановитьПараметрыСписков", 0.2, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// Условное оформление динамического списка "Список"
	СписокУсловноеОформление = ТоварыНакладной.КомпоновщикНастроек.Настройки.УсловноеОформление;
	СписокУсловноеОформление.Элементы.Очистить();
	
	// Положительная общая разница
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru='Положительная общая разница';uk='Позитивна загальна різниця'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ДельтаОтгрузкиОбщая");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДельтаОтгрузкиОбщая");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);
	
	// Отрицательная общая разница
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru='Отрицательная общая разница';uk='Від''ємна загальна різниця'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ДельтаОтгрузкиОбщая");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДельтаОтгрузкиОбщая");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);
	
	// Положительная разница
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru='Положительная разница';uk='Позитивна різниця'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ДельтаОтгрузки");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДельтаОтгрузки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ОтметкаПоложительногоВыполненияЗадачи);
	
	// Отрицательная разница
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru='Отрицательная разница';uk='Від''ємна різниця'");
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ДельтаОтгрузки");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДельтаОтгрузки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Меньше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ОтметкаОтрицательногоВыполненияЗадачи);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыСписков()
	
	ДанныеНакладная = Элементы.ОформленныеНакладные.ТекущиеДанные;
	Если ДанныеНакладная <> Неопределено Тогда
		ТоварыНакладной.Параметры.УстановитьЗначениеПараметра("Накладная", ДанныеНакладная.Накладная);
	Иначе
		ТоварыНакладной.Параметры.УстановитьЗначениеПараметра("Накладная", Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перезаполнить(Команда)
	
	ПерезаполняемаяНакладная = Элементы.ОформленныеНакладные.ТекущиеДанные.Накладная;
	
	Если ПерезаполняемаяНакладная <> Неопределено Тогда
		
		Форма = ПолучитьФорму("Документ.ПередачаМатериаловВПроизводство.ФормаОбъекта", Новый Структура("Ключ", ПерезаполняемаяНакладная));
		ДанныеФормы = Форма.Объект;
		
		СтруктураЗаполнения = ПродажиВызовСервера.ПерезаполнитьНакладнуюПоОрдерам(ДанныеФормы);
		СостояниеОтгрузки = СтруктураЗаполнения.СостояниеОтгрузки;
		
		Если СтруктураЗаполнения.ЕстьИзменения Тогда
			Форма.Модифицированность = Истина;
			КопироватьДанныеФормы(ДанныеФормы, Форма.Объект); 
			
			Форма.Открыть();
			
			ПродажиКлиент.ОповеститьОбОкончанииПерезаполненияТоваровПоОтгрузке(
				СтруктураЗаполнения.ЕстьИзменения,
				СостояниеОтгрузки.ЕстьОтгруженныеТовары Или СостояниеОтгрузки.ЕстьНесобранныеТовары,
				СостояниеОтгрузки.ЕстьНесобранныеТовары);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПоОтгрузке(Команда)
	
	ИмяФормыРеализации = "Документ.ПередачаМатериаловВПроизводство.ФормаОбъекта";
	МассивЗаказов = СписокЗаказов.ВыгрузитьЗначения();
	ПараметрыОформления = Новый Структура("ПоЗаказам, ПоОрдерам", Ложь, Истина);
	ПараметрыОснования = Новый Структура;
	
	Если МассивЗаказов.Количество() = 1 Тогда
		
		ПараметрыОснования.Вставить("ДокументОснование",   Заказ);
		ПараметрыОснования.Вставить("СкладОтгрузки",       Склад);
		Если ПараметрыОформления <> Неопределено Тогда
			ПараметрыОснования.Вставить("ПараметрыОформления", ПараметрыОформления);
		КонецЕсли;
		ОткрытьФорму(ИмяФормыРеализации, Новый Структура("Основание", ПараметрыОснования));
	Иначе
		РеквизитыШапки = Новый Структура();
		ОчиститьСообщения();
		
		Если ПродажиВызовСервера.СформироватьДанныеЗаполненияРеализации(МассивЗаказов, РеквизитыШапки) Тогда
			
			ПараметрыОснования.Вставить("РеквизитыШапки",      РеквизитыШапки);
			ПараметрыОснования.Вставить("ДокументОснование",   МассивЗаказов);
			ПараметрыОснования.Вставить("СкладОтгрузки",       Склад);
			Если ПараметрыОформления <> Неопределено Тогда
				ПараметрыОснования.Вставить("ПараметрыОформления", ПараметрыОформления);
			КонецЕсли;
			ОткрытьФорму(ИмяФормыРеализации, Новый Структура("Основание", ПараметрыОснования));
			
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция НакладныеПоЗаказам(Заказы)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор      КАК Накладная
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке КАК Таблица
		|ГДЕ
		|	Таблица.Активность
		|	И Таблица.ДокументОтгрузки В(&Заказы)
		|	И Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	И Таблица.КОформлению > 0";
	
	Запрос.УстановитьПараметр("Заказы", Заказы);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти