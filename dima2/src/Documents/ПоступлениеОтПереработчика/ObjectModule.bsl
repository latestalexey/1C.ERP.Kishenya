#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ЗаполнениеУсловийЗакупок

// Заполняет условия продаж в заказе поставщику
//
// Параметры:
//	УсловияЗакупок - Структура - Структура для заполнения
//
Процедура ЗаполнитьУсловияЗакупок(Знач УсловияЗакупок) Экспорт 
	
	Если УсловияЗакупок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Организация) И Организация.Пустая() Тогда
		Организация = УсловияЗакупок.Организация;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Склад) И Склад.Пустая() Тогда
		
		Склад = УсловияЗакупок.Склад;
		
		СтруктураОтветственного = ЗакупкиСервер.ПолучитьОтветственногоПоСкладу(Склад, Менеджер);
		Если СтруктураОтветственного <> Неопределено Тогда
			Принял = СтруктураОтветственного.Ответственный;
			ПринялДолжность = СтруктураОтветственного.ОтветственныйДолжность;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УсловияЗакупок.Контрагент) И УсловияЗакупок.Контрагент <> Контрагент Тогда
		Контрагент = УсловияЗакупок.Контрагент;
		БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент);
	КонецЕсли;
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
	
	Если УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов <> Неопределено И УсловияЗакупок.ИспользуютсяДоговорыКонтрагентов Тогда
		
		ХозяйственнаяОперацияДоговора = Перечисления.ХозяйственныеОперации.ПроизводствоУПереработчика;
		Параметрыобъекта = ПараметрыОбъектаССоглашением();
		Договор = ЗакупкиСервер.ПолучитьДоговорПоУмолчанию(Параметрыобъекта, ХозяйственнаяОперацияДоговора);
		
		ЗакупкиВызовСервера.ЗаполнитьБанковскиеСчетаПоДоговору(
			Договор,
			БанковскийСчетОрганизации,
			БанковскийСчетКонтрагента);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		НалоговоеНазначение = Справочники.Организации.НалоговоеНазначениеНДС(Организация, Дата);
	КонецЕсли;	
	
КонецПроцедуры

// Заполняет условия закупок по торговому соглашению с поставщиком
//
// Параметры:
//	ПересчитатьЦены - Булево - Истина, если необходимо пересчитать цены в табличной части документа
//
Процедура ЗаполнитьУсловияЗакупокПоУмолчанию() Экспорт 
	
	Если ЗначениеЗаполнено(Партнер) Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("УчитыватьГруппыСкладов", Истина);
		ПараметрыОтбора.Вставить("ИсключитьГруппыСкладовДоступныеВЗаказах", Истина);
		
		УсловияЗакупокПоУмолчанию = ЗакупкиСервер.ПолучитьУсловияЗакупокПоУмолчанию(Партнер, ПараметрыОтбора);
		
		Если УсловияЗакупокПоУмолчанию <> Неопределено Тогда
			ЗаполнитьУсловияЗакупок(УсловияЗакупокПоУмолчанию);
		Иначе
			ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
		КонецЕсли;
		
		БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(
			Контрагент,
			,
			БанковскийСчетКонтрагента);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) 
	
	///////////////////////////////////////////////////////////////////////////
	// Вспомогательные переменные
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	///////////////////////////////////////////////////////////////////////////
	// ТЧ - Характеристики и Количество
	
	// Характеристики, Количество
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект,
		МассивНепроверяемыхРеквизитов,
		Отказ);
	
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ);
		
	ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
	ПараметрыПроверки.ИмяТЧ = "ВозвратныеОтходы";
	НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(
		ЭтотОбъект,
		МассивНепроверяемыхРеквизитов,
		Отказ,
		ПараметрыПроверки);
	
	ПараметрыПроверки = ОбщегоНазначенияУТ.ПараметрыПроверкиЗаполненияКоличества();
	ПараметрыПроверки.ИмяТЧ = "ВозвратныеОтходы";
	ОбщегоНазначенияУТ.ПроверитьЗаполнениеКоличества(ЭтотОбъект, ПроверяемыеРеквизиты, Отказ, ПараметрыПроверки);
	
	///////////////////////////////////////////////////////////////////////////
	// ТЧ - Серии
	
	Если Не НаправленияДеятельностиСервер.УказаниеНаправленияДеятельностиОбязательно(ХозяйственнаяОперация) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НаправлениеДеятельности");
	КонецЕсли;
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПоступлениеОтПереработчика);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
		ПараметрыУказанияСерий.Товары,
		Отказ,
		МассивНепроверяемыхРеквизитов);
	НоменклатураСервер.ПроверитьЗаполнениеСерий(ЭтотОбъект,
		ПараметрыУказанияСерий.ВозвратныеОтходы,
		Отказ,
		МассивНепроверяемыхРеквизитов);

	///////////////////////////////////////////////////////////////////////////
	// ТЧ - Заказы переработчику
	
#Область ЗапросПроверки
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	""Товары"" КАК ИмяТЧ,
	|	&СинонимТовары КАК СинонимТЧ,
	|	Т.НомерСтроки,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.ЗаказПереработчику
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""ВозвратныеОтходы"" ИмяТЧ,
	|	&СинонимВО КАК СинонимТЧ,
	|	Т.НомерСтроки,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	Т.ЗаказПереработчику,
	|	Т.Цена,
	|	Т.Сумма
	|ПОМЕСТИТЬ ВозвратныеОтходы
	|ИЗ
	|	&ВозвратныеОтходы КАК Т
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ИмяТЧ,
	|	Т.СинонимТЧ,
	|	Т.НомерСтроки,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	ЕСТЬNULL(Заказ.Ссылка, &ЗаказПереработчику) КАК ЗаказПереработчику,
	|	Т.Цена,
	|	Т.Сумма
	|ПОМЕСТИТЬ ВтДанные
	|ИЗ
	|	ВозвратныеОтходы КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК Заказ
	|		ПО Т.ЗаказПереработчику = Заказ.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Т.ИмяТЧ,
	|	Т.СинонимТЧ,
	|	Т.НомерСтроки,
	|	Т.Номенклатура,
	|	Т.Характеристика,
	|	ЕСТЬNULL(Заказ.Ссылка, &ЗаказПереработчику),
	|	NULL КАК Цена,
	|	NULL КАК Сумма
	|ИЗ
	|	Товары КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК Заказ
	|		ПО Т.ЗаказПереработчику = Заказ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.ИмяТЧ,
	|	Т.СинонимТЧ,
	|	Т.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР КОГДА &ПроверятьЗаказ И Т.ЗаказПереработчику = ЗНАЧЕНИЕ(Документ.ЗаказПереработчику.ПустаяСсылка) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаЗаказ,
	|	ВЫБОР КОГДА Т.Цена = 0 ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаЦена,
	|	ВЫБОР КОГДА Т.Сумма = 0 ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаСумма,
	|	ВЫБОР КОГДА Товар.Ссылка ЕСТЬ NULL ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаТип,
	|	ВЫБОР КОГДА ЕСТЬNULL(Обороты.ЗаказаноПриход, 0) > 0 ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаСостав
	|ИЗ
	|	ВтДанные КАК Т
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товар
	|		ПО Т.Номенклатура = Товар.Ссылка
	|		И Товар.ТипНоменклатуры В (ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|									ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа),
	|									ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара))
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыКлиентов.Обороты(, ,
	|						Период,
	|						(ЗаказКлиента, Номенклатура, Характеристика) В (ВЫБРАТЬ РАЗЛИЧНЫЕ Т.ЗаказПереработчику, Т.Номенклатура, Т.Характеристика ИЗ ВтДанные КАК Т)) КАК Обороты
	|		ПО Т.Номенклатура = Обороты.Номенклатура
	|		И Т.Характеристика = Обороты.Характеристика
	|		И Т.ЗаказПереработчику = Обороты.ЗаказКлиента
	|ГДЕ
	|	(&ПроверятьЗаказ И Т.ЗаказПереработчику = ЗНАЧЕНИЕ(Документ.ЗаказПереработчику.ПустаяСсылка))
	|	ИЛИ (Товар.Ссылка ЕСТЬ NULL)
	|	ИЛИ (Т.Цена = 0)
	|	ИЛИ (Т.Сумма = 0)
	|	ИЛИ (ЕСТЬNULL(Обороты.ЗаказаноПриход, 0) > 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяТЧ,
	|	НомерСтроки
	|");
#КонецОбласти
	
	Запрос.УстановитьПараметр("ЗаказПереработчику",	ЗаказПереработчику);
	Запрос.УстановитьПараметр("СинонимТовары",		НСтр("ru='Продукция';uk='Продукція'"));
	Запрос.УстановитьПараметр("СинонимВО",			НСтр("ru='Возвратные отходы';uk='Зворотні відходи'"));
	Запрос.УстановитьПараметр("Товары",				Товары.Выгрузить(, "НомерСтроки, Номенклатура, Характеристика, ЗаказПереработчику"));
	Запрос.УстановитьПараметр("ВозвратныеОтходы",	ВозвратныеОтходы.Выгрузить(, "НомерСтроки, Номенклатура, Характеристика, ЗаказПереработчику, Цена, Сумма"));
	Запрос.УстановитьПараметр("ПроверятьЗаказ",		(ПоступлениеПоЗаказам И Не ЗначениеЗаполнено(ЗаказПереработчику)));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Цена");
	МассивНепроверяемыхРеквизитов.Добавить("ВозвратныеОтходы.Сумма");
	
#Область СообщенияОбОшибках
	Пока Выборка.Следующий() Цикл
		
		АдресОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru=' в строке %1 списка ""%2""';uk=' у рядку %1 списку ""%2""'"),
			Выборка.НомерСтроки,
			Выборка.СинонимТЧ);
		
		// Если заказ переработчику не указан в шапке, то он должен быть указан в ТЧ
		Если Выборка.ОшибкаЗаказ Тогда
			
			ТекстОшибки = НСтр("ru='Не заполнено поле ""Заказ переработчику""';uk='Не заповнено поле ""Замовлення переробнику""'");
			ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				Выборка.ИмяТЧ,
				Выборка.НомерСтроки,
				"ЗаказПереработчику");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки, ЭтотОбъект, ПутьКТЧ, , Отказ);
			
		КонецЕсли;
		
		// Нельзя вернуть от переработчика номенклатуру, которая была (или будет) передана ему в качестве сырья,
		// т.к. это противоречит принципу переработки
		Если Выборка.ОшибкаСостав Тогда
			
			ТекстОшибки = НСтр("ru='Указана номенклатура, которая была заказана переработчиком в качестве сырья,';uk='Зазначена номенклатура, яка була замовлена переробником в якості сировини,'");
			ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				Выборка.ИмяТЧ,
				Выборка.НомерСтроки,
				"Номенклатура");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки, ЭтотОбъект, ПутьКТЧ, , Отказ);
			
		КонецЕсли;
		
		// Только товары
		Если Выборка.ОшибкаТип Тогда
			
			ТекстОшибки = НСтр("ru='Необходимо выбрать номенклатуру с типом ""Товар"" или ""Тара""';uk='Необхідно вибрати номенклатуру з типом ""Товар"" або ""Тара""'");
			ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				Выборка.ИмяТЧ,
				Выборка.НомерСтроки,
				"Номенклатура");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки, ЭтотОбъект, ПутьКТЧ, , Отказ);
			
		КонецЕсли;
		
		// Цена актуальна только для фиксированной стоимости
		Если Выборка.ОшибкаЦена Тогда
			
			ТекстОшибки = НСтр("ru='Необходимо указать цену, по которой будет оприходованы возвратные отходы';uk='Необхідно зазначити ціну, по якій будуть оприбутковані зворотні відходи'");
			ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				Выборка.ИмяТЧ,
				Выборка.НомерСтроки,
				"Цена");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки, ЭтотОбъект, ПутьКТЧ, , Отказ);
			
		КонецЕсли;
		
		// Сумма актуальна только для фиксированной стоимости
		Если Выборка.ОшибкаСумма Тогда
			
			ТекстОшибки = НСтр("ru='Необходимо указать сумму, по которой будет оприходованы возвратные отходы';uk='Необхідно зазначити суму, на яку будуть оприбутковані зворотні відходи'");
			ПутьКТЧ = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти(
				Выборка.ИмяТЧ,
				Выборка.НомерСтроки,
				"Сумма");
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки + АдресОшибки, ЭтотОбъект, ПутьКТЧ, , Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
#КонецОбласти
	
	///////////////////////////////////////////////////////////////////////////
	// Док - Подразделение
	
	МассивНепроверяемыхРеквизитов.Добавить("Подразделение");
	
	Если Не ЗначениеЗаполнено(Подразделение) Тогда 
		
		ТекстОшибки = НСтр("ru='Необходимо указать подразделение';uk='Необхідно вказати підрозділ'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "Подразделение", , Отказ);
		
	КонецЕсли;
	
	///////////////////////////////////////////////////////////////////////////
	// Исключим проверенные реквизиты из дальнейшей проверки
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	///////////////////////////////////////////////////////////////////////////
	// Проверка остальных реквизитов
	
	Если Не Отказ И ОбщегоНазначенияУТ.ПроверитьЗаполнениеРеквизитовОбъекта(ЭтотОбъект, ПроверяемыеРеквизиты) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ЗакупкиСервер.ПроверитьКорректностьЗаполненияДокументаЗакупки(ЭтотОбъект,Отказ);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка) 
	Перем СкладПоступления, РеквизитыШапки;
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		
		Если ДанныеЗаполнения.Свойство("СписокПродукции") Тогда
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения.РеквизитыШапки);
			ДанныеЗаполнения.Свойство("РеквизитыШапки", РеквизитыШапки);
			ЗаполнитьНаОснованииЗаказаПереработчику(ДанныеЗаполнения.ЗаказПереработчику, ДанныеЗаполнения.СписокПродукции);
			
		Иначе
			
			ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);
			
		КонецЕсли;
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
		
		ЗаполнитьНаОснованииОтчетаПереработчика(ДанныеЗаполнения, СкладПоступления);
		
	ИначеЕсли ТипДанныхЗаполнения = Тип("ДокументСсылка.ЗаказПереработчику") Тогда
		
		ЗаполнитьНаОснованииЗаказаПереработчику(ДанныеЗаполнения);
		
	КонецЕсли;
	
	ИнициализироватьДокумент(ДанныеЗаполнения);
	ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);
	
	ЗаполнениеСвойствПоСтатистикеСервер.ЗаполнитьСвойстваОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения) 
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПроведениеСервер.УстановитьРежимПроведения(ЭтотОбъект, РежимЗаписи, РежимПроведения);
	
	ДополнительныеСвойства.Вставить("ЭтоНовый",    ЭтоНовый());
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	ПараметрыОкругления = ОбщегоНазначенияУТ.ПараметрыОкругленияКоличестваШтучныхТоваров();
	ПараметрыОкругления.ИмяТЧ = "Товары";
	ОбщегоНазначенияУТ.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи, ПараметрыОкругления);
	ПараметрыОкругления.ИмяТЧ = "ВозвратныеОтходы";
	ОбщегоНазначенияУТ.ОкруглитьКоличествоШтучныхТоваров(ЭтотОбъект, РежимЗаписи, ПараметрыОкругления);
	
	Если ПоступлениеПоЗаказам И ЗначениеЗаполнено(ЗаказПереработчику) Тогда
		
		Для Каждого ТекСтрока Из Товары Цикл
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ЗаказПереработчику) Тогда
				ТекСтрока.ЗаказПереработчику = ЗаказПереработчику;
				ТекСтрока.Распоряжение = Распоряжение;
			КонецЕсли;
			
		КонецЦикла;
		
		Для Каждого ТекСтрока Из ВозвратныеОтходы Цикл
			
			Если Не ЗначениеЗаполнено(ТекСтрока.ЗаказПереработчику) Тогда
				ТекСтрока.ЗаказПереработчику = ЗаказПереработчику;
				ТекСтрока.Распоряжение = Распоряжение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ПоступлениеПоЗаказам Тогда
		ЗаказыСервер.УстановитьКлючВСтрокахТабличнойЧасти(ЭтотОбъект, "Товары", "МаксимальныйКодСтроки");
		ЗаказыСервер.УстановитьКлючВСтрокахТабличнойЧасти(ЭтотОбъект, "ВозвратныеОтходы", "МаксимальныйКодСтроки");
	КонецЕсли; 
	
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПоступлениеОтПереработчика);
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий.Товары);
	НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий.ВозвратныеОтходы);
	
	ОбщегоНазначенияУТ.ИзменитьПризнакСогласованностиДокумента(ЭтотОбъект, РежимЗаписи);
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		МестаУчета = РегистрыСведений.АналитикаУчетаНоменклатуры.МестаУчета(
			Перечисления.ХозяйственныеОперации.ПоступлениеОтПереработчика,
			Склад,
			Подразделение,
			Партнер);
		
		// Если Склад - группа, то для аналитики учета номенклатуры склад берем из ТЧ
		ИменаПолей = РегистрыСведений.АналитикаУчетаНоменклатуры.ИменаПолейКоллекцииПоУмолчанию();
		Если Склад.ЭтоГруппа И Склад.ВыборГруппы = Перечисления.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных Тогда
			ИменаПолей.Вставить("Произвольный", "Склад");
		КонецЕсли;
		
		РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(
			Товары,
			МестаУчета,
			ИменаПолей);
			
		РегистрыСведений.АналитикаУчетаНоменклатуры.ЗаполнитьВКоллекции(
			ВозвратныеОтходы,
			МестаУчета,
			ИменаПолей);
		
		ЗаполнитьВидыЗапасовДокумента();
		ЗаполнитьКлючиАналитикиУчетаПартийДокумента();
		ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(Товары);
		ВзаиморасчетыСервер.ЗаполнитьИдентификаторыСтрокВТабличнойЧасти(ВозвратныеОтходы);
		
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		
		ЗапасыСервер.ОчиститьАналитикуУчетаПартийВТабличнойЧасти(Товары);
		ЗапасыСервер.ОчиститьАналитикуУчетаПартийВТабличнойЧасти(ВозвратныеОтходы);
		
	КонецЕсли;
	
	
	ЕстьНазначение = Ложь;
	Если Товары.Количество() > 0 Тогда
		
		ПараметрыОтбора = Новый Структура("Назначение", Справочники.Назначения.ПустаяСсылка());
		Если Товары.Количество() <> Товары.НайтиСтроки(ПараметрыОтбора).Количество() Тогда
			ЕстьНазначение = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВозвратныеОтходы.Количество() > 0 И Не ЕстьНазначение Тогда
		
		ПараметрыОтбора = Новый Структура("Назначение", Справочники.Назначения.ПустаяСсылка());
		Если ВозвратныеОтходы.Количество() <> ВозвратныеОтходы.НайтиСтроки(ПараметрыОтбора).Количество() Тогда
			ЕстьНазначение = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	УчетГотовойПродукцииПоПлановойСтоимости = Истина;											
	Если НЕ УчетГотовойПродукцииПоПлановойСтоимости Тогда
		ВидЦены = Справочники.ВидыЦен.ПустаяСсылка();
		Для Каждого Стр Из Товары Цикл 
			Стр.Цена = 0;
			Стр.Сумма = 0;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства, РежимПроведения);
	
	Документы.ПоступлениеОтПереработчика.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	ЗаказыСервер.ОтразитьЗаказыПоставщикам(ДополнительныеСвойства, Движения, Отказ);
	ЗаказыСервер.ОтразитьТоварыКПоступлению(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьТоварыНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьСвободныеОстатки(ДополнительныеСвойства, Движения, Отказ);
	ЗаказыСервер.ОтразитьДвижениеТоваров(ДополнительныеСвойства, Движения, Отказ);
	СкладыСервер.ОтразитьДвиженияСерийТоваров(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьОбеспечениеЗаказов(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьДатыПоступленияТоваровОрганизаций(ДополнительныеСвойства, Отказ);
	ЗапасыСервер.ОтразитьТоварыОрганизаций(ДополнительныеСвойства, Движения, Отказ);
	ДоходыИРасходыСервер.ОтразитьСебестоимостьТоваров(ДополнительныеСвойства, Движения, Отказ);
	ПартионныйУчетСервер.ОтразитьПартииТоваровОрганизаций(ДополнительныеСвойства, Движения, Отказ);
	ЗапасыСервер.ОтразитьТоварыПолученныеОтПереработчика(ДополнительныеСвойства, Движения, Отказ);
	ВзаиморасчетыСервер.ОтразитьСуммыДокументаВВалютеРегл(ДополнительныеСвойства, Движения, Отказ);
	ЗатратыСервер.ОтразитьМатериалыИРаботыВПроизводстве(ДополнительныеСвойства, Движения, Отказ);
	ЗатратыСервер.ОтразитьПартииПроизводственныхЗатрат(ДополнительныеСвойства, Движения, Отказ);
	РегистрыНакопления.ТоварыКОформлениюПоступления.ОтразитьДвижения(ДополнительныеСвойства, Движения, Отказ);
	ЗатратыСервер.ОтразитьВыпускПродукции(ДополнительныеСвойства, Движения, Отказ);
	
	РеглУчетПроведениеСервер.ЗарегистрироватьКОтражению(ЭтотОбъект, ДополнительныеСвойства, Движения, Отказ);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ) 
	
	ПроведениеСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	СформироватьСписокРегистровДляКонтроля();
	
	ПроведениеСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ПроведениеСервер.ВыполнитьКонтрольРезультатовПроведения(ЭтотОбъект, Отказ);
	
	ПроведениеСервер.ОчиститьДополнительныеСвойстваДляПроведения(ДополнительныеСвойства);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования) 

	Согласован               = Ложь;
	ВидЗапасов               = Неопределено;
	ПоступлениеПоЗаказам     = Ложь;
	ЗаказПереработчику       = Документы.ЗаказПереработчику.ПустаяСсылка();
	ДатаВходящегоДокумента   = Дата(1,1,1);
	НомерВходящегоДокумента  = "";
	
	Для Каждого ТекСтрока Из Товары Цикл
		ТекСтрока.ЗаказПереработчику = Документы.ЗаказПереработчику.ПустаяСсылка();
		ТекСтрока.Распоряжение = Неопределено;
		ТекСтрока.КодСтроки = 0;
	КонецЦикла;

	Для Каждого ТекСтрока Из ВозвратныеОтходы Цикл
		ТекСтрока.ЗаказПереработчику = Документы.ЗаказПереработчику.ПустаяСсылка();
		ТекСтрока.Распоряжение = Неопределено;
		ТекСтрока.КодСтроки = 0;
	КонецЦикла;

	ТоварыСерии.Очистить();
	ВозвратныеОтходыСерии.Очистить();
	
	ИнициализироватьДокумент();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ИнициализацияИЗаполнение

Процедура ЗаполнитьНаОснованииОтчетаПереработчика(Знач ДокументОснование, Знач СкладПоступления) 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Текст =
	// 0
	"ВЫБРАТЬ
	|	ОтчетПереработчика.Сделка				КАК Сделка,
	|	ОтчетПереработчика.ЗаказПереработчику	КАК ЗаказПереработчику,
	|	ОтчетПереработчика.Партнер				КАК Партнер,
	|	ОтчетПереработчика.Контрагент			КАК Контрагент,
	|	ОтчетПереработчика.Договор				КАК Договор,
	|	ОтчетПереработчика.Организация			КАК Организация,
	|	ОтчетПереработчика.Валюта				КАК Валюта,
	|	ОтчетПереработчика.ПоЗаказам			КАК ПоступлениеПоЗаказам,
	|
	|	ВЫБОР КОГДА Заказ.Склад.ЭтоГруппа
	|			  И Заказ.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах) ТОГДА
	|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|	ИНАЧЕ
	|		Заказ.Склад
	|	КОНЕЦ									КАК Склад,
	|
	|	ВЫБОР КОГДА Заказ.Склад.ЭтоГруппа
	|			  И Заказ.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах) ТОГДА
	|		ИСТИНА
	|	ИНАЧЕ
	|		ЛОЖЬ
	|	КОНЕЦ									КАК ЗапрещеноВыбиратьГруппуСкладов,
	|
	|	Заказ.Статус							КАК СтатусДокумента,
	|	ОтчетПереработчика.Подразделение		КАК Подразделение,
	|	НЕ Заказ.Проведен						КАК ЕстьОшибкиПроведен,
	|
	|	ВЫБОР КОГДА Заказ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПереработчикам.КИсполнению)
	|			ИЛИ Заказ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПереработчикам.Закрыт) ТОГДА
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ									КАК ЕстьОшибкиСтатус,
	|
	|	Заказ.БанковскийСчет					КАК БанковскийСчетОрганизации,
	|	Заказ.НаправлениеДеятельности			КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВтКОформлению
	|ИЗ
	|	Документ.ОтчетПереработчика КАК ОтчетПереработчика
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказПереработчику КАК Заказ
	|	ПО
	|		ОтчетПереработчика.ЗаказПереработчику = Заказ.Ссылка
	|
	|ГДЕ
	|	ОтчетПереработчика.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 1
	|ВЫБРАТЬ
	|	*
	|ИЗ
	|	ВтКОформлению КАК ВтКОформлению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаказыОстатки.Склад КАК Склад
	|ИЗ
	|	РегистрНакопления.ЗаказыПоставщикам.Остатки(, ЗаказПоставщику В (ВЫБРАТЬ Т.ЗаказПереработчику ИЗ ВтКОформлению КАК Т)) КАК ЗаказыОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	// 3
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаПродукция.ЗаказПереработчику КАК Распоряжение,
	|	ТаблицаПродукция.КодСтроки          КАК КодСтроки
	|ИЗ
	|	Документ.ОтчетПереработчика.Продукция КАК ТаблицаПродукция
	|ГДЕ
	|	ТаблицаПродукция.Ссылка = &ДокументОснование";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РеквизитыЗаказа = РезультатЗапроса[1].Выбрать();
	Если НЕ РеквизитыЗаказа.Следующий() Тогда
		ТекстОшибки = НСтр("ru='Отчет переработчика оформлен не по заказу.
                            |Поступление от переработчика необходимо создавать до оформления отчета переработчика.'
                            |;uk='Звіт переробника оформлений не по замовленню.
                            |Надходження від переробника необхідно створювати до оформлення звіту переробника.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	МассивДопустимыхСтатусов = Новый Массив();
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовПереработчикам.КИсполнению);
	МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовПереработчикам.Закрыт);
	
	ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
		РеквизитыЗаказа.ЗаказПереработчику,
		РеквизитыЗаказа.СтатусДокумента,
		РеквизитыЗаказа.ЕстьОшибкиПроведен,
		РеквизитыЗаказа.ЕстьОшибкиСтатус,
		МассивДопустимыхСтатусов);
	
	// Заполнение шапки.
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);
	
	Если РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов Тогда
		МассивСкладов = РезультатЗапроса[2].Выгрузить().ВыгрузитьКолонку("Склад");
	КонецЕсли;
	
	СписокПродукции = РезультатЗапроса[3].Выгрузить();
	
	// Заполнение т.ч. товары.
	Если Не ЗначениеЗаполнено(СкладПоступления) Тогда
		
		Если РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов Тогда
			
			// Услуги или товары
			Если МассивСкладов.Количество() = 1 Тогда
				СкладПоступления = МассивСкладов[0];
				Склад = СкладПоступления;
				
			// Услуги и товары
			ИначеЕсли МассивСкладов.Количество() = 2 Тогда
				Если Не ЗначениеЗаполнено(МассивСкладов[0]) Или
					Не ЗначениеЗаполнено(МассивСкладов[1]) Тогда
					СкладПоступления = ?(ЗначениеЗаполнено(МассивСкладов[0]), МассивСкладов[0], МассивСкладов[1]);
					Склад = СкладПоступления;
				КонецЕсли;
			КонецЕсли;
			
		Иначе
			
			СкладПоступления = Склад;
			
		КонецЕсли;
		
	Иначе
		
		Если РеквизитыЗаказа.ЗапрещеноВыбиратьГруппуСкладов Тогда
			Склад = СкладПоступления;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПоступлениеПоЗаказам Тогда
		
		Документы.ПоступлениеОтПереработчика.ЗаполнитьПоЗаказам(ЭтотОбъект, СписокПродукции);
		
		СоответствиеРеквизитов = Новый Соответствие;
		СоответствиеРеквизитов.Вставить("ЗаказПереработчику","ЗаказПереработчику");
		СоответствиеРеквизитов.Вставить("Распоряжение","Распоряжение");
		ЗаказыСервер.ЗаполнитьРеквизитыШапкиПоТабличнымЧастям(СоответствиеРеквизитов, "Товары,ВозвратныеОтходы", ЭтотОбъект);
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПоступлениеПоНесколькимЗаказам") Тогда
		Сделка = Справочники.СделкиСКлиентами.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьНаОснованииЗаказаПереработчику(ДокументОснование, СписокПродукции = Неопределено)
	
	Если ЗначениеЗаполнено(ДокументОснование) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказПереработчику.Сделка           КАК Сделка,
		|	ЗаказПереработчику.Ссылка           КАК ЗаказПереработчику,
		|	ЗаказПереработчику.Партнер          КАК Партнер,
		|	ЗаказПереработчику.Контрагент       КАК Контрагент,
		|	ЗаказПереработчику.Договор          КАК Договор,
		|	ЗаказПереработчику.Организация      КАК Организация,
		|	ЗаказПереработчику.Валюта           КАК Валюта,
		|
		|	ВЫБОР КОГДА ЗаказПереработчику.Склад.ЭтоГруппа
		|			  И ЗаказПереработчику.Склад.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах) ТОГДА
		|		ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|	ИНАЧЕ
		|		ЗаказПереработчику.Склад
		|	КОНЕЦ                               КАК Склад,
		|
		|	ЗаказПереработчику.Статус           КАК СтатусДокумента,
		|	ЗаказПереработчику.Подразделение    КАК Подразделение,
		|	НЕ ЗаказПереработчику.Проведен      КАК ЕстьОшибкиПроведен,
		|
		|	ВЫБОР КОГДА ЗаказПереработчику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПереработчикам.КИсполнению)
		|			ИЛИ ЗаказПереработчику.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПереработчикам.Закрыт) ТОГДА
		|		ЛОЖЬ
		|	ИНАЧЕ
		|		ИСТИНА
		|	КОНЕЦ                               КАК ЕстьОшибкиСтатус,
		|
		|	ЗаказПереработчику.НалоговоеНазначение КАК НалоговоеНазначение,
		|	ЗаказПереработчику.БанковскийСчет   КАК БанковскийСчетОрганизации,
		|	ЗаказПереработчику.НаправлениеДеятельности КАК НаправлениеДеятельности
		|ИЗ
		|	Документ.ЗаказПереработчику КАК ЗаказПереработчику
		|ГДЕ
		|	ЗаказПереработчику.Ссылка = &ДокументОснование";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		РеквизитыЗаказа = РезультатЗапроса.Выбрать();
		РеквизитыЗаказа.Следующий();
		
		МассивДопустимыхСтатусов = Новый Массив();
		МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовПереработчикам.КИсполнению);
		МассивДопустимыхСтатусов.Добавить(Перечисления.СтатусыЗаказовПереработчикам.Закрыт);
		
		ОбщегоНазначенияУТ.ПроверитьВозможностьВводаНаОсновании(
			РеквизитыЗаказа.ЗаказПереработчику,
			РеквизитыЗаказа.СтатусДокумента,
			РеквизитыЗаказа.ЕстьОшибкиПроведен,
			РеквизитыЗаказа.ЕстьОшибкиСтатус,
			МассивДопустимыхСтатусов);
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыЗаказа);
		
	КонецЕсли;
	
	Документы.ПоступлениеОтПереработчика.ЗаполнитьПоЗаказам(ЭтотОбъект, СписокПродукции);
	
	СоответствиеРеквизитов = Новый Соответствие;
	СоответствиеРеквизитов.Вставить("ЗаказПереработчику","ЗаказПереработчику");
	СоответствиеРеквизитов.Вставить("Распоряжение","Распоряжение");
	ЗаказыСервер.ЗаполнитьРеквизитыШапкиПоТабличнымЧастям(СоответствиеРеквизитов, "Товары,ВозвратныеОтходы", ЭтотОбъект);
		
	ПоступлениеПоЗаказам = Истина;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПоступлениеПоНесколькимЗаказам") Тогда
		Сделка = Справочники.СделкиСКлиентами.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоОтбору(Знач ДанныеЗаполнения) 
	
	Если ДанныеЗаполнения.Свойство("Партнер") Тогда
		
		Партнер = ДанныеЗаполнения.Партнер;
		ЗаполнитьУсловияЗакупокПоУмолчанию();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализироватьДокумент(ДанныеЗаполнения = Неопределено) 
	
    Валюта                    = ЗначениеНастроекПовтИсп.ПолучитьВалютуРегламентированногоУчета(Валюта);
	Менеджер                  = Пользователи.ТекущийПользователь();
	Организация               = ЗначениеНастроекПовтИсп.ПолучитьОрганизациюПоУмолчанию(Организация);
	
	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация    		= Организация;
	СтруктураПараметров.БанковскийСчет		= БанковскийСчетОрганизации;
	БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
	
	БанковскийСчетКонтрагента = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент, , БанковскийСчетКонтрагента);
	Склад                     = ЗначениеНастроекПовтИсп.ПолучитьСкладПоУмолчанию(Склад, ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовЗакупки"), Истина);
	
	Если Не ЗначениеЗаполнено(НалоговоеНазначение) Тогда
		НалоговоеНазначение = Справочники.Организации.НалоговоеНазначениеНДС(Организация, Дата);
	КонецЕсли; 
	
	СтруктураОтветственного = ЗакупкиСервер.ПолучитьОтветственногоПоСкладу(Склад, Менеджер);
	Если СтруктураОтветственного <> Неопределено Тогда
		Принял = СтруктураОтветственного.Ответственный;
		ПринялДолжность = СтруктураОтветственного.ОтветственныйДолжность;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВидыЗапасов

Процедура ЗаполнитьВидыЗапасовДокумента() 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки                КАК НомерСтроки,
	|	ТаблицаТоваров.ЗаказПереработчику         КАК ЗаказПереработчику,
	|	ТаблицаТоваров.КодСтроки                  КАК КодСтроки,
	|	ТаблицаТоваров.Номенклатура               КАК Номенклатура,
	|	ТаблицаТоваров.Сделка                     КАК Сделка,
	|	ТаблицаТоваров.Назначение                 КАК Назначение,
	|	ТаблицаТоваров.ВидЗапасов                 КАК ВидЗапасов
	|
	|ПОМЕСТИТЬ ТаблицаТоваровДокумента
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки									КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура									КАК Номенклатура,
	|	ЛОЖЬ														КАК ЭтоВозвратнаяТара,
	|	Назначения.Ссылка											КАК Назначение,
	|	&Подразделение												КАК Подразделение,
	|	&Менеджер													КАК Менеджер,
	|	&Организация												КАК Организация,
	|	&ХозяйственнаяОперация										КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)	КАК Соглашение,
	|	&Валюта														КАК Валюта,
	|	&НалоговоеНазначение 									    КАК НалоговоеНазначение,
	|	&НалоговоеНазначениеОрганизации								КАК НалоговоеНазначениеОрганизации,
	|	
	
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказДавальца.Партнер ЕСТЬ NULL
	|			ТОГДА ЗаказДавальца.Партнер
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	КОНЕЦ                                                                                       КАК Поставщик,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказДавальца.Контрагент ЕСТЬ NULL
	|			ТОГДА ЗаказДавальца.Контрагент
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	КОНЕЦ                                                                                       КАК Контрагент,
	|	ВЫБОР
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказДавальца.Договор ЕСТЬ NULL
	|			ТОГДА ЗаказДавальца.Договор
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
	|	КОНЕЦ                                                                                       КАК Договор,
	|	                                                                                              
	|	ВЫБОР 
	//++ НЕ УТКА
	|		КОГДА НЕ ЗаказДавальца.Ссылка ЕСТЬ NULL 
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|	КОНЕЦ КАК ТипЗапасов,
	|	
	|	ВЫБОР КОГДА &ПоступлениеПоЗаказам И ТаблицаТоваров.Сделка <> &ПустаяСделка ТОГДА
	|		ТаблицаТоваров.Сделка
	|	ИНАЧЕ
	|		&Сделка
	|	КОНЕЦ КАК Сделка,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаПродукции
	|ПОМЕСТИТЬ ИсходнаяТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровДокумента КАК ТаблицаТоваров
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.ВидыЗапасов КАК ВидыЗапасов
	|	ПО
	|		ТаблицаТоваров.ВидЗапасов = ВидыЗапасов.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказПереработчику КАК ЗаказНаПереработку
	|	ПО
	|		ТаблицаТоваров.ЗаказПереработчику = ЗаказНаПереработку.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказПереработчику.Услуги КАК ЗаказНаПереработкуУслуги
	|	ПО
	|		ТаблицаТоваров.ЗаказПереработчику = ЗаказНаПереработкуУслуги.Ссылка
	|		И ТаблицаТоваров.КодСтроки = ЗаказНаПереработкуУслуги.Ссылка
	|	
	//++ НЕ УТКА
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ЗаказНаПроизводство.Продукция КАК ЗаказНаПроизводство
	|	ПО
	|		ЗаказНаПереработкуУслуги.Распоряжение = ЗаказНаПроизводство.Ссылка
	|		И ЗаказНаПереработкуУслуги.КодСтрокиПродукция = ЗаказНаПроизводство.КодСтроки
	//-- НЕ УТКА
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Назначения КАК Назначения
	|
	//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			Документ.ЗаказДавальца КАК ЗаказДавальца
	|		ПО
	|			Назначения.Заказ = ЗаказДавальца.Ссылка
	//-- НЕ УТКА
	|
	|	ПО
	//++ НЕ УТКА
	|		ЕСТЬNULL(ЗаказНаПроизводство.Назначение, 
	//-- НЕ УТКА
	|			ТаблицаТоваров.Назначение
	//++ НЕ УТКА
	|		) 
	//-- НЕ УТКА
	|		= Назначения.Ссылка
	|
	|ГДЕ
	|	ТаблицаТоваров.ВидЗапасов = ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка)
	|	ИЛИ 
	|		ВЫБОР 
	//++ НЕ УТКА
	|			КОГДА ЗаказДавальца.Ссылка ЕСТЬ НЕ NULL 
	|				ТОГДА ВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.ПродукцияДавальца)
	//-- НЕ УТКА
	|			КОГДА ИСТИНА
	|				ТОГДА ВидыЗапасов.ТипЗапасов <> ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)
	|		КОНЕЦ
	|	ИЛИ ВидыЗапасов.Организация <> &Организация
	|	ИЛИ (Не &ФормироватьВидыЗапасовПоСделкам                  И ВидыЗапасов.Сделка        <> ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка))
	|	ИЛИ (Не &ФормироватьВидыЗапасовПоПоставщикам              И ВидыЗапасов.Поставщик     <> НЕОПРЕДЕЛЕНО)
	|	ИЛИ (Не &ФормироватьВидыЗапасовПоПодразделениямМенеджерам И ВидыЗапасов.Менеджер      <> ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))
	|	ИЛИ (Не &ФормироватьВидыЗапасовПоПодразделениямМенеджерам И ВидыЗапасов.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
	|");
	
	КолонкиТаблицыТоваров = "НомерСтроки, Номенклатура, ЗаказПереработчику, Сделка, Назначение, ВидЗапасов, КодСтроки";
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",             Товары.Выгрузить(, КолонкиТаблицыТоваров));
	Запрос.УстановитьПараметр("Организация",                Организация);
	Запрос.УстановитьПараметр("ПустаяСделка",               Справочники.СделкиСклиентами.ПустаяСсылка());
	Запрос.УстановитьПараметр("Валюта",                     Справочники.Валюты.ПустаяСсылка());
	Запрос.УстановитьПараметр("НалоговоеНазначение",        НалоговоеНазначение);
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", НДСОбщегоНазначенияСервер.ПолучитьНалоговоеНазначениеНДС(Организация, Неопределено, Дата, Истина));
	Запрос.УстановитьПараметр("Сделка",                     Сделка);
	Запрос.УстановитьПараметр("Менеджер",                   Менеджер);
	Запрос.УстановитьПараметр("Подразделение",              Подразделение);
	Запрос.УстановитьПараметр("ХозяйственнаяОперация",      ХозяйственнаяОперация);
	Запрос.УстановитьПараметр("ПоступлениеПоЗаказам",       ПоступлениеПоЗаказам);
	
	Запрос.УстановитьПараметр("ИспользоватьРаздельныйУчетПоНалогообложению",      ПолучитьФункциональнуюОпцию("ИспользоватьРаздельныйУчетПоНалогообложению"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоСделкам",                  ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоСделкам"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоПоставщикам",              ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПоставщикам"));
	Запрос.УстановитьПараметр("ФормироватьВидыЗапасовПоПодразделениямМенеджерам", ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам"));
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
	ЗапасыСервер.ЗаполнитьВидыЗапасовВТабличнойЧастиТовары(Запрос.МенеджерВременныхТаблиц, Товары);
	
	Запрос.УстановитьПараметр("ТаблицаТоваров",             ВозвратныеОтходы.Выгрузить(, КолонкиТаблицыТоваров));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	ЗапасыСервер.ЗаполнитьВидыЗапасовВТабличнойЧастиТовары(Запрос.МенеджерВременныхТаблиц, ВозвратныеОтходы);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПараметрыОбъектаССоглашением(ИменаРеквизитов = "")
	
	Если ПустаяСтрока(ИменаРеквизитов) Тогда
		ИменаРеквизитов = "Партнер, Договор, Контрагент, Организация";
	КонецЕсли;
	
	ПараметрыОбъекта = Новый Структура(ИменаРеквизитов);
	ЗаполнитьЗначенияСвойств(ПараметрыОбъекта, ЭтотОбъект);
	
	ПараметрыОбъекта.Вставить("Соглашение", Справочники.СоглашенияСПоставщиками.ПустаяСсылка());
	
	Возврат ПараметрыОбъекта;
	
КонецФункции

Процедура СформироватьСписокРегистровДляКонтроля()
	
	Массив = Новый Массив;
	
	// Контроль выполняется при проведении\отмене проведения не нового документа.
	Если Не ДополнительныеСвойства.ЭтоНовый Тогда
		Массив.Добавить(Движения.ТоварыОрганизаций);
	КонецЕсли;
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Массив.Добавить(Движения.ЗаказыПоставщикам);
	КонецЕсли;
	
	Массив.Добавить(Движения.ОбеспечениеЗаказов);
	
	ДополнительныеСвойства.ДляПроведения.Вставить("РегистрыДляКонтроля", Массив);
	
КонецПроцедуры

Процедура ЗаполнитьКлючиАналитикиУчетаПартийДокументаВТабличнойЧасти(ТабличнаяЧасть) 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура         КАК Номенклатура,
	|	ТаблицаТоваров.АналитикаУчетаПартий КАК АналитикаУчетаПартий
	|ПОМЕСТИТЬ ТаблицаТоваровДокумента
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки          КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура         КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО                        КАК СтавкаНДС,
	|	&НалоговоеНазначение                КАК НалоговоеНазначение,
	|	&Поставщик                          КАК Поставщик,
	|	&Контрагент                         КАК Контрагент,
	|	ТаблицаТоваров.АналитикаУчетаПартий КАК АналитикаУчетаПартий
	|
	|ПОМЕСТИТЬ ИсходнаяТаблицаТоваров
	|ИЗ
	|	ТаблицаТоваровДокумента КАК ТаблицаТоваров
	|ГДЕ
	|	ТаблицаТоваров.АналитикаУчетаПартий = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПартий.ПустаяСсылка)");
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаТоваров",     ТабличнаяЧасть.Выгрузить(, "НомерСтроки, Номенклатура, АналитикаУчетаПартий"));
	Запрос.УстановитьПараметр("Поставщик",          Партнер);
	Запрос.УстановитьПараметр("Контрагент",         Контрагент);
	Запрос.УстановитьПараметр("НалоговоеНазначение", НалоговоеНазначение);
	
	Запрос.Выполнить();
	
	ПартионныйУчетСервер.ЗаполнитьАналитикуУчетаПартийВТабличнойЧастиТовары(МенеджерВременныхТаблиц, ТабличнаяЧасть);
	
КонецПроцедуры

Процедура ЗаполнитьКлючиАналитикиУчетаПартийДокумента() Экспорт 
	
	ЗаполнитьКлючиАналитикиУчетаПартийДокументаВТабличнойЧасти(Товары);
	ЗаполнитьКлючиАналитикиУчетаПартийДокументаВТабличнойЧасти(ВозвратныеОтходы);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
