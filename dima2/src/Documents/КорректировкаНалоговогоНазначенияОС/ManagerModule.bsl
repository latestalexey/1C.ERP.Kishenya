
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет список команд создания на основании.
//
// Параметры:
// 		КомандыСоздатьНаОсновании - ТаблицаЗначений - состав полей см. в функции ВводНаОсновании.СоздатьКоллекциюКомандСоздатьНаОсновании
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСоздатьНаОсновании) Экспорт
	
	Документы.ИзменениеПараметровОС.ДобавитьКомандуСоздатьНаОсновании(КомандыСоздатьНаОсновании);
		
КонецПроцедуры


// Заполняет список команд отчетов.
// 
// Параметры:
//   КомандыОтчетов - ТаблицаЗначений - состав полей см. в функции МенюОтчеты.СоздатьКоллекциюКомандОтчетов
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов) Экспорт

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуСтруктураПодчиненности(КомандыОтчетов);

	ВариантыОтчетовУТПереопределяемый.ДобавитьКомандуДвиженияДокумента(КомандыОтчетов);

КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Проведение

Функция ДополнительныеИсточникиДанныхДляДвижений(ИмяРегистра) Экспорт
	
	ИсточникиДанных = Новый Соответствие;
	Возврат ИсточникиДанных;
	
КонецФункции

Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства, Регистры = Неопределено) Экспорт
	
	ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(НСтр("ru='Корректировка налогового назначения ОС';uk='Коригування податкового призначення ОС'"));
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства);
	
	ТекстыЗапроса = Новый СписокЗначений;
	УчетОСВызовСервера.ПрочиеРасходы(ТекстыЗапроса, Регистры);
	УчетОСВызовСервера.ПартииПрочихРасходов(ТекстыЗапроса, Регистры);
	УчетОСВызовСервера.ПорядокОтраженияПрочихОпераций(ТекстыЗапроса, Регистры);
	УчетОСВызовСервера.ОтражениеДокументовВРеглУчете(ТекстыЗапроса, Регистры);
	УчетОСВызовСервера.ПереоценкаОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ГрафикиАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	НалоговыеНазначенияОС(ТекстыЗапроса, Регистры);
	НДСУсловныеПродажи(ТекстыЗапроса, Регистры);		
	НачислениеАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры);
	СобытияОСОрганизаций(ТекстыЗапроса, Регистры);
	//СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры);
	
	ПроведениеСервер.ИницализироватьТаблицыДляДвижений(Запрос, ТекстыЗапроса, ДополнительныеСвойства.ТаблицыДляДвижений, Ложь, Ложь, Истина);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка, ДополнительныеСвойства)
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Дата КАК Период,
	|	ДанныеДокумента.Номер КАК Номер,
	|	ДанныеДокумента.СобытиеОС,
	|	ДанныеДокумента.ХозяйственнаяОперация,
	|	ДанныеДокумента.Организация,
	|	ДанныеДокумента.НалоговоеНазначение,
	|	ДанныеДокумента.НовоеНалоговоеНазначение,
	|	ДанныеДокумента.УсловнаяПродажа,
	|	ДанныеДокумента.СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов,
	|	ВЫБОР ДанныеДокумента.ХозяйственнаяОперация
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперацииРеглУчет.ПеремещениеОСвПодразделениеВыделенноеНаБаланс)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ	
	|	КОНЕЦ КАК ЭтоПередача
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Ссылка = &Ссылка";
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выбрать();
	Реквизиты.Следующий();
	
	УчетОСВызовСервера.ИнициализироватьПараметрыЗапросаПриОтраженииАмортизации(Запрос, ДополнительныеСвойства);
	Для Каждого Колонка Из Результат.Колонки Цикл
		Запрос.УстановитьПараметр(Колонка.Имя, Реквизиты[Колонка.Имя]);
	КонецЦикла;
	Запрос.УстановитьПараметр("Граница", Новый Граница(НачалоМесяца(Реквизиты.Период), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("ГраницаКонецМесяца", Новый Граница(КонецМесяца(Реквизиты.Период), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НазваниеДокумента", НСтр("ru='Корректировка налогового назначения ОС';uk='Коригування податкового призначення ОС'"));
	Запрос.УстановитьПараметр("НалоговоеНазначениеОрганизации", Справочники.Организации.НалоговоеНазначениеНДС(Реквизиты.Организация, Реквизиты.Период));
	
КонецПроцедуры

Процедура ВременнаяТаблицаПоУсловнойПродажи(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаПоУсловнойПродаже";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаПоУсловнойПродаже
	|"+
	"ВЫБРАТЬ
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.Организация КАК Организация,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка КАК Номенклатура,
	|	КорректировкаНалоговогоНазначенияОСОС.СтавкаНДС,
	|	СУММА(КорректировкаНалоговогоНазначенияОСОС.СуммаНДС) КАК НДСРегл,
	|	СУММА(ВЫБОР
	|			КОГДА КорректировкаНалоговогоНазначенияОСОС.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС20)
	|				ТОГДА КорректировкаНалоговогоНазначенияОСОС.СуммаНДС / 0.2
	|			КОГДА КорректировкаНалоговогоНазначенияОСОС.СтавкаНДС = ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.НДС7)
	|				ТОГДА КорректировкаНалоговогоНазначенияОСОС.СуммаНДС / 0.07
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА КорректировкаНалоговогоНазначенияОСОС.Ссылка.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|					И КорректировкаНалоговогоНазначенияОСОС.Ссылка.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|				ИЛИ КорректировкаНалоговогоНазначенияОСОС.Ссылка.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность)
	|					И КорректировкаНалоговогоНазначенияОСОС.Ссылка.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВосстановлениеНДС,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.НалоговоеНазначение,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.НовоеНалоговоеНазначение КАК НалоговоеНазначениеПоФакту,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.УсловнаяПродажа
	|ПОМЕСТИТЬ ТаблицаПоУсловнойПродаже
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС.ОС КАК КорректировкаНалоговогоНазначенияОСОС
	|ГДЕ
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.Организация,
	|	КорректировкаНалоговогоНазначенияОСОС.СтавкаНДС,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.НалоговоеНазначение,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.НовоеНалоговоеНазначение,
	|	ВЫБОР
	|		КОГДА КорректировкаНалоговогоНазначенияОСОС.Ссылка.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|					И КорректировкаНалоговогоНазначенияОСОС.Ссылка.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|				ИЛИ КорректировкаНалоговогоНазначенияОСОС.Ссылка.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяХозДеятельность)
	|					И КорректировкаНалоговогоНазначенияОСОС.Ссылка.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_Облагаемая)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.УсловнаяПродажа
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПоТабличнойЧастиОС(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаПоТабличнойЧасти";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ОсновныеСредства
	|"+
	"ВЫБРАТЬ
	|	КорректировкаНалоговогоНазначенияОСОС.ОсновноеСредство КАК ОсновноеСредство,
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ТаблицаПоТабличнойЧасти
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС.ОС КАК КорректировкаНалоговогоНазначенияОСОС
	|ГДЕ
	|	КорректировкаНалоговогоНазначенияОСОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаПоТабличнойЧасти(ТекстыЗапроса)
	
	ИмяТаблицы = "ОсновныеСредства";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧастиОС(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ОсновныеСредства
	|"+
	"ВЫБРАТЬ
	|	ТаблицаПоТабличнойЧасти.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаПоТабличнойЧасти.Организация КАК Организация,
	|	ТаблицаПоТабличнойЧасти.НаправлениеДеятельности,
	|	МестонахождениеОСБухгалтерскийУчетСрезПоследних.Местонахождение КАК Подразделение
	|ПОМЕСТИТЬ ОсновныеСредства
	|ИЗ
	|	ТаблицаПоТабличнойЧасти КАК ТаблицаПоТабличнойЧасти
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|				&Период,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						ТаблицаПоТабличнойЧасти.Организация,
	|						ТаблицаПоТабличнойЧасти.ОсновноеСредство
	|					ИЗ
	|						ТаблицаПоТабличнойЧасти КАК ТаблицаПоТабличнойЧасти)) КАК МестонахождениеОСБухгалтерскийУчетСрезПоследних
	|		ПО ТаблицаПоТабличнойЧасти.ОсновноеСредство = МестонахождениеОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОсновноеСредство,
	|	Организация,
	|	Подразделение"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчетаОС(ТекстыЗапроса)
	
	ИмяТаблицы = "СчетаУчетаОС";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧасти(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица СчетаУчетаОС
	|"+
	"ВЫБРАТЬ
	|	ОсновныеСредства.ОсновноеСредство,
	|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетУчета,
	|	СчетаБухгалтерскогоУчетаОССрезПоследних.СчетНачисленияАмортизации КАК СчетАмортизации
	|ПОМЕСТИТЬ СчетаУчетаОС
	|ИЗ
	|	ОсновныеСредства КАК ОсновныеСредства
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаБухгалтерскогоУчетаОС.СрезПоследних(
	|				&Период,
	|				(Организация, ОсновноеСредство) В
	|					(ВЫБРАТЬ
	|						ОсновныеСредства.Организация,
	|						ОсновныеСредства.ОсновноеСредство
	|					ИЗ
	|						ОсновныеСредства КАК ОсновныеСредства)) КАК СчетаБухгалтерскогоУчетаОССрезПоследних
	|		ПО ОсновныеСредства.ОсновноеСредство = СчетаБухгалтерскогоУчетаОССрезПоследних.ОсновноеСредство
	|"+";";;
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСчетаУчетаОССгруппированные(ТекстыЗапроса)
	
	ИмяТаблицы = "СчетаУчетаОССгруппированные";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчетаОС(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица СчетаУчетаОССгруппированные
	|"+
	"ВЫБРАТЬ
	|	СчетаУчетаОС.СчетУчета КАК Счет
	|ПОМЕСТИТЬ СчетаУчетаОССгруппированные
	|ИЗ
	|	СчетаУчетаОС КАК СчетаУчетаОС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СчетаУчетаОС.СчетАмортизации
	|ИЗ
	|	СчетаУчетаОС КАК СчетаУчетаОС
	|
	|"+";";;
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса)
	
	ИмяТаблицы = "втОстаткиПоСчетам";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаСчетаУчетаОССгруппированные(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица втОстаткиПоСчетам
	|"+
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Субконто1 КАК ОсновноеСредство,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаБУ,
	|	ХозрасчетныйОстатки.СуммаНУОстаток КАК СуммаНУ
	|ПОМЕСТИТЬ втОстаткиПоСчетам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&Период,
	|			Счет В
	|				(ВЫБРАТЬ
	|					Т.Счет
	|				ИЗ
	|					СчетаУчетаОССгруппированные КАК Т),
	|			,
	|			(Организация, Подразделение, Субконто1, НаправлениеДеятельности) В
	|				(ВЫБРАТЬ
	|					ОсновныеСредства.Организация,
	|					ОсновныеСредства.Подразделение,
	|					ОсновныеСредства.ОсновноеСредство,
	|					ОсновныеСредства.НаправлениеДеятельности
	|				ИЗ
	|					ОсновныеСредства КАК ОсновныеСредства)) КАК ХозрасчетныйОстатки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ВременнаяТаблицаСгруппированнаяНачисленнаяАмортизация(ТекстыЗапроса)
	
	ИмяТаблицы = "НачисленнаяАмортизация";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	УчетОСВызовСервера.ВременнаяТаблицаНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица НачисленнаяАмортизация
	|"+
	"ВЫБРАТЬ
	|	НачисленнаяАмортизация.ОбъектУчета КАК ОсновноеСредство,
	|	СУММА(НачисленнаяАмортизация.СуммаБУ) КАК СуммаБУ,
	|	СУММА(НачисленнаяАмортизация.СуммаНУ) КАК СуммаНУ
	|ПОМЕСТИТЬ НачисленнаяАмортизация
	|ИЗ
	|	втНачисленнаяАмортизация КАК НачисленнаяАмортизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НачисленнаяАмортизация.ОбъектУчета"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

	

Процедура ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса)
	
	ИмяТаблицы = "ТаблицаОС";
	
	Если ПроведениеСервер.ЕстьТаблицаЗапроса(ИмяТаблицы, ТекстыЗапроса) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоТабличнойЧастиОС(ТекстыЗапроса);	
	ВременнаяТаблицаПоТабличнойЧасти(ТекстыЗапроса);
	ВременнаяТаблицаСчетаУчетаОС(ТекстыЗапроса);
	ВременнаяТаблицаОстаткиПоСчетам(ТекстыЗапроса);
	ВременнаяТаблицаСгруппированнаяНачисленнаяАмортизация(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Временная таблица ТаблицаОС
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка,
	|	ТаблицаОС.Ссылка.НалоговоеНазначение,
	|	ТаблицаОС.Ссылка.НовоеНалоговоеНазначение,
	|	ТаблицаОС.Ссылка.УсловнаяПродажа,
	|	ТаблицаОС.НачальнаяСтоимостьНУ,
	|	ТаблицаОС.СуммаНДС,
	|	ЕСТЬNULL(втОстаткиСтоимость.СуммаБУ, 0)- ЕСТЬNULL(-втОстаткиАмортизация.СуммаБУ, 0) - ЕСТЬNULL(НачисленнаяАмортизация.СуммаБУ, 0)
	|	+ ТаблицаОС.СуммаНДС КАК СтоимостьБУ,
	|	ЕСТЬNULL(втОстаткиСтоимость.СуммаНУ, 0) - ЕСТЬNULL(НачисленнаяАмортизация.СуммаНУ, 0) 
	|   - ЕСТЬNULL(-втОстаткиАмортизация.СуммаНУ, 0)
	|	+ ТаблицаОС.СуммаНДС + ТаблицаОС.НачальнаяСтоимостьНУ КАК СтоимостьНУ,
	|	ЕСТЬNULL(ПервоначальныеСведения.ПоказательНаработки, ЗНАЧЕНИЕ(Справочник.ПоказателиНаработки.ПустаяСсылка)) КАК ПоказательНаработки,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС.ОС КАК ТаблицаОС
	|	ЛЕВОЕ СОЕДИНЕНИЕ СчетаУчетаОС КАК СчетаУчетаОС
	|	ПО ТаблицаОС.ОсновноеСредство = СчетаУчетаОС.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиСтоимость
	|	ПО ТаблицаОС.ОсновноеСредство = втОстаткиСтоимость.ОсновноеСредство
	|	И (СчетаУчетаОС.СчетУчета = втОстаткиСтоимость.Счет)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиАмортизация
	|	ПО (ТаблицаОС.ОсновноеСредство = втОстаткиАмортизация.ОсновноеСредство)
	|	И (СчетаУчетаОС.СчетАмортизации = втОстаткиАмортизация.Счет)
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ НачисленнаяАмортизация КАК НачисленнаяАмортизация
	|	ПО ТаблицаОС.ОсновноеСредство = НачисленнаяАмортизация.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|			&Период,
	|			(Организация, ОсновноеСредство) В
	|				(ВЫБРАТЬ
	|					ОсновныеСредства.Организация,
	|					ОсновныеСредства.ОсновноеСредство
	|				ИЗ
	|					ОсновныеСредства КАК ОсновныеСредства)) КАК ПервоначальныеСведения
	|	ПО ТаблицаОС.ОсновноеСредство = ПервоначальныеСведения.ОсновноеСредство
    |
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяТаблицы, Ложь);
	
КонецПроцедуры

Процедура ГрафикиАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ГрафикиАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ГрафикиАмортизацииОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ГрафикиАмортизацииОСБухгалтерскийУчет.ГрафикАмортизации КАК ГрафикАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.ОсновноеСредство
	|					ИЗ
	|						ТаблицаОС КАК Т)) КАК ГрафикиАмортизацииОСБухгалтерскийУчет
	|		ПО ТаблицаОС.ОсновноеСредство = ГрафикиАмортизацииОСБухгалтерскийУчет.ОсновноеСредство
	|ГДЕ
	|	&ЭтоПередача
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

 

Процедура НачислениеАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НачислениеАмортизацииОСНалоговыйУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НачислениеАмортизацииОСНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|		&Организация КАК Организация,
	|	
    |	ВЫБОР КОГДА &НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|		ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК НачислятьАмортизацию
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НачислениеАмортизацииОСНалоговыйУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.ОсновноеСредство
	|					ИЗ
	|						ТаблицаОС КАК Т)) КАК НачислениеАмортизацииОСНалоговыйУчет
	|		ПО ТаблицаОС.ОсновноеСредство = НачислениеАмортизацииОСНалоговыйУчет.ОсновноеСредство
	|	
	|ГДЕ
	|	&НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|	ИЛИ &НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыАмортизацииОСБухгалтерскийУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСБухгалтерскийУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыАмортизацииОСБухгалтерскийУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования,
	|	ПараметрыАмортизацииОСБУСрезПервых.СрокИспользованияДляВычисленияАмортизации
	|	- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСБУСрезПервых.Период, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьБУ КАК СтоимостьДляВычисленияАмортизации,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.ОбъемПродукцииРабот КАК ОбъемПродукцииРабот,
	|	ПараметрыАмортизацииОСБухгалтерскийУчет.ОбъемПродукцииРаботДляВычисленияАмортизации
	|	- ЕСТЬNULL(НаработкиОбъектовЭксплуатации.Значение, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ТаблицаОС КАК ТаблицаОС)) КАК ПараметрыАмортизацииОСБухгалтерскийУчет
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыАмортизацииОСБухгалтерскийУчет.ОсновноеСредство
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПервых(
	|				,
	|				Организация = &Организация
	|					И Регистратор <> &Ссылка
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							ТаблицаОС.ОсновноеСредство
	|						ИЗ
	|							ТаблицаОС КАК ТаблицаОС)) КАК ПараметрыАмортизацииОСБУСрезПервых
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыАмортизацииОСБУСрезПервых.ОсновноеСредство
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НаработкиОбъектовЭксплуатации.СрезПоследних(
	|				&ГраницаКонецМесяца,
	|				(ОбъектЭксплуатации, ПоказательНаработки) В
	|					(ВЫБРАТЬ
	|						ТаблицаОС.ОсновноеСредство, ТаблицаОС.ПоказательНаработки
	|					ИЗ
	|						ТаблицаОС КАК ТаблицаОС)) КАК НаработкиОбъектовЭксплуатации
	|		ПО ТаблицаОС.ОсновноеСредство = НаработкиОбъектовЭксплуатации.ОбъектЭксплуатации
	|ГДЕ
	|	&УсловнаяПродажа		
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура ПараметрыАмортизацииОСНалоговыйУчет(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "ПараметрыАмортизацииОСНалоговыйУчет";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица ПараметрыАмортизацииОСНалоговыйУчет
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|
	|	ПараметрыАмортизацииОСНалоговыйУчет.СрокИспользованияДляВычисленияАмортизации 
	|	- РАЗНОСТЬДАТ(ЕСТЬNULL(ПараметрыАмортизацииОСНалоговыйУчет.Период, &Период), &Период, МЕСЯЦ) КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьНУ КАК СтоимостьДляВычисленияАмортизации,
	 
	|	ПараметрыАмортизацииОСНалоговыйУчет.СрокПолезногоИспользования КАК СрокПолезногоИспользования
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСНалоговыйУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|				И Регистратор <> &Ссылка
	|				И ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						Т.ОсновноеСредство
	|					ИЗ
	|						ТаблицаОС КАК Т)) КАК ПараметрыАмортизацииОСНалоговыйУчет
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыАмортизацииОСНалоговыйУчет.ОсновноеСредство
	|ГДЕ
	|	&УсловнаяПродажа
	|	ИЛИ
	|   (&НалоговоеНазначение =
	|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)) 
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры


 
Процедура СобытияОСОрганизаций(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "СобытияОСОрганизаций";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица СобытияОСОрганизаций
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	
	|	&СобытиеОС КАК Событие,
	|	
	|	&НазваниеДокумента КАК НазваниеДокумента,
	|	&Номер КАК НомерДокумента,
	|	0 КАК СуммаЗатратБУ,
	|	0 КАК СуммаЗатратНУ
	|	
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры


Процедура НалоговыеНазначенияОС(ТекстыЗапроса, Регистры) 
	ИмяРегистра = "НалоговыеНазначенияОС";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаОсновныхСредств(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НалоговыеНазначенияОС
	|"+
	"ВЫБРАТЬ
	|	ТаблицаОС.НомерСтроки,
	|	
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	
	|	&Организация КАК Организация,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	&НовоеНалоговоеНазначение КАК НалоговоеНазначение
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры

Процедура НДСУсловныеПродажи(ТекстыЗапроса, Регистры)
	
	ИмяРегистра = "НДСУсловныеПродажи";
	
	Если Не ПроведениеСервер.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат;
	КонецЕсли;
	
	ВременнаяТаблицаПоУсловнойПродажи(ТекстыЗапроса);
	
	Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|// Таблица НДСУсловныеПродажи
	|"+
	"ВЫБРАТЬ
	|	&Ссылка КАК Регистратор,
	|	&Период КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаПоУсловнойПродаже.Номенклатура,
	|	ТаблицаПоУсловнойПродаже.СтавкаНДС,
	|	ТаблицаПоУсловнойПродаже.НДСРегл,
	|	ТаблицаПоУсловнойПродаже.СтоимостьРегл,
	|	ВЫБОР
	|		КОГДА ТаблицаПоУсловнойПродаже.УсловнаяПродажа
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемаяУсловнаяПродажаНеоборотныхАктивов)
	|		КОГДА ТаблицаПоУсловнойПродаже.ВосстановлениеНДС
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПредполагаемоеВосстановлениеНДС)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидУсловнойПродажи.ПустаяСсылка)
	|	КОНЕЦ КАК ВидУсловнойПродажи,
	|	ТаблицаПоУсловнойПродаже.НалоговоеНазначение,
	|	ТаблицаПоУсловнойПродаже.НалоговоеНазначениеПоФакту
	|ИЗ
	|	ТаблицаПоУсловнойПродаже КАК ТаблицаПоУсловнойПродаже
	|ГДЕ
	|	(ТаблицаПоУсловнойПродаже.УсловнаяПродажа
	|			ИЛИ ТаблицаПоУсловнойПродаже.ВосстановлениеНДС)"+";";
	
	ТекстыЗапроса.Добавить(Текст, ИмяРегистра, Истина);
	
КонецПроцедуры


#КонецОбласти

#Область ПроведениеПоРеглУчету

Функция ТекстОтраженияВРеглУчетеУКР() Экспорт
	
	Разделитель = Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС;
	
	Возврат УчетОСВызовСервера.ТекстОтраженияВРеглУчетеНачисленнойАмортизации()
		+ Разделитель + КорректировкаНалоговогоНазначенияСчетУчета()
		+ Разделитель + КорректировкаНалоговогоНазначенияСчетНачисленияАмортизации()
		+ Разделитель + УсловнаяПродажа()
		;
		
КонецФункции

Функция ТекстЗапросаВТОтраженияВРеглУчетеУКР() Экспорт
	
	Возврат УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеНачисленнойАмортизацииУКР("КорректировкаНалоговогоНазначенияОС")
		+ УчетОСВызовСервера.ТекстЗапросаВТОтраженияВРеглУчетеОстаткиПоСчетам();
	
КонецФункции

Функция КорректировкаНалоговогоНазначенияСчетУчета()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Корректировка налогового назначения ОС (Дт СчетУчета новое НН:: Кт СчетУчета старое НН)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьБУ, 0) КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втМестонахожденияОС.Подразделение КАК ПодразделениеДт,		
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	//|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|   Операция.НовоеНалоговоеНазначение КАК НалоговоеНазначениеДт,		
	|	
	|	СчетаОтражения.СчетУчета КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА Операция.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		КОГДА Операция.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА Строки.НачальнаяСтоимостьНУ
	|		ИНАЧЕ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0)
	|	КОНЕЦ КАК СуммаНУДт,
	|   0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втМестонахожденияОС.Подразделение КАК ПодразделениеКт,		
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	//|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|   Операция.НалоговоеНазначение КАК НалоговоеНазначениеКт,		
	|	
	|	СчетаОтражения.СчетУчета КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА Операция.НалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(втОстаткиПоСчетам.ВосстановительнаяСтоимостьНУ, 0)
	|	КОНЕЦ КАК СуммаНУКт,		
	|   0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Корректировка налогового назначения ОС"" КАК Содержание
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаНалоговогоНазначенияОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияОС КАК втМестонахожденияОС
	|		ПО Строки.ОсновноеСредство = втМестонахожденияОС.ОбъектУчета	
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|";
	
КонецФункции

Функция  КорректировкаНалоговогоНазначенияСчетНачисленияАмортизации()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Перемещение амортизации ОС (Дт СчетНачисленияАмортизации:: Кт СчетНачисленияАмортизации)
	|ВЫБРАТЬ
	|
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияБУ, 0) КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втМестонахожденияОС.Подразделение КАК ПодразделениеДт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	|   Операция.НалоговоеНазначение КАК НалоговоеНазначениеДт,		
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0) КАК СуммаНУДт,
	|	0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втМестонахожденияОС.Подразделение КАК ПодразделениеКт,
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	|   Операция.НовоеНалоговоеНазначение КАК НалоговоеНазначениеКт,		
	|	
	|	СчетаОтражения.СчетНачисленияАмортизации КАК СчетКт,
	|	Строки.ОсновноеСредство КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	ВЫБОР
	|		КОГДА Операция.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(втОстаткиПоСчетам.НакопленнаяАмортизацияНУ, 0)
	|	КОНЕЦ КАК СуммаНУКт,
	|	0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Корректировка налогового назначения ОС"" КАК Содержание		
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаНалоговогоНазначенияОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета
	|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияОС КАК втМестонахожденияОС
	|		ПО Строки.ОсновноеСредство = втМестонахожденияОС.ОбъектУчета		
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|";
	
КонецФункции

Функция УсловнаяПродажа()
	
	Возврат "
	|////////////////////////////////////////////////////////////////////////////////////////////////////
	|// Корректировка налогового назначения ОС (Дт СчетУчета новое НН:: Кт УсловнаяПродажа)
	|ВЫБРАТЬ
	|	
	|	Операция.Ссылка КАК Ссылка,
	|	Операция.Дата КАК Период,
	|	Операция.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторСтроки,
	|	
	|	Строки.СуммаНДС КАК Сумма,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаДт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаДт,
	|	
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаДт,
	|	втМестонахожденияОС.Подразделение КАК ПодразделениеДт,		
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиДт,
	//|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеДт,//НужноДобавитьНалоговоеНазначениеДт 
	|   Операция.НовоеНалоговоеНазначение КАК НалоговоеНазначениеДт,		
	|	
	|	СчетаОтражения.СчетУчета КАК СчетДт,
	|	Строки.ОсновноеСредство КАК СубконтоДт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоДт3,
	|	
	|	0 КАК ВалютнаяСуммаДт,
	|	0 КАК КоличествоДт,
	|	ВЫБОР
	|		КОГДА Операция.НовоеНалоговоеНазначение = ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.НДС_НеоблагаемаяНеХозДеятельность)
	|			ТОГДА 0
	|		ИНАЧЕ Строки.СуммаНДС
	|	КОНЕЦ КАК СуммаНУДт,		
	|   0 КАК СуммаПРДт,
	|	0 КАК СуммаВРДт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВидСчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК ГруппаФинансовогоУчетаКт,
	|	НЕОПРЕДЕЛЕНО КАК МестоУчетаКт,
	|	
	|	НЕОПРЕДЕЛЕНО КАК ВалютаКт,
	|	втМестонахожденияОС.Подразделение КАК ПодразделениеКт,		
	|	Строки.ОсновноеСредство.НаправлениеДеятельности КАК НаправлениеДеятельностиКт,
	//|	ЗНАЧЕНИЕ(Справочник.НалоговыеНазначенияАктивовИЗатрат.ПустаяСсылка) КАК НалоговоеНазначениеКт,//НужноДобавитьНалоговоеНазначениеКт 
	|   НЕОПРЕДЕЛЕНО КАК НалоговоеНазначениеКт,		
	|	
	|	ЗНАЧЕНИЕ(ПланСчетов.Хозрасчетный.УсловнаяПродажа) КАК СчетКт,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт1,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт2,
	|	НЕОПРЕДЕЛЕНО КАК СубконтоКт3,
	|	
	|	0 КАК ВалютнаяСуммаКт,
	|	0 КАК КоличествоКт,
	|	Строки.СуммаНДС КАК СуммаНУКт,
	|   0 КАК СуммаПРКт,
	|	0 КАК СуммаВРКт,
	|	""Условная Продажа"" КАК Содержание
	|ИЗ
	|	Документ.КорректировкаНалоговогоНазначенияОС КАК Операция
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаНалоговогоНазначенияОС.ОС КАК Строки
	|		ПО Строки.Ссылка = Операция.Ссылка
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втСчетаОтражения КАК СчетаОтражения
	|		ПО Строки.ОсновноеСредство = СчетаОтражения.ОбъектУчета	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втМестонахожденияОС КАК втМестонахожденияОС
	|		ПО Строки.ОсновноеСредство = втМестонахожденияОС.ОбъектУчета	
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ втОстаткиПоСчетам КАК втОстаткиПоСчетам
	|		ПО Строки.ОсновноеСредство = втОстаткиПоСчетам.ОбъектУчета
	|	
	|ГДЕ
	|	Операция.Ссылка = &Ссылка
	|	И Операция.УсловнаяПродажа		
	|";
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

#КонецОбласти

#Область БлокировкаПриОбновленииИБ

Процедура ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ПредставлениеОперации)
	
	ВходящиеДанные = Новый Соответствие;
	
	УчетОСВызовСервера.ЗаполнитьВходящиеДанныеАмортизации(ВходящиеДанные);
	
	ЗакрытиеМесяцаУТВызовСервера.ПроверитьБлокировкуВходящихДанныхПриОбновленииИБ(ВходящиеДанные, ПредставлениеОперации);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли