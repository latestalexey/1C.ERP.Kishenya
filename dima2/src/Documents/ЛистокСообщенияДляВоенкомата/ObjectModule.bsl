#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Сотрудники") Тогда
		ЗарплатаКадры.ЗаполнитьПоОснованиюСотрудником(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОбновитьВторичныеДанныеДокумента(ДанныеОрганизации = Истина, ДанныеСотрудника = Истина) Экспорт 
	
	Модифицирован = Ложь;
	
	Если ОбъектЗафиксирован() Тогда
		Возврат Модифицирован;
	КонецЕсли;
	
	Если ДанныеОрганизации И ЗаполнитьДанныеОрганизации() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Если ДанныеСотрудника И ЗаполнитьДанныеСотрудника() Тогда
		Модифицирован = Истина;
	КонецЕсли;
	
	Возврат Модифицирован;
	
КонецФункции

Функция ЗаполнитьДанныеСотрудника()
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	СтрокаРеквизиты = "Подразделение, Должность, ДатаРождения, Образование, СемейноеПоложение, Звание, ВУС, СоставСемьи, АдресМестаПроживанияПредставление";
	РеквизитыДокумента = Новый Структура(СтрокаРеквизиты);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	&Период КАК Период
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка = &Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Сотрудник,
	|	"""" КАК ТабельныйНомер,
	|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ГоловнаяОрганизация,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыЗанятости.ПустаяСсылка) КАК ВидЗанятости,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка) КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка) КАК Должность,
	|	ЗНАЧЕНИЕ(Перечисление.ПолФизическогоЛица.ПустаяСсылка) КАК Пол,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаРождения,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК МестоРождения,
	|	ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка) КАК Страна,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК Фамилия,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК Имя,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК Отчество,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ФИОПолные,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК АдресПоПрописке,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК АдресПоПропискеПредставление,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК АдресМестаПроживания,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК АдресМестаПроживанияПредставление,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ТелефонДомашний,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ТелефонДомашнийПредставление,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ТелефонРабочий,
	|	ВЫРАЗИТЬ("""" КАК СТРОКА(500)) КАК ТелефонРабочийПредставление,
	|	ЗНАЧЕНИЕ(Справочник.СостояниеВБраке.ПустаяСсылка) КАК СостояниеВБраке,
	|	ЗНАЧЕНИЕ(Справочник.ВидыОбразованияФизическихЛиц.ПустаяСсылка) КАК Образование1ВидОбразования,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииЗапасаВоеннообязанных.ПустаяСсылка) КАК ВоинскийУчетКатегорияЗапаса,
	|	ЗНАЧЕНИЕ(Справочник.ЗванияГражданскогоВоинскогоУчета.ПустаяСсылка) КАК ВоинскийУчетЗвание,
	|	ЗНАЧЕНИЕ(Справочник.СоставыВоеннослужащих.ПустаяСсылка) КАК ВоинскийУчетСостав,
	|	"""" КАК ВоинскийУчетВУС,
	|	ЗНАЧЕНИЕ(Перечисление.ГодностьКВоеннойСлужбе.ПустаяСсылка) КАК ВоинскийУчетГодность,
	|	ЗНАЧЕНИЕ(Справочник.Военкоматы.ПустаяСсылка) КАК ВоинскийУчетВоенкомат,
	|	ЛОЖЬ КАК ВоинскийУчетНаличиеМобпредписания,
	|	"""" КАК ВоинскийУчетНомерКомандыПартии,
	|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка) КАК ВоинскийУчетЗабронированОрганизацией,
	|	ЗНАЧЕНИЕ(Справочник.ПереченьДолжностейДляБронированияГраждан.ПустаяСсылка) КАК ВоинскийУчетПунктПеречня,
	|	ЗНАЧЕНИЕ(Перечисление.ОтношениеКВоинскойОбязанности.ПустаяСсылка) КАК ВоинскийУчетОтношениеКВоинскойОбязанности,
	|	ЗНАЧЕНИЕ(Перечисление.ОтношениеКВоинскомуУчету.ПустаяСсылка) КАК ВоинскийУчетОтношениеКВоинскомуУчету
	|ПОМЕСТИТЬ Представления_КадровыеДанныеСотрудников
	|ИЗ
	|	ВТСотрудники КАК ВТСотрудники
	|ГДЕ
	|	""ТолькоРазрешенные"" = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Представления_КадровыеДанныеСотрудников.Сотрудник КАК Сотрудник,
	|	Представления_КадровыеДанныеСотрудников.ТабельныйНомер КАК ТабельныйНомер,
	|	Представления_КадровыеДанныеСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Представления_КадровыеДанныеСотрудников.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	Представления_КадровыеДанныеСотрудников.ВидЗанятости КАК ВидЗанятости,
	|	Представления_КадровыеДанныеСотрудников.Организация КАК Организация,
	|	Представления_КадровыеДанныеСотрудников.Подразделение КАК Подразделение,
	|	Представления_КадровыеДанныеСотрудников.Должность КАК Должность,
	|	Представления_КадровыеДанныеСотрудников.Пол КАК Пол,
	|	Представления_КадровыеДанныеСотрудников.ДатаРождения КАК ДатаРождения,
	|	Представления_КадровыеДанныеСотрудников.МестоРождения КАК МестоРождения,
	|	Представления_КадровыеДанныеСотрудников.Страна КАК Страна,
	|	Представления_КадровыеДанныеСотрудников.Фамилия КАК Фамилия,
	|	Представления_КадровыеДанныеСотрудников.Имя КАК Имя,
	|	Представления_КадровыеДанныеСотрудников.Отчество КАК Отчество,
	|	Представления_КадровыеДанныеСотрудников.ФИОПолные КАК ФИОПолные,
	|	Представления_КадровыеДанныеСотрудников.АдресПоПрописке КАК АдресПоПрописке,
	|	Представления_КадровыеДанныеСотрудников.АдресПоПропискеПредставление КАК АдресПоПропискеПредставление,
	|	Представления_КадровыеДанныеСотрудников.АдресМестаПроживания КАК АдресМестаПроживания,
	|	Представления_КадровыеДанныеСотрудников.АдресМестаПроживанияПредставление КАК АдресМестаПроживанияПредставление,
	|	Представления_КадровыеДанныеСотрудников.ТелефонДомашний КАК ТелефонДомашний,
	|	Представления_КадровыеДанныеСотрудников.ТелефонДомашнийПредставление КАК ТелефонДомашнийПредставление,
	|	Представления_КадровыеДанныеСотрудников.ТелефонРабочий КАК ТелефонРабочий,
	|	Представления_КадровыеДанныеСотрудников.ТелефонРабочийПредставление КАК ТелефонРабочийПредставление,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетОтношениеКВоинскойОбязанности КАК ОтношениеКВоинскойОбязанности,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетОтношениеКВоинскомуУчету КАК ОтношениеКВоинскомуУчету,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетКатегорияЗапаса КАК КатегорияЗапаса,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетЗвание КАК Звание,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетСостав КАК Состав,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетВУС КАК ВУС,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетГодность КАК Годность,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетВоенкомат КАК Военкомат,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетНаличиеМобпредписания КАК НаличиеМобпредписания,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетНомерКомандыПартии КАК НомерКомандыПартии,
	|	Представления_КадровыеДанныеСотрудников.ВоинскийУчетЗабронированОрганизацией КАК ЗабронированОрганизацией,
	|	Представления_КадровыеДанныеСотрудников.СостояниеВБраке КАК СемейноеПоложение,
	|	Представления_КадровыеДанныеСотрудников.Образование1ВидОбразования КАК Образование
	|ИЗ
	|	Представления_КадровыеДанныеСотрудников КАК Представления_КадровыеДанныеСотрудников";
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаменитьЗапросыКПредставлениямВиртуальныхТаблиц(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	Запрос.УстановитьПараметр("Период", Дата);
	
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда 
		
		ЗаполнитьЗначенияСвойств(РеквизитыДокумента, Выборка);
		СведенияОСемье = Документы.ЛистокСообщенияДляВоенкомата.СведенияОСоставеСемьи(Выборка.ФизическоеЛицо);
		РеквизитыДокумента.СоставСемьи = СведенияОСемье.СоставСемьиКраткий;
		
	КонецЕсли;
	
	Запрос = ЗапросПоСтруктуре(РеквизитыДокумента);
	Запрос.Выполнить();
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ЭтотОбъект.Метаданные().ПолноеИмя());
	ПараметрыФиксацииВторичныхДанных = МенеджерОбъекта.ПараметрыФиксацииВторичныхДанных();
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект, , , ПараметрыФиксацииВторичныхДанных);
	
КонецФункции

Функция ЗаполнитьДанныеОрганизации()
	
	Модифицирован = Ложь;
	
	ОтветственныеЛица = Новый Структура("Организация,ОтветственныйЗаВУР", Организация);
	ЗарплатаКадры.ПолучитьЗначенияПоУмолчанию(ОтветственныеЛица, Дата);
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ОтветственныйЗаВУР", ОтветственныеЛица.ОтветственныйЗаВУР);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	&ОтветственныйЗаВУР
	               |ПОМЕСТИТЬ ВТВторичныеДанные";
				   
	Запрос.Выполнить();			   
	
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ЭтотОбъект.Метаданные().ПолноеИмя());
	ПараметрыФиксацииВторичныхДанных = МенеджерОбъекта.ПараметрыФиксацииВторичныхДанных();
	
	Возврат ФиксацияВторичныхДанныхВДокументах.ОбновитьВторичныеДанные(Запрос.МенеджерВременныхТаблиц, ЭтотОбъект, , , ПараметрыФиксацииВторичныхДанных);
	
КонецФункции

Функция ЗапросПоСтруктуре(Структура)
	
	Запрос = Новый Запрос;  
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	ШаблонПоля = " &#Парам# КАК #Парам#,";

	Текст = "ВЫБРАТЬ #Поля#
	|	ПОМЕСТИТЬ ВТВторичныеДанные";
	
	Поля = "";
	
	Для каждого Поле Из Структура Цикл
		Поля = Поля + Символы.ПС + СтрЗаменить(ШаблонПоля, "#Парам#", Поле.Ключ);
		Запрос.Параметры.Вставить(Поле.Ключ);
	КонецЦикла;
	
	СтроковыеФункцииКлиентСервер.УдалитьПоследнийСимволВСтроке(Поля, 1);
	Запрос.Текст = СтрЗаменить(Текст, "#Поля#", Поля);
	
	ЗаполнитьЗначенияСвойств(Запрос.Параметры, Структура); 

	Возврат Запрос;
	
КонецФункции 

Функция ОбъектЗафиксирован() Экспорт
	
	Возврат Проведен;
	
КонецФункции 

#КонецОбласти

#КонецЕсли
