
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	АдресРезультата = Параметры.АдресРезультата;
	ИДТаблицы = Параметры.ИДТаблицы;
	
	ПараметрыЭкземпляраБюджета = ПолучитьИзВременногоХранилища(Параметры.ПараметрыВыводаВидаБюджета);
	КолонкиТаблиц = ПараметрыЭкземпляраБюджета.КолонкиТаблиц;
	
	//колонки текущей таблицы
	СтруктураКолонок = КолонкиТаблиц[Параметры.ИДТаблицы];
	КолонкиПоИзмерениям = Параметры.КолонкиПоИзмерениям;
	
	ДобавляемыеЭлементы = Новый Массив;
	ДобавляемыеКолонки = Новый Массив;
	Документы.ЭкземплярБюджета.РассчитатьЭлементыСпискаБюджетаКДобавлению(ДобавляемыеЭлементы, ДобавляемыеКолонки, КолонкиПоИзмерениям);
	
	//структура с ключами полей текущей колонки
	СтруктураАналогРасшифровки = Новый Структура;
	Документы.ЭкземплярБюджета.ДобавитьКолонкиСпискаБюджетаПоТаблице(ДобавляемыеЭлементы, ДобавляемыеКолонки, 
														КолонкиПоИзмерениям, СтруктураКолонок, СтруктураАналогРасшифровки);
		
	Документы.ЭкземплярБюджета.ОтразитьИзмененияКолонокСпискаБюджетаНаФорме(ЭтаФорма, ДобавляемыеЭлементы, ДобавляемыеКолонки);
	
	РедактируемыеКолонкиСписка = СтроковыеФункцииКлиентСервер.СтрокаИзМассиваПодстрок(КолонкиПоИзмерениям.Измерения);
	
	МассивЗначений = Параметры.Значения;
	ГлубинаДерева = БюджетнаяОтчетностьВыводСервер.ГлубинаДерева(СтруктураКолонок.Строки) + 1;
	
	СоответствиеИндексовЗначений = Новый Соответствие;
	
	Для Строка = ГлубинаДерева по МассивЗначений[0].ВГраница() Цикл
		
		Колонка = 0;
		НоваяСтрока = СписокРедактированияБюджета.Добавить();
		
		СтруктураСопоставления = Новый Структура;
		
		Для Каждого Измерение из КолонкиПоИзмерениям.Измерения Цикл
			Если Колонка > МассивЗначений.ВГраница() Тогда
				Значение = Неопределено;
			Иначе
				Значение = МассивЗначений[Колонка][Строка];
			КонецЕсли;
			
			СтруктураСопоставления.Вставить(Измерение, Значение);
			
			Колонка = Колонка + 1;
		КонецЦикла;
		
		НоваяСтрока.ИндексСтроки = СписокРедактированияБюджета.Индекс(НоваяСтрока);
		СоответствиеИндексовЗначений.Вставить(НоваяСтрока.ИндексСтроки, СтруктураСопоставления);
		
		Для Каждого КлючИЗначение из СтруктураАналогРасшифровки Цикл
			Если Колонка > МассивЗначений.ВГраница() Тогда
				НоваяСтрока[КлючИЗначение.Ключ] = Неопределено;
			КонецЕсли;
			
			Значение = МассивЗначений[Колонка][Строка];
			НоваяСтрока[КлючИЗначение.Ключ] = Значение;
			
			Колонка = Колонка + 1;
		КонецЦикла;
		
	КонецЦикла;
	
	СопоставитьДанныеЗагрузкиСДаннымиИБ(КолонкиПоИзмерениям, СоответствиеИндексовЗначений);
	
	ЭлементыЗагрузки = Новый Массив;
	АдресаЭлементов = Новый Структура;
	Для Сч = 0 по КолонкиПоИзмерениям.Измерения.ВГраница() Цикл
		
		ИмяИзмерения = КолонкиПоИзмерениям.Измерения[Сч];
		Элемент = Новый РеквизитФормы("ЗначениеЗагрузки_" + ИмяИзмерения, 
							Новый ОписаниеТипов("Строка"), , КолонкиПоИзмерениям.НастройкиКолонок[ИмяИзмерения].Заголовок);
		ЭлементыЗагрузки.Добавить(Элемент);
		АдресаЭлементов.Вставить("ЗначениеЗагрузки_" + ИмяИзмерения, "Группа" + (Сч % 3))
		
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(ЭлементыЗагрузки);
	Для Каждого Элемент из ЭлементыЗагрузки Цикл
		Адрес = АдресаЭлементов[Элемент.Имя];
		ЭлементФормы = Элементы.Добавить(Элемент.Имя, Тип("ПолеФормы"), Элементы[Адрес]);
		ЭлементФормы.ПутьКДанным = Элемент.Имя;
		ЭлементФормы.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная, 1);
	КонецЦикла;
	
	АдресСоответствияЗагрузки = ПоместитьВоВременноеХранилище(СоответствиеИндексовЗначений, УникальныйИдентификатор);
	
	ИмяКолонкиПериод = Неопределено;
	Для Каждого Измерение из КолонкиПоИзмерениям.Измерения Цикл
		
		Если БюджетнаяОтчетностьКлиентСервер.ЛеваяЧастьИмениСовпадает(Измерение, "Период") Тогда
			ИмяКолонкиПериод = Измерение;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ИмяКолонкиПериод = Неопределено Тогда
		ПериодНеСоответствуетПериодуБюджета = Ложь;
	Иначе
		СписокПериодов = СписокРедактированияБюджета.Выгрузить(,ИмяКолонкиПериод);
		СписокПериодов.Свернуть(ИмяКолонкиПериод);
		СписокПериодов.Сортировать(ИмяКолонкиПериод);
		Если СписокПериодов.Количество() Тогда
			МинимальныйПериод = СписокПериодов[0][ИмяКолонкиПериод];
			МаксимальныйПериод = СписокПериодов[СписокПериодов.Количество() - 1][ИмяКолонкиПериод];
			Если Не (МинимальныйПериод >= Параметры.НачалоПериода
					И МаксимальныйПериод <= Параметры.ОкончаниеПериода) Тогда
				ПериодНеСоответствуетПериодуБюджета = Истина;
				ПериодЗагружаемыхДанных = НСтр("ru='Период загружаемых данных (%1) не соответствует периоду бюджета (%2).
                                                    |Строки, период которых не входит в период бюджета загружены не будут.
                                                    |Продолжить?'
                                                    |;uk='Період завантажуваних даних (%1) не відповідає періоду бюджету (%2).
                                                    |Рядки, період яких не входить у період бюджету завантажені не будуть.
                                                    |Продовжити?'");
				
				ПериодЗагружаемыхДанных = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ПериодЗагружаемыхДанных,
					Формат(МинимальныйПериод, "ДЛФ=D") + "-" + Формат(МаксимальныйПериод, "ДЛФ=D"),
					Формат(Параметры.НачалоПериода, "ДЛФ=D") + "-" + Формат(Параметры.ОкончаниеПериода, "ДЛФ=D"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПериодНеСоответствуетПериодуБюджета Тогда
		ПодключитьОбработчикОжидания("ПоказатьВопросНесоответствияПериода", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантОтображенияСтрокПриИзменении(Элемент)
	
	Элементы.СписокРедактированияБюджета.ОтборСтрок = Неопределено;
	
	Если ВариантОтображенияСтрок = 1 Тогда
		Элементы.СписокРедактированияБюджета.ОтборСтрок = Новый ФиксированнаяСтруктура("Загружать", Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРедактированияБюджетаЗагружатьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СписокРедактированияБюджета.ТекущиеДанные;
	
	Если ТекущиеДанные.Загружать Тогда
		Колонки = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РедактируемыеКолонкиСписка);
		Для Каждого Измерение из Колонки Цикл
			СписокВыбора = Элементы[Измерение].СписокВыбора;
			Если Не СписокВыбора.Количество() Тогда
				Продолжить;
			КонецЕсли;
			
			Если СписокВыбора.НайтиПоЗначению(ТекущиеДанные[Измерение]) = Неопределено Тогда
				ТекущиеДанные.Загружать = Ложь;
				ТекстПредупреждения = НСтр("ru='В колонке ""%1"" выбрано неверное значение';uk='У колонці ""%1"" обрано невірне значення'");
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, Элементы[Измерение].Заголовок);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НастроитьВариантыОтображенияСтрок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормы

&НаКлиенте
Процедура СписокРедактированияБюджетаПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтруктураПолей = ПолучитьИзВременногоХранилища(АдресСоответствияЗагрузки)[ТекущиеДанные.ИндексСтроки];
	
	Для Каждого КлючИЗначение из СтруктураПолей Цикл
		ЭтаФорма["ЗначениеЗагрузки_" + КлючИЗначение.Ключ] = КлючИЗначение.Значение;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРедактированияБюджетаПослеУдаления(Элемент)
	
	НастроитьВариантыОтображенияСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРедактированияБюджетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗакрытьФормы(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Отбор = Новый Структура("Загружать", Ложь);
	СтрокиСНесопоставленнымиДанными = СписокРедактированияБюджета.НайтиСтроки(Отбор);
	
	Отказ = (СтрокиСНесопоставленнымиДанными.Количество() > 0);
	
	Если Отказ Тогда
		ШаблонСообщения = НСтр("ru='Не все строки сопоставлены';uk='Не всі рядки зіставлені'");
		ПоказатьПредупреждение(, ШаблонСообщения);
		Возврат;
	КонецЕсли;
	
	Закрыть();
	ПоместитьДанныеВоВременноеХранилищеНаСервере();
	ОповеститьОВыборе(АдресРезультата);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокРедактированияБюджетаОсновнаяКолонкаПриИзменении(Команда)
	
	СписокРедактированияПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВопросНесоответствияПериода() Экспорт
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПриОткрытииФрагмент", ЭтаФорма);
	ПоказатьВопрос(ОповещениеОЗакрытии, ПериодЗагружаемыхДанных, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииФрагмент(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СписокРедактированияПриИзмененииНаСервере()
	
	СоответствиеИндексовЗначений = ПолучитьИзВременногоХранилища(АдресСоответствияЗагрузки);
	
	МассивКолонок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(РедактируемыеКолонкиСписка);
	Колонка = Элементы.СписокРедактированияБюджета.ТекущийЭлемент.Имя;
	Строка = Элементы.СписокРедактированияБюджета.ТекущаяСтрока;
	Если МассивКолонок.Найти(Колонка) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаЗагрузки = СписокРедактированияБюджета[Строка];
	СтруктураСопоставления = СоответствиеИндексовЗначений[СтрокаЗагрузки.ИндексСтроки];
	ИмяПоиска = СтруктураСопоставления[Колонка];
	
	Для Каждого СтрокаСписка из СписокРедактированияБюджета Цикл
		Если СтрокаЗагрузки = СтрокаСписка Тогда
			Продолжить;
		КонецЕсли;
		СтруктураСопоставления = СоответствиеИндексовЗначений[СтрокаСписка.ИндексСтроки];
		Если Не ЗначениеЗаполнено(СтруктураСопоставления[Колонка]) Тогда
			Продолжить;
		КонецЕсли;
		Если СтруктураСопоставления[Колонка] = ИмяПоиска Тогда
			СтрокаСписка[Колонка] = СтрокаЗагрузки[Колонка];
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СопоставитьДанныеЗагрузкиСДаннымиИБ(КолонкиПоИзмерениям, СоответствиеИндексовЗначений)
	
	Измерения = КолонкиПоИзмерениям.Измерения;
	НастройкиКолонок = КолонкиПоИзмерениям.НастройкиКолонок;
	
	ТаблицаНастройкиПоискаЗначений = Новый ТаблицаЗначений;
	ТаблицаНастройкиПоискаЗначений.Колонки.Добавить("СтрокаПоиска", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(1024)));
	ТаблицаНастройкиПоискаЗначений.Колонки.Добавить("Тип");
	
	Для каждого СтрокаЗагрузки Из СписокРедактированияБюджета Цикл
		
		СтрокаЗагрузки.Загружать = Истина;
		
		СтруктураСопоставления = СоответствиеИндексовЗначений[СтрокаЗагрузки.ИндексСтроки];
		Для Каждого Измерение из Измерения Цикл
			
			ЗагружаемаяАналитика = СтруктураСопоставления[Измерение];
			
			Если Не ЗначениеЗаполнено(ЗагружаемаяАналитика) Тогда
				Продолжить;
			КонецЕсли;
			
			СписокОграничений = Неопределено;
			Если Элементы[Измерение].СписокВыбора.Количество() Тогда
				СписокОграничений = Элементы[Измерение].СписокВыбора;
			КонецЕсли;
			
			Для каждого Тип Из НастройкиКолонок[Измерение].ТипЗначения.Типы() Цикл
				Если Тип = Тип("Дата") Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрока = ТаблицаНастройкиПоискаЗначений.Добавить();
				НоваяСтрока.СтрокаПоиска = "%" + СокрЛП(ЗагружаемаяАналитика) + "%";
				НоваяСтрока.Тип = Тип;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	РазныеТипы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаНастройкиПоискаЗначений.ВыгрузитьКолонку("Тип"));
	
	ТекстВременныхТаблиц = "";
	ТекстВыборки = "";
	Запрос = Новый Запрос;
	
	Для Каждого Тип из РазныеТипы Цикл
		
		РазныеНаименования = ТаблицаНастройкиПоискаЗначений.Скопировать(Новый Структура("Тип", Тип));
		РазныеНаименования.Свернуть("СтрокаПоиска");
		
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(Тип);
		ПолноеИмя = МетаданныеОбъекта.ПолноеИмя();
		ИмяВременнойТаблицы = "Фильтр_" + СтрЗаменить(ПолноеИмя, ".", "");
		
		ТекстВременныхТаблиц = ТекстВременныхТаблиц + 
		"ВЫБРАТЬ ТаблицаПоиска.СтрокаПоиска 
		|
		|ПОМЕСТИТЬ " + ИмяВременнойТаблицы + "
		|
		|ИЗ &" + ИмяВременнойТаблицы + " КАК ТаблицаПоиска
		|
		|;";
		
		Запрос.УстановитьПараметр(ИмяВременнойТаблицы, РазныеНаименования);
		
		МассивПолей = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Наименование");
		Если МетаданныеОбъекта.Реквизиты.Найти("НаименованиеПолное") <> Неопределено Тогда
			МассивПолей.Добавить("НаименованиеПолное");
		КонецЕсли;
		
		Для Каждого Поле из МетаданныеОбъекта.ВводПоСтроке Цикл
			Если МассивПолей.Найти(Поле.Имя) = Неопределено Тогда
				МассивПолей.Добавить(Поле.Имя);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Поле Из МассивПолей Цикл
			
			ТекстВыборки = ТекстВыборки + 
			?(ЗначениеЗаполнено(ТекстВыборки), "ОБЪЕДИНИТЬ ВСЕ", "") + "
			|
			|ВЫБРАТЬ ТаблицаФильтра.СтрокаПоиска, Таблица.Ссылка, Таблица." + Поле + " КАК Наименование
			|
			|ИЗ " + ИмяВременнойТаблицы + " КАК ТаблицаФильтра
			|	ЛЕВОЕ СОЕДИНЕНИЕ " + ПолноеИмя + " КАК Таблица
			|	ПО Таблица." + Поле + " ПОДОБНО ТаблицаФильтра.СтрокаПоиска
			|
			|ГДЕ НЕ Таблица.Ссылка Есть NULL
			|";
			
			Для Сч = 0 по МетаданныеОбъекта.СтандартныеРеквизиты.Количество()-1 Цикл
				Если МетаданныеОбъекта.СтандартныеРеквизиты[Сч].Имя = "ЭтоГруппа" Тогда
					ТекстВыборки = ТекстВыборки +
					"И НЕ Таблица.ЭтоГруппа
					|";
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
	
	КонецЦикла;
	
	ТекстЗапроса = ТекстВременныхТаблиц + ТекстВыборки;
	Если Не ПустаяСтрока(ТекстЗапроса) Тогда
		Запрос.Текст = ТекстЗапроса;
		РезультатПоиска = Запрос.Выполнить().Выгрузить();
	Иначе
		РезультатПоиска = Новый ТаблицаЗначений;
		РезультатПоиска.Колонки.Добавить("СтрокаПоиска");
		РезультатПоиска.Колонки.Добавить("Ссылка");
		РезультатПоиска.Колонки.Добавить("Наименование");
	КонецЕсли;
	РезультатПоиска.Индексы.Добавить("СтрокаПоиска");
	
	Для каждого СтрокаЗагрузки Из СписокРедактированияБюджета Цикл
		
		СтрокаЗагрузки.Загружать = Истина;
		
		СтруктураСопоставления = СоответствиеИндексовЗначений[СтрокаЗагрузки.ИндексСтроки];
		Для Каждого Измерение из Измерения Цикл
			
			ЗагружаемаяАналитика = СтруктураСопоставления[Измерение];
			
			Если Не ЗначениеЗаполнено(ЗагружаемаяАналитика) Тогда
				Продолжить;
			КонецЕсли;
			
			Если БюджетнаяОтчетностьКлиентСервер.ЛеваяЧастьИмениСовпадает(Измерение, "Период") Тогда
				СтрокаЗагрузки[Измерение] = ЗагружаемаяАналитика;
				Продолжить;
			КонецЕсли;
			
			КлючПоиска = "%" + СокрЛП(ЗагружаемаяАналитика) + "%";
			
			НайденныеЭлементы = РезультатПоиска.НайтиСтроки(Новый Структура("СтрокаПоиска", КлючПоиска));
			ТаблицаПодходящихНаименований = Новый ТаблицаЗначений;
			ТаблицаПодходящихНаименований.Колонки.Добавить("Ссылка");
			ТаблицаПодходящихНаименований.Колонки.Добавить("УсловныйПроцентПодобия");
			
			ИскомоеНаименование = СокрЛП(ЗагружаемаяАналитика);
			Для Каждого НайденнаяСтрока из НайденныеЭлементы Цикл
				
				Если Элементы[Измерение].СписокВыбора.Количество() Тогда
					СписокВыбора = Элементы[Измерение].СписокВыбора;
					Если СписокВыбора.НайтиПоЗначению(НайденнаяСтрока.Ссылка) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				НоваяСтрока = ТаблицаПодходящихНаименований.Добавить();
				НоваяСтрока.Ссылка = НайденнаяСтрока.Ссылка;
				
				СтрокаСравнения = СокрЛП(НайденнаяСтрока.Наименование);
				Если СтрокаСравнения = ИскомоеНаименование Тогда
					НоваяСтрока.УсловныйПроцентПодобия = 100;
					Продолжить;
				КонецЕсли;
				
				Если Не СтрДлина(СтрокаСравнения) Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока.УсловныйПроцентПодобия = СтрДлина(ИскомоеНаименование) / СтрДлина(СтрокаСравнения) * 100;
				
				Если БюджетнаяОтчетностьКлиентСервер.ЛеваяЧастьИмениСовпадает(СтрокаСравнения, ИскомоеНаименование) Тогда
					НоваяСтрока.УсловныйПроцентПодобия = Мин(НоваяСтрока.УсловныйПроцентПодобия + 10, 99);
				КонецЕсли;
			КонецЦикла;
			
			ТаблицаПодходящихНаименований.Сортировать("УсловныйПроцентПодобия УБЫВ");
			Если ТаблицаПодходящихНаименований.Количество()
				И ТаблицаПодходящихНаименований[0].УсловныйПроцентПодобия > 70 Тогда
				
				СтрокаЗагрузки[Измерение] = ТаблицаПодходящихНаименований[0].Ссылка;
				Продолжить;
				
			КонецЕсли;
			
			СтрокаЗагрузки.Загружать = Ложь;
			
		КонецЦикла;
		
	КонецЦикла;
	
	НастроитьВариантыОтображенияСтрок();
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВариантыОтображенияСтрок()
	
	СписокВыбора = Элементы.ВариантОтображенияСтрок.СписокВыбора;
	СписокВыбора.Очистить();
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Все (%1)';uk='Всі (%1)'"), 
		СписокРедактированияБюджета.Количество());
		
	СписокВыбора.Добавить(0, Представление); 
	
	СтрокиЕстьНесопоставленныеДанные = СписокРедактированияБюджета.НайтиСтроки(Новый Структура("Загружать", Ложь));
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Несопоставленные (%1)';uk='Незіставлені (%1)'"), 
		СтрокиЕстьНесопоставленныеДанные.Количество());
		
	СписокВыбора.Добавить(1, Представление);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого СтрокаЗагрузки из СписокРедактированияБюджета Цикл
		СтрокаЗагрузки.Загружать = Истина;
	КонецЦикла;
	
	НастроитьВариантыОтображенияСтрок();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	
	Для Каждого СтрокаЗагрузки из СписокРедактированияБюджета Цикл
		СтрокаЗагрузки.Загружать = Ложь;
	КонецЦикла;
	
	НастроитьВариантыОтображенияСтрок();
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеВоВременноеХранилищеНаСервере()
	
	Таблица = СписокРедактированияБюджета.Выгрузить();
	СтрокаСвертки = "";
	СтрокаСуммирования = "";
	Для Каждого Колонка из Таблица.Колонки Цикл
		Если СтрНайти(Колонка.Имя, "Примечание") Тогда
			Продолжить;
		КонецЕсли;
		Если СтрНайти(РедактируемыеКолонкиСписка, Колонка.Имя) Тогда
			СтрокаСвертки = СтрокаСвертки + ?(ПустаяСтрока(СтрокаСвертки), "", ",") + Колонка.Имя;
		Иначе
			СтрокаСуммирования = СтрокаСуммирования + ?(ПустаяСтрока(СтрокаСуммирования), "", ",") + Колонка.Имя;
		КонецЕсли;
	КонецЦикла;
	
	Таблица.Свернуть(СтрокаСвертки, СтрокаСуммирования);
	
	Результат = Новый Структура("СписокРедактированияБюджета,
								|ИДТаблицы",
								Таблица,
								ИДТаблицы);
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
КонецПроцедуры

#КонецОбласти
